     A*%%TS  SD  20070210  130252  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK100D
     A* Beskrivelse . . : Kunder, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
PDF  A*       080610 BSA  Foreløpig fjernes mulighet for oppslag mot arkiv.
7.xx A*       150216 bsa  Justeringer basert på ny GUI og regler
7.05 A*       270918 BSA  Integrasjon mot SmartKalk
7.08 A*K000   280121 BSA  Feilmedling ved sletting av systemkunde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20020723  130247  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
7.05 A                                      VALUES(2 3 4 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O  9  4REFFLD(RKKUND)
     A            B1NAVN    R        O  9 11REFFLD(RKNAVN)
     A            B1STED        27A  O  9 42
     A            B1TLFN    R        O  9 70REFFLD(RKTLFN)
     A            B1ALFA    R        H      REFFLD(RKALFA)
     A            B1PONR    R        H      REFFLD(RKPONR)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070210  130252  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA09
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.11 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kunder'
     A                                      COLOR(WHT)
     A                                  2 38'RK100B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk .'
     A            B2STEK        35A  B  4 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.02 A                                  4 48'Kontaktper.'
7.02 A            B2STKO        20A  B  4 60COLOR(YLW)
7.02 A  22                                  DSPATR(PC)
7.02 A                                      OVRDTA
7.02 A                                  5 52'Bruk frisøk + kontaktper.søk'
7.xx A                                  5  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Kundenavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 41'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
7.05 A                                       6=Utskrift  7=Smartkalk  8=Vis p-
7.05 A                                      oster'
pdf  A*                                      6=Utskrift  8=Vis poster 9=Vis ark-
pdf  A*                                     iv'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Kundenavn                    '
7.xx A**                                8  2'V Kunde  Kundenavn                -
7.xx A**                                        '
     A                                      COLOR(WHT)
7.xx A N80                              8 42'Postnr/poststed            '
     A                                      COLOR(WHT)
7.xx A  80                              8 42'Gateadresse                '
     A                                      COLOR(WHT)
7.xx A N80                              8 70'Telefon    '
     A                                      COLOR(WHT)
7.xx A  80                              8 70'Mobilnr.   '
     A                                      COLOR(WHT)
7.xx A  81                              8 70'Foretaksnr.'
     A                                      COLOR(WHT)
7.xx A            B2KUND    R        B  6  4REFFLD(RKKUND)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  6 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PONR    R        B  6 42REFFLD(RKPONR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20050827  113051  KOB540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 22  2'F3=Gå tilbake  F6=Opprett  F8=Post-
     A                                      er  F9=Gjenta søk  F11=Endre liste'
     A                                      COLOR(BLU)
     A                                 23  2'F5=Aktive/Alle  F7=Vis hurtigoppre-
     A                                      ttet'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020723  155104  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.11 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kunder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kundenummer. . . . . . . . .'
     A            C1KUND    R        B  5 31REFFLD(RKKUND)
     A  31                                  ERRMSGID(MMR0010 *LIBL/QMFUMAL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.05 A          R C7BLD
7.05 A*%%TS  SD  20181017  153023  ASK630      REL-V7R3M0  5770-WDS
7.05 A                                      CA03
7.05 A                                      CF04
7.05 A                                      CF07
7.05 A                                      CA10
7.05 A                                      CF11
7.05 A                                      CA12
7.05 A                                      PUTOVR
7.05 A            L_FNAV        30A  O  1  2COLOR(WHT)
7.05 A            B2FIRM         3S 0O  1 33DSPATR(ND)
7.05 A                                  1 46'Økonomi'
7.05 A                                      COLOR(WHT)
7.05 A                                  1 66'Kl. :'
7.05 A                                      COLOR(WHT)
7.05 A                                  1 72TIME
7.05 A                                      COLOR(WHT)
7.05 A                                  2  2'Vedlikehold av kunder'
7.05 A                                      COLOR(WHT)
7.05 A                                  2 46'Bruker:'
7.05 A                                      COLOR(WHT)
7.05 A                                  2 54USER
7.05 A                                      COLOR(WHT)
7.05 A                                  2 66'Dato:'
7.05 A                                      COLOR(WHT)
7.05 A                                  2 72DATE
7.05 A                                      EDTCDE(Y)
7.05 A                                      COLOR(WHT)
7.05 A                                  3  1'-----------------------------------
7.05 A                                      ------------------------------------
7.05 A                                      -----------'
7.05 A                                      COLOR(WHT)
7.05 A                                  5  2'Kundenummer. . . . . . . . .'
7.05 A            C1KUND    R        O  5 31REFFLD(RKKUND)
7.05 A  31                                  ERRMSG('Organisasjonsnummer finnes -
7.05 A                                      ikke på kunden' 31)
7.05 A                                      OVRDTA
7.05 A            C1NAVN    R        O  5 39REFFLD(RKNAVN)
7.05 A                                      OVRDTA
7.05 A                                  7  2'Smartkalk nøkkel'
7.05 A            C1SKEY        36A  O  8  2
7.05 A  32                                  ERRMSG('Nøkkel må genereres. Bruk F-
7.05 A                                      7 ' 32)
7.05 A                                      COLOR(YLW)
7.05 A                                      OVRDTA
7.05 A            C1PASS         7A  O  8 41
7.05 A  34                                  COLOR(RED)
7.05 A                                      OVRDTA
7.05 A                                 10  2'Kontaktperson'
7.05 A            C1KPRS        35A  O 11  2COLOR(YLW)
7.05 A                                      OVRDTA
7.05 A            C1KMAI        40   O 11 39
7.05 A  33                                  ERRMSG('eMail adresse må finnes. Ka-
7.05 A                                      n ikke sende nøkkel' 33)
7.05 A                                      OVRDTA
7.05 A                                      COLOR(YLW)
7.05 A                                 22  2'F3=Gå tilbake  F4=Kontaktperson  F-
7.05 A                                      7=Generer nøkkel'
7.05 A                                      COLOR(BLU)
7.05 A                                 23  2'F10=Resend nøkkel på mail  F11=Set-
7.05 A                                      t nøkkel passiv/aktiv '
7.05 A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020723  125918  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kundenummer finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020106  200250  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slett kunde '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde . . . . .'
     A            D1KUND    R        O  4 18REFFLD(RKKUND)
     A  32                                  ERRMSGID(RMK0360 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMK0370 *LIBL/QMFOKON 33)
     A  34                                  ERRMSGID(RMK0380 *LIBL/QMFOKON 34)
     A  35                                  ERRMSGID(RMK0430 *LIBL/QMFOKON 35)
7.08 A  36                                  ERRMSG('Systemkunde kan ikke -
7.08 A                                      slettes' 36)
     A                                  5  2'Kundenavn . . .'
     A            RKNAVN    R        O  5 18
     A                                  6  2'Adresse . . . .'
     A            RKGATE    R        O  6 18
     A                                  7  2'Adresse 2 . . .'
     A            RKADR2    R        O  7 18
     A                                  8  2'Postadresse . .'
     A            RKSTED    R        O  8 18
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R K1WIN
     A*%%TS  SD  20020808  150157  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopier kunde '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde . . . . :'
     A            K1KUND    R        B  4 18REFFLD(RKKUND)
     A  31                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 31)
     A  37                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4 26'Alfa.:'
     A            K1ALFA    R        B  4 33REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kundenavn . . :'
     A            K1NAVN    R        B  5 18REFFLD(RKNAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      DSPATR(PC)
     A                                      OVRDTA
     A                                  6  2'Adresse . . . :'
     A            K1GATE    R        B  6 18REFFLD(RKGATE)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7  2'Adresse 2 . . :'
     A            K1ADR2    R        B  7 18REFFLD(RKADR2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Postnummer. . :'
     A            K1PONR    R        B  8 18REFFLD(RKPONR)
     A  32                                  ERRMSGID(RM01000 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Postadresse . :'
     A            K1STED    R        B  9 18REFFLD(RKSTED)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Telefon . . . :'
     A            K1TLFN    R        B 10 18REFFLD(RKTLFN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 30'Fax. :'
     A            K1TLFX    R        B 10 37REFFLD(RKTLFX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Betalingsbet. :'
     A            K1BETB    R        B 11 18REFFLD(RKBETB)
     A  33                                  ERRMSGID(RM01030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 24'Kreditgrense. :'
     A            K1GRNS    R        B 13 40REFFLD(RKGRNS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Kundekategori :'
     A            K1KATG    R        B 12 18REFFLD(RKKATG)
     A  35                                  ERRMSGID(RM01110 *LIBL/QMFOKON 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Rabattkategori:'
     A            K1RABK    R        B 13 18REFFLD(RKRABK)
     A  39                                  ERRMSGID(RM01060 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 24'Betalingsmåte :'
     A            K1BETM    R        B 11 40REFFLD(RKBETM)
     A  34                                  ERRMSGID(RM01020 *LIBL/QMFOKON 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 24'Selger. . . . :'
     A            K1SLGR    R        B 12 40REFFLD(RKSLGR)
     A  36                                  ERRMSGID(RM01090 *LIBL/QMFOKON 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Påslagstab. . :'
     A            K1PTAB    R        B 14 18REFFLD(RKPTAB)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14 24'Spes.F/L :'
     A            K1SPFA    R        B 14 35REFFLD(RKSPFA)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            K1SPL1    R        B 14 37REFFLD(RKSPL1)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            K1SPL2    R        B 14 39REFFLD(RKSPL2)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            K1SPL3    R        B 14 41REFFLD(RKSPL3)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            K1SPL4    R        B 14 43REFFLD(RKSPL4)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            K1SPL5    R        B 14 45REFFLD(RKSPL5)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
