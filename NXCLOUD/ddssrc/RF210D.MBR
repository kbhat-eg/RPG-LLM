     A*%%TS  SD  20060913  161816  KOB550      REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A* Ref.   Dato  Sign Endringsbeskrivelse
7.xx A*       240216 bsa  Endringer relatert til ny GUI
Q.02 A*       280319 bsa  Quick wins - kopiering av kontaktpersoner
7.02 A*       260619 bsa  Feilmelding hvis forsøker opprette i feil intervall
     A*
8.01 A*K0000  170921 bsa  Søkemulighet på rolle.
8.03 A*800    160223 ask  Endret skjermstørrelse
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A*                                     DSPSIZ(24 80 *DS3)
8.03 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050920  174140  KOB540      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
Q.02 A*                                      VALUES(2 3 4 5 6)
7.02 A                                      VALUES(2 3 4 5 6 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1KPNR    R        O  8  4REFFLD(RUKPNR)
     A            B1ENAV    R   25   O  8 11REFFLD(RUENAV)
     A            B1FNAV    R   25   O  8 37REFFLD(RUFNAV)
     A            B1KROL    R   -2   O  8 63REFFLD(RUKROL)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20060913  161816  KOB550      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontaktpersoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
Q.02 A                                  6  2'2=Endre  3=Kopier  4=Slett  -
Q.02 A                                      5=Vis  6=Skriv kort'
     A                                      COLOR(BLU)
7.02 A  41                              6 52'7=Tilganger'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'K.pers'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Etternavn          '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 37'Fornavn            '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Rolle'
7.xx A                                      COLOR(WHT)
7.xx A**                                7  2'V K.pers. Etternavn               -
7.xx A**                                      Fornavn                   Rolle'
7.xx A**                                    COLOR(WHT)
     A  70                              4  2'Kunde:'
     A N70                              4  2'Levr.:'
     A                                      COLOR(WHT)
     A            B2KUND    R        O  4  9REFFLD(RKKUND)
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A          R B2CMD
     A*%%TS  SD  20040831  092353  GRO         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F6=Opprett  F5=Forn-
     A                                      y'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20050531  160319  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontaktperson'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontaktpersonnummer. . . . .'
     A            C1KPNR    R        B  5 31REFFLD(RUKPNR)
     A  31                                  ERRMSGID(MMR0010 *LIBL/QMFUMAL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20050531  153404  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kontaktpersonnummer finnes'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'fra før'
     A          R C2BLD
     A*%%TS  SD  20050920  180455  KOB540      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA05
7.02 A  41                                  CA07
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontaktpersoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A  70                              4  2'Kunde. . . . .'
     A N70                              4  2'Leverandør . .'
     A            C2KUND    R        O  4 17REFFLD(RUKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV        30A  O  4 24COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kontaktpersnr.'
     A            C2KPNR    R        O  6 17REFFLD(RUKPNR)
     A                                  8  2'Etternavn. . .'
     A            C2ENAV    R        B  8 17REFFLD(RUENAV)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSG('Etternavn må utfylles' 31)
     A                                  7  2'Fornavn. . . .'
     A            C2FNAV    R        B  7 17REFFLD(RUFNAV)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  32                                  ERRMSG('Fornavn må utfylles' 31)
     A                                  9  2'Personnr.. . .'
     A            C2PENR    R        B  9 17REFFLD(RUPENR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
8.01 A                                  9 37'Rolle. . . . +'
     A            C2KROL    R        B  9 52REFFLD(RUKROL)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                 10  2'Tlf. arbeid. .'
     A            C2KTLF    R        B 10 17REFFLD(RUKTLF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 10 37'Tlf. privat. .'
     A            C2PTLF    R        B 10 52REFFLD(RUPTLF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 11  2'Fax. arbeid. .'
     A            C2KFAX    R        B 11 17REFFLD(RUKFAX)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 11 37'Fax. privat. .'
     A            C2PFAX    R        B 11 52REFFLD(RUPFAX)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                 12  2'Mobiltelefon .'
     A            C2MTLF    R        B 12 17REFFLD(RUMTLF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 13  2'Status . . . .'
     A            C2STAT    R        B 13 17REFFLD(RUSTAT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 14  2'E-mail adresse'
     A            C2MAIL    R        B 14 17REFFLD(RUMAIL)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                 21  2'Sist endret dato:'
     A            C2EDAT         6Y 0O 21 20EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 21 33'Endret av:'
     A            C2EUSR    R        O 21 44REFFLD(RUEUSR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring  F5=Ko-
     A                                      rt'
     A                                      COLOR(BLU)
     A          R V2MSG
     A*%%TS  SD  20050926  003445  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 8 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Kontaktperson med samme navn og pe-
     A                                      rson-'
     A                                  3  2'nummer finnes fra før med nr.:'
     A            V2KPNR    R        O  3 33REFFLD(RUKPNR)
     A                                      DSPATR(UL)
     A  71                              4  2'Kundenr.'
     A  71        V2KUND    R        O  4 11REFFLD(RUKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  71        V2KTXT        23A  O  4 18OVRDTA
     A  72                              5  2'Lever.'
     A  72        V2LEVR    R        O  5 11REFFLD(RULEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  72        V2LTXT        23A  O  5 18OVRDTA
     A                                  6  2'Trykk enter for å godta'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050531  165910  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 12 60)
     A                                      CA03
     A                                      CA12
     A            D1KTSQ    R        H      REFFLD(RUKTSQ)
     A                                  2  2' Slette kontaktperson '
     A                                      DSPATR(RI)
     A                                  4  2'Kontaktperson. . .'
     A            D1KPNR    R        O  4 21REFFLD(RUKUND)
     A            D1ENAV        30A  O  4 28
     A  75                              6  2'Kan ikke slettes.'
     A                                      COLOR(WHT)
     A  75                              7  2'Kontaktperson både i kunde- og lev-
     A                                      erandørregister.'
     A                                      COLOR(WHT)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            D1FNAV    R        O  5 28REFFLD(RFRFR/RUFNAV *LIBL/RFRF)
      *
Q.02 A          R K1WIN
Q.02 A*%%TS  SD  19990112  150031  HKO         REL-V4R2M0  5769-PW1
Q.02 A                                      WINDOW(*DFT 15 56)
Q.02 A                                      CA03
Q.02 A                                      CA12
Q.02 A                                      PUTOVR
Q.02 A            K1KPNR    R        H      REFFLD(RUKPNR)
Q.02 A                                  2  2' Kopiere til: '
Q.02 A                                      DSPATR(RI)
Q.02 A                                  4  2'Etternavn . . .'
Q.02 A            K1ENAV    R        B  4 21REFFLD(RUENAV)
Q.02 A  31                                  ERRMSG('Navn må fylles ut' 31)
Q.02 A                                      COLOR(YLW)
Q.02 A                                      CHECK(LC)
Q.02 A                                      OVRDTA
Q.02 A                                  5  2'Fornavn . . . .'
Q.02 A            K1FNAV    R        B  5 21REFFLD(RUFNAV)
Q.02 A                                      COLOR(YLW)
Q.02 A                                      CHECK(LC)
Q.02 A                                      OVRDTA
Q.02 A                                  7  2' Kopiere fra: '
Q.02 A                                      DSPATR(RI)
Q.02 A                                  8  2'Etternavn. . . . .'
Q.02 A            RUENAV    R        O  8 21OVRDTA
Q.02 A                                  9  2'Fornavn. . . . . .'
Q.02 A            RUFNAV    R        O  9 21OVRDTA
Q.02 A                                 14  2'F3=Gå tilbake'
Q.02 A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
