     A*%%TS  SD  20120913  134057  BOF620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO312D
     A* Beskrivelse . . : Kasseoppgjør rapport hode, vis
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050906 bø   Nytt program
7.xx A* 7.xx  260516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20120913  122223  BOF620      REL-V7R1M0  5770-WDS
     A            B1VALG         1Y 0B  8  2COLOR(YLW)
     A                                      VALUES(0 5 7)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A                                      EDTCDE(Z)
     A            B1TELL    R        O  8  5REFFLD(BOTRST/BRTELL *LIBL/BOTRST)
     A            B1STRT    R        O  8 13REFFLD(BOTRST/BRSTRT *LIBL/BOTRST)
     A                                      EDTCDE(L)
     A            B1SLUT    R        O  8 25REFFLD(BOTRST/BRSLUT *LIBL/BOTRST)
     A                                      EDTCDE(L)
     A            B1BERG    R        O  8 37REFFLD(BOTRST/BRBERG *LIBL/BOTRST)
     A                                      EDTCDE(L)
     A            B1GODK         1A  O  8 74
     A            B1ODAT         6Y 0O  8 62EDTWRD('  .  .  ')
     A            B1DIFF    R        O  8 49REFFLD(BOTRST/BRDIFF *LIBL/BOTRST)
     A                                      EDTCDE(L)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20120913  134057  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis rapport hode'
     A                                      COLOR(WHT)
7.xx A                                  2 38'BO313B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Telling'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 49'Differanse'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 63'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'5=Vis  7=Detaljer'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Telling'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Start beh.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 25'Opptalt beh.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 38'Bereg.beh.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Differanse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63' Dato '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 73'ok'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V  Telling Start beh.  Opptalt beh-
7.xx A**                                    . beholdning Differanse    Dato    -
7.xx A**                                      ok    '
7.xx A**                                    COLOR(WHT)
7.xx A            B2TELL    R        B  5  5REFFLD(BOTRST/BRTELL *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2DIFF    R        B  5 49REFFLD(BOTRST/BRDIFF *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
7.xx A            B2ODAT         6Y 0B  5 62COLOR(YLW)
     A  31                                  ERRMSGID(BM00500 *LIBL/QMFSHOP 31)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
7.xx A**                                5 38'Beregnet'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20120913  094430  BOF620      REL-V7R1M0  5770-WDS
     A                                 23  2'F1=Filter   F3=Gå Tilbake   F5=For-
     A                                      ny                             '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20120913  122919  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis rapport hode '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Telling  . . . . . . . . . .'
     A            C1TELL    R        O  5 31REFFLD(BOTRST/BRTELL *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2'Start beholdning . . . . . .'
     A            C2STRT    R        O  7 31REFFLD(BOTRST/BRSTRT *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  8  2'Slutt beholdning . . . . . .'
     A            C2SLUT    R        O  8 31REFFLD(BOTRST/BRSLUT *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  2'Beregnet beholdning  . . . .'
     A            C2BERG    R        O  9 31REFFLD(BOTRST/BRBERG *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  2'Differanse . . . . . . . . .'
     A            C2DIFF    R        O 10 31REFFLD(BOTRST/BRDIFF *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  2'Godkjent . . . . . . . . . .'
     A            C2GODK    R        O 11 31REFFLD(BOTRST/BRGODK *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 34'0=Nei  1=Ja'
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2OTIM         6Y 0O 18 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ETIM         6Y 0O 19 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(BOTRST/BREUSR *LIBL/BOTRST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 32'F8=Godkjenn'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20120913  095851  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 11 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
     A                                  4  2'Differanse lik 0 er med   .'
     A            F1DIFF         1Y 0B  4 30COLOR(YLW)
     A                                      OVRDTA
     A                                  4 34'0=Ja  1=Nei'
     A                                  5  2'Ta med godkjente  . . . . .'
     A            F1GDOK         1Y 0B  5 30COLOR(YLW)
     A                                      OVRDTA
     A                                  5 34'0=Ja  1=Nei'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
