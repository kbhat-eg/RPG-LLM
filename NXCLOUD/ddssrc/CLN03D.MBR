     A*%%TS  SD  20160927  224228  ASP_G0XA    REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : EHANDEL
     A* Format. . . . . : CLN03D
     A* Beskrivelse . . : VEDLIKEHOLD AV HOVEDLEVERANDØRER
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130916 ASK  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20160906  105907  ASP_G0XA    REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1NLKA         4Y 0O  8  5
     A            B1NNTX        30A  O  8 10
     A            B1NLLV         6Y 0O  8 41
     A            B1NLTX        30A  O  8 48
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160913  154019  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Etablering av hovedleverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Kode Nobbklasifisering         -
     A                                           Leverandør'
     A                                      COLOR(WHT)
     A            B2NLKA         4Y 0B  7  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20160927  224228  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F4=Søk  F5=Forny  F-
     A                                      6=Opprett  F8=Nobbkategorier F9=Var-
     A                                      egrupper'
     A                                      COLOR(BLU)
     A                                 24  6' '
     A                                 24  8' '
     A          R C1BLD
     A*%%TS  SD  20160913  154019  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Etablering av hovedleverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Nobb kategori. . .'
     A            C1NLKA         4Y 0B  5 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1NNTX        30A  O  5 29COLOR(GRN)
     A                                      OVRDTA
     A                                  6  2'Leverandør . . . .'
     A            C1NLLV         6Y 0B  6 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1NLTX        30A  O  6 29
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Søk'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20160913  155145  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandels løsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Etablering av hovedleverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Nobbkategori . . .'
     A            C2NLKA         4Y 0O  5 22COLOR(YLW)
     A                                      OVRDTA
     A            C2NNTX        30A  O  5 29OVRDTA
     A                                      COLOR(GRN)
     A                                  6  2'Leverandør . . . .'
     A            C2NLLV         6Y 0B  6 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2NLTX        30A  O  6 29
     A*
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2EODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EEUS    R        O 20 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Søking'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20160913  155145  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Denne leverandøren er valgt'
     A                                  3  2'som hovedleverandør på denne'
     A                                  4  2'Nobbkategorien fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20160913  154019  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette hovedleverandør '
     A                                      DSPATR(RI)
     A                                  4  2'Nobbkategori '
     A            D1NLKA         4Y 0O  4 17
     A            D1NNTX        30A  O  4 24
     A                                  5  2'Leverandør'
     A            D1NLLV         6Y 0O  5 17EDTCDE(4)
     A            D1NLTX        30   O  5 24
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
