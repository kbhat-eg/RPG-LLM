     A*%%TS  SD  20040622  144045  AMD         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA028D
     A* Beskrivelse . . : Bibliotekslister, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       110504 AMD  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040621  153040  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  6  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1PLAS    R        O  6  6REFFLD(ALPLAS)
     A            B1BIBL    R        O  6 12REFFLD(ALBIBL)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040622  144045  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Vedlikehold av bibliotekslister'
     A                                      COLOR(WHT)
     A                                  3  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'V  Plass  Bibliotek    Bruker'
     A                                      COLOR(WHT)
     A            B2PLAS    R        B  5  6REFFLD(ALPLAS)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2BIBL    R        B  5 12REFFLD(ALBIBL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2USER    R        O  5 25REFFLD(ALUSER)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20040621  160452  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 18 35)
     A                                      OVERLAY
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                 17  2'F3=Tilbake  F5=Forny  F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040622  144045  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Vedlikehold av bibliotekslister'
     A                                      COLOR(WHT)
     A                                  5  2'Plassering . . .'
     A            C1PLAS    R        B  5 19REFFLD(ALPLAS)
     A  31                                  ERRMSGID(AML0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040511  141000  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bibliotek finnes fra før   '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040622  144045  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Vedlikehold av bibliotekslister'
     A                                      COLOR(WHT)
     A                                  5  2'Plassering . .'
     A            C1PLAS    R        O  5 17REFFLD(ALPLAS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Bibliotek. . .'
     A            C2BIBL    R        B  7 17REFFLD(ALBIBL)
     A  31                                  ERRMSGID(AML0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17  2'F3=Tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040621  161149  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bibliotek    '
     A                                      DSPATR(RI)
     A                                  4  2'Plassering . . . .'
     A            D1PLAS    R        O  4 21REFFLD(ALPLAS)
     A                                  6  2'Bibliotek. . . . .'
     A            ALBIBL    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040621  161149  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Plassering . . . .'
     A            K1PLAS    R        B  4 21REFFLD(ALPLAS)
     A  31                                  ERRMSGID(AML0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AML0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Plassering . . . .'
     A            ALPLAS    R        O  8 21OVRDTA
     A                                  9  2'Bibliotek. . . . .'
     A            ALBIBL    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19940113  133204  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 18 35)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A          R ASUME                     ASSUME
     A                                 24 79' '
