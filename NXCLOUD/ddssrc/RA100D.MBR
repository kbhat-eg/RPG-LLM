     A*%%TS  SD  20090920  221129  ASK610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA100D
     A* Beskrivelse . . : KODERtgruppe, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.01  060819  nho  Nytt valg for Returkoder
8.01 A* 800  021221   ask  Reorganisert skjermbilde
8.01 A*                    og lagt inn koder for kredittgrenser
8.02 A* 800  090322   ask  Lagt inn koder for kontaktpersonroller
8.03 A* 800  061022   ask  Lagt inn koder eksternt lager
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20090920  221129  ASK610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koder'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RA100A2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6 02'01. Bilagstekst'
     A                                  7 02'02. Betalingsmåte'
     A                                  8 02'03. Betalingsbetingelse'
     A                                  9 02'04. Faste konti'
     A                                 10 02'05. MVA-koder'
     A                                 11 02'06. Rabattkategori'
     A                                 12 02'07. Avdeling'
     A                                 13 02'08. Distrikt'
     A                                 14 02'09. Selger'
     A                                 15 02'10. Lager'
     A                                 16 02'11. Kundekategori'
     A                                 17 02'12. Land'
     A                                 18 02'13. Fordelingsnøkkel'
     A                                 19 02'14. Leverandørkategori'
     A                                 20  2'15. Mengdekoder'
     A                                  6 33'16. Valutakurser'
     A                                  7 33'17. Forsendelsesmåte'
     A                                  8 33'18. Leveringsbetingelse'
     A                                  9 33'19. Kontoklasse'
     A                                 10 33'20. Kontogruppe'
     A                                 11 33'21. Aktivitet'
     A                                 12 33'22. Speditør'
     A                                 13 33'23. Virksomhet'
     A                                 14 33'24. Lagertransaksjonskoder'
     A                                 15 33'25. Kjørerute'
     A                                 16 33'26. Produktansvarlig'
     A                                 17 33'27. Kostnadsbærergruppe'
     A                                 18 33'28. Kostnadsbærer'
     A                                 19 33'29. Mobiloperatør'
     A                                 20 33'30. Prisgrupper  '
     A                                  6 66'31. Forsendelsesmåte bestilling'
7.01 A                                  7 66'32. Returkoder'
8.01 A                                  8 66'33. Kredittgrenser Bisnode'
8.02 A                                  9 66'34. Roller på kontaktpersoner'
8.03 A                                 10 66'37. Eksterne lager'
     A                                 22 19'Valg:'
     A            A2VALG         2Y 0B 22 25DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      CHECK(RZ)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
