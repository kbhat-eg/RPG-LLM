     A*%%TS  SD  20070609  104844  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RR100D
     A* Beskrivelse . . : Resultatrapport, vedlikehold rapporthode
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       120516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20031208  152139  HKO         REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 6 7 8 9)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1ALME    R        O  9  4REFFLD(RMALME)
7.xx A            B1NAVN    R        O  9  9REFFLD(RMNAVN)
7.xx A            B1KPRS    R   -1   O  9 40REFFLD(RMKPRS)
7.xx A            B1KMOB    R   -4   O  9 70REFFLD(RMKMOB)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20031210  125350  HKO         REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av almenning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Alm.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'6=Eiendommer  7=Eindomsknytning  8-
     A                                      =Vareknytning  9=Bruksrett-typer'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Alm.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Navn                          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Bestyrer/kontaktperson'           -
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'Mobil.nr'
7.xx A**                                7  2'V Alm. Navn                       -
7.xx A**                                        Bestyrer/kontaktperson        M-
7.xx A**                                    obil.nr'
     A                                      COLOR(WHT)
7.xx A            B2ALME    R        B  5  4REFFLD(RMALME)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NAVN    R        B  5  9REFFLD(RMNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20031210  120209  HKO         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20031208  161930  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av almenning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Almenning. . . . . . . . . .'
     A            C1ALME    R        B  5 31REFFLD(RMALME)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20031208  161701  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 31)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Almenning finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070609  104844  KOB550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av almenning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Almenning . . . .'
     A            C1ALME    R        O  5 20REFFLD(RMALME)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Navn. . . . . . .'
     A            C2NAVN    R        B  7 20REFFLD(RMNAVN)
     A  31                                  ERRMSGID(RMM0100 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Adresse 1.. . . .'
     A            C2ADR1    R        B  8 20REFFLD(RMADR1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  9  2'Adresse 2.. . . .'
     A            C2ADR2    R        B  9 20REFFLD(RMADR2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A                                 10  2'Postnummer. . . +'
     A            C2PONR    R        B 10 20REFFLD(RMPONR)
     A  32                                  ERRMSGID(RMM0200 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2STED        25A  O 10 25COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Telefon . . . . .'
     A            C2TLFN    R        B 11 20REFFLD(RMTLFN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Faks. . . . . . .'
     A            C2FAKS    R        B 12 20REFFLD(RMFAKS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'E-mail adresse. .'
     A            C2MAIL    R        B 13 20REFFLD(RMMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Hjemmeside/URL. .'
     A            C2URLA    R        B 14 20REFFLD(RMURLA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Beløpsgrense. . .'
     A            C2BGRN    R        B 15 20REFFLD(RMBGRN)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 17  2'Bestyrer/kontaktperson:'
     A                                      COLOR(WHT)
     A                                 18  2'Navn. . . . . . .'
     A            C2KPRS    R        B 18 20REFFLD(RMKPRS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 19  2'Telefon . . . . .'
     A            C2KTLF    R        B 19 20REFFLD(RMKTLF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Mobiltelefon. . .'
     A            C2KMOB    R        B 20 20REFFLD(RMKMOB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21  2'E-mail adresse. .'
     A            C2KMAI    R        B 21 20REFFLD(RMKMAI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C2EDAT         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C2EUSR    R        O 23 71REFFLD(RMEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20031208  162339  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette almenning '
     A                                      DSPATR(RI)
     A                                  4  2'Almenning. . . . .'
     A            D1ALME    R        O  4 21REFFLD(RMALME)
     A                                  5  2'Navn . . . . . . .'
     A            RMNAVN    R        O  5 21
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20031208  162339  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Almenning. . . . .'
     A            K1ALME    R        B  4 21REFFLD(RMALME)
     A  31                                  ERRMSGID(RMM0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMM0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Almenning. . . . .'
     A            RMALME    R        O  8 21OVRDTA
     A                                  9  2'Navn . . . . . . .'
     A            RMNAVN    R        O  9 21OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
