     A*%%TS  SD  20050914  205243  HKO530      REL-V5R3M0  5722-WDS
     A*
7.xx A*      280716 bsa Endringer relatert til ny GUI
8.01 A*      220324 bof Tillate brede skjermbilder
     A*
     A*%%EC
8.01 A*                                      DSPSIZ(24 80 *DS3)
8.01 A                                      DSPSIZ(24 80 *DS3 -
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990824  173433  HKO         REL-V4R2M0  5769-PW1
     A            B1VARE    R        H      REFFLD(JVVARE)
     A            B1NOBB    R        H      REFFLD(JVNOBB)
     A            B1LVAR    R        H      REFFLD(JVLVAR)
7.xx A            B1VALG    R        B 10  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1TEK1    R        O 10  4REFFLD(JVTEK1)
7.xx A            B1NYPO        15A  O 10 40
7.xx A            B1NAVN        17A  O 10 56
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050914  205243  HKO530      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0010)
7.xx A                                      SFLPAG(0009)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF23
     A                                      KEEP
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge vare '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Varegruppe . . +'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  3 19REFFLD(JVOGRP)
     A  31                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  3 22'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  3 24REFFLD(JVHGRP)
     A  32                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  3 27'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  3 29REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4  2'Søketekst. . . .'
     A                                      COLOR(BLU)
     A            B2STEK    R        B  4 19REFFLD(JSSTEK)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Frisøk . . . . .'
     A                                      COLOR(BLU)
     A            B2FTEK    R        B  5 19REFFLD(JSSTEK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  4'Varetekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 35'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 44'Nobbnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 53'Lev.varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'1=Velg  5=Vis'
     A                                      COLOR(BLU)
7.xx A**                                8  2'V Varetekst                      V-
7.xx A**                                    arenr   Nobbnr   Lev. varenr'
7.xx A**                                    COLOR(WHT)
7.xx A            B2TEK1    R   -5   B  7  4REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2VARE         8Y 0B  7 35COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2NOBB    R        B  7 44REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LVAR    R        B  7 53REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Varetekst                  '
7.xx A                                      COLOR(WHT)
7.xx A  80                              9 40'Varenr'
     A  80                                  COLOR(WHT)
7.xx A  81                              9 40'Nobbnr'
     A  81                                  COLOR(WHT)
7.xx A  82                              9 40'Lev. varenr'
     A  82                                  COLOR(WHT)
7.xx A                                  9 56'Leverandør'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020916  122514  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 21 73)
     A                                 20  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny                   F23=Bytt inf-
     A                                      o.'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
