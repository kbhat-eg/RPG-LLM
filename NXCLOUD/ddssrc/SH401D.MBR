     A*%%TS  SD  20081030  144557  AMD560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : SH401D
     A* Beskrivelse . . : Spørring på fakturaregister
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       180516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20001031  211559  KOB         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B 10  2REFFLD(LÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('5' '6' '7' '9' ' ')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A            B1LDOR    R        H      REFFLD(SHLDOR)
     A            B1BINR    R        O 10  4REFFLD(SHBINR)
6.NU A            B1NUMM    R        O 10 13REFFLD(SHNUMM)
6.NU A            B1SUFF    R        O 10 22REFFLD(SHSUFF)
6.NU A            B1OTYP    R        O 10 25REFFLD(SHOTYP)
6.NU A            B1FKDA         6Y 0O 10 28EDTCDE(Z)
6.NU A            B1NAVN    R   -2   O 10 35REFFLD(SHNAVN)
6.NU A            B1FSUM    R        O 10 64REFFLD(SHFSUM)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080324  233208  KOB560      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
7.02 A*                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
7.02 A                                      CF21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på fakturaregister'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SH401B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.02 A            B2FSOK        35A  B  4 12COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A  22                                  DSPATR(PC)
7.02 A            B2FSO2        10A  O  4 48COLOR(WHT)
7.02 A                                      OVRDTA
7.02 A                                  4  4'Frisøk'
7.02 A                                      COLOR(WHT)
7.02 A                                  4 59'(F21)'
7.02 A                                      COLOR(WHT)
     A                                  4 65'År . . .'
     A                                      COLOR(BLU)
     A            B2AARR    R        O  4 74REFFLD(SHAARR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  4'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'Bestill'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 25'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 28'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 35'Leverandør'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis faktura  6=Skriv fakturakopi-
     A                                        7=Vis ordrehode'
     A                                      COLOR(BLU)
     A                                  5 65'Ord.type'
     A                                      COLOR(BLU)
     A            B2OTY1    R        O  5 74REFFLD(SHOTYP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  8  2'9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  6 65'Lev. . .'
     A                                      COLOR(BLU)
     A            B2LEVR    R        O  6 74REFFLD(SHLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 13'Bestil '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 25'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 28' Dato '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 35'Leverandør      '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 64'Fakturasum'
7.xx A**                                9  2'V Faktnr   Bestil      OT  Dato  L-
7.xx A**                                    everandør                  Fakturas-
7.xx A**                                    um'
     A                                      COLOR(WHT)
7.xx A            B2BINR    R        B  6  4REFFLD(SHBINR)
     A                                      COLOR(YLW)
     A*  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NUMM    R        B  6 13REFFLD(SHNUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OTYP    R        B  6 25REFFLD(SHOTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2FKDA         6Y 0B  6 28COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2LEVA        20A  B  6 35COLOR(YLW)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  19980108  144330  HKO         REL-V3R7M0  5716-PW1
     A                                 23  2'F3=Gå tilbake  F5=Forny'
     A                                      COLOR(BLU)
