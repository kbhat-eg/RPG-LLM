     A*%%TS  SD  20041022  152011  AMD         REL-V5R2M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041022  152011  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1KUND    R        O  9  4REFFLD(FLKUND)
     A            B1KPRO    R        O  9 11REFFLD(FLKPRO)
     A            B1NAVN        23A  O  9 18
     A            B1EPRO    R        O  9 42REFFLD(FLEPRO)
     A            B1TDAT         6Y 0O  9 58EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041022  152011  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge kundeprosjekt '
     A                                      DSPATR(RI)
     A                                  4  2'Frisøk. . . . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  4 18COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 18'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 18'Navn              '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 42'Ekst.prosj.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 58'Til-dato'
     A                                      COLOR(WHT)
7.xx A            B2KUND    R        B  6  4REFFLD(FLKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KPRO    R        B  6 11REFFLD(FLKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NAVN        23A  B  6 18COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990412  104455  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 19 66)
     A                                 18  2'F3=Gå tilbake   F5=Forny   F9=Gjen-
     A                                      ta søk'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
