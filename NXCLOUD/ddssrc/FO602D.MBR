     A*%%TS  SD  20111220  134945  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A*       160216 bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*K0000  200723 bsa  Endringer i plukking av ordre.
8.02 A*K0000  110424 bsa  Styring skjer i RPG
     A*
     A*
     A*-----------------------------------------------------------------
7.02 A                                      DSPSIZ(24 80 *DS3 -
7.02 A                                             27 132 *WIDE)
7.02 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R D0BLD
     A*%%TS  SD  20060818  145631  AMD540      REL-V5R3M0  5722-WDS
     A                                      CA03
8.02 A* 34                                  CA06
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(4 36 17 40)
     A                                  2  2'Utplukk/Utskrift av ordrenummer'
     A                                      COLOR(WHT)
     A                                  4  2'Fra ordretype .'
     A            D0FOTY    R        B  4 18REFFLD(FOOTYP)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                  5  2'Til ordretype .'
     A            D0TOTY    R        B  5 18REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A  32                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMA5050 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
7.xx A                                  5 24'Blank=Utskrift'
     A                                  7  2'Seleksjon . . .'
     A            D0SVAR    R        B  7 18REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1 2 3 4 5 6)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  7 24'1=fra/til'
     A                                  8 24'2=sekvens'
     A                                  9 24'3=tilfeldig'
     A                                 10 24'4=andre valg'
     A                                 11 24'5=alt. fakt.'
     A                                 12 24'6=Ekstern fil'
     A                                 13  2'Droppe faste. .'
     A                                 14  2'tekstlinjer . .'
     A            D0PTXT    R        B 14 18REFFLD(FKPTXT)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 14 24'1=Ja,0=Nei'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.02 A* 34                             16 17'F6=Fakturer samlet'
8.02 A*                                     COLOR(BLU)
     A*
     A          R D1BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(10 36 12 40)
     A                                  2  2'Ordrenummer      fra      til'
     A                                      DSPATR(UL)
     A            D1FONR    R        B  3 16REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(FM00020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A            D1TONR    R        B  3 25REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  34                                  ERRMSGID(FM00030 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R D2BLD
     A*%%TS  SD  20111220  134945  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(3 33 19 43)
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  2  2'Seleksjon vha sekvens'
     A                                      COLOR(WHT)
     A                                  4  2'Sekvenser. . . . .'
     A            D2SEK1         2Y 0B  4 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(FM00040 *LIBL/QMFOFAK 31)
     A            D2SEK2         2Y 0B  4 25EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D2SEK3         2Y 0B  4 29EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D2SEK4         2Y 0B  4 33EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5  2'Ta med sekv. 00 ?'
     A            D2SEK0         1Y 0B  5 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 33'1=Ja,0=Nei'
     A                                  7  2'Valg på avdeling .'
     A            D2AVDE         4Y 0B  7 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8  2'Ta med avd. 0 ?. .'
     A            D2AVD0         1Y 0B  8 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8 33'1=Ja,0=Nei'
     A                                 10  2'T.o.m. ordredato .'
     A            D2BDAT         6Y 0B 10 21COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 32)
7.xx A                                      EDTWRD('  .  .  ')
     A                                 11  2'T.o.m. lev.dato  .'
     A            D2LDAT         6Y 0B 11 21COLOR(YLW)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
7.xx A                                      EDTWRD('  .  .  ')
     A                                 12  2'Beløpsgrense . . .'
     A            D2BELG         8Y 0B 12 21OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 13  2'Kode . . . . . . .'
     A            D2BELK         1A  B 13 21OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 13 33'U=Under'
     A                                 14 33'O=Over'
     A                                 15  2'Ordrenummer fra. .'
     A            D2FONR    R        B 15 21REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  34                                  ERRMSGID(FM00030 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                 16  2'Ordrenummer til. .'
     A            D2TONR    R        B 16 21REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R D3BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(10 36 12 40)
     A                                  2  2'Tilfeldige ordrenummer'
     A                                      COLOR(WHT)
     A            D3UONR    R        O  4  7REFFLD(FONUMM)
     A                                      OVRDTA
     A            D3USUF    R        O  4 16REFFLD(FOSUFF)
     A                                      OVRDTA
     A            D3IONR    R        B  6  7REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0040 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A            D3ISUF    R        B  6 16REFFLD(FOSUFF)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A                                  8  2'ENTER gir oppdatering og mulighet'
     A                                  9  2'for inntasting av flere nummer'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D4BLD
     A*%%TS  SD  20020503  125018  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(3 33 13 43)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Seleksjon andre valg '
     A                                      COLOR(WHT)
     A                                  4  2'Valg på avdeling .'
     A            D4AVDE         4Y 0B  4 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5  2'Ta med avd. 0 ?. .'
     A            D4AVD0         1Y 0B  5 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 33'1=Ja,0=Nei'
     A                                  7  2'Kundenummer. . . .'
     A            D4KUND         6Y 0B  7 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                  8  2'Kundeprosjekt. . .'
     A            D4KPRO         6Y 0B  8 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                 10  2'Infokode . . . . .'
     A            D4ITYP         2A  B 10 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R D5BLD
     A                                      WINDOW(*DFT 16 30)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Velg alt. fakturering'
     A                                      DSPATR(RI)
     A                                  4  2'1=Valg'
     A                                      COLOR(BLU)
     A                                  5  2'V'
     A                                      COLOR(WHT)
     A            D5ALTH        13A  O  5  5COLOR(WHT)
     A            D5VAL1         1A  B  6  2COLOR(YLW)
     A            D5ALT1        25A  O  6  5
     A            D5VAL2         1A  B  7  2COLOR(YLW)
     A            D5ALT2        25A  O  7  5
     A            D5VAL3         1A  B  8  2COLOR(YLW)
     A            D5ALT3        25A  O  8  5
     A            D5VAL4         1A  B  9  2COLOR(YLW)
     A            D5ALT4        25A  O  9  5
     A            D5VAL5         1A  B 10  2COLOR(YLW)
     A            D5ALT5        25A  O 10  5
     A            D5VAL6         1A  B 11  2COLOR(YLW)
     A            D5ALT6        25A  O 11  5
     A            D5VAL7         1A  B 12  2COLOR(YLW)
     A            D5ALT7        25A  O 12  5
     A            D5VAL8         1A  B 13  2COLOR(YLW)
     A            D5ALT8        25A  O 13  5
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D6BLD
     A*%%TS  SD  20060818  145631  AMD540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(3 33 13 43)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Generering fra ekstern fil'
     A                                      COLOR(WHT)
     A                                  5  2'Starte generering ? . . . . .'
     A            D6STRT         1Y 0B  5 32EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 34'1=Ja,0=Nei'
     A                                  7  2'Vis innhold i ekstern fil ? .'
     A            D6VISE         1Y 0B  7 32EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7 34'1=Ja,0=Nei'
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R E2BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(7 36 12 40)
     A                                  2  2'Utskrift av tilbud'
     A                                      COLOR(WHT)
     A                                  4  2'Alternativ'
     A            E2PALT    R        B  4 14REFFLD(FKPALT)
     A                                      VALUES(0 1 2 3)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 17'0=Standard forslag'
     A                                  5 17'1=Netto priser'
     A                                  6 17'2=Kun priser'
     A                                  7 17'3=Spesial/15'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E3BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(7 36 12 40)
     A                                  2  2'Utskrift av ordre/plukkliste'
     A                                      COLOR(WHT)
     A                                  4  2'Alternativ'
     A            E3PALT    R        B  4 13REFFLD(FKPALT)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 16'0=Ordrebekreftelse'
     A                                  5 16'1=Plukk-liste'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
