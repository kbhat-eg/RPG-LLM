     A*%%TS  SD  20020312  204646  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : FO642D
     A* Beskrivelse . . : Utskrift av kjøre-liste
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      CF03
     A                                      CF12
     A          R B1BLD
     A*%%TS  SD  20020312  204646  HKO         REL-V5R1M0  5722-WDS
     A                                      KEEP
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av kjøre-liste'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO642B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Utvalgskriterier:'
7.xx A                                  7  2'Ordre-type . . . . . . . . +'
     A            B1OTYP    R        B  7 31REFFLD(FOOTYP)
     A  31                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
7.xx A                                  7 42'(Blank er alle)'
7.xx A                                  9 31'Fra       Til'
     A                                      COLOR(WHT)
7.xx A                                 10  2'Avdeling . . . . . . . . . +'
     A            B1FAVD    R        B 10 31REFFLD(FOAVDE)
     A  32                                  ERRMSGID(FRA0720 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 10 39'+'
7.xx A            B1TAVD    R        B 10 41REFFLD(FOAVDE)
     A  33                                  ERRMSGID(FRA0720 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 11  2'Kjørerute. . . . . . . . . +'
     A            B1FKJO    R        B 11 31REFFLD(FOKJOR)
     A  34                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 11 39'+'
7.xx A            B1TKJO    R        B 11 41REFFLD(FOKJOR)
     A  35                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 12  2'Leveringsdato. . . . . . . .'
     A            B1FDAT         6Y 0B 12 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  36                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 36)
     A  38                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 38)
     A                                 12 44'Til dato. . '
7.xx A            B1TDAT         6Y 0B 12 57COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A                                 14  2'Utskriftsvariant . . . . . .'
     A            B1VARI         1Y 0B 14 31COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A                                 14 41'1 = Detaljert liste'
7.xx A                                 15 41'2 = Sammendrag'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
