     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO741D
     A* Beskrivelse . . : Vise bestillinger med feil ihht NEB
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230616 bsa  Nytt program
7.xx A* K000  230620 bsa  Takler brede skjermbilder
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  6  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LINE    R        O  6  4REFFLD(LDLINE)
     A            B1VARE    R   -7   O  6  9REFFLD(LDVARE)
     A            B1TEK1    R  -15   O  6 18REFFLD(LDTEK1)
     A            B1EANN    R        O  6 40REFFLD(LDEANN)
     A            B1VTYP         1A  O  6 55
     A            B1STAT         1A  O  6 57
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Linjer ikke NEB godkjent'
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  4  2'5=Vise'
     A                                      COLOR(BLU)
     A                                  5  4'Lnr'
     A                                      COLOR(WHT)
     A                                  5  9'Varenr'
     A                                      COLOR(WHT)
     A                                  5 18'Beskrivelse     '
     A                                      COLOR(WHT)
     A                                  5 40'GTIN nr'
     A                                      COLOR(WHT)
     A                                  5 54'VT'
     A                                      COLOR(WHT)
     A                                  5 57'St'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 61)
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R B1WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 52)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Linjestatus '
     A                                      DSPATR(RI)
     A                                  4  2'Bestilling .'
     A            C1NUMM    R        O  4 15REFFLD(LONUMM)
     A                                      OVRDTA
     A            C1SUFF    R        O  4 24REFFLD(LOSUFF)
     A                                      OVRDTA
     A                                  5  2'Varenr . . .'
     A            C1VARE    R        O  5 15REFFLD(LDVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Beskrivelse'
     A            C1TEK1    R        O  6 15REFFLD(LDTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'GTIN nummer'
     A            C1EANN    R        O  7 15REFFLD(LDEANN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'NOBB nummer'
     A            C1NOBB    R        O  8 15REFFLD(LDNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varetype . .'
     A            C1VTYP         1A  O  9 15
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Feilkode . .'
     A            C1ETXT    R        O 10 15REFFLD(LDTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
