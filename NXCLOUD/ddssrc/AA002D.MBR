     A*%%TS  SD  20040507  132326  AMD         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA002D
     A* Beskrivelse . . : Valg av Firma-nummer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       070504 AMD  Nytt program
7.xx A* 7.xx  020516 bsa  Justeringer basert på ny GUI og regler
7.01 A* K00   050520 bsa  Tillater brede skjermbilder
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040507  132326  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  5  3REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1FFIR    R        O  5  8REFFLD(AAFFIR)
     A            B1FNAV    R        O  5 15REFFLD(AAFNAV)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040507  132326  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  3  3'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  1  6'Firmanr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  1'Valg'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  6'Firmanr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 15'Firmanavn         '
     A                                      COLOR(WHT)
7.xx A            B2FFIR    R        B  2  8REFFLD(AAFFIR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(&W_ACLI &W_ACPO 20 52)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A            W_ACLI         2S 0P
     A            W_ACPO         3S 0P
     A          R ASUME                     ASSUME
     A                                 24 79' '
