     A*%%TS  SD  20031217  162658  RHO         REL-V5R2M0  5722-WDS
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL114D
     A* Beskrivelse . . : Avtaleregister
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  170117 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A          R A1WIN
     A*%%TS  SD  20031217  162658  RHO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 21 58)
     A                                      TEXT('Vedlikehold av avtaleregister-
     A                                      ')
     A                                      CA03
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      PRINT
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2 11'VEDLIKEHOLD AV AVTALEREGISTER - AS-
     A                                      TELE'
     A                                      COLOR(WHT)
     A                                  4  2'Filgruppe . . .:'
     A            A1FILG    R        B  4 19REFFLD(GCFILG)
     A N40                                  DSPATR(UL)
     A                                      COLOR(YLW)
     A  40                                  DSPATR(PR)
     A  32                                  ERRMSGID(RML0490 *LIBL/QMFOKON 32)
     A  23                                  ERRMSGID(GM00006 *LIBL/QMFRREM 23)
     A  23                                  DSPATR(PC)
     A                                  5  2'Firma . . . . .:'
     A            A1FIRM    R        B  5 19REFFLD(GCFIRM)
     A N40                                  DSPATR(UL)
     A                                      EDTCDE(Z)
     A  40                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            A1FNAV        25A  O  5 23
     A                                  6  2'Bankforbindelse:'
     A            A1BANK    R        B  6 19REFFLD(GCBANK)
     A  42 40                               DSPATR(PR)
     A N40                                  DSPATR(UL)
     A  21                                  DSPATR(PC)
     A                                      TEXT('Type bank')
     A                                      COLOR(YLW)
     A  21                                  ERRMSGID(GM00004 *LIBL/QMFRREM 21)
     A  22                                  ERRMSGID(GM00005 *LIBL/QMFRREM 22)
     A                                  7  2'Bankgironummer.:'
     A            A1BGIR    R        B  7 19REFFLD(GCBGIR)
     A                                      EDTCDE(Z)
     A  42                                  DSPATR(PR)
     A N40                                  DSPATR(UL)
     A                                      CHECK(RZ)
     A  33
     AO 34
     AO 35                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(RML0520 *LIBL/QMFOKON 34)
     A  33                                  ERRMSGID(RML0500 *LIBL/QMFOKON 33)
     A  35                                  ERRMSGID(RML0550 *LIBL/QMFOKON 35)
     A                                  7 31'Postgironummer:'
     A            A1PGIR    R        B  7 47REFFLD(GCPGIR)
     A                                      EDTCDE(Z)
     A                                      TEXT('Postgironummer......')
     A  34                                  ERRMSGID(RML0520 *LIBL/QMFOKON 34)
     A  36                                  ERRMSGID(RML0580 *LIBL/QMFOKON 36)
     A  35                                  ERRMSGID(RML0550 *LIBL/QMFOKON 35)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(UL)
     A                                      CHECK(RZ)
     A  33                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9  2'S O N E  I N F O R M A S J O N:'
     A                                      DSPATR(UL)
     A                                 10  2'Sonekode. . . .:'
     A            A1SKOD    R        B 10 19REFFLD(GCSKOD)
     A N40                                  DSPATR(UL)
     A                                      TEXT('Sondekode')
     A                                      COLOR(YLW)
     A  20                                  ERRMSGID(GM00002 *LIBL/QMFRREM 20)
     A  20                                  DSPATR(PC)
     A  40                                  DSPATR(PR)
7.xx A**                               10 32'|||'
7.xx A**                                    COLOR(RED)
     A                                 10 36'Må være likt for alle'
     A                                 11  2'Sonekontonummer:'
     A            A1SKTO    R        B 11 19REFFLD(GCSKTO)
     A                                      EDTCDE(Z)
     A N40                                  DSPATR(UL)
     A                                      CHECK(RZ)
     A  37                                  DSPATR(PC)
     A                                      TEXT('Sonektonummer......')
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(GM00000 *LIBL/QMFRREM 37)
     A  40                                  DSPATR(PR)
7.xx A**                               11 32'|||'
7.xx A**                                    COLOR(RED)
     A                                 11 36'avdelingene i sonen. '
     A                                 12  2'Sonelokasjon. .:'
     A            A1SLOK    R        B 12 19REFFLD(GCSLOK)
     A N40                                  DSPATR(UL)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A  39                                  ERRMSGID(GM00003 *LIBL/QMFRREM 39)
     A  39                                  DSPATR(PC)
     A  40                                  DSPATR(PR)
7.xx A**                               13  2'Hovedbokskonto.:'
7.xx A**                                    DSPATR(ND)
7.xx A**          A1HKON    R        B 13 19REFFLD(GCHKON)
7.xx A**                                    EDTCDE(Z)
7.xx A**40                                  DSPATR(PR)
7.xx A**                                    TEXT('Hovedbokskontonummer')
7.xx A**38                                  ERRMSGID(GM00001 *LIBL/QMFRREM 38)
7.xx A**38                                  DSPATR(PC)
7.xx A**                                    DSPATR(ND)
7.xx A**                               13 26'Avd:'
7.xx A**                                    DSPATR(ND)
7.xx A**          A1AVDE    R        B 13 31REFFLD(GCAVDE)
7.xx A**                                    EDTCDE(Z)
7.xx A**40                                  DSPATR(PR)
7.xx A**                                    TEXT('Avdeling')
7.xx A**                                    DSPATR(ND)
7.xx A**                               13 37'Spes:'
7.xx A**                                    DSPATR(ND)
7.xx A**          A1SPES    R        B 13 43REFFLD(RXSPES)
7.xx A**                                    EDTCDE(Z)
7.xx A**40                                  DSPATR(PR)
7.xx A**                                    TEXT('Spesifikasjon')
7.xx A**                                    DSPATR(ND)
     A                                 14  2'Avtalenummer. .:'
     A            A1AVTN    R     Y  B 14 19REFFLD(GCAVTN)
     A N40                                  DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  40                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                 15  2'Løpenummer. . .:'
     A            A1LØPN    R     Y  O 15 19REFFLD(GCLØPN)
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                 16  2'Avtaledato. . .:'
     A            A1DATO         8Y 0B 16 19
     A N40                                  DSPATR(UL)
     A                                      CHECK(FE)
     A                                      EDTWRD('  .  .    ')
     A  40                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16 31'(dd.mm.åååå)'
     A                                 17  2'Kommentar . . .:'
     A            A1KOMM    R        B 17 19REFFLD(GCKOMM)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Kommentar')
     A                                      COLOR(YLW)
     A                                 20  2'F3 Gå tilbake'
     A          R A2WIN
     A*%%TS  SD  20001221  105139  ASP         REL-V4R4M0  5769-PW1
     A                                      TEXT('Feilmelding for Type Bank')
     A                                      CA03
     A                                      WINDOW(&W_AROW &W_ACOL 12 32)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  3'Gyldige verdier for Bank er:'
     A                                      DSPATR(UL)
     A                                  4  3'DNB:  Den norske Bank       '
     A                                  5  3'FOK:  Fokus Bank.           '
     A                                  6  3'KBA:  K-Bank.               '
     A                                  7  3'FPC:  Sparebanken.          '
     A                                  8  3'NBA:  Norlandsbanken.       '
     A                                  9  3'NOR:  Nornett.              '
     A                                 11  3'Trykk ENTER   '
     A                                      COLOR(BLU)
     A          R D1WSF                     SFL
     A*%%TS  SD  20020314  173149  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Subfile - vise filgruppe/firm-
     A                                      a')
     A            B1KOMH    R        H      REFFLD(GCKOMM)
     A            B1LØPN    R        H      REFFLD(GCLØPN)
     A            B1DATO    R        H      REFFLD(GCDATO)
     A            B1AVTN    R        H      REFFLD(GCAVTN)
     A            B1PGIR    R        H      REFFLD(GCPGIR)
     A            B1KOMM    R        H      REFFLD(GCKOMM)
     A            B1BANK    R        H      REFFLD(GCBANK)
     A            B1VALG    R        B  9  2REFFLD(AAVALG)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      VALUES(' ' '2' '4' '5')
     A            B1BGIR    R     Y  O  9  4REFFLD(GCBGIR)
     A                                      TEXT('Gironummer')
     A                                      EDTCDE(Z)
     A            B1FGRP         3A  O  9 16TEXT('Filgruppe')
     A            B1FIRM    R        O  9 20REFFLD(GCFIRM)
     A                                      TEXT('Firmanummer')
     A            B1SKOD    R        O  9 25REFFLD(GCSKOD)
     A                                      TEXT('Sonekode')
     A            B1SKTO    R        O  9 29REFFLD(GCSKTO)
     A                                      EDTCDE(Z)
     A                                      TEXT('Sonenummer')
     A            B1SLOK    R        O  9 41REFFLD(GCSLOK)
     A                                      TEXT('Lokasjon')
     A**          B1HKON    R        O  9 57REFFLD(GCHKON)
     A**                                    TEXT('Hovedbok')
     A**          B1AVDE    R        O  9 63REFFLD(GCAVDE)
     A**                                    TEXT('Avdeling')
     A**          B1SPES    R    4   O  9 68REFFLD(GCSPES)
     A**                                    TEXT('Spesifikasjon')
     A          R D2WCT                     SFLCTL(D1WSF)
     A*%%TS  SD  20031217  160452  RHO         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(D2CMD)
     A                                      TEXT('Hovedbilde - vise filgruppe/f-
     A                                      irma')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      KEEP
     A                                      OVERLAY
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  2  4'VEDLIKEHOLD AV AVTALEREGISTER'
     A                                      COLOR(WHT)
     A                                  4  2'Velg post og trykk Enter'
     A                                      COLOR(BLU)
     A                                  5  2'2=Endre  4=Slette  5=Vise'
     A                                      COLOR(BLU)
     A                                  7  2'V Bankgiro    Fgr Fir Kode Kontonu-
     A                                      mmer Sted/Lokasjon                 -
     A                                       '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            B2VALG    R        B  8  2REFFLD(AAVALG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  30                                  ERRMSGID(RML0510 *LIBL/QMFOKON 30)
     A  31                                  ERRMSGID(RM00030 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RML0490 *LIBL/QMFOKON 32)
     A                                      VALUES(' ' '2' '4' '5')
     A                                      DSPATR(ND)
     A            B2FGRP    R        B  8 16REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DSPATR(ND)
     A            B2FIRM    R     Y  B  8 20REFFLD(GCFIRM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RZ)
     A                                      DSPATR(ND)
     A            B2BGIR    R     Y  B  8  4REFFLD(GCBGIR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                      CHECK(RZ)
     A                                      TEXT('Bankgironummer')
     A                                      COLOR(YLW)
     A                                      DSPATR(ND)
     A                                  6  2'                      _ _ _ _ _ _ -
     A                                      _ _ _  S  O  N  E _ _ _ _ _ _ _ _ _-
     A                                       '
     A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  20001221  145352  ASP         REL-V4R4M0  5769-PW1
     A                                      TEXT('Kommandotaster - vise filgrup-
     A                                      pe/firma')
     A                                      CA03
     A                                      OVERLAY
     A                                      WINDOW(&W_AROW &W_ACOL 17 71)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F5=Forny'
     A                                      COLOR(BLU)
     A                                 16 27'F6=Ny avtale'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
