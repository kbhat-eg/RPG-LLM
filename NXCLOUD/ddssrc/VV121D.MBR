     A*%%TS  SD  20050409  113026  HKO530      REL-V5R2M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20050409  113026  HKO530      REL-V5R2M0  5722-WDS
     A            B1WORK    R        H      REFFLD(VVAAVA)
     A            B1AAVA    R        H      REFFLD(VVAAVA)
7.xx A                                  4  2'+'
7.xx A            B1AVAR         8Y 0B  4  4COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
7.xx A            B1TEK1    R   -2   O  4 13REFFLD(VVTEK1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050409  112852  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CA05
     A                                      KEEP
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  31                                  SFLMSGID(VMV0030 *LIBL/QMFOFAK 31)
     A  32                                  SFLMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Vedlikehold av alternative varer '
     A                                      DSPATR(RI)
     A          R B2CMD
     A*%%TS  SD  20050302  132141  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 13 46)
     A                                 12  2'F3=Gå tilbake  F4=Spørring  F5=For-
     A                                      ny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
