7.xx  * 7.00  260618 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
     A*%%TS  SD  20050804  104738  HKO530      REL-V5R2M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050804  104738  HKO530      REL-V5R2M0  5722-WDS
     A            B1LPNR    R        H      REFFLD(FVLPNR)
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4)
     A  22                                  DSPATR(PC)
     A            B1LENG         6Y 0O 10  5EDTCDE(Z)
     A            B1ANTA         6Y 0O 10 17EDTCDE(4)
     A            B1LOPM         9Y 3O 10 26EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050804  104738  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            B2LPNR    R        H      REFFLD(FVLPNR)
     A                                  1  2'Registrering lengdespesifikasjon'
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
     A                                  3  2'Varenr.'
     A                                      COLOR(WHT)
     A            B2VARE         8Y 0O  3 10EDTCDE(Z)
     A                                      COLOR(YLW)
     A            B2VTXT        25A  O  3 20COLOR(WHT)
     A                                  5  2'Lengde (mm)    Antall         Tot.-
     A                                      løpemeter'
     A                                      COLOR(WHT)
     A            B2LENG         6Y 0B  6  5CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  23                                  DSPATR(PC)
     A            B2ANTA         6Y 0B  6 17CHECK(RB)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 24'(ant)'
7.xx A                                      COLOR(WHT)
     A            B2LOPT         9Y 3O  6 35EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  8  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  8 10'4=Slett'
     A                                      COLOR(BLU)
7.xx A*                                 9  2'V Lengde (mm)  Antall    Løpemeter'
7.xx A*                                     COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Lengde (mm)'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 17'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 26'Løpemeter'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20040607  154402  GRO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 22 45)
     A                                 21  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
     A          R D1WIN
     A*%%TS  SD  20040525  110904  GRO         REL-V5R2M0  5722-WDS
7.xx A                                      WINDOW(2 2 10 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette ordre/tilleggslinje  '
     A                                      DSPATR(RI)
     A                                  4  2'Lengde  Antall  Løpemeter'
     A            D1LENG         6Y 0O  6  2EDTCDE(Z)
     A            D1ANTA         6Y 0O  6 10EDTCDE(4)
     A            D1LOPM        10Y 5O  6 17EDTCDE(4)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
