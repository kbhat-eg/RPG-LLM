     A*%%TS  SD  20060619  160856  ASP_MGR     REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL516D
     A* Beskrivelse . . : Leverandørposteringer, spørring utbet.forslag
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       170305 KOB  Nytt program
7.xx A*       030516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20060619  160856  ASP_MGR     REL-V5R2M0  5722-WDS
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BFNR    R        O  9  4REFFLD(ROBFNR)
     A            B1BELØ        11Y 2O  9  8EDTCDE(J)
     A            B1REST        11Y 2O  9 24EDTCDE(J)
     A            B1FODA    R        O  9 40REFFLD(ROFODA)
     A            B1FTIM    R        O  9 51REFFLD(ROFTIM)
     A            B1FSIG    R   -2   O  9 60REFFLD(ROFSIG)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20060619  160856  ASP_MGR     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på utbetalingsforslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Fnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  5 60'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 76REFFLD(RORAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Fnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 24'Restbeløp'
7.xx A**                                8  2'V Fnr    Beløp        Restbeløp   -
7.xx A**                                        <-------- Forslag -------->'
     A                                      COLOR(WHT)
7.xx A                                  8 42'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'Tid'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'Bruker'
7.xx A                                      COLOR(WHT)
7.xx A            B2BFNR    R        B  6  4REFFLD(ROBFNR)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
7.xx A**                                8 42'Dato       Tid     Bruker'
7.xx A**                                    COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
