     A*%%TS  SD  20101109  115725  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A*          250116 bsa  Justeringer basert på ny GUI og regler
7.03 A*          050219 bsa  Legger inn å vise vare
7.04 A*          290819 ask  Lagt inn vise lagerlokasjoner
     A*----------------------------------------------------------------
7.08 A                                      DSPSIZ(27 132 *WIDE)
7.08 A*                                      DSPSIZ(24 80 *DS3 -
7.08 A*                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090910  100553  BOF610      REL-V5R4M0  5722-WDS
     A            B1VARE    R        H      REFFLD(VVVARE)
7.08 A*            B1LVAR    R        H      REFFLD(VVLVAR)
     A            B1NOBB    R        H      REFFLD(VVNOBB)
7.08 A            B1VALG         1Y 0B 10  2COLOR(YLW)
7.04 A                                      VALUES(5 6 7 8 9 0)
     A                                      CHKMSGID(BM00010 *LIBL/QMFVEDL)
     A                                      EDTCDE(4)
7.xx A*N22                                  DSPATR(PC)
7.08 A            B1TEK1    R        O 10  4REFFLD(VVTEK1)
     A  50                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
7.08 A            B1TEK2    R  -10   O 10 40REFFLD(VVTEK2)
7.08 A  50                                  COLOR(RED)
7.08 A  54                                  COLOR(BLU)
7.08 A            B1NYPO         8A  O 10 66
6.34 A  51                                  COLOR(BLU)
6.34 A  52                                  COLOR(YLW)
7.08 A            B1BEHL         5Y 0O 10 75EDTCDE(M)
7.08 A            B1LNAV        11A  O 10 82
6.34 A  51                                  COLOR(BLU)
6.34 A  52                                  COLOR(YLW)
7.08 A            B1ENHE    R        O 10 94REFFLD(VPENHE)
7.08 A            B1SAIM    R        O 10 98REFFLD(VPSAPR)
7.08 A            B1SORT    R   -9   O 10109REFFLD(VVSORT)
7.08 A            B1STAT         4A  O 10111
7.08 A            B1LVAR    R   -4   O 10116REFFLD(VVLVAR)
7.08 A            B1SENT         1A  H
     A            B1SESS         1A  H
     A            B1KPRI    R        H      REFFLD(VVKPRI)
     A            B1VTYP    R        H      REFFLD(VVVTYP)
7.08 A*            B1TEK2    R        H      REFFLD(VVTEK2)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090910  100553  BOF610      REL-V5R4M0  5722-WDS
7.08 A                                      SFLSIZ(0016)
7.08 A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
7.01 A*                                     ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF08
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CA21
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  40                                  SFLMSGID(FM00700 *LIBL/QMFOFAK 40)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
7.08 A                                  1118'Kl. :'
7.08 A                                      COLOR(WHT)
7.08 A                                  1124TIME
7.08 A                                      COLOR(WHT)
     A                                  2  2'Spørring på vare/pris'
     A                                      COLOR(WHT)
7.08 A                                  2 38'VV550Z1'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.08 A                                  2118'Dato:'
7.08 A                                      COLOR(WHT)
7.08 A                                  2124DATE
7.08 A                                      EDTCDE(Y)
7.08 A                                      COLOR(WHT)
7.08 A                                  3  1'-----------------------------------
7.08 A                                      ------------------------------------
7.08 A                                      ------------------------------------
7.08 A                                      ---------------------------'
7.08 A                                      COLOR(WHT)
7.08 A                                  4  2'Kunde. . . . +'
     A                                      COLOR(BLU)
     A            B2KUND    R        B  4 17REFFLD(FLKUND)
     A  31                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            B2KNAV        30A  O  4 25COLOR(YLW)
     A                                      OVRDTA
7.08 A  53                              4118'Listen er tom'
7.06 A                                      COLOR(RED)
7.08 A  53                              5118'Bruk søk     '
7.06 A                                      COLOR(RED)
7.xx A                                  5  2'Kundeprosjekt+'
     A                                      COLOR(BLU)
     A            B2KPRO    R        B  5 17REFFLD(FLKPRO)
     A  32                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            B2PNAV    R        O  5 25REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  6 57'Leveringstype+'
     A                                      COLOR(BLU)
7.08 A            B2LETY    R        B  6 72REFFLD(FRLETY)
     A  33                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  4 57'Lager  . . . +'
     A                                      COLOR(BLU)
7.08 A            B2LAGE    R        B  4 72REFFLD(VLLAGE)
     A  38                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A            B2LNAV        30A  O  4 80COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  5 57'Varegruppe . +'
     A                                      COLOR(BLU)
7.08 A            B2OGRP    R        B  5 72REFFLD(VVHGRP)
     A  34                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  5 75'+'
7.xx A                                      COLOR(BLU)
7.08 A            B2HGRP    R        B  5 77REFFLD(VVHGRP)
     A  35                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  5 80'+'
7.xx A                                      COLOR(BLU)
7.08 A            B2UGRP    R        B  5 82REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.08 A                                  6  2'Frisøk . . . .'
     A                                      COLOR(BLU)
7.08 A            B2STEK        35A  B  6 17COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.08 A                                  7  2'Varetekst'
7.xx A                                      COLOR(WHT)
7.08 A                                  7 57'Varenr'
7.xx A                                      COLOR(WHT)
7.08 A                                  7 78'Nobbnr'
7.xx A                                      COLOR(WHT)
7.08 A                                  7 98'Lev.varenr'
7.xx A                                      COLOR(WHT)
7.08 A            B2TEK1    R   -2   B  7 17REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A            B2VARE         8Y 0B  7 64COLOR(YLW)
     A  36                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 37)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.08 A            B2NOBB    R        B  7 85REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.08 A            B2LVAR    R        B  7109REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.08 A                                  8  2'5=Vis/kalkuler  7=Vis vare  -
7.03 A                                        8=Produktinfo  -
7.04 A                                      9=Beholdning  6=Lokasjon'
     A                                      COLOR(BLU)
7.08 A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.08 A                                  9  4'Varetekst'
     A                                      COLOR(WHT)
7.08 A                                  9 40'Varetekst'
7.08 A                                      COLOR(WHT)
7.08 A  80                              9 66'Varenr '
7.xx A  80                                  COLOR(WHT)
7.08 A  80                              9 74'Tilg.beh'
     A  80                                  COLOR(WHT)
7.08 A  81                              9 66'Nobbnr'
7.xx A  81                                  COLOR(WHT)
7.08 A*  81                              9 74'Tilg.beh'
7.08 A*  81                                  COLOR(WHT)
7.08 A*  82                              9 66'L.Vnr.'
7.08 A*  82                                  COLOR(WHT)
7.08 A*  82                              9 74'Tilg.beh'
7.08 A*  82                                  COLOR(WHT)
7.08 A                                  9 74'Tilg.beh'
7.08 A                                      COLOR(WHT)
7.08 A                                  9 83'Lev. '
7.xx A                                      COLOR(WHT)
7.08 A                                  9 94'Enh'
     A                                      COLOR(WHT)
7.08 A                                  9100'Inkl.mva'
     A                                      COLOR(WHT)
7.08 A                                  9109'S'
     A                                      COLOR(WHT)
7.08 A                                  9111'CPKT'
     A                                      COLOR(WHT)
7.08 A                                  9116'Lev.Varenr'
7.08 A                                      COLOR(WHT)
7.08 A          R B2CMD
     A*%%TS  SD  20031125  141615  HKO         REL-V5R2M0  5722-WDS
7.08 A                                 26  2'F3=Gå tilbake F4=Spørring F5=Forny-
7.08 A                                       F8=Skaffereg. F9=Gjenta søk'
     A                                      COLOR(BLU)
7.08 A                                 26 65'F13=Scanning'
     A                                      COLOR(BLU)
7.08 A N83                             26 78'F21=Inkl.skaffevarer'
     A                                      COLOR(BLU)
7.08 A  83                             26 78'F21=Eksl.skaffevarer'
     A                                      COLOR(BLU)
7.08 A                                 26 99'F23=Bytt info.'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  19990428  224045  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Scanning '
     A                                      DSPATR(RI)
     A                                  4  2'GTIN-nummer .'
     A            E1EANN        14A  B  4 16COLOR(YLW)
     A  31                                  ERRMSGID(VMX0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A                                      CHECK(ER)
     A                                      OVRDTA
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.04 A          R F1WIN
7.04 A*%%TS  SD  19990428  224045  HKO         REL-V4R2M0  5769-PW1
7.04 A                                      WINDOW(*DFT 11 60)
7.04 A                                      CA03
7.04 A                                      CF04
7.04 A                                      CA12
7.04 A                                      PUTOVR
7.04 A                                  2  2' Lagerlokasjoner '
7.04 A                                      DSPATR(RI)
7.04 A                                  2 35'Beholdning'
7.04 A            F1BEHL         5Y 0O  2 46EDTCDE(M)
7.04 A                                  4  2'Lager. +'
7.04 A            F1LAGE         2Y 0B  4 16COLOR(YLW)
7.04 A                                      OVRDTA
7.04 A            F1LTEK        20A  O  4 25COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A                                  6  2'Hovedlokasjon'
7.04 A            F1LOK1         8A  O  6 16COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A            F1TEK1        30A  O  6 25COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A                                  7  2'Alt. lok. 1'
7.04 A            F1LOK2         8A  O  7 16COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A            F1TEK2        30A  O  7 25COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A                                  8  2'Alt. lok. 2'
7.04 A            F1LOK3         8A  O  8 16COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A            F1TEK3        30A  O  8 25COLOR(WHT)
7.04 A                                      OVRDTA
7.04 A                                 10  1'F3=Gå tilbake'
7.04 A                                      COLOR(BLU)
