     A*%%TS  SD  20120229  084513  ASK620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : SH410D
     A* Beskrivelse . . : Vise faktura
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       180516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19981002  140234  AMD         REL-V4R2M0  5769-PW1
     A            B1SUFF    R        H      REFFLD(SLSUFF)
     A            B1KTXT    R        H      REFFLD(SLKTXT)
     A            B1LINE    R        H      REFFLD(SLLINE)
     A            B1VALG    R        B 15  2REFFLD(LÅVALA)
     A                                      COLOR(YLW)
6.31 A                                      VALUES('5' '7' ' ')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A            B1NUMM    R        O 15  4REFFLD(SHNUMM)
6.NU A            B1TEXT        68A  O 15 13
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20120229  084513  ASK620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF15
     A                                      CF20
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise faktura'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SH410B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 47'Oppdatert av'
     A            B2OUSR        10A  O  4 60COLOR(YLW)
     A                                  5  2'Lev. . .'
     A            B2LEVR    R        O  5 11REFFLD(SHLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LNAV    R   -7   O  5 18REFFLD(SHNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 47'Faktura. . .'
     A            B2BINR    R        O  5 60REFFLD(SHBINR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LGAT    R        O  6 11REFFLD(SHADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 47'Dato . . . .'
     A            B2FKDA         6Y 0O  6 60EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LSTE    R        O  7 11REFFLD(SHSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 47'Forfall. . .'
     A            B2FFDA         6Y 0O  7 60EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 47'Bet.bet. . .'
     A            B2CTXT        20A  O  8 60COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lev.adr.'
     A            B2NAVN        30A  O  9 11COLOR(YLW)
     A                                      OVRDTA
     A                                  9 47'Lev.måte . .'
     A            B2LMTX    R        O  9 60REFFLD(SHLMTX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ADR1    R        O 10 11REFFLD(SHADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 47'Lev. onr.. .'
     A            B2LONR    R        O 10 60REFFLD(SHLONR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2STED    R        O 11 11REFFLD(SHSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 47'Lev. faknr .'
     A            B2LFNR        15   O 11 60COLOR(YLW)
     A                                 12 47'Fakturasum .'
     A            B2FSUM    R        O 12 60REFFLD(SHFSUM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.31 A                                 13  2'5=Vis  7=Vis ordrehode'
     A                                      COLOR(BLU)
7.xx A                                 14  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 14  4'Ordre  '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 13'Varenr. '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 22'Varetekst          '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 54'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 62'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 67'Rab'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 74'Beløp'
7.xx A**                               14  2'V Ordre    Varenr.  Varetekst     -
7.xx A**                                                      Antall  Enh  Rab -
7.xx A**                                       Beløp'
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  20040503  094138  ASK         REL-V5R2M0  5722-WDS
     A                                 23  3'F3=Tilbake'
     A                                      COLOR(BLU)
     A                                 23 15'F15=Kopier til bestilling'
     A                                      COLOR(BLU)
     A                                 23 42'F20=Skriv fakturakopi'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20010620  101928  AMD         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      WINDOW(*DFT 13 40)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Generere best. fra valgt faktura.'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Dette valg gir mulighet for å skap-
     A                                      e'
     A                                  5  2'en bestilling på bakgrunn av en   -
     A                                         '
     A                                  6  2'inng. faktura (eller kreditnota) f-
     A                                      ra'
     A                                  7  2'historisk faktura-register.'
     A                                  9  2'Oppgi ordretype'
     A                                 10  2'som skal benyttes :'
     A            H1OTYP    R        B 10 23REFFLD(LOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(LRO0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LM01110 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 33)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
