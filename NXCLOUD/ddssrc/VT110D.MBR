     A*%%TS  SD  20120221  185315  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A* 7.00  030216  bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081129  091844  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.01 A            B1PRGR    R        O 10  4REFFLD(VTPRGR)
     A            B1LETY    R        O 10  7REFFLD(VTLETY)
     A            B1FDAT         6Y 0O 10  9EDTWRD('  .  .  ')
     A            B1FTIM         4Y 0O 10 18EDTWRD('  :  ')
     A            B1TDAT         6Y 0O 10 25EDTWRD('  .  .  ')
     A            B1TTIM         4Y 0O 10 34EDTWRD('  :  ')
     A            B1KAMP    R        O 10 40REFFLD(VTKAMP)
     A            B1ENH1    R        O 10 46REFFLD(VTENH1)
     A            B1TIP1    R        O 10 50REFFLD(VTTIP1)
     A            B1KOP1    R        O 10 61REFFLD(VTKOP1)
     A            B1ENH2    R        O 10 73REFFLD(VTENH2)
     A            B1ENH3    R        O 10 77REFFLD(VTENH3)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081129  091844  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspriser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VT110B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Vare . . . . . . . .'
     A                                      COLOR(BLU)
7.xx A            B2VARE    R   -7   O  4 23REFFLD(VTVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2TEK1    R        O  4 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  6  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  8'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'--- Tilbudsperiode ---'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 40'Kamp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2PRGR    R        B  7  4REFFLD(VTPRGR)
7.xx A  31                                  ERRMSGID(VMT0010 *LIBL/QMFOFAK 31)
7.xx A  32                                  ERRMSGID(VMT0020 *LIBL/QMFOFAK 32)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      OVRDTA
8.01 A                                  7  7'+'
7.xx A                                      COLOR(BLU)
8.01 A            B2LETY    R        B  7  9REFFLD(VTLETY)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
8.01 A            B2FDAT         6Y 0B  7 12COLOR(YLW)
7.xx A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A            B2TDAT         6Y 0B  7 25COLOR(YLW)
7.xx A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A            B2KAMP    R        B  7 40REFFLD(VTKAMP)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                  8  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  7'L'
7.xx A                                      COLOR(WHT)
8.01 A                                  9  9' Fradato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 18' Tid '
7.xx A                                      COLOR(WHT)
8.01 A                                  9 25' Tildato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 34' Tid '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 40'Kamp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 51'Tilb.pris'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 62'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 73'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 77'Enh'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020226  111025  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  111025  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  6  2'Prisgruppe . . . . . . . . +'
     A            C1PRGR    R        B  6 31REFFLD(VTPRGR)
     A  43                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT    R        O  6 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Leveringstype. . . . . . . +'
     A            C1LETY    R        B  7 31REFFLD(VTLETY)
     A  36                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O  7 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Fra dato/tidspunkt . . . . .'
     A            C1FDAT         6Y 0B  9 31COLOR(YLW)
     A  31                                  ERRMSGID(VMT0060 *LIBL/QMFOFAK 31)
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A  41                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  9 40'-'
     A            C1FTIM         4Y 0B  9 42COLOR(YLW)
     A  38                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 38)
     A  42                                  ERRMSGID(FM00065 *LIBL/QMFOFAK 42)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10  2'Til dato/tidspunkt . . . . .'
     A            C1TDAT         6Y 0B 10 31COLOR(YLW)
     A  39                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 39)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10 40'-'
     A            C1TTIM         4Y 0B 10 42COLOR(YLW)
     A  40                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 40)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990516  192110  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Tilbudspris finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20120221  185315  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  5 31REFFLD(VTVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 42REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C1PRGR    R        O  6 31REFFLD(VTPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT    R        O  6 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O  7 31REFFLD(VTLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O  7 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Fra dato/tidspunkt . . . . .'
     A            C1FDAT         6Y 0O  9 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  9 40'-'
     A            C1FTIM         4Y 0O  9 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 10  2'Til dato/tidspunkt . . . . .'
     A            C1TDAT         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 40'-'
     A            C1TTIM         4Y 0O 10 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 12  2'Kampanje . . . . . . . . . .'
     A            C2KAMP    R        B 12 31REFFLD(VTKAMP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 14 23'Kostpris'
     A  80                             14 67'Omr.faktor'
7.xx A                                 15  4'Enh'
7.xx A                                 15 10'Tilbudspris'
7.xx A                                 15 23'tilb. per.'
     A                                 14 36'Innk.pris'
     A                                 15 36'tilb .per.'
     A  80                             15 50'Salgspris'
     A  80                             15 69'volum'
7.xx A                                 16  2'+'
7.xx A            C2ENH1    R        B 16  4REFFLD(VTENH1)
     A  32                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2TIP1    R        B 16 10REFFLD(VTTIP1)
     A  31                                  ERRMSGID(VMT0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2KOP1    R        B 16 23REFFLD(VTKOP1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2INP1    R        B 16 36REFFLD(VTINP1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP1    R        O 16 50REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 17  2'+'
     A            C2ENH2    R        B 17  4REFFLD(VTENH2)
     A  34                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2TIP2    R        B 17 10REFFLD(VTTIP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2KOP2    R        B 17 23REFFLD(VTKOP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2INP2    R        B 17 36REFFLD(VTINP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP2    R        O 17 50REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80        C2OMR2    R        O 17 64REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 18  2'+'
7.xx A            C2ENH3    R        B 18  4REFFLD(VTENH3)
     A  36                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2TIP3    R        B 18 10REFFLD(VTTIP3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2KOP3    R        B 18 23REFFLD(VTKOP3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2INP3    R        B 18 36REFFLD(VTINP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP3    R        O 18 50REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80        C2OMR3    R        O 18 64REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VTEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  19990525  200957  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette tilbudspris '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer . . . .'
     A            D1VARE    R        O  4 21REFFLD(VTVARE)
     A                                  5  2'Prisgruppe . . . .'
     A            D1PRGR    R        O  5 21REFFLD(VTPRGR)
     A                                  6  2'Leveringstype. . .'
     A            D1LETY    R        O  6 21REFFLD(VTLETY)
     A                                  7  2'Fra dato/tidspunkt'
     A            D1FDAT         6Y 0O  7 21EDTWRD('  .  .  ')
     A                                  7 30'-'
     A            D1FTIM         4Y 0O  7 32EDTWRD('  :  ')
     A                                  8  2'Til dato/tidspunkt'
     A            D1TDAT         6Y 0O  8 21EDTWRD('  .  .  ')
     A                                  8 30'-'
     A            D1TTIM         4Y 0O  8 32EDTWRD('  :  ')
     A                                 10  2'Kampanje . . . . .'
     A            VTKAMP    R        O 10 21
     A                                 11  2'Enhet. . . . . . .'
     A            VTENH1    R        O 11 21
     A                                 12  2'Salgspris. . . . .'
     A            VTTIP1    R        O 12 21
     A                                 13  2'Kostpris . . . . .'
     A            VTKOP1    R        O 13 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990518  142656  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Prisgruppe . . . +'
     A            K1PRGR    R        B  3 21REFFLD(VTPRGR)
     A  32                                  ERRMSGID(VMT0030 *LIBL/QMFOFAK 32)
     A  43                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  4  2'Leveringstype. . +'
     A            K1LETY    R        B  4 21REFFLD(VTLETY)
     A  36                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Fra dato/tidspunkt'
     A            K1FDAT         6Y 0B  5 21COLOR(YLW)
     A  31                                  ERRMSGID(VMT0060 *LIBL/QMFOFAK 31)
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A  41                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5 30'-'
     A            K1FTIM         4Y 0B  5 32COLOR(YLW)
     A  38                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 38)
     A  42                                  ERRMSGID(FM00065 *LIBL/QMFOFAK 42)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Til dato/tidspunkt'
     A            K1TDAT         6Y 0B  6 21COLOR(YLW)
     A  39                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 39)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6 30'-'
     A            K1TTIM         4Y 0B  6 32COLOR(YLW)
     A  40                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 40)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Prisgruppe . . . .'
     A            VTPRGR    R        O  9 21OVRDTA
     A                                 10  2'Leveringstype. . .'
     A            VTLETY    R        O 10 21OVRDTA
     A                                 11  2'Fra dato/tidspunkt'
     A            K2FDAT         6Y 0O 11 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 11 30'-'
     A            K2FTIM         4Y 0O 11 32EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 12  2'Til dato/tidspunkt'
     A            K2TDAT         6Y 0O 12 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12 30'-'
     A            K2TTIM         4Y 0O 12 32EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
