     A*%%TS  SD  20030120  124543  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL122D
     A* Beskrivelse . . : Betalingsartkoder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
7.xx A* 7.xx  251116 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A          R A1WIN
     A*%%TS  SD  19950809  092131  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Vedlikehold av dek.txt')
     A                                      CA03
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 10 43)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  7'VEDLIKEHOLD AV BETALINGSARTKODER'
     A                                      COLOR(WHT)
     A                                  4  2'Kode. . .:'
     A            A1RDEC    R        B  4 13REFFLD(GDRDEC)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(RML0530 *LIBL/QMFOKON 32)
     A                                  6  2'Tekst . .:'
     A            A1RTX1    R        B  6 13REFFLD(GDRTX1)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A                                      TEXT('tekst')
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A N40                                  DSPATR(PC)
     A            A1RTX2    R        B  7 13REFFLD(GDRTX2)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A                                      TEXT('Tekst')
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 27'Enter=Slette'
     A                                      COLOR(BLU)
     A N41                                  DSPATR(ND)
     A  41                                  DSPATR(RI)
     A          R D1WSF                     SFL
     A*%%TS  SD  19950130  132713  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Subfile - vise dek.txt')
     A            B1VALG    R        B  9  2REFFLD(AAVALG)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      VALUES(' ' '2' '4' '5')
     A            B1RTXT        60A  O  9 12
     A            B1RDEC    R        O  9  4REFFLD(GDRDEC)
     A                                      TEXT('Kode')
     A          R D2WCT                     SFLCTL(D1WSF)
     A*%%TS  SD  19950130  141325  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Hovedbilde - vise dek.txt')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      KEEP
     A                                      OVERLAY
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(D2CMD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
7.xx A                                  1 60'GL122D2'
     A                                  2 21'VEDLIKEHOLD AV BETALINGSARTKODER'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Kode'
7.xx A                                  6  2'Velg post og trykk Enter'
     A                                      COLOR(BLU)
7.xx A                                  7  2'1=Opprette 2=Endre 4=Slette 5=Vise'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 12'Tekst 1                           '
7.xx A*                                 7  2'V Kode    Tekst                   -
7.xx A*                                                                        -
7.xx A*                                      '
7.xx A*                                     DSPATR(UL)
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  5  2REFFLD(AAVALG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  30                                  ERRMSGID(RML0540 *LIBL/QMFOKON 30)
     A  31                                  ERRMSGID(RM00030 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RML0530 *LIBL/QMFOKON 32)
     A                                      VALUES(' ' '1' '2' '4' '5')
7.xx A            B2RDEC    R        B  5  4REFFLD(GDRDEC)
     A                                      TEXT('Kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A          R D2CMD
     A*%%TS  SD  19950130  141325  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Kommandotaster - vise dek.txt-
     A                                      ')
     A                                      CA03
     A                                      OVERLAY
     A                                      WINDOW(&W_AROW &W_ACOL 18 72)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME
     A*%%TS  SD  19950130  122915  RH          REL-V2R3M0  5738-PW1
     A                                      ASSUME
     A                                 24 79' '
