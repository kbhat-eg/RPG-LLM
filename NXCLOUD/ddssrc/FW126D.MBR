     A*%%TS  SD  20020312  224313  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19981111  081912  HKO         REL-V4R2M0  5769-PW1
     A            B1LINE    R        H      REFFLD(FWLINE)
     A            B1VARE        15   H
     A            B1SAGM    R        H      REFFLD(FWSAPR)
     A            B1VALG         1Y 0B  9  2COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(2 3 5 6 7 8 0)
     A                                      CHKMSGID(BM00010 *LIBL/QMFVEDL)
     A            B1TEK1        31   O  9  4
     A            B1NYPO        15   O  9 36
     A            B1ENHE    R        O  9 52REFFLD(FWENHE)
     A            B1SAPR    R        O  9 56REFFLD(FWSAPR)
     A            B1ENDR         5Y 2O  9 68EDTCDE(M)
     A            B1EKOD    R        O  9 76REFFLD(FWEKOD)
     A            B1STAT         1Y 0O  9 78EDTCDE(Z)
     A            B1HKOD         1   O  9 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  224313  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF20
     A                                      CF23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppdatering av varer/priser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FW126B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 17REFFLD(FWLDOR)
     A                                      COLOR(BLU)
     A            B2NAVN        30A  O  4 24COLOR(BLU)
7.xx A                                  5  2'Varetekst 1'
7.xx A                                      COLOR(WHT)
7.xx A            B2TEK1        31A  B  5 14COLOR(YLW)
7.xx A  31                                  ERRMSGID(FMW0110 *LIBL/QMFOFAK 31)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      OVRDTA
7.xx A  80                              6  2'Lev. varenr'
7.xx A  80                                  COLOR(WHT)
7.xx A  81                              6  2'GTIN-nr'
7.xx A  81                                  COLOR(WHT)
7.xx A  82                              6  2'Nobbnr'
7.xx A  82                                  COLOR(WHT)
7.xx A  80        B2LVAR    R   -5   B  6 14REFFLD(FWLVAR)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A  81        B2EANN    R        B  6 14REFFLD(FWEANN)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A  82        B2NOBB    R        B  6 14REFFLD(FWNOBB)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  7  2'2=Endre  3=Holde tilbake  5=Vis  6-
     A                                      =Frigi  7=Vare  8=Søketekster'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  4'Varetekst 1'
7.xx A                                      COLOR(WHT)
7.xx A  80                              8 36'Lev. varenr'
     A  80                                  COLOR(WHT)
7.xx A  81                              8 36'GTIN-nr'
     A  81                                  COLOR(WHT)
7.xx A  82                              8 36'Nobbnr'
     A  82                                  COLOR(WHT)
7.xx A                                  8 52'Enh'
     A                                      COLOR(WHT)
7.xx A                                  8 56'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 68'%-endr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 76'K'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 78'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'H'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19981111  082624  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå ut  F5=Forny  F13=Status  F1-
     A                                      4=Gruppebehandling  F20=Overfør  F2-
     A                                      3=Ny pos.'
     A                                      COLOR(BLU)
