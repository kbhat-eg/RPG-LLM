     A*%%TS  SD  20140714  115310  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A* 030316 bsa Endringer relatert til ny GUI
8.01 A*PRG2 140622 BOF : utvidet prisgruppe fra 1 til 2 posisjoner
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20140714  115310  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  2 38'JV125B1'
7.xx A                                      COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
7.xx A                                  2  2'Oppdatering av pris og vareinfo'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Type oppdatering. . . . . .'
     A            B1OTYP         1Y 0B  4 30COLOR(YLW)
     A  31                                  ERRMSGID(JMB0170 *LIBL/QMFVADM 31)
     A                                      VALUES(1 2 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                  4 41'1=Pris- og vareinformasjons-oppdat-
     A                                      ering'
     A                                  5 41'2=Kun prisoppdatering'
     A                                  6  2'Prisendringsdato  . . . . .'
     A            B1EDAT         6Y 0B  6 30COLOR(YLW)
     A  32                                  ERRMSGID(JMB0180 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JM00250 *LIBL/QMFVADM 33)
     A  34                                  ERRMSGID(JM00280 *LIBL/QMFVADM 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  8  2'Utplukk:'
7.xx A                                      DSPATR(RI)
     A                                      COLOR(WHT)
     A                                  7  2'Prisdato leverandørpris . .'
     A            B1PDAT         6Y 0B  7 30COLOR(YLW)
     A  32                                  ERRMSGID(JMB0180 *LIBL/QMFVADM 32)
     A  48                                  ERRMSGID(JM00250 *LIBL/QMFVADM 48)
     A  49                                  ERRMSGID(JM00280 *LIBL/QMFVADM 49)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                  9  2'Leverandør  . . . . . . . +'
     A            B1LDOR    R        B  9 30REFFLD(JVLDOR)
     A  35                                  ERRMSGID(JML0300 *LIBL/QMFVADM 35)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A  46                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 46)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1NAVN    R        O  9 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Varegruppe  . . . . . . . +'
     A            B1OGRP    R        B 10 30REFFLD(JVOGRP)
     A  37                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10 33'+'
7.xx A            B1HGRP    R        B 10 35REFFLD(JVHGRP)
     A  38                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10 38'+'
7.xx A            B1UGRP    R        B 10 40REFFLD(JVUGRP)
     A  39                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 39)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B1UTXT        35A  O 10 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Sortimentskode  . . . . . +'
     A            B1OSOR    R        B 11 30REFFLD(JSOSOR)
     A                                      COLOR(YLW)
     A  44                                  ERRMSGID(JMT0020 *LIBL/QMFVADM 44)
     A                                      OVRDTA
7.xx A            B1STXT    R        O 11 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Ta med utgåtte priser . . .'
     A            B1UTGP         1Y 0B 12 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
7.xx A                                 12 44'1=Ja,0=Nei'
7.xx A                                 13  2'Prisgivende leverandør. . +'
     A            B1PGIV    R        B 13 30REFFLD(JVLDOR)
     A  40                                  ERRMSGID(JML0020 *LIBL/QMFVADM 40)
     A  47                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 47)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1PNAV    R        O 13 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Se kalkyle for:'
     A                                      COLOR(WHT)
7.xx A                                      DSPATR(RI)
7.xx A                                 15  2'Leveringstype . . . . . . +'
     A            B1LETY         1Y 0B 15 30COLOR(YLW)
     A  42                                  ERRMSGID(JML0020 *LIBL/QMFVADM 42)
     A                                      OVRDTA
7.xx A            B1LBES        35A  O 15 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16  2'Prisgruppe  . . . . . . . +'
8.01 A            B1PRGR         2A  B 16 30COLOR(YLW)
     A  43                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 43)
     A                                      OVRDTA
     A            B1PBES        35A  O 16 41COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Overstyringer:'
7.xx A                                      DSPATR(RI)
     A                                      COLOR(WHT)
     A                                 17 46'Tillegg:'
     A                                      COLOR(WHT)
7.xx A                                      DSPATR(RI)
     A                                 18  2'Kun samvarende leverandør .'
     A            B1SAMS         1Y 0B 18 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
     A                                 18 33'1=Ja,0=Nei'
     A                                 18 46'Flere prisgivere .'
     A            B1FLPR         1Y 0B 18 65COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
     A                                 18 67'1=Ja,0=Nei'
     A                                 19  2'Utelat varer utgått i EVR .'
     A            B1UTGD         1Y 0B 19 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
     A                                 19 33'1=Ja,0=Nei'
     A                                 20  2'Ikke oppdat. innkjøpspris .'
     A            B1INIO         1Y 0B 20 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
     A                                 20 33'1=Ja,0=Nei'
     A                                 21  2'Ikke oppdat. kostpris . . .'
     A            B1KOIO         1Y 0B 21 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      OVRDTA
     A                                 21 33'1=Ja,0=Nei'
     A                                 22  2'Ikke oppdat. salgspris  . .'
     A            B1SAIO         1Y 0B 22 30COLOR(YLW)
     A  41                                  ERRMSGID(JMB0160 *LIBL/QMFVADM 41)
     A  45                                  ERRMSGID(JMB0190 *LIBL/QMFVADM 45)
     A                                      VALUES(0 1)
     A                                      OVRDTA
     A                                 22 33'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Parametervalget ditt gjør at'
     A                                  3  2'hovedleverandør på varen    '
     A                                  4  2'blir endret!                '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
