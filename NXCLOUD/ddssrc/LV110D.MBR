     A*%%TS  SD  20140604  134837  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1WIN
     A*%%TS  SD  20020925  092710  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 12 40)
     A                                      CA03
     A                                      PUTOVR
     A                                  2  2' Valg av subliste-nummer '
     A                                      DSPATR(RI)
     A                                  4  2'Buntnr. . . . .'
     A            A1NUMM    R        O  4 18REFFLD(LUNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Lager . . . . .'
     A            A1LAGB    R        O  5 18REFFLD(LULAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Subliste-nummer'
     A            A1SUFF    R        B  7 18REFFLD(LESUFF)
     A  31                                  ERRMSGID(LMU0180 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140604  134837  BOF630      REL-V7R1M0  5770-WDS
     A            B1LINE    R        O  8  2REFFLD(LVLINE)
     A            B1VARE         8S 0O  8  9
     A            B1LAGE    R        O  8 18REFFLD(LVLAGE)
     A            B1TEK1    R        O  8 21REFFLD(LVTEK1)
     A            B1ENH1         3A  O  8 63
     A            B1TALT        10Y 3B  8 68EDTWRD('       ,   ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140604  134837  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      CF10
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Registrering mot subliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bunt . .'
     A                                      COLOR(WHT)
     A            B2TEXT    R        O  4 20REFFLD(LUTEXT)
     A                                  6  2'Linje'
     A                                      COLOR(WHT)
     A            B2LINE    R        B  7  2REFFLD(LVLINE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7  9'Varenr.  LN Tekst                 -
     A                                                          Enh    Antall'
     A                                      COLOR(WHT)
     A            B2NUMM    R        O  4 11REFFLD(LUNUMM)
     A          R B2CMD
     A*%%TS  SD  20020925  092346  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 29'F10=Hent lagersaldo'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
