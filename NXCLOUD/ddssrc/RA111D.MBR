     A*%%TS  SD  20150511  174608  ASP_B02     REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA111D
     A* Beskrivelse . . : Kundekategorier, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
6.20 A*       111113 NHO  Lagt inn feilmelding dersom kundekategori prøves slet-
6.20 A*                   es og den finnes på kunderegisteret
6.30 A*       110515 BOF  Endret fax til mobilnr (gjenbruk av felt RAKFAX)
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
7.01 A*       030718 bsa  Lagt inn felt for kundegruppe.
7.02 A*       240918 bsa  Lagt inn felt for krav til prosjekt.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020326  212329  KOB         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1KKOD    R        O  9  5REFFLD(RAKKOD)
7.xx A            B1KTXT    R        O  9 11REFFLD(RAKTXT)
7.xx A            B1KKAT    R        O  9 37REFFLD(RAKKAT)
7.xx A            B1KAVD    R        O  9 42REFFLD(RFRFR/RAKAVD *LIBL/RFRF)
7.xx A            B1KKTO    R        O  9 47REFFLD(RFRFR/RAKKTO *LIBL/RFRF)
7.xx A            B1KSPS    R        O  9 53REFFLD(RFRFR/RAKSPS *LIBL/RFRF)
7.xx A            B1KAVP    R        O  9 61REFFLD(RAKAVP)
     A                                      EDTCDE(1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  211319  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundekategori'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Beskrivelse      '
     A                                      COLOR(WHT)
7.xx A                                  7 36'Spes'
     A                                      COLOR(WHT)
7.xx A                                  8 35'kateg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 42'Avd.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 47'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'Bærer'
     A                                      COLOR(WHT)
7.xx A                                  7 61'Avvik'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'rente'
7.xx A                                      COLOR(WHT)
7.xx A            B2KKOD    R        B  5  5REFFLD(RAKKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KTXT    R        B  5 11REFFLD(RAKTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A**                                7 35'kateg'
7.xx A**                                    COLOR(WHT)
7.xx A**                                7 61'rente'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  210643  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundekategorier'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kundekategori. . . . . . . .'
     A            C1KKOD    R        B  5 31REFFLD(RAKKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020325  210643  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
6.20 A**                                2  2'Kundekategori finnes fra før'
6.20 A N40                              2  2'Kundekategori finnes fra før'
6.20 A  40                              2  2'Kundekat. kan ikke slettes'
6.20 A  40                              3  2'Kat.finnes i kunderegisteret'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20150511  174608  ASP_B02     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundekategorier'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kundekategori. . . . . . . .'
     A            C1KKOD    R        O  5 31REFFLD(RAKKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KTXT    R        B  5 36REFFLD(RAKTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
7.xx A                                  6  2'Avdeling/Konto/Bærer . . . +'
     A            C2KAVD    R        B  6 31REFFLD(RAKAVD)
     A  33                                  ERRMSGID(RM02020 *LIBL/QMFOKON 33)
     A  34                                  ERRMSGID(RM01070 *LIBL/QMFOKON 34)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
7.xx A                                  6 36'+'
7.xx A            C2KKTO    R        B  6 38REFFLD(RAKKTO)
     A  35                                  ERRMSGID(RM02000 *LIBL/QMFOKON 35)
     A  36                                  ERRMSGID(RM02010 *LIBL/QMFOKON 36)
     A  37                                  ERRMSGID(RM02030 *LIBL/QMFOKON 37)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
7.xx A                                  6 44'+'
7.xx A            C2KSPS    R        B  6 46REFFLD(RAKSPS)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
7.xx A                                  7  2'Betalingsbetingelse. . . . +'
     A            C2KBEB    R        B  7 31REFFLD(RAKBEB)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  38                                  ERRMSG('Betalingsbetingelse er feil-
     A                                      ' 38)
     A            C2CTXT    R        O  7 34REFFLD(RABTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                  8  2'Betalingsmåte. . . . . . . +'
     A            C2KBEM    R        B  8 31REFFLD(RAKBEM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  39                                  ERRMSG('Betalingsmåte er feil' 39)
     A            C2BTXT    R        O  8 34REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Spes. Kat.(J/N). . . . . . .'
     A            C2KKAT    R        B  9 31REFFLD(RAKKAT)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                  9 46'Avvikende rente. . . . . . .'
     A            C2KAVP    R        B  9 75REFFLD(RAKAVP)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 10  2'Kreditgrense . . . . . . . .'
     A            C2KGRN    R        B 10 31REFFLD(RAKGRN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 10 46'Kreditgrense butikk. . . . .'
     A            C2BGRN    R        B 10 75REFFLD(RABGRN)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                 11  1'Standardverdier:'
     A                                      COLOR(WHT)
     A                                 12  2'Pris/pakkseddel. . . . . . .'
     A            C2KPPA    R        B 12 31REFFLD(RAKPPA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 46'Pris på faktura. . . . . . .'
     A            C2KNTO    R        B 12 75REFFLD(RAKNTO)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 13  2'Samlefaktura . . . . . . . .'
     A            C2KSAM    R        B 13 31REFFLD(RAKSAM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A*                                     VALUES(0 1 2 3 4)
     A                                 13 46'Fakturer etter prisbokenhet.'
     A            C2KBOK    R        B 13 75REFFLD(RAKBOK)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Sekvenskode. . . . . . . . .'
     A            C2KSEK    R        B 14 31REFFLD(RAKSEK)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A                                 14 46'Kundegruppe. . . . . +'
7.01 A            C2KUGR    R        B 14 70REFFLD(RAKUGR)
7.01 A  40                                  ERRMSG('Ugyldig kundegruppe' 40)
7.01 A                                      DSPATR(UL)
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
     A                                 15  1'Krav til registrering:'
     A                                      COLOR(WHT)
     A                                 16  2'Organisasjonsnummer. . . . .'
     A            C2KORG    R        B 16 31REFFLD(RAKORG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      VALUES(' ' 'J')
     A                                      COLOR(YLW)
     A                                 16 46'Personnummer . . . . . . . .'
     A            C2KPNR    R        B 16 75REFFLD(RAKPNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES(' ' 'J')
     A                                 17  2'Telefon. . . . . . . . . . .'
     A            C2KTLF    R        B 17 31REFFLD(RAKTLF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                      OVRDTA
     A                                 17 46'Kontaktperson. . . . . . . .'
     A            C2KKPE    R        B 17 75REFFLD(RAKKPE)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES(' ' 'J')
     A                                 18  2'E-mail . . . . . . . . . . .'
     A            C2KEMA    R        B 18 31REFFLD(RAKEMA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                      OVRDTA
6.30 A                                 18 46'Mobil  . . . . . . . . . . .'
     A            C2KFAX    R        B 18 75REFFLD(RAKFAX)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES(' ' 'J')
7.02 A                                 19 46'Krav til prosjekt. . . . . .'
7.02 A            C2KKPR    R        B 19 75REFFLD(RAKKPR)
7.02 A                                      DSPATR(UL)
7.02 A                                      COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A                                      VALUES(' ' 'J')
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  210643  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kundekategori '
     A                                      DSPATR(RI)
     A                                  4  2'Kundekategori. . .'
     A            D1KKOD    R        O  4 21REFFLD(RAKKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAKTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020325  210643  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kundekategori. . .'
     A            K1KKOD    R        B  4 21REFFLD(RAKKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kundekategori. . .'
     A            RAKKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAKTXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
