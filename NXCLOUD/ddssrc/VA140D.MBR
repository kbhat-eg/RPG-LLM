     A*%%TS  SD  20020308  084349  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020307  160346  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LTYP    R        O  8  6REFFLD(VALTYP)
     A            B1LTXT    R        O  8 11REFFLD(VALTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020308  084349  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av linjetype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2LTYP    R        B  5  6REFFLD(VALTYP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19931028  142557  AMD         REL-V2R2M0  5738-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020308  081215  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av linjetype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjetype. . . . . . . . . .'
     A            C1LTYP    R        B  5 31REFFLD(VALTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020308  083326  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Linjetype finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020308  080524  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av linjetype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjetype. . . . . . . . . .'
     A            C1LTYP    R        O  5 31REFFLD(VALTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Beskrivelse. . . . . . . . .'
     A            C2LTXT    R        B  6 31REFFLD(VALTXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Tekstlinje . . . . . . . . .'
     A            C2LKTX    R        B  8 31REFFLD(VALKTX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 46'1=Ja,0=Nei'
     A                                  9  2'Pris er beløp  . . . . . . .'
     A            C2LPBB    R        B  9 31REFFLD(VALPBB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 46'1=Ja,0=Nei'
     A                                 11  2'Oppdaterer Lager . . . . . .'
     A            C2LLAG    R        B 11 31REFFLD(VALLAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 46'1=Ja,0=Nei'
     A                                 12  2'Oppdaterer Økonomi . . . . .'
     A            C2LOKO    R        B 12 31REFFLD(VALOKO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 46'1=Ja,0=Nei'
     A                                 13  2'Oppdaterer Statistikk  . . .'
     A            C2LSTA    R        B 13 31REFFLD(VALSTA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 46'1=Ja,0=Nei'
     A                                 15  2'Fortegn  . . . . . . . . . .'
     A            C2LKRE    R        B 15 31REFFLD(VALKRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 46'(blank eller  - )'
     A                                 17  2'Skrives ikke på ordretype. .'
     A            C2LO01    R        B 17 31REFFLD(VALO01)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LO02    R        B 17 34REFFLD(VALO02)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LO03    R        B 17 37REFFLD(VALO03)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LO04    R        B 17 40REFFLD(VALO04)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020308  083326  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette linjetype '
     A                                      DSPATR(RI)
     A                                  4  2'Linjetype. . . . .'
     A            D1LTYP    R        O  4 21REFFLD(VALTYP)
     A                                  5  2'Beskrivelse. . . .'
     A            VALTXT    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020308  083706  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Linjetype. . . . .'
     A            K1LTYP    R        B  4 21REFFLD(VALTYP)
     A  31                                  ERRMSGID(VMA4010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA4030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Linjetype. . . . .'
     A            VALTYP    R        O  9 21OVRDTA
     A                                 10  2'Beskrivelse. . . .'
     A            VALTXT    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
