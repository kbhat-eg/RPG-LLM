     A*%%TS  SD  20051013  105647  HKO530      REL-V5R3M0  5722-WDS
     A*%%EC
7.xx A*     BSA  230216    Endringer relatert til ny GUI
7.01 A*     BSA  281119    Switch for visning av rabatt og spesialpriser
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041022  125743  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1KUND    R        O 10  4REFFLD(FLKUND)
     A            B1KNAV         9A  O 10 11
     A            B1KPRO    R        O 10 21REFFLD(FLKPRO)
     A            B1NAVN        23A  O 10 28
     A            B1SELG    R        O 10 52REFFLD(FLSELG)
     A            B1EPRO    R        O 10 57REFFLD(FLEPRO)
     A            B1TDAT         6Y 0O 10 73EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050608  122949  AMD530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundeprosjekt'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Frisøk . . . . .'
     A                                      COLOR(BLU)
7.xx A            B2STEK        35A  B  5 19COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 19'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 26'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 52'Sn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 57'Ekst.prosjnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Kunde '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 21'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 28'Navn                  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 52' Sn '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 57'Ekst. prosjnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 73'Til-dato'
     A                                      COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A            B2KUND    R        B  7  4REFFLD(FLKUND)
     A  31                                  ERRMSGID(FML0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 17'+'
7.xx A            B2KPRO    R        B  7 19REFFLD(FLKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NAVN        23A  B  7 26COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 50'+'
7.xx A            B2SELG    R        B  7 52REFFLD(FLSELG)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2EPRO    R        B  7 57REFFLD(FLEPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020308  143824  HKO         REL-V5R1M0  5722-WDS
     A                                 22  2'F1=Filter      F3=Gå tilbake      -
     A                                      F4=Spørring   F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
7.01 A                                 23  2'F9=Gjenta søk'
7.01 A                                      COLOR(BLU)
7.01 A N33                             23 17'F13=Spesialpriser'
7.01 A                                      COLOR(BLU)
7.01 A N34                             23 37'F14=Rabattmatrise'
7.01 A                                      COLOR(BLU)
     A N80                             23 61'F23=Vis kun utgåtte'
     A                                      COLOR(BLU)
     A  80                             23 61'F23=Vis alle'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051013  105321  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1KPRO    R        H      REFFLD(FLKPRO)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprette kundeprosjekt for kunde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kunde. . . . . . . . . . . +'
     A            C1KUND    R        B  5 31REFFLD(FLKUND)
     A  32                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 39COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020308  143824  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kundeprosjekt '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . .'
     A            D1KUND    R        O  4 21REFFLD(FLKUND)
     A                                  5  2'Prosjektnummer . .'
     A            D1KPRO    R        O  5 21REFFLD(FLKPRO)
     A                                  7  2'Navn . . . . . . .'
     A            FLNAVN    R        O  7 21
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20051013  104108  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(4 31 13 45)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Filter '
     A                                      DSPATR(RI)
     A                                  4  2'Utelate utgåtte . .'
     A            H1UTLA    R        B  4 22REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 30'1=Ja,0=Nei'
     A                                  7  2'Kundenr . . . . . +'
     A            H1KUND    R        B  7 22REFFLD(FLKUND)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  2'Eksternt prosjektnr'
     A            H1EPRO    R        B  9 22REFFLD(FLEPRO)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020308  144959  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . +'
     A            K1KUND    R        B  4 21REFFLD(FLKUND)
     A  31                                  ERRMSGID(FRK0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1KPRO    R        B  5 21REFFLD(FLKPRO)
     A                                      COLOR(YLW)
     A                                      DSPATR(ND)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Kunde. . . . . . .'
     A            FLKUND    R        O  9 21OVRDTA
     A                                 10  2'Prosjektnummer . .'
     A            FLKPRO    R        O 10 21OVRDTA
     A                                 12  2'Navn . . . . . . .'
     A            FLNAVN    R        O 12 21OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
