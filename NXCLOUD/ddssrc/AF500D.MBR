     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPFTP
     A* Format. . . . . : AF500D
     A* Beskrivelse . . : FTP jobber, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       101202 MH   Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%TS  SD  20020228  092906  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A*                                     CHKMSGID(MM00010 *LIBL/QMFUMAL)
     A            B1NAVN    R        O  7  5REFFLD(AFNAVN)
     A            B1BESK    R        O  7 16REFFLD(AFBESK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020228  092906  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge FTP overføring '
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V  FTP jobb   Beskrivelse'
     A                                      COLOR(WHT)
     A            B2NAVN    R        B  6  5REFFLD(AFNAVN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 70)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
