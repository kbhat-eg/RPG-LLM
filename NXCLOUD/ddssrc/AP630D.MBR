     A*%%TS  SD  20041103  231309  ASP         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AP630D
     A* Beskrivelse . . : Skrivere, vedlikehold PROA
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       140212 BSA  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1SPRT    R        O  8  5REFFLD(AFSPRT)
     A            B1SBES    R        O  8 16REFFLD(AFSBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printere - PROA'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  4'Printer'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A            B2SPRT    R        B  6  5REFFLD(AFSPRT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printere - PROA'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Printer. . . . . . . . . . .'
     A            C1SPRT    R        B  5 31REFFLD(AFSPRT)
     A  31                                  ERRMSGID(AMA0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A**                                    CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Printer finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20041103  231309  ASP         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printere - PROA'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Printer. . . . . . . . . . .'
     A            C1SPRT    R        O  5 31REFFLD(AFSPRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Beskrivelse. . . . . . . . .'
     A            C2SBES    R        B  6 31REFFLD(AFSBES)
     A  31                                  ERRMSGID(AMA0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 19  2'Opprettet dato . . . . . . .'
     A            C2SODA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2SEDA         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2SEUS    R        O 21 31REFFLD(AAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040907  100515  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette printer      '
     A                                      DSPATR(RI)
     A                                  4  2'Printer. . . . . .'
     A            D1SPRT    R        O  4 21REFFLD(AFSPRT)
     A                                  6  2'Beskrivelse. . . .'
     A            D1SBES        30A  O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040907  100515  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Printer. . . . . .'
     A            K1SPRT    R        B  4 21REFFLD(AFSPRT)
     A  31                                  ERRMSGID(AMA0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMA0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Printer. . . . . .'
     A            AFSPRT    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            K1SBES        30A  O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
