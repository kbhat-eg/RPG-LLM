     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
6.21 A* 191113   NHO Feilmelding dersom lager er ugyldig
6.21 A* 281113   NHO Endret feilindikator slik at PC kan
     A*              brukes for å se hvilket felt som er feil
6.22 A* 291113   NHO Var angitt CA04 istedenfor CF04 vedr F4
     A*              på bildet.  Felter ble blanket ut
6.31 A* 080219   bof Utvidet med kode forsmåte fabrikk.
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020521  094751  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG         1Y 0B 10  2COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A                                      EDTCDE(Z)
     A            B1LBUT    R        O 10  4REFFLD(AOLBUT)
     A            B1LNAV    R        O 10 20REFFLD(AOLNAV)
     A            B1LFGR    R        O 10 57REFFLD(AOLFGR)
     A            B1LFIR    R        O 10 63REFFLD(AOLFIR)
     A            B1LAVD    R        O 10 68REFFLD(AOLAVD)
     A            B1LLAG    R        O 10 74REFFLD(AOLLAG)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Systemadm.'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nettbutikk'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Butikkeier. . . .'
     A                                      COLOR(BLU)
     A            B2LEIE    R        O  5 20REFFLD(AOLEIE)
     A                                      COLOR(BLU)
     A            B2HNAV    R        O  5 31REFFLD(AOHNAV)
     A                                      COLOR(BLU)
     A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  8  2'V Butikk          Navn            -
     A                                                          Fgrp. Firma Avd-
     A                                      . Lager'
     A                                      COLOR(WHT)
     A            B2LBUT    R        B  9  4REFFLD(AOLBUT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2LNAV    R        B  9 20REFFLD(AOLNAV)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  094005  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Systemadm.'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nettbutikk'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Butikkeier . . . . . . . . .'
     A            C1LEIE    R        O  5 31REFFLD(AOLEIE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HNAV    R        O  5 35REFFLD(AOHNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Nettbutikk . . . . . . . . .'
     A            C1LBUT    R        B  6 31REFFLD(AOLBUT)
     A  31                                  ERRMSGID(AMO0060 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020515  135648  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 31)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Nettbutikk finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
6.20 A***                                   CA04
6.22 A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
6.20 A**                                    RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
6.20 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
6.20 A            W_ARCD        10A  H
6.20 A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Systemadm.'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nettbutikk'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Butikkeier . . . . . . . . .'
     A            C1LEIE    R        O  5 31REFFLD(AOLEIE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HNAV    R        O  5 35REFFLD(AOHNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Nettbutikk . . . . . . . . .'
     A            C1LBUT    R        O  6 31REFFLD(AOLBUT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Navn . . . . . . . . . . . .'
     A            C2LNAV    R        B  8 31REFFLD(AOLNAV)
     A  31                                  ERRMSGID(AMO0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Filgruppe. . . . . . . . . .'
     A            C2LFGR    R        B 10 31REFFLD(AOLFGR)
     A  32                                  ERRMSGID(AMO0110 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Firma. . . . . . . . . . . .'
     A            C2LFIR    R        B 11 31REFFLD(AOLFIR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12  2'Avdeling . . . . . . . . . .'
     A            C2LAVD    R        B 12 31REFFLD(AOLAVD)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14  2'Lager. . . . . . . . . . . .'
     A            C2LLAG    R        B 14 31REFFLD(AOLLAG)
6.21 A  34                                  ERRMSG('Koden er ikke gyldig')
     A                                      COLOR(YLW)
6.21 A  34                                  DSPATR(PC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 42'Blank = alle lagre'
6.20 A                                 15  2'Forsendelsesmåte hentes. . .'
6.20 A            C2HKOD    R        B 15 31REFFLD(AOHKOD)
6.20 A  33                                  ERRMSG('Koden er ikke gyldig')
6.20 A                                      COLOR(YLW)
6.21 A  33                                  DSPATR(PC)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C2HFTX        20A  O 15 42OVRDTA
6.20 A                                 16  2'Forsendelsesmåte leveres . .'
6.20 A            C2LKOD    R        B 16 31REFFLD(AOLKOD)
6.21 A**33                                  ERRMSG('Koden er ikke gyldig')
6.21 A  35                                  ERRMSG('Koden er ikke gyldig')
6.20 A                                      COLOR(YLW)
6.21 A  35                                  DSPATR(PC)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C2LFTX        20A  O 16 42OVRDTA
6.31 A                                 17  2'Forsendelsesmåte fabrikk . .'
6.31 A            C2FKOD    R        B 17 31REFFLD(AOFKOD)
6.31 A  36                                  ERRMSG('Koden er ikke gyldig')
6.31 A                                      COLOR(YLW)
6.31 A  36                                  DSPATR(PC)
6.31 A  30                                  DSPATR(PR)
6.31 A                                      OVRDTA
6.31 A            C2FFTX        20A  O 17 42OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2LODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2LEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2LEUS    R        O 20 31REFFLD(AOLEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.20 A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A**33                                  ERRMSG('Koden er ikke gyldig')
     A          R D1WIN
     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette nettbutikk '
     A                                      DSPATR(RI)
     A                                  4  2'Butikkeier . . . .'
     A            D1LEIE    R        O  4 21REFFLD(AOLEIE)
     A                                  5  2'Nettbutikk . . . .'
     A            D1LBUT    R        O  5 21REFFLD(AOLBUT)
     A                                  6  2'Beskrivelse. . . .'
     A            AOLNAV    R        O  6 21
     A                                  7  2'Filgruppe. . . . .'
     A            AOLFGR    R        O  7 21
     A                                  8  2'Firma. . . . . . .'
     A            AOLFIR    R        O  8 21
     A                                  9  2'Avdeling . . . . .'
     A            AOLAVD    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020521  122511  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Nettbutikk . . . .'
     A            K1LBUT    R        B  4 21REFFLD(AOLBUT)
     A  31                                  ERRMSGID(AMO0060 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMO0080 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Butikkeier . . . .'
     A            AOLEIE    R        O  9 21OVRDTA
     A                                 10  2'Nettbutikk . . . .'
     A            AOLBUT    R        O 10 21OVRDTA
     A                                 11  2'Beskrivelse. . . .'
     A            AOLNAV    R        O 11 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
