     A*%%TS  SD  20020429  124844  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : FO122D
     A* Beskrivelse . . : Vedlikehold av leveransedata
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19951101  180137  ASP         REL-V2R3M0  5738-PW1
     A            B1SUFF    R        H      REFFLD(FOSUFF)
     A            B1NUMM    R        O  7  2REFFLD(FONUMM)
6.NU A            B1OTYP    R        O  7 11REFFLD(FOOTYP)
6.NU A            B1KUNA        21A  O  7 14
     A            B1AVDE    R        B  7 36REFFLD(FOAVDE)
     A                                      COLOR(YLW)
     A            B1LDAT         6Y 0B  7 41EDTCDE(Y)
     A                                      COLOR(YLW)
     A            B1VREF    R        B  7 50REFFLD(FOVREF)
     A                                      COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  130153  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leveransedata'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 14'Kundenavn       '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 36'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 41'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 50'Vår referanse'
7.xx A**                                5  2'Ordre  OT Kundenavn        -
7.xx A**                                           Avd  Lev.dato Vår referan-
7.xx A**                                    se'
     A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  2REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020429  124755  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Bla-alternativer  F3=Gå tilbake-
     A                                         F5=Forny'
     A                                      COLOR(BLU)
     A          R H1BLD
     A*%%TS  SD  20020429  124844  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 40)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Bla alternativer:'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Ordre-type.'
     A            H1OTYP    R        B  4 14REFFLD(FOOTYP)
     A                                      COLOR(YLW)
     A                                      DLTCHK
     A                                      OVRDTA
7.xx A                                  5  2'Kunde-nr. +'
     A            H1KUND    R        B  5 14REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Bla bakfra'
     A            H1VALG    R        B  7 14REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 24'1=Ja,0=Nei'
     A                                 10  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
