     A*%%TS  SD  20110527  132024  BOF620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL520D
     A* Beskrivelse . . : Lager pr.vare, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       260511 bof  Nytt program
6.20 A*       130312 bof  paramterstyring om man får velge med seg lager
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20110526  145740  BOF620      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A  30                                  DSPATR(PR)
     A            B1LNAV    R  -15   O  7  7REFFLD(VVTEK1)
     A            B1BEHL         9Y 2O  7 28EDTWRD('     0 ,  -')
     A            B1LAGE    R        O  7  4REFFLD(VLLAGE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110527  132024  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A N50                              2  3'Lagervarer  '
     A                                      DSPATR(RI)
     A  50                              2  3'Skaffevarer '
     A                                      DSPATR(RI)
     A            B2TEK1    R   -7   B  4 11REFFLD(VVTEK1)
7.xx A            B2VARE    R   -7   B  4  2REFFLD(VLVARE)
7.xx A                                  5  2'Lager'
7.xx A            B2LAGE    R        B  5  8REFFLD(VLLAGE)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'LN'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  7'Lager'
7.xx A                                      COLOR(WHT)
     A                                  6 28'Beholdning'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051110  104501  SST540S     REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 40)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
