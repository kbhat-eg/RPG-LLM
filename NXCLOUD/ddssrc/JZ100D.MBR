     A*%%TS  SD  20080229  102145  BOF560      REL-V5R4M0  5722-WDS
     A*
7.xx A*   290716 bsa Endringer relatert til ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030826  094853  HKO         REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1OGRP    R        O  9  4REFFLD(JZOGRP)
7.xx A            B1HGRP    R        O  9  7REFFLD(JZHGRP)
7.xx A            B1UGRP    R        O  9 10REFFLD(JZUGRP)
7.xx A            B1UTXT        10A  O  9 14
7.xx A            B1LDOR    R        O  9 25REFFLD(JZLDOR)
7.xx A            B1MODN    R        O  9 32REFFLD(JZMODN)
7.xx A            B1VARE    R   -7   O  9 41REFFLD(JZVARE)
7.xx A            B1TEK1        11A  O  9 50
7.xx A            B1ENH1    R        O  9 62REFFLD(JZENH1)
7.xx A            B1ENH2    R        O  9 66REFFLD(JZENH2)
7.xx A            B1ENH3    R        O  9 70REFFLD(JZENH3)
7.xx A            B1ENI1    R        O  9 74REFFLD(JZENI1)
7.xx A            B1ENP1    R        O  9 78REFFLD(JZENP1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20071214  121347  BOF550      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forpakninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Varegruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 20'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 27'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 38'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7 62'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 66'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 74'In'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 78'Pb'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V Varegrp.             Lev.   Modu-
7.xx A**                                    lnr. Varenr.              Enh Enh E-
7.xx A**                                    nh In  Pb'
7.xx A**                                    COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 10'Ug '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 41'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 62'-1 '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 66'-2 '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'-3 '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 74'-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 78'-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2OGRP    R        B  5  4REFFLD(JZOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  7'+'
7.xx A            B2HGRP    R        B  5  9REFFLD(JZHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 12'+'
7.xx A            B2UGRP    R        B  5 14REFFLD(JZUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 18'+'
7.xx A            B2LDOR    R        B  5 20REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2MODN    R        B  5 27REFFLD(JZMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 36'+'
7.xx A            B2VARE    R   -7   B  5 38REFFLD(JZVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A**                                7 62'-1  -2  -3  -1  -1'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020819  142918  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19990820  125555  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forpakninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  4 31REFFLD(JZOGRP)
     A  31                                  ERRMSGID(JMZ0050 *LIBL/QMFVADM 31)
     A  35                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  4 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  5 31REFFLD(JZHGRP)
     A  36                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  6 31REFFLD(JZUGRP)
     A  37                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 37)
     A  38                                  ERRMSGID(JMZ0060 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  7 31REFFLD(JPLDOR)
     A  39                                  ERRMSGID(JML0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV    R        O  7 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        B  8 31REFFLD(JZMODN)
     A  41                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MNAV    R        O  8 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(JZVARE)
     A  40                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  9 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990817  144537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Forpakning finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20071214  123812  BOF550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Styring av enheter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  4 31REFFLD(JZOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  5 31REFFLD(JZHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  6 31REFFLD(JZUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  7 31REFFLD(JVLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  7 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O  8 31REFFLD(JZMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MNAV    R        O  8 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(JZVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  9 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Salgsenhet-1 . . . . . . . +'
     A            C2ENH1    R        B 11 31REFFLD(JZENH1)
     A  31                                  ERRMSGID(JMZ0100 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ETX1        35A  O 11 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Salgsenhet-2 . . . . . . . +'
     A            C2ENH2    R        B 12 31REFFLD(JZENH2)
     A  33                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ETX2        35A  O 12 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Salgsenhet-3 . . . . . . . +'
     A            C2ENH3    R        B 13 31REFFLD(JZENH3)
     A  34                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ETX3        35A  O 13 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 15  2'Innkjøpsenhet-1. . . . . . +'
     A            C2ENI1    R        B 15 31REFFLD(JZENI1)
     A  35                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ITX1        35A  O 15 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16  2'Innkjøpsenhet-2. . . . . . +'
     A            C2ENI2    R        B 16 31REFFLD(JZENI2)
     A  36                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ITX2        35A  O 16 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 17  2'Innkjøpsenhet-3. . . . . . +'
     A            C2ENI3    R        B 17 31REFFLD(JZENI3)
     A  37                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ITX3        35A  O 17 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 19  2'Prisbokenhet-1 . . . . . . +'
     A            C2ENP1    R        B 19 31REFFLD(JZENP1)
     A  38                                  ERRMSGID(JMZ0110 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PTX1        35A  O 19 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 20  2'Prisbokenhet-2 . . . . . . +'
     A            C2ENP2    R        B 20 31REFFLD(JZENP2)
     A  39                                  ERRMSGID(JMZ0110 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PTX2        35A  O 20 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 21  2'Prisbokenhet-3 . . . . . . +'
     A            C2ENP3    R        B 21 31REFFLD(JZENP3)
     A  40                                  ERRMSGID(JMZ0110 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PTX3        35A  O 21 41COLOR(YLW)
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 58'/'
     A            C2EDAT         6Y 0O 22 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 69'/'
     A            C2EUSR    R        O 22 71REFFLD(JZEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030826  094447  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette forpakning '
     A                                      DSPATR(RI)
     A                                  4  2'Varegruppe . . . .'
     A            D1OGRP    R        O  4 21REFFLD(JZOGRP)
     A            D1HGRP    R        O  4 24REFFLD(JZHGRP)
     A            D1UGRP    R        O  4 27REFFLD(JZUGRP)
     A                                  5  2'Leverandør . . . .'
     A            D1LDOR    R        O  5 21REFFLD(JZLDOR)
     A                                  6  2'Modulnr. . . . . .'
     A            D1MODN    R        O  6 21REFFLD(JZMODN)
     A                                  7  2'Varenummer . . . .'
     A            D1VARE    R        O  7 21REFFLD(JZVARE)
     A                                  9  2'Salgsenhet-1 . . .'
     A            JZENH1    R        O  9 21
     A                                 10  2'Salgsenhet-2 . . .'
     A            JZENH2    R        O 10 21
     A                                 11  2'Salgsenhet-3 . . .'
     A            JZENH3    R        O 11 21
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20080229  102145  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varegruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(JZOGRP)
     A  31                                  ERRMSGID(JMZ0050 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMZ0030 *LIBL/QMFVADM 32)
     A  35                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 24'+'
7.xx A            K1HGRP    R        B  4 26REFFLD(JZHGRP)
     A  36                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 29'+'
7.xx A            K1UGRP    R        B  4 31REFFLD(JZUGRP)
     A  37                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Leverandør . . . +'
     A            K1LDOR    R        B  5 21REFFLD(JZLDOR)
     A  38                                  ERRMSGID(JMZ0060 *LIBL/QMFVADM 38)
     A  39                                  ERRMSGID(JML0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Modulnr. . . . . .'
     A            K1MODN    R        B  6 21REFFLD(JZMODN)
     A  41                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  7  2'Varenummer . . . +'
     A            K1VARE    R   -7   B  7 21REFFLD(JZVARE)
     A  40                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Varegruppe . . . .'
     A            JZOGRP    R        O 10 21OVRDTA
     A            JZHGRP    R        O 10 24OVRDTA
     A            JZUGRP    R        O 10 27OVRDTA
     A                                 11  2'Leverandør . . . .'
     A            JZLDOR    R        O 11 21OVRDTA
     A                                 12  2'Modulnr. . . . . .'
     A            JZMODN    R        O 12 21OVRDTA
     A                                 13  2'Varenummer . . . .'
     A            JZVARE    R        O 13 21OVRDTA
     A                                 14  2'Salgsenhet-1 . . .'
     A            JZENH1    R        O 14 21OVRDTA
     A                                 15  2'Salgsenhet-2 . . .'
     A            JZENH2    R        O 15 21OVRDTA
     A                                 16  2'Salgsenhet-3 . . .'
     A            JZENH3    R        O 16 21OVRDTA
     A                                 17  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
