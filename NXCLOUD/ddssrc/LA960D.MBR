     A*%%TS  SD  20071011  152614  ASK550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LA660D
     A* Beskrivelse . . : Oppdatering av lager fra lagerbok
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       260802 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20071011  152614  ASK550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A  32        B1MLD         78   M
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppdatering av lager utfra lagerbo-
     A                                      k'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Oppdater lager nr. . . . . .'
     A            B1LAGE         2Y 0B  5 31COLOR(YLW)
     A  31                                  ERRMSGID(LMQ0320 *LIBL/QMFLAGR 31)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1LATX        30A  O  5 40COLOR(GRN)
     A                                  6  2'Varenr - hvis en vare  . . .'
     A            B1VARE         8Y 0B  6 31COLOR(YLW)
     A  33                                  ERRMSGID(LMV0010 *LIBL/QMFLAGR 33)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1VATX        35A  O  6 40COLOR(GRN)
     A                                  7  2'Dato f.o.m. i lagerbok . . .'
     A            B1DATE         6Y 0B  7 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LM00250 *LIBL/QMFLAGR 32)
     A                                 10  2'Programmet rekalkulerer lagerbehol-
     A                                      dning på alle varer i angitt lager.'
     A                                 12  2'Beholdning fra angitt dato brukes -
     A                                      som grunlag for rekalkuleringen.'
     A                                 13  2'Eventuelle lagertellinger vil gi n-
     A                                      y startbeholdning for rekalkulering-
     A                                      .'
     A                                 14  2'Akkumulerte beholdningstall i lage-
     A                                      rboka oppdateres.'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 11  2'Dersom en enkelt vare er angitt, b-
     A                                      ehandles bare denne'
