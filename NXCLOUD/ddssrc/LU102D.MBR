     A*%%TS  SD  20030629  201202  ASK         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LU102D
     A* Beskrivelse . . : Registrering av lager-tellebunter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R E2BLD
     A*%%TS  SD  20020925  092242  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 18 61)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CSRLOC(W_AWRO W_AWCO)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL +
     A                                      &W_AWRO &W_AWCO)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_AWRO         3S 0H
     A            W_AWCO         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Behandling av bunt-hode lagertell-
     A                                      ing '
     A                                      DSPATR(RI)
     A                                  5  2'Buntnummer  . .'
     A            E2NUMM    R        B  5 18REFFLD(LUNUMM)
     A                                      CHECK(RB)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  5 27'(Blank gir neste)'
     A                                  6  2'Beskrivelse . .'
     A            E2TEXT    R        B  6 18REFFLD(LUTEXT)
     A  32                                  ERRMSGID(LMU0120 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  21                                  DSPATR(PC)
     A                                  7  2'Operatør  . . .'
     A            E2OPER    R        B  7 18REFFLD(LUOPER)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
7.xx A                                  9  2'Lager-nummer. +'
     A            E2LAGE    R        B  9 18REFFLD(LULAGE)
     A  34                                  ERRMSGID(LMU0140 *LIBL/QMFLAGR 34)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            E2LATX        20A  O  9 22
7.xx A                                 11  2'Type  . . . . +'
     A            E2TYPE    R        B 11 18REFFLD(LUTYPE)
     A  35                                  ERRMSGID(LMU0150 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LMU0160 *LIBL/QMFLAGR 36)
     A  37                                  ERRMSGID(LMU0170 *LIBL/QMFLAGR 37)
     A                                      CHKMSGID(LMU0150 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A                                      VALUES('LISTE' 'MANUELL' 'HTSK' 'HT-
     A                                      PC' 'DIVERSE' 'SCANNE')
     A                                 17  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
