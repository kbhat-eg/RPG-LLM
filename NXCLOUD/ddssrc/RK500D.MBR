     A*%%TS  SD  20030702  232436  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK500D
     A* Beskrivelse . . : Kunder, spørring - subrogram
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
6.30 A*       141114 BSA  Justeringer pga Jacada
7.xx A*       010216 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020730  212736  KOB         REL-V5R1M0  5722-WDS
6.30 A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
6.30 A            B1NAVN    R        O  8  4REFFLD(RKNAVN)
6.30 A            B1GATE        20A  O  8 35
6.30 A            B1STED        10A  O  8 56
6.30 A            B1KUND         6Y 0O  8 67EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030702  232436  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  1 20'Spørring på kunder'
     A                                      COLOR(WHT)
7.xx A                                  1  1'RK500B2'
7.xx A                                      COLOR(WHT)
     A                                  3  2'Frisøk . . . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  3 17COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  4  2'Alfasøk. . . .'
7.xx A                                      COLOR(BLU)
7.xx A            B2ALFA    R        B  4 17REFFLD(RKALFA)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                  6  2' 1=Velg  5=Vis'
     A                                      COLOR(BLU)
6.30 A*                                 4 10'5=Vis'
6.30 A*                                     COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Navn                          '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 35'Adresse             '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 56'Poststed  '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'Kunde '
7.xx A**                                6  2'V Navn                           A-
7.xx A**                                    dresse              Poststed   Kund-
7.cc A**                                    e'
     A                                      COLOR(WHT)
7.xx A**          B2ALFA    R        B  7  4REFFLD(RKALFA)
7.xx A**                                    COLOR(YLW)
7.xx A**                                    OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020921  132454  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      WINDOW(1 2 20 73)
6.30 A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.30 A                                 19 17'F5=Forny'
     A                                      COLOR(BLU)
6.30 A                                 19 27'F6=Alle/Aktive'
     A                                      COLOR(BLU)
6.30 A                                 19 43'F9=Gjenta søk'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
