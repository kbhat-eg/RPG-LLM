     A*%%TS  SD  20021027  141145  MHA         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : HV700D
     A* Beskrivelse . . : Produsere info til håndterminal
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  220316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20000518  101214  ASK         REL-V4R4M0  5769-PW1
     A*                                     WINDOW(*DFT 16 67)
     A                                      CA03
     A                                      CF04
     A                                      CF09
     A                                      CA12
     A*                                     PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Produsere info til håndterminal  '
     A                                      DSPATR(RI)
7.xx A                                  5  2'Varesortiment. . +'
     A            B1VSOR    R        B  5 21REFFLD(VCVSOR)
     A  31                                  ERRMSGID(LM00260 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LM00270 *LIBL/QMFLAGR 32)
     A*                                     OVRDTA
     A                                      COLOR(YLW)
     A            B1BESK    R        O  5 32REFFLD(VCBESK)
7.xx A                                  6  2'Lager  . . . . . +'
     A            B1LAGE    R        B  6 21REFFLD(VCLAGE)
     A  39                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 39)
     A*                                      OVRDTA
     A                                      COLOR(YLW)
     A            B1LTXT    R        O  6 32REFFLD(VCBESK)
     A                                  7  2'Prisdato . . . . .'
     A            B1DATO         6Y 0B  7 21COLOR(YLW)
     A  33                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 33)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A*                                      OVRDTA
7.xx A                                  9  2'Kundekategorier. +'
     A            B1KAT1         4   B  9 21COLOR(YLW)
     A  34                                  ERRMSGID(LM00280 *LIBL/QMFLAGR 34)
     A*                                      OVRDTA
     A            B1KTE1        20   O  9 26
     A*                                      OVRDTA
7.xx A                                 10  2'("ALLE" gir alle)+'
     A            B1KAT2         4   B 10 21COLOR(YLW)
     A  35                                  ERRMSGID(LM00280 *LIBL/QMFLAGR 35)
     A*                                      OVRDTA
     A            B1KTE2        20   O 10 26
     A*                                      OVRDTA
7.xx A                                 11 19'+'
     A            B1KAT3         4   B 11 21COLOR(YLW)
     A  36                                  ERRMSGID(LM00280 *LIBL/QMFLAGR 36)
     A*                                      OVRDTA
     A            B1KTE3        20   O 11 26
     A*                                      OVRDTA
7.xx A                                 12 19'+'
     A            B1KAT4         4   B 12 21COLOR(YLW)
     A  37                                  ERRMSGID(LM00280 *LIBL/QMFLAGR 37)
     A*                                      OVRDTA
     A            B1KTE4        20   O 12 26
     A*                                      OVRDTA
7.xx A                                 13 19'+'
     A            B1KAT5         4   B 13 21COLOR(YLW)
     A  38                                  ERRMSGID(LM00280 *LIBL/QMFLAGR 38)
     A*                                      OVRDTA
     A            B1KTE5        20   O 13 26
     A*                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Spørring  F9=Var-
     A                                      esortiment'
     A                                      COLOR(BLU)
     A*
     A          R B2BLD
     A*%%TS  SD  20021027  141145  MHA         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 8 30)
     A                                      KEEP
     A                                      OVERLAY
     A                                      CA03
     A                                  2  2'Valg av håndterminal'
     A                                      COLOR(WHT)
     A                                  4  2'Valg. . . . .'
     A            B2VALG    R        B  4 17REFFLD(FÅSVAR)
     A  40                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 40)
     A                                      COLOR(YLW)
     A                                  4 20'1=Meto'
     A                                  5 20'2=PSI'
     A                                  6 20'3=Annen'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
