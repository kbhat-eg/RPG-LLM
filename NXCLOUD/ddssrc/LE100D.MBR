     A*%%TS  SD  20020311  105820  ASK         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LE100D
     A* Beskrivelse . . : Vedlikehold av telleliste
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*  6.21 101013 AMD  Editword skapte problemer i skjermbildet,
     A*                   dette er endret til edit kode i stedet.
7.xx A* 7.xx  190516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%TS  SD  20021216  230453  ASK         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20010726  152413  ASK         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 4 7)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1LINE    R        O  8  4REFFLD(LELINE)
     A            B1VARE    R   -7   O  8 11REFFLD(LEVARE)
     A            B1LAGE    R        O  8 20REFFLD(LELAGE)
     A            B1TEK1    R        O  8 23REFFLD(LETEK1)
6.21 A            B1ANTT        11  3O  8 59EDTCDE(4)
     A            B1ENH1    R        O  8 72REFFLD(LEENH1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20021216  203757  ASK         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av telleliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 13'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  4=Slett  7=Fortløpende'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Varenr '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 23'Beskrivelse        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 72'Enh'
7.xx A**                                7  2'V Linje  Varenr   Lg Beskrivelse  -
7.xx A**                                                               Antall E-
7.xx A**                                    nh'
     A                                      COLOR(WHT)
7.xx A            B2LINE    R        B  5  4REFFLD(LELINE)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  5 11'+'
7.xx A            B2VARE    R   -7   B  5 13REFFLD(LEVARE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010725  151033  ASK         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Forny'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20021216  230453  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(9 16 10 38)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                  1  2'Endre informasjon på angitt vare'
     A                                      DSPATR(RI)
     A            C2TEK1    R        O  3  2REFFLD(LETEK1)
     A                                  5  2'Antall    :'
     A            C2ANTT    R        B  5 15REFFLD(LEANTT)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C2ENH1    R        B  5 29REFFLD(FFRFR/VVENHL *LIBL/FFRF)
     A  31                                  ERRMSGID(LM00080 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  6  2'Salgspris :'
     A            C2SPNY    R        B  6 15REFFLD(LESPNY)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7  2'Kostpris  :'
     A            C2KPNY    R        B  7 15REFFLD(LEKPNY)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20021216  230453  ASK         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 8 38)
     A                                  1  2' Slette tellelinje '
     A                                      DSPATR(RI)
     A                                  3  2'Linje'
     A            B4LINE    R        O  3  9REFFLD(LELINE)
     A            B4TEK1    R        O  4  2REFFLD(LETEK1)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
