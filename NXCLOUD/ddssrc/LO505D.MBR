     A*%%TS  SD  20070301  121009  ASK550      REL-V5R4M0  5722-WDS
6.31 A*          020315 nho  Endret teskt på bildet
7.xx A*          020216 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A*%%EC
7.01 A                                      DSPSIZ(27 132 *WIDE)
7.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19990618  010445  ASK         REL-V4R2M0  5769-PW1
     A            B1NUMM    R        H      REFFLD(LONUMM)
     A            B1SUFF    R        H      REFFLD(LOSUFF)
     A            B1LINE    R        H      REFFLD(LOLINE)
7.01 A            B1VALG    R        B 12  2REFFLD(LÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('5' ' ')
     A                                      CHKMSGID(LMO0270 *LIBL/QMFLAGR)
7.01 A            B1VARE    R        O 12  4REFFLD(LDVARE)
7.01 A            B1TEXT        70A  O 12 20
7.01 A            B1ANTA    R        O 12 91REFFLD(LDANTA)
7.01 A            B1ENH1    R        O 12106REFFLD(LDENH1)
7.01 A            B1RABA    R        O 12110REFFLD(LDRAB1)
7.01 A                                      EDTCDE(M)
7.01 A            B1SUMI    R        O 12118REFFLD(LDSUMI)
7.01 A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070301  121009  ASK550      REL-V5R4M0  5722-WDS
7.01 A                                      SFLSIZ(0015)
7.01 A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
6.30 A                                      CF14
     A                                      CA12
     A                                      CF20
     A                                      PUTOVR
     A                                      SFLCSRRRN(&WCURRN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
7.01 A            L_FNAV        30A  O  1  2COLOR(WHT)
7.01 A                                  1 46'Lager/Innkjøp'
7.01 A                                      COLOR(WHT)
7.01 A                                  1118'Kl. :'
7.01 A                                      COLOR(WHT)
7.01 A                                  1124TIME
7.01 A                                      COLOR(WHT)
7.01 A                                  2  2'Vise bestilling'
7.01 A                                      COLOR(WHT)
7.01 A                                  2 38'LO505B2'
7.01 A                                      COLOR(WHT)
7.01 A                                  2 46'Bruker:'
7.01 A                                      COLOR(WHT)
7.01 A                                  2 54USER
7.01 A                                      COLOR(WHT)
7.01 A                                  2118'Dato:'
7.01 A                                      COLOR(WHT)
7.01 A                                  2124DATE
7.01 A                                      EDTCDE(Y)
7.01 A                                      COLOR(WHT)
7.01 A                                  3  1'-----------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.01 A                                  4  2'Lever.'
7.01 A            B2KUND    R        O  4 11REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A            B2KNAV    R   -7   O  4 18REFFLD(LONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  4 71'Best.nr. . '
7.01 A            B2NUMM    R        O  4 83REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A            B2SUFF    R        O  4 92REFFLD(LOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A            B2KGAT    R        O  5 11REFFLD(LOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  5 71'Best.dato. '
7.01 A            B2BDAT         6  0O  5 83EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A            B2KSTE    R        O  6 11REFFLD(LOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  6 71'Lev. dato. '
7.01 A            B2LDAT         6  0O  6 83EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.31 A**                                7 42'Mott.dato. .'
7.01 A                                  7 71'Bekr.LevDto'
7.01 A            B2LBDA         6  0O  7 83EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Lev.adr'
7.01 A            B2NAVN    R        O  8 11REFFLD(LONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  4 95'Ordrenr. .'
7.01 A            B2ORNR    R        O  4106REFFLD(LOORNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ADR1    R        O  9 11REFFLD(LOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  5 95'Vår ref. .'
7.01 A            B2VREF        26A  O  5106COLOR(YLW)
     A                                      OVRDTA
7.01 A            B2STED    R        O  9 42REFFLD(LOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  6 95'Lever. ref'
7.01 A            B2DREF        26A  O  6106COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  6 43'Lev.onr.'
7.01 A            B2SORN    R        O  6 52REFFLD(LOSORN)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.01 A                                  7 95'Best.total'
7.01 A            B2SALG    R        O  7114REFFLD(LOTOTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                 10  2'5=Vis'
     A                                      COLOR(BLU)
7.01 A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.01 A                                 11  4'Varenr.'
7.01 A                                      COLOR(WHT)
7.01 A                                 11 20'Varetekst'
7.01 A                                      COLOR(WHT)
7.01 A                                 11 91'Antall'
7.01 A                                      COLOR(WHT)
7.01 A                                 11106'Enh'
7.01 A                                      COLOR(WHT)
7.01 A                                 11110'Rab'
7.01 A                                      COLOR(WHT)
7.01 A                                 11118'Beløp'
7.01 A                                      COLOR(WHT)
7.01 A*                                13  4'Varenr.  Varetekst              -
7.01 A*                                                 Antall   Enh   Rab     -
7.01 A*                                      Beløp'
7.xx A*                                13  2'V   Varenr.  Varetekst            -
7.xx A*                                                 Antall Enh  Rab       B-
7.xx A*                                     eløp  '
7.01 A*                                     COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19990602  000120  AMD         REL-V4R2M0  5769-PW1
7.01 A                                 27  2'F3=Gå tilbake  F14=Leverd.info.   -
     A                                                                         -
     A                                               '
     A                                      COLOR(BLU)
