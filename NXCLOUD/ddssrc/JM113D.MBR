     A*%%TS  SD  19990813  140436  HKO         REL-V4R4M0  5769-PW1
     A*%%EC
     A**--------------------------------------------------------
7.xx A*       171116 bsa  Endringer relatert til ny GUI
     A*
     A**--------------------------------------------------------
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990813  140436  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 14 61)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Påslag til kostpris '
     A                                      DSPATR(RI)
     A                                  4  2'Utplukk:'
     A                                      DSPATR(UL)
7.xx A                                  5  2'Prisgivende leverandør +'
7.xx A            B1PLDO    R        B  5 27REFFLD(JLLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JML0020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1LNAV    R  -15   O  5 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Varegruppe . . . . . . +'
7.xx A            B1OGRP    R        B  6 27REFFLD(JVOGRP)
     A  33                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 30'+'
     A            B1HGRP    R        B  6 32REFFLD(JVHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 35'+'
     A            B1UGRP    R        B  6 37REFFLD(JVUGRP)
     A  35                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1UTXT        20A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kostpris:'
     A                                      DSPATR(UL)
7.xx A                                  9  2'Påslag til kostpris. . +'
7.xx A            B1PSLK         4Y 2B  9 27COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
7.xx A                                  9 33'%'
     A                                 13  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
