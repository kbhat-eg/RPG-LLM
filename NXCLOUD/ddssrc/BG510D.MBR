     A*%%TS  SD  20080225  210242  ASK560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BG510D
     A* Beskrivelse . . : Vise gavekort
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041102  191648  HKO530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5 7)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A            B1ODAT         6Y 0O  8  4EDTWRD('  .  .  ')
     A            B1GAVE    R        O  8 13REFFLD(BGGAVE)
     A            B1KUND    R        O  8 26REFFLD(BGKUND)
7.xx A            B1NAVN    R  -11   O  8 33REFFLD(BGNAVN)
7.xx A            B1OBEL    R        O  8 58REFFLD(BGGBEL)
     A            B1GBEL    R        O  8 70REFFLD(BGGBEL)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041102  193557  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Gavekort'
     A                                      COLOR(WHT)
7.xx A                                  2 38'BG510B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  6'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 13'Gavekortnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 28'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 35'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  5=Vis  7=Kjøp på gavekort-
     A                                      et'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  6'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 13'Gavekortnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 26'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 33'Navn           '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'OprBel'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 72'GjeBel'
7.xx A**                                6  2'V   Dato   Gavekortnr   Kunde  Nav-
7.xx A**                                    n                          Oppr.   -
7.xx A**                                     Gjenst.'
     A                                      COLOR(WHT)
7.xx A            B2ODAT         6Y 0B  5  4COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2GAVE    R        B  5 13REFFLD(BGGAVE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 26'+'
7.xx A            B2KUND    R        B  5 28REFFLD(BGKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NAVN    R  -10   B  5 35REFFLD(BGNAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A**                                7 63'beløp    beløp'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20041102  192054  HKO530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080225  210242  ASK560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A N30                              2  2'Vise gavekort'
     A  30                              2  2'Endre gavekort'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gavekortnr . . . . . . . . . .'
     A            C1GAVE    R        O  5 33REFFLD(BGGAVE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Mottaker:'
     A                                      COLOR(WHT)
     A                                  8  2'Kunde. . . . . . . . . . . . .'
     A            C2KUND    R        O  8 33REFFLD(BGKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Navn . . . . . . . . . . . . .'
     A            C2NAVN    R        O  9 33REFFLD(BGNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Opprinnelig beløp. . . . . . .'
     A            C2OBEL    R        O 11 33REFFLD(BGOBEL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Gjenstående beløp. . . . . . .'
     A            C2GBEL    R        B 12 33REFFLD(BGGBEL)
     A N30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      CHECK(RB)
     A                                 14  2'Bongnr . . . . . . . . . . . .'
     A            C2BONG    R        O 14 33REFFLD(BGBONG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 58'/'
     A            C2EDAT         6Y 0O 22 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 69'/'
     A            C2EUSR    R        O 22 71REFFLD(BGEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F13=Kjøp på gaveko-
     A                                      rtet'
     A                                      COLOR(BLU)
