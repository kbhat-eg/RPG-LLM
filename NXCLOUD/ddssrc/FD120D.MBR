6.20  * R6.20 110211 NHO  Prosjekt og aktivitet fjernet fra bildet.    ttet
      *
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.22 A* R6.20 240913 BOF  Ved endring levtype,sjekk om best.er laget
6.24 A* R6.20 030114 BOF  Ved endring levtype og akktyp>= 2,så feil
6.30 A* R6.30 100114 BOF  BM Pakke - nytt popup Oppsalg/Mersalg
6.35 A* R6.30 151014 BSA  Innfører F tast for visning av NOBB info. Erstatter
6.35 A*                   koding i jacada, samt gir tilsvarenede tilgang
6.35 A*                   i grønt.
6.36 A* R6.30 160318 BKV  Nye koder på i FFRR FDKPRI
7.xx A* 7.xx  270117 bkv  Justeringer basert på ny GUI og regler
7.xx A*                   Popup til window
7.13 A* R     021023 MST  Endret editeringskode på felt C1ODEK
     A*
8.01 A*PRG2   080622 bof  Utvidet prisgruppe 1 til 2 posisjoner
     A*
     A*
     A*
     A*%%TS  SD  20091104  110941  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C1WIN
     A*%%TS  SD  20091104  110941  BOF610      REL-V5R4M0  5722-WDS
     A*                                     WINDOW(*DFT 22 67)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF08
     A                                      CF10
     A                                      CF13
     A                                      CF14
     A                                      CF15
6.35 A                                      CF18
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1BLDO    R        H      REFFLD(VVLDOR)
     A            C1BOGR    R        H      REFFLD(VVOGRP)
     A            C1BHGR    R        H      REFFLD(VVHGRP)
     A            C1BUGR    R        H      REFFLD(VVUGRP)
     A            C1BMOD    R        H      REFFLD(VVNMOD)
     A            C1BVAR    R        H      REFFLD(VVVARE)
     A            C1BVTY    R        H      REFFLD(VVVTYP)
     A            C1BLET         1Y 0H
     A            C1BBET        35A  H
     A            C1BNTP         9Y 2H
     A            C1IFAK         8Y 6H
     A            C1INTP         9Y 2H
     A            C1POGR    R        H      REFFLD(VVOGRP)
     A            C1PHGR    R        H      REFFLD(VVHGRP)
     A            C1PUGR    R        H      REFFLD(VVUGRP)
     A            C1PLDO    R        H      REFFLD(VVLDOR)
     A            C1PMOD    R        H      REFFLD(VVNMOD)
     A            C1PVAR    R        H      REFFLD(VVVARE)
     A            C1PKPR    R        H      REFFLD(VVKPRI)
     A            C1PVTY    R        H      REFFLD(VVVTYP)
     A            C1PLET         1Y 0H
     A            C1KFAK         8Y 6H
     A            C1KBEL         7Y 2H
     A            C1SFAK         8Y 6H
     A            C1ALME    R        H      REFFLD(FDALME)
     A            C1OMVA    R        H      REFFLD(FDOMVA)
7.xx A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  1 46'Ordre/Fakturering'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 72TIME
7.xx A                                      COLOR(WHT)
7.xx A                                  2  2'Reg av varelinjer - skaffreg.'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 38'FD120C1'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 54USER
7.xx A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 72DATE
7.xx A                                      EDTCDE(Y)
7.xx A                                      COLOR(WHT)
7.xx A                                  3  1'-----------------------------------
7.xx A                                      ------------------------------------
7.xx A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Antall        Enh  Varenr   Nobbnr'
     A                                      COLOR(WHT)
     A            C1ODEK         5  2O  4 61COLOR(BLU)
7.13 A*                                     EDTCDE(4)
7.13 A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ANT1    R        B  5  2REFFLD(FDANTA)
     A  33                                  ERRMSGID(FMD0150 *LIBL/QMFOFAK 33)
     A  49                                  ERRMSGID(FM00630 *LIBL/QMFOFAK 49)
     A  50                                  ERRMSGID(FMD0710 *LIBL/QMFOFAK 50)
6.3g A  51                                  ERRMSGID(FMD0151 *LIBL/QMFOFAK 51)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENH1    R        B  5 16REFFLD(FDENH1)
     A  31                                  ERRMSGID(VMA1010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 32)
     A  81                                  COLOR(YLW)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1VARE    R   -7   O  5 20REFFLD(FDVARE)
     A                                      OVRDTA
     A            C1NOBB    R        O  5 29REFFLD(FDNOBB)
     A                                      OVRDTA
     A            C1NTSU        11Y 2O  5 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTPR    R        O  5 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1ANT2    R        B  6  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  82                                  DSPATR(ND)
     A  82                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH2    R        O  6 16REFFLD(FDENH1)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1NISU        11Y 2O  6 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTIM    R        O  6 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1ANT3    R        B  7  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  83                                  DSPATR(ND)
     A  83                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH3    R        O  7 16REFFLD(FDENH1)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                  8 21'Salgspris'
     A                                      COLOR(WHT)
7.xx A                                  8 31'Rab-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 39'Rab-2'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'K'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 72'LT'
7.xx A                                      COLOR(WHT)
7.xx A            C1ANTA    R        O  9  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENHE    R        O  9 16REFFLD(FDENH1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SAPR         9Y 2B  9 20EDTWRD('     0 ,  ')
     A  39                                  ERRMSGID(FMD0170 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1RAB1    R        B  9 31REFFLD(FDRAB1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1RAB2    R        B  9 39REFFLD(FDRAB2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
7.xx A                                  9 47'+'
7.xx A            C1LETY    R        B  9 49REFFLD(FDLETY)
     A  34                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 34)
6.22 A  45                                  ERRMSGID(VMY0200 *LIBL/QMFOFAK 45)
6.24 A  46                                  ERRMSGID(FMO0740 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1KPRI    R        B  9 51REFFLD(FDKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 53'(prisk)'
7.xx A            C1KOPR    R        B  9 61REFFLD(FDKOPR)
     A                                      COLOR(YLW)
7.03 A*                                      EDTWRD('       ,  ')
7.03 A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            C1LTYP    R        B  9 72REFFLD(FDLTYP)
     A  42                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 42)
     A  43                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 43)
     A  44                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        B 10 20REFFLD(FDTEK1)
     A  35                                  ERRMSGID(FMD0180 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            C1BRTY    R        O 10 59REFFLD(FDBRTY)
     A                                      OVRDTA
     A            C1BRR1    R        O 10 61REFFLD(FDBRR1)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1TEK2    R        B 11 20REFFLD(FDTEK2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BRR2    R        O 11 61REFFLD(FDBRR2)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 12 35'Prisgivende lev.'
     A            C1LDOR    R        O 12 52REFFLD(FDLDOR)
     A                                      OVRDTA
     A                                 12  2'Salgspris inkl. .'
     A            C1SAIM         9Y 2B 12 20EDTWRD('       ,  ')
     A  36                                  ERRMSGID(FMD0210 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        32A  O 13 35OVRDTA
     A                                 13  2'Dekningsgrad (DG)'
     A            C1DEKN    R        B 13 20REFFLD(FDPASL)
     A  37                                  ERRMSGID(FMD0220 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A                                      OVRDTA
     A                                 14 35'Pris fra-dato. .'
     A            C1GDAT         6Y 0O 14 52EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2'Påslag. . . . . .'
     A            C1PASL         5Y 2B 14 20COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A                                      OVRDTA
     A                                 15 35'Nobb-pris. . . .'
     A            C1NOPR         9Y 2O 15 52EDTCDE(4)
     A                                      OVRDTA
     A                                 15  2'Lassnummer. . . .'
     A            C1LASS    R        B 15 20REFFLD(FDLASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16 35'Innkjøpspris . .'
     A            C1INPR         9Y 2O 16 52EDTCDE(4)
     A                                      OVRDTA
     A                                 16  2'Lev.dato. . . . .'
     A            C1LDAT         6Y 0B 16 20COLOR(YLW)
     A  40                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(FMD0200 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17 35'Modulnr. . . . .'
     A            C1NMOD    R        O 17 52REFFLD(VVNMOD)
     A                                      OVRDTA
6.20 A**                               16  2'Prosjekt/akt. . .'
6.20 A**          C1PROJ    R        B 16 20REFFLD(FDPROJ)
6.20 A            C1PROJ         6A  H
6.20 A**45                                  ERRMSGID(FRH0220 *LIBL/QMFOFAK 45)
6.20 A**                                    CHECK(RB)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
6.20 A**          C1AKTI    R        B 16 27REFFLD(FDAKTI)
6.20 A            C1AKTI         3A  H
6.20 A**46                                  ERRMSGID(FMA0220 *LIBL/QMFOFAK 46)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
     A                                 18 35'GTIN-nr. . . . .'
     A            C1EANN    R        O 18 52REFFLD(VXEANN)
     A                                      OVRDTA
     A                                 18  2'Privat. . . . . .'
     A N85                                  DSPATR(ND)
     A            C1PRIV    R        B 18 20REFFLD(FDPRIV)
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 22'1=Ja,0=Nei'
     A N85                                  DSPATR(ND)
     A                                 19 35'Erstattes av . .'
     A            C1ENOB    R        O 19 52REFFLD(VVNOBB)
     A                                      OVRDTA
     A                                 20  2'Kampanje  . . . .'
     A N86                                  DSPATR(ND)
     A            C1KAMP    R        O 20 20REFFLD(FDKAMP)
     A N86                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20 35'Utgår dato . . .'
     A N84                                  DSPATR(ND)
     A            C1UDAT         6Y 0O 20 52EDTWRD('  .  .  ')
     A N84                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                 22  2'F3=Gå tilbake   F4=Spørring   F8=P-
     A                                      roduktinfo.  F10=Vanlig kalkyle'
     A                                      COLOR(BLU)
7.xx A                                 23  2'F13=Velg prisg. lev.  F14=Velg bet-
     A                                        F15=Kalkulasjon  F18=NOBB info'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020530  172007  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Salgspris er lavere enn kostpris !'
     A                                  5  2'Bekreft salget med Enter eller <OK-
     A                                      >, eller'
     A                                  6  2'gå tilbake til registrering ved å -
     A                                      velge'
     A                                  7  2'<Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C7WIN
     A*%%TS  SD  20120222  085139  BOF620      REL-V7R1M0  5770-WDS
7.01 A                                      WINDOW(12 2 11 65)
     A                                      CA03
     A                                      CA12
7.01 A                                  2  2'Oppsalg og mersalg'
7.01 A                                      COLOR(WHT)
7.01 A                                      DSPATR(RI)
7.01 A                                  4  2'Oppsalg:'
7.01 A            C7OPP1        50A  O  4 11
7.01 A            C7OPP2        50A  O  5 11
7.01 A                                  6  2'Mersalg:'
7.01 A            C7MER1        50A  O  6 11
7.01 A            C7MER2        50A  O  7 11
7.01 A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A*         R ASUME                     ASSUME
7.xx A*                                24 79' '
