     A*%%TS  SD  20050822  130126  ASK540      REL-V5R2M0  5722-WDS
     A*%%EC
     A*---------------------------------------------------------------
6.31 A* 260214 BOF Utvidet med virkelig leveringsdato
6.32 A* 110414 BSA Justert for at jacada skal identifisere datofelt
6.32 A* 280514 BOF Utvidelse mtp. nummerutvidelse
6.33 A* 280814 BOF ny feilmelding på leverandør  - sperres/passiv
6.34 A* 110914 Bsa Ny feilmelding på bestillingsnummer
7.xx A* 030316 bsa Endringer relatert til ny GUI
     A*---------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20050822  130126  ASK540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      CF11
     A                                      KEEP
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Registrering av Bestilling/Innkjøp'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LO110B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lag bunt for denne skjerm .  .'
     A            B1BIND    R        B  5 34REFFLD(LAABND)
     A                                      COLOR(YLW)
     A                                  5 44'(J=Ja, N=Nei)'
7.xx A                                  7  2'Ordre-type .  .  .  .  .  .  +'
     A            B1OTYP    R        B  7 34REFFLD(LOOTYP)
     A  31                                  ERRMSGID(LSO0010 *LIBL/QMFLAGR 31)
     A  42                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 42)
     A                                      COLOR(YLW)
     A                                  8  2'Bestillings-dato .  .  .  .  .'
     A            B1BDAT         6Y 0B  8 34COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 32)
6.32 A                                      EDTWRD('  .  .  ')
     A                                  9  2'Bestillings-nummer  .  .  .  .'
     A            B1ONUM    R        B  9 34REFFLD(LONUMM)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  23                                  DSPATR(PC)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
6.34 A  43                                  ERRMSGID(LSO0540 *LIBL/QMFLAGR 43)
     A            B1OSUF    R        B  9 44REFFLD(LOSUFF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  38                                  ERRMSGID(LSO0060 *LIBL/QMFLAGR 38)
     A                                  9 50'Ordre-nummer'
     A  45                                  DSPATR(ND)
     A            B1ORNR    R        O  9 63REFFLD(LOORNR)
     A  45                                  DSPATR(ND)
     A            B1ORSU    R        O  9 72REFFLD(LOORSU)
     A  45                                  DSPATR(ND)
     A                                 11  2'Lev. adresse .'
     A            B1LELE    R        B 11 18REFFLD(LOLELE)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 11 25'(Kunde)'
     A                                      DSPATR(ND)
     A                                 11 37'Leverandør +'
     A            B1LDOR    R        B 11 50REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  26                                  DSPATR(PC)
     A  44                                  ERRMSGID(LMO0650 *LIBL/QMFLAGR 44)
     A  39                                  ERRMSGID(LSO0070 *LIBL/QMFLAGR 39)
     A  40                                  ERRMSGID(LSO0080 *LIBL/QMFLAGR 40)
     A  41                                  ERRMSGID(LSO0090 *LIBL/QMFLAGR 41)
6.33 A  46                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 46)
6.23 A                                 11 59'Kampanje'
6.23 A            B1KAMP    R        B 11 68REFFLD(LOKAMP)
6.23 A                                      COLOR(YLW)
     A                                 12  2'Navn.  .  .  .'
     A            B1NAVN    R        B 12 18REFFLD(LONAVN)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A  25                                  DSPATR(PC)
     A            B2NAVN    R        O 12 50REFFLD(LONAVN)
     A                                 13  2'Gate.  .  .  .'
     A            B1ADR1    R        B 13 18REFFLD(LOADR1)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A            B2GATE    R        O 13 50REFFLD(LOADR1)
     A                                 14  2'Adresse.  .  .'
     A            B1ADR2    R        B 14 18REFFLD(LOADR2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A            B2ADR2    R        O 14 50REFFLD(LOADR2)
     A                                 15  2'Postaddresse .'
     A            B1STED    R        B 15 18REFFLD(LOSTED)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A            B2STED    R        O 15 50REFFLD(LOSTED)
     A                                 17  2'Vår referanse.'
     A            B1VREF    R        B 17 18REFFLD(LOVREF)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A            B2AVTS    R        O 17 50REFFLD(LONAVN)
     A  28                                  DSPATR(RI)
7.xx A                                 18  2'Deres ref .  +'
     A            B1DREF    R        B 18 18REFFLD(LODREF)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                 19  2'Rekv.nummer  .'
     A            B1REKV    R        B 19 18REFFLD(LOREKV)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                 20  2'Leveringsdato.'
     A            B1LDAT         6Y 0B 20 18COLOR(YLW)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 33)
     A  34                                  ERRMSGID(LSO0030 *LIBL/QMFLAGR 34)
6.32 A                                      EDTWRD('  .  .  ')
     A                                 21  2'Bekr.lev.dato.'
     A            B1MODA         6Y 0B 21 18COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LSO0030 *LIBL/QMFLAGR 36)
6.32 A                                      EDTWRD('  .  .  ')
6.32 A                                 21 30'Lever. onr.'
6.32 A            B1SORN    R        B 21 42REFFLD(LOSORN)
6.22 A                                      COLOR(YLW)
6.31 A                                 22  2'Virk.lev.dato.'
6.31 A            B1VIDA         6Y 0O 22 18COLOR(YLW)
6.32 A                                      EDTWRD('  .  .  ')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F10=Faste opplysninger'
     A                                      COLOR(BLU)
     A                                 23 54'F11=Fritekst'
     A                                      COLOR(BLU)
     A          R M1WIN
     A*%%TS  SD  20010525  140848  ASK         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 9 37)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'OBS! Denne bestilling er overført '
     A                                      COLOR(RED)
     A                                  2  2'elektronisk og bør ikke endres på '
     A                                      COLOR(RED)
     A                                  3  2'varelinjenivå. Dette vil føre til '
     A                                      COLOR(RED)
     A                                  4  2'manuell behandling.               '
     A                                      COLOR(RED)
     A                                  6  2'KONTAKT INNKJØPSANSVARLIG!        '
     A                                      COLOR(RED)
     A                                  8  2'ENTER=OK   F3=Gå tilbake'
     A                                      COLOR(BLU)
