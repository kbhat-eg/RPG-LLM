     A*%%TS  SD  20181123  141857  NHO630      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : VG220D
     A* Beskrivelse . . : Vedlikehold av faktorer på beregning av gj.
     A*                   kostpris
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A*       311018 nho  Nytt program                                 øves
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/VGKFST)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20181113  103349  NHO630      REL-V7R3M0  5770-WDS
     A            B1FLEV    R        H      REFFLD(VGFLEV)
     A            B1FLAG    R        H      REFFLD(VGFLAG)
     A            B1VALG         1Y 0B  9  2EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A            B1FTYP    R        O  9 30REFFLD(VGFTYP)
     A            B1FNAV        25A  O  9  4
     A            B1FOGR    R        O  9 51REFFLD(VGFOGR)
     A                                      EDTCDE(Z)
     A            B1FHGR    R        O  9 54REFFLD(VGFHGR)
     A                                      EDTCDE(Z)
     A            B1FUGR    R        O  9 57REFFLD(VGFUGR)
     A                                      EDTCDE(Z)
     A            B1FVAR         8A  O  9 61
     A            B1FFAK    R        O  9 70REFFLD(VGFFAK)
     A                                      EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  194950  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kostpris faktorer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  4  2'Leverandør+'
     A                                      COLOR(WHT)
     A            B2LDOR    R        B  4 15REFFLD(VGFLEV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LENA        30A  O  4 23
     A                                  5  2'Lager +'
     A                                      COLOR(WHT)
     A  35                                  DSPATR(ND)
     A            B2LAGE    R        B  5 15REFFLD(VGFLAG)
     A  35                                  DSPATR(PR)
     A  35                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LANA        30A  O  5 23
     A                                  7  1'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8 31'Faktortype '
     A                                      COLOR(WHT)
     A                                  8  4'Leverandør   '
     A                                      COLOR(WHT)
     A                                  8 51'Og'
     A                                      COLOR(WHT)
     A                                  8 54'Hg'
     A                                      COLOR(WHT)
     A                                  8 57'Ugr'
     A                                      COLOR(WHT)
     A                                  8 62'Varenr'
     A                                      COLOR(WHT)
     A                                  8 71'Faktor'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett   F7=Faktortyper  F11=Hent fra -
     A                                      Excel'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  004349  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A  41                              2  2'Faktortype finnes ikke   '
     A  42                              2  2'Faktor må angis'
     A  43                              2  2'Leverandørnr. finnes ikke'
     A  44                              2  2'Overgr. er ugyldig'
     A  45                              2  2'Hovedgr. er ugyldig'
     A  46                              2  2'Undergr. er ugyldig'
     A  47                              2  2'Varenummer finnes ikke'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  125617  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faktorer  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A N30                              5  2'Faktortype     +'
     A  30                              5  2'Faktortype      '
     A                                      COLOR(WHT)
     A            C2FTYP    R        B  5 21REFFLD(VGFTYP)
     A  30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                              6  2'Leverandør     +'
     A  30                              6  2'Leverandør      '
     A                                      COLOR(WHT)
     A            C2FLEV    R        B  6 21REFFLD(VGFLEV)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2LTXT        30   O  6 30COLOR(YLW)
     A                                      OVRDTA
     A N30                              7  2'Overgruppe     +'
     A  30                              7  2'Overgruppe      '
     A                                      COLOR(WHT)
     A            C2FOGR    R        B  7 21REFFLD(VGFOGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2OTXT        35   O  7 30COLOR(YLW)
     A                                      OVRDTA
     A N30                              8  2'Hovedgruppe    +'
     A  30                              8  2'Hovedgruppe     '
     A                                      COLOR(WHT)
     A            C2FHGR    R        B  8 21REFFLD(VGFHGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2HTXT        35   O  8 30COLOR(YLW)
     A                                      OVRDTA
     A N30                              9  2'Undergruppe    +'
     A  30                              9  2'Undergruppe     '
     A                                      COLOR(WHT)
     A            C2FUGR    R        B  9 21REFFLD(VGFUGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2UTXT        35   O  9 30COLOR(YLW)
     A                                      OVRDTA
     A N30                             10  2'Varenummer     +'
     A  30                             10  2'Varenummer      '
     A                                      COLOR(WHT)
     A            C2FVAR         8   B 10 21
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2VTXT        35   O 10 30COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Faktor (%)'
     A                                      COLOR(WHT)
     A            C2FFAK    R        B 13 21REFFLD(VGFFAK)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 13 33'Faktor er en prosentsats med 6 des-
     A                                      imaler'
     A                                 14 33'(Et fraktpåslag på 6,5% gir faktor-
     A                                       6,50000)'
     A                                 15 33'Gjennomsnittlig kostpris beregnes -
     A                                       i lagerenhet'
     A                                 19  2'Sist endret dato '
     A                                      COLOR(WHT)
     A            C2EDAT         6Y 0O 19 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A                                      COLOR(WHT)
     A            C2EUSR    R        O 20 21REFFLD(VGFUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  004349  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette faktortype '
     A                                      DSPATR(RI)
     A                                  4  2'Faktortype . . . .'
     A            D1FTYP    R        O  4 21REFFLD(VGFTYP)
     A                                  5  2'Lager  . . . . . .'
     A            D1FLAG    R        O  5 21REFFLD(VGFLAG)
     A                                  6  2'Leverandør . . . .'
     A            D1FLEV    R        O  6 21REFFLD(VGFLEV)
     A            D1FNAV        25A  O  6 28
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20181102  121745  NHO630      REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Faktor +'
     A            K1FTYT        20A  B  4 12
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Faktor'
     A            K1FTYF        20A  O  8 12
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
