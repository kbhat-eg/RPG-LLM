     A*%%TS  SD  20190814  143054  G0XEGSVSTR  REL-V7R3M0  5770-WDS
6.20 A* 6.20 310311  NHO  Brukerkontroll fra IP til IF
6.NU A* 6.30 300514  bof  Utvidelse mtp. nummerutvidelse
7.00 A* K000 020620  bsa  Takler brede skjermbilder
     A*
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R D1SFL                     SFL
     A*%%TS  SD  20190814  143054  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A            D1KODE         1A  B  8  2
     A N21                                  DSPATR(PR)
     A  21                                  COLOR(YLW)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      VALUES('1' '3' '4' '5' '9' ' ')
     A            D1LINE    R        O  8  4REFFLD(LDLINE)
     A  55
     AO 56
     AO 57
     AO 58                                  DSPATR(RI)
     A  55                                  COLOR(RED)
     A  56                                  COLOR(TRQ)
     A  58                                  COLOR(PNK)
     A  57                                  COLOR(BLU)
     A  59                                  COLOR(GRN)
     A            D1LEVE         8Y 3B  8 25EDTWRD('     ,   ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D1ANTA    R   -3   O  8 35REFFLD(LDANTA)
     A                                      EDTWRD('     ,   ')
     A            D1ENH1    R        O  8 45REFFLD(LDENH1)
     A            D1TEK1    R   -6   O  8 49REFFLD(LDTEK1)
     A            D1EVAR    R        H      REFFLD(LDVARE)
     A            D1VARE    R   -5   O  8  9REFFLD(LFRFR/LDVARE *LIBL/LFRF)
     A            D1VTYP    R        O  8 79REFFLD(FFRFR/VVVTYP *LIBL/FFRF)
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20190814  143054  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av enkeltlinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bestnr'
     A                                      COLOR(WHT)
     A            D2SUFF    R        O  4 18REFFLD(LOSUFF)
     A                                  4 21'Lev.dør'
     A                                      COLOR(WHT)
     A            D2LDOR    R        O  4 29REFFLD(LOLDOR)
     A            D2LENA    R        O  4 36REFFLD(LOLENA)
     A                                  5  3'1=Plukkes'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Linje'
     A                                      COLOR(WHT)
     A                                  6 11'Varenr.'
     A                                      COLOR(WHT)
     A                                  6 27'Antall'
     A                                      COLOR(WHT)
     A                                  6 36'Bestilt'
     A                                      COLOR(WHT)
     A                                  6 45'Enh'
     A                                      COLOR(WHT)
     A                                  6 49'Varetekst'
     A                                      COLOR(WHT)
     A            D2LINE    R        B  7  4REFFLD(LDLINE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  6 79'T'
     A                                      COLOR(WHT)
     A            D2NUMM    R        O  4  9REFFLD(LONUMM)
     A          R D2CMD
     A*%%TS  SD  19950920  153356  ASP         REL-V2R3M0  5738-PW1
     A                                 23 25'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 40'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
