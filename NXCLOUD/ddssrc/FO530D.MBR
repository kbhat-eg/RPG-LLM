     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
     A*
7.xx A*     180216 bsa  Endringer relatert til ny GUI
7.03 A*K000 bsa 130121  Legger inn F5 for forny.
7.06 A*K000 MST 050523  Kun kompilert pga endring i RPGSRC
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19980930  154338  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
6.34 A                                      VALUES(2 5 6 7 8)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM    R        O  8  4REFFLD(SONUMM)
6.NU A            B1SUFF    R        O  8 13REFFLD(SOSUFF)
6.NU A            B1OTYP    R        O  8 16REFFLD(SOOTYP)
6.NU A            B1BDAT         6Y 0O  8 20EDTCDE(Z)
6.NU A            B1KUNA        20A  O  8 27
9.01 A            B1AVDE         4Y 0O  8 49EDTCDE(Z)
6.NU A            B1FSUM    R        O  8 54REFFLD(SOFSUM)
6.NU A            B1SITY         2A  O  8 71
6.NU A            B1KSJK         1A  O  8 76
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA12
9.01 A                                      CA13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Belaste kreditordre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Ordre       OT         Kunde'
7.xx A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  4REFFLD(SONUMM)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A            B2OTYP    R        B  5 16REFFLD(SOOTYP)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A            B2KUNA        25A  B  5 27COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                  6  2'2=Arbeide med ordre  5=Vis info  6-
7.xx A                                      =Sjekk saldo  7=Trekk beløp  8=Frig-
7.xx A                                      i ordre'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V Ordre       OT  Dato   Kunde    -
7.xx A                                                          Fakturasum   Ko-
7.xx A                                      de Stat '
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19980108  144330  HKO         REL-V3R7M0  5716-PW1
7.03 A                                 23  2'F1=Filter  F3=Gå tilbake  F5=Forny'
7.03 A                                      COLOR(BLU)
7.03 A                                 23 38'F7=Trekk-
9.01 A                                       beløp for alle  F13=Status'
     A                                      COLOR(BLU)
6.34 A          R C2BLD
6.34 A*%%TS  SD  20040217  123227  KOB         REL-V5R2M0  5722-WDS
6.34 A                                      CA03
6.34 A                                      CA12
6.34 A                                      PUTOVR
6.34 A                                      CSRLOC(W_AROW     W_ACOL)
6.34 A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
6.34 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
6.34 A            W_AROW         3S 0H
6.34 A            W_ACOL         3S 0H
6.34 A            W_ARCD        10A  H
6.34 A            W_AFLD        10A  H
6.34 A            L_FNAV        30A  O  1  2COLOR(WHT)
6.34 A                                  1 46'Ordre/fakturering'
6.34 A                                      COLOR(WHT)
6.34 A                                  1 66'Kl. :'
6.34 A                                      COLOR(WHT)
6.34 A                                  1 72TIME
6.34 A                                      COLOR(WHT)
6.34 A                                  2  2'Belaste kreditordre '
6.34 A                                      COLOR(WHT)
6.34 A                                  2 46'Bruker:'
6.34 A                                      COLOR(WHT)
6.34 A                                  2 54USER
6.34 A                                      COLOR(WHT)
6.34 A                                  2 66'Dato:'
6.34 A                                      COLOR(WHT)
6.34 A                                  2 72DATE
6.34 A                                      EDTCDE(Y)
6.34 A                                      COLOR(WHT)
6.34 A                                  3  1'-----------------------------------
6.34 A                                      ------------------------------------
6.34 A                                      -----------'
6.34 A                                      COLOR(WHT)
6.34 A                                  5  2'Ordrenummer. . . . . .'
6.34 A            C1NUMM    R        O  5 25REFFLD(SONUMM)
6.34 A                                      COLOR(YLW)
6.34 A                                      OVRDTA
6.34 A                                  7  5'Statusmelding'
6.34 A            C2MSG1        70A  O  8  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG2        70A  O  9  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG3        70A  O 10  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG4        70A  O 11  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG5        70A  O 12  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG6        70A  O 13  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG7        70A  O 14  5
6.34 A                                      COLOR(YLW)
6.34 A            C2MSG8        70A  O 15  5
6.34 A                                      COLOR(YLW)
6.34 A                                      OVRDTA
6.34 A                                 23  2'F3=Gå tilbake'
6.34 A                                      COLOR(BLU)
     A          R B1MSG
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' INFOMASJONSMELDING'
     A                                      DSPATR(RI)
     A            B1MSG1        25A  O  5  2
     A            B1MSG2        25A  O  6  2
     A            B1MSG3        25A  O  7  2
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
9.01 A          R B13WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Statuskoder '
     A                                      DSPATR(RI)
6.34 A                                  5  2'S = Betaling sendt'
6.34 A                                  6  2'R = Betaling avvist'
6.34 A                                  7  2'U = Ukjent kortnr'
6.34 A                                  8  2'E = Ukjent feil'
6.34 A                                  9  2'B = Betalt'
6.34 A                                 10  2'Z = Betaling utført'
6.34 A                                 11  2'N = Nettverksfeil, prøv igjen'
6.34 A                                 12  2'0 = Ikke behandlet'
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
6.32 A          R F15WI5
6.32 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
6.32 A                                      WINDOW(*DFT 11 50)
6.32 A                                      CA03
6.32 A                                      CA12
6.32 A                                      PUTOVR
6.32 A                                  2  2' Kortkontroll'
6.32 A                                      DSPATR(RI)
6.32 A                                  4  2' Forespørsel om trekk av beløp'
6.32 A                                  5  2' er sendt. Ordren kan ikke frislip-
6.32 A                                      pes'
6.32 A                                 10  2'F3=Gå tilbake'
6.32 A                                      COLOR(BLU)
     A*
9.01 A          R F1WIN
9.01 A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
9.01 A                                      WINDOW(*DFT 15 35)
9.01 A                                      CA03
9.01 A                                      CA04
9.01 A                                      CA12
9.01 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
9.01 A                                      PUTOVR
9.01 A            W_ARCD        10A  H
9.01 A            W_AFLD        10A  H
9.01 A                                  2  2'Bla alternativer'
9.01 A                                      DSPATR(RI)
9.01 A                                  5  2'Kunde..........+'
9.01 A            F1KUND    R        B  5 19REFFLD(SOKUND)
9.01 A                                      DLTCHK
9.01 A                                      OVRDTA
9.01 A                                      COLOR(YLW)
9.01 A                                  6  2'Ordretype......+'
9.01 A            F1OTYP    R        B  6 19REFFLD(SOOTYP)
9.01 A                                      DLTCHK
9.01 A                                      OVRDTA
9.01 A                                      COLOR(YLW)
9.01 A                                  7  2'Avdeling.......+'
9.01 A            F1AVDE    R        B  7 19REFFLD(SOAVDE)
9.01 A                                      OVRDTA
9.01 A                                      COLOR(YLW)
9.01 A                                 14  2'F3=Tilbake  F4=Spørring'
9.01 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
