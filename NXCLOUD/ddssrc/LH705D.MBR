     A*%%TS  SD  20020924  123042  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LH705D
     A* Beskrivelse . . : Bestillingsforslag, innlesning fra håndterm.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120302 ASK  Nytt program
6.NU A*       260514 bof  Sjekket ihht. nummerutvidelse
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R A1BLD
     A*%%TS  SD  20020924  123042  HKO         REL-V5R1M0  5722-WDS
     A                                      CF04
     A                                      KEEP
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring til fra håndterminal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Ordre-type . . . . . . . . .'
     A            A1OTYP    R        B  6 31REFFLD(LOOTYP)
     A  31                                  ERRMSGID(LSO0010 *LIBL/QMFLAGR 31)
     A  40                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 40)
     A                                      COLOR(YLW)
     A  38                                  DSPATR(PR)
     A                                  7  2'Leverandør . . . . . . . . .'
     A            A1LDOR    R        B  7 31REFFLD(LOLDOR)
     A  32                                  ERRMSGID(LSO0080 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A  38                                  DSPATR(PR)
     A            A1LETX        30A  O  7 38COLOR(GRN)
     A                                  9  2'Lager. . . . . . . . . . . .'
     A            A1LAGE    R        B  9 31REFFLD(LOLAGE)
     A  33                                  ERRMSGID(LSO0230 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A  38                                  DSPATR(PR)
     A            A1LATX        20A  O  9 38COLOR(GRN)
     A                                 11  2'Bestillingsdato. . . . . . .'
     A            A1BDAT         6Y 0B 11 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 34)
     A  35                                  ERRMSGID(LSO0030 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 36)
     A  38                                  DSPATR(PR)
     A                                 12  2'Leveringsdato. . . . . . . .'
     A            A1LDAT         6Y 0B 12 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 36)
     A  38                                  DSPATR(PR)
     A  37                             15  2'INGEN DATA OVERFØRT FRA HÅNDTERMIN-
     A                                      AL!'
     A                                      DSPATR(RI)
     A  38                             15  4'DATA OVERFØRT FRA HÅNDTERMINAL'
     A                                      DSPATR(RI)
     A  38                             17  2'Bestillingsforslagsnr. . . .'
     A  38        A1NUMM         6Y 0O 17 31EDTWRD('      ')
     A                                      COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
