     A*%%TS  SD  20020817  122612  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL511D
     A* Beskrivelse . . : Leverandørposteringer, spørring samlegiro
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060602 KOB  Nytt program
7.xx A*       030516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20020817  122612  KOB         REL-V5R1M0  5722-WDS
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1SAMB    R        O  9  4REFFLD(ROSAMB)
6.NU A            B1BILN    R        O  9 13REFFLD(ROBILN)
6.NU A            B1BIDA         6Y 0O  9 22
6.NU A            B1BILK    R        O  9 29REFFLD(ROBILK)
6.NU A            B1TEXT         9A  O  9 32
6.NU A            B1BELØ         9Y 2O  9 42EDTCDE(J)
6.NU A            B1LEVR    R        O  9 56REFFLD(RLLEVR)
6.NU A            B1NAVN    R  -12   O  9 63REFFLD(RLNAVN)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020805  220443  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på samlegiro'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Sam.gir'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 46'Lever'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  5 60'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 76REFFLD(RORAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  4'Sam.gir'
     A                                      COLOR(WHT)
7.xx A                                  8 13'Bil.nr'
     A                                      COLOR(WHT)
7.xx A                                  8 22'Dato'
     A                                      COLOR(WHT)
7.xx A                                  8 29'Bk'
     A                                      COLOR(WHT)
7.xx A                                  8 32'Tekst'
     A                                      COLOR(WHT)
7.xx A                                  8 46'Beløp'
     A                                      COLOR(WHT)
7.xx A                                  8 56'Lever'
     A                                      COLOR(WHT)
7.xx A                                  8 63'Navn'
     A                                      COLOR(WHT)
7.xx A            B2SAMB    R        B  6  4REFFLD(ROSAMB)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
7.xx A            B2LEVR    R        B  6 46REFFLD(ROLEVR)
7.xx A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for visning
     A*
     A          R C2WIN
     A*%%TS  SD  20020806  154044  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
     A                                      CA03
     A                                      CA04
     A                                      PUTOVR
     A                                  2 29'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
6.NU A**                                    EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        O  6 19REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        O  7 19REFFLD(ROREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlegiro-nr.. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        O  8 19REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        O  9 19REFFLD(ROAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(ROREST)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 10  1'Attestasjonskode:'
     A            C2ATTS    R        O 10 19REFFLD(ROATTS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        O 11 19REFFLD(ROBETK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        O 12 19REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        O 13 19REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        O 13 61REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        O 14 19REFFLD(RODECK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        O 15 17REFFLD(ROKIDI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
