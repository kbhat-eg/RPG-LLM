     A*%%TS  SD  20030702  082201  HKO         REL-V5R2M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FR190D
     A* Beskrivelse . . : Utskrift av spesialpriser
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010416  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20030702  082201  HKO         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(*DFT 16 63)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiering av avtale til annen fil-
     A                                      gruppe/rabattkategori/kunde '
     A                                      DSPATR(RI)
     A                                  5  2'Kopier fra:'
     A                                      COLOR(WHT)
7.xx A                                  6  2'Rabattkategori. . . . +'
     A            B1RKAF    R        B  6 26REFFLD(FRRKAT)
     A  31                                  ERRMSGID(FMR0300 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMR0310 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 33)
     A  39                                  ERRMSGID(FMR0320 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1TXTF        20A  O  6 33COLOR(YLW)
7.xx A                                  7  2'Kundenummer . . . . . +'
     A            B1KUNF    R        B  7 26REFFLD(FRKUND)
     A  34                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1NAVF        30A  O  7 33COLOR(YLW)
     A                                  9  2'Kopier til:'
     A                                      COLOR(WHT)
     A                                 10  2'Filgruppe . . . . . . .'
     A            B1FGRP         3A  B 10 26COLOR(YLW)
7.xx A                                 11  2'Rabattkategori. . . . +'
     A            B1RKAT    R        B 11 26REFFLD(FRRKAT)
     A  35                                  ERRMSGID(FMR0300 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FMR0310 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1TXTT        20A  O 11 33COLOR(YLW)
7.xx A                                 12  2'Kundenummer . . . . . +'
     A            B1KUNT    R        B 12 26REFFLD(FRKUND)
     A  38                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1NAVT        30A  O 12 33COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
