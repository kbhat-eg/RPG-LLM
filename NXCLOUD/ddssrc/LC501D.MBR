     A*%%TS  SD  20020922  234345  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LC501D
     A* Beskrivelse . . : Velge batchnummer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990322  205355  ASK         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1BATC    R        O  7  4REFFLD(LCBATC)
     A            B1ANSV    R        O  7 15REFFLD(LCANSV)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020922  234345  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  2  2' Velge batch '
     A                                      DSPATR(RI)
7.xx A                                  3  4'Batch'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Batch  '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 15'Ansvarlig'
     A                                      COLOR(WHT)
7.xx A            B2BATC    R        B  4  4REFFLD(LCBATC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  ASK         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 46)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
