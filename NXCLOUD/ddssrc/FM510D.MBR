     A*%%TS  SD  20050208  205735  GRO530      REL-V5R2M0  5722-WDS
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK101D
     A* Beskrivelse . . : Kunder, vedlikehold - subprogram
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
8.01 A* 800   211024 bsa  Takler brede skjermbilder
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
8.01 A                                      DSPSIZ(24 80 *DS3 +
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050208  151758  GRO530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1FATY    R        O  7  7REFFLD(FMTFAT)
     A            B1BESK    R        O  7 11REFFLD(FMTBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050208  151758  GRO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge fakturatype'
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V  Kode  Fakturatype'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050208  205735  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 35)
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
