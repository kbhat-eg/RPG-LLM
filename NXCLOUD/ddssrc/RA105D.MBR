     A*%%TS  SD  20220505  140731  SST_800     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA105D
     A* Beskrivelse . . : MVA-koder, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.xx  290320  bof  sydd inn saft-endringer
8.00 A*       040322  sst  Utvidet med felter vedr MVA-Oppgave på xml-format
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020307  003657  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1EKOD    R        O  8  5REFFLD(RAEKOD)
     A            B1ETXT    R        O  8 11REFFLD(RAETXT)
     A            B1EPRO    R        O  8 43REFFLD(RAEPRO)
     A                                      EDTCDE(1)
     A            B1EMDA         6Y 0O  8 51EDTWRD('  .  .  ')
     A            B1EGPR    R        O  8 61REFFLD(RAEGPR)
     A                                      EDTCDE(1)
7.01 A            B1EBMK    R        O  8 69REFFLD(RAEBMK)
7.01 A            B1ESMK    R        O  8 75REFFLD(RAEBMK)
7.01 A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  181136  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av MVA-koder'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RA105B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse              '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 42'Ny sats'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'Endr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 60'Gml.sats'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 69'Beh'
7.xx A                                      COLOR(WHT)
7.01 A                                  7 73'Std.mk'
7.01 A                                      COLOR(WHT)
7.xx A**                                6 42'Ny sats   Endr.  Gml.sats    Beh.'
7.xx A**                                    COLOR(WHT)
7.xx A            B2EKOD    R        B  5  5REFFLD(RAEKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ETXT    R        B  5 11REFFLD(RAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  181136  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av MVA-koder'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RA105C1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'MVA-kode . . . . . . . . . .'
     A            C1EKOD    R        B  5 31REFFLD(RAEKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  003657  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'MVA-kode finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20220505  140731  SST_800     REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA07
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av MVA-koder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'MVA-kode . . . . . . . . . .'
     A            C1EKOD    R        O  5 31REFFLD(RAEKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2ETXT    R        B  7 31REFFLD(RAETXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  8  2'MVA-%. . . . . . . . . . . .'
     A            C2EPRO    R        B  8 31REFFLD(RAEPRO)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                  9  2'Investeringsavgift . . . . .'
     A            C2EINV    R        B  9 31REFFLD(RAEINV)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 10  2'Endrings dato. . . . . . . .'
     A            C2EMDA         6Y 0B 10 31EDTWRD('  .  .  ')
     A  32                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 32)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 11  2'Gamel MVA % . . . . . . . . '
     A            C2EGPR    R        B 11 31REFFLD(RAEGPR)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 12  2'Gamel Investeringsavgift. . '
     A            C2EGIN    R        B 12 31REFFLD(RAEGIN)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 13  2'Behandlingskode . . . . . . '
     A            C2EBMK    R        B 13 31REFFLD(RAEBMK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                 14  2'Std.MV-kode (SAF-T) . . . + '
     A            C2ESMK    R        B 14 31REFFLD(RAEBMK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                 16  2'-andel som behandles. . . . '
     A            C2EBPR    R        B 16 31REFFLD(RAEBPR)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 17  2'Endring dato. . . . . . . . '
     A            C2EPDA         6Y 0B 17 31EDTWRD('  .  .  ')
     A  33                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 33)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 18  2'-andel som behandles. . . . '
     A            C2EBGP    R        B 18 31REFFLD(RAEBGP)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 20  2'Sis endret dato . . . . . . '
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endre av. . . . . . . . . . '
     A            C2EUSR    R        O 21 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spøring      F7-
     A                                      =MVA-Historik'
     A                                      COLOR(BLU)
     A            C2STXT        45A  B 14 35COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Spesifikasjo SAF-T  . . . . '
     A            C2SPSS         1A  B 15 31
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 15 34'(J/N)'
     A                                 15 42'Regnskapsprosjek for uttak: '
     A            C2PRUT         6A  B 15 71
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 10 41'(Ny sats gjelder fra dato)'
     A                                 23 51' '
     A          R D1WIN
     A*%%TS  SD  20020307  003657  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette mva-kode '
     A                                      DSPATR(RI)
     A                                  4  2'MVA-kode . . . . .'
     A            D1EKOD    R        O  4 21REFFLD(RAEKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAETXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020307  003657  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'MVA-kode . . . . .'
     A            K1EKOD    R        B  4 21REFFLD(RAEKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'MVA-kode . . . . .'
     A            RAEKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAETXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R L1WIN
     A*%%TS  SD  20220314  100706  SST_800     REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Logg av endrede mva-koder'
     A                                      DSPATR(RI)
     A                                  4  2'MVA-kode . . . . .'
     A            L1EKOD    R        B  4 21REFFLD(RAEKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A            L1LIN1        50A  O  6  4
     A            L1LIN2        50A  O  7  4
     A            L1LIN3        50A  O  8  4
     A            L1LIN4        50A  O  9  4
     A            L1LIN5        50A  O 10  4
     A            L1LIN6        50A  O 11  4
     A            L1LIN7        50A  O 12  4
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  9'Gjelder_til_dato   Sats'
