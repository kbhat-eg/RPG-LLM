     A*%%TS  SD  20020226  101457  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx A* 7.00  040216  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.00  040521  BOF  TAKLER BREDERE SKJERMBILDER
     A*
8.01 A*K0000  141124  bsa  Kan ikke legge WIDE først, da det gir bredt
8.01 A*K0000               skjermbilde. Da må det kodes om hele bildet.
     A*
     A*
     A*
     A*---------------------------------------------------------------
8.01 A*                                     DSPSIZ(27 132 *WIDE -
8.01 A*                                            24 80 *DS3)
8.01 A                                      DSPSIZ(24 80 *DS3 -
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20001010  003851  KOB         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1HHGR    R        O 10  6REFFLD(VGHHGR)
     A            B1HTXT    R        O 10 11REFFLD(VGHTXT)
     A            B1HKHV    R        O 10 48REFFLD(VGHKHV)
     A            B1HPRA    R        O 10 56REFFLD(VGHPRA)
     A            B1HENS    R        O 10 63REFFLD(VGHENS)
     A            B1HENL    R        O 10 70REFFLD(VGHENL)
     A            B1HENP    R        O 10 76REFFLD(VGHENP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  095117  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av hovedgruppe'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Overgruppe . . . . .'
     A                                      COLOR(BLU)
7.xx A            B2HOGR    R        O  4 23REFFLD(VGHOGR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2OTXT    R        O  4 26REFFLD(VGOTXT)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  5'Hgrp'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Beskrivelse                    '
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Behandle undergruppe'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Hgrp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Beskrivelse                    '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 48'Konto-'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'Prod.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Stat'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'Lag.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 76'Prb.'
7.xx A**                                8  2'V  Hgrp  Beskrivelse              -
7.xx A**                                                Konto-  Prod.  Stat   L-
7.xx A**                                    ag.  Prb.'
     A                                      COLOR(WHT)
7.xx A            B2HHGR    R        B  6  6REFFLD(VGHHGR)
     A  31                                  ERRMSGID(VMG2010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2HTXT    R        B  6 11REFFLD(VGHTXT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A                                  9 48'henv.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 56'ansv'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'enh.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 70'enh.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 76'enh.'
7.xx A**                                9 48'henv.   ansv   enh.   enh.  enh.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020226  101457  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  101457  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av hovedgruppe'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1HOGR    R        O  5 31REFFLD(VGHOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT    R        O  5 35REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1HHGR    R        B  6 31REFFLD(VGHHGR)
     A  31                                  ERRMSGID(VMG2010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990112  191325  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Hovedgruppe finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20001010  003851  KOB         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av hovedgruppe'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1HOGR    R        O  5 31REFFLD(VGHOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT    R        O  5 35REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1HHGR    R        O  6 31REFFLD(VGHHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2HTXT    R        B  8 31REFFLD(VGHTXT)
     A  31                                  ERRMSGID(VMG2100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A                                 10  2'Kontohenvisning. . . . . . +'
     A            C2HKHV    R        B 10 31REFFLD(VGHKHV)
     A  32                                  ERRMSGID(VMA2020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KTXT    R        O 10 36REFFLD(VAKTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Produktansvarlig . . . . . +'
     A            C2HPRA    R        B 12 31REFFLD(VGHPRA)
     A  36                                  ERRMSGID(FRA2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ZTXT    R        O 12 36REFFLD(RFRFR/RAZTXT *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 14  2'Statistikkenhet. . . . . . +'
     A            C2HENS    R        B 14 31REFFLD(VGHENS)
     A  33                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 15  2'Lagerenhet . . . . . . . . +'
     A            C2HENL    R        B 15 31REFFLD(VGHENL)
     A  34                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 16  2'Prisbokenhet . . . . . . . +'
     A            C2HENP    R        B 16 31REFFLD(VGHENP)
     A  35                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2HEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2HEUS    R        O 20 31REFFLD(VGHEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020226  101457  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Slette hovedgruppe '
     A                                      DSPATR(RI)
     A                                  4  2'Overgruppe . . . .'
     A            D1HOGR    R        O  4 21REFFLD(VGHOGR)
     A                                  5  2'Hovedgruppe. . . .'
     A            D1HHGR    R        O  5 21REFFLD(VGHHGR)
     A                                  6  2'Beskrivelse. . . .'
     A            VGHTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  101457  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      WINDOW(*DFT 15 56)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Hovedgruppe. . . .'
     A            K1HHGR    R        B  4 21REFFLD(VGHHGR)
     A  31                                  ERRMSGID(VMG2010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMG2030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Overgruppe . . . .'
     A            VGHOGR    R        O  8 21OVRDTA
     A                                  9  2'Hovedgruppe. . . .'
     A            VGHHGR    R        O  9 21OVRDTA
     A                                 10  2'Beskrivelse. . . .'
     A            VGHTXT    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
