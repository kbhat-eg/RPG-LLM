     A*%%TS  SD  20220427  123148  SST_800     REL-V7R3M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040202  104639  HKO         REL-V5R2M0  5722-WDS
     A            B1TELL    R        H      REFFLD(NOTELL)
     A            B1ODAT         6Y 0H
7.xx A**          B1VALG    R       0B  9  2REFFLD(FÅVALG)
7.0x A            B1VALG         2Y 0B  9  2
7.17 A**                                    VALUES(2 4 5 7 8 9 0 10 11 12 15)
7.17 A                                      VALUES(2 4 5 6 7 8 9 0 10 11 12 15)
     A                                      COLOR(YLW)
7.0x A                                      EDTCDE(Z)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.0x A            B1FSYS    R        O  9  5REFFLD(NOFSYS)
6.20 A**          B1NREF    R        O  8  8REFFLD(NONREF)
7.0x A            B1NREF        10A  O  9  9
7.0x A            B1LMAT    R        O  9 20REFFLD(NOLMAT)
7.0x A            B1KNAV        20A  O  9 23
7.0x A            B1PNAV        10A  O  9 44
7.0x A            B1USER    R        O  9 55REFFLD(NOUSER)
7.0x A            B1LDAT         6Y 0O  9 66EDTWRD('  .  .  ')
7.0x A            B1MELD         1A  O  9 75
7.0x A            B1STAT         1Y 0O  9 77EDTCDE(Z)
7.0x A            B1OVER         1Y 0O  9 79EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051201  112514  HKO550      REL-V5R3M0  5722-WDS
7.08 A                                      SFLSIZ(0013)
7.08 A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
7.05 A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
7.0x A                                      CF09
     A                                      CF13
     A                                      CF14
7.08 A                                      CF15
7.17 A                                      CF16
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  60                                  SFLMSGID(FMO0400 *LIBL/QMFOFAK 60)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordre fra eksterne-
     A                                       systemer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.0x A                                  4  5'Sys'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Referanse'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 24'Kundenavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 45'Kundeprosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 57'Bruker-id'
7.xx A                                      COLOR(WHT)
7.17 A*                                 6  2'2=Endre  4=Slett  5=Vis  7=Behandl-
7.17 A*                                     e ordrelin.  8=Overfør til ordre  9-
7.17 A*                                     =Melding'
7.17 A                                  6  2'2=Endre  4=Slett  5=Vis  6=Ovf.Pro-
7.17 A                                      sj. 7=Beh.Ordrelinj.  8=Ovf.ord.  9-
7.17 A                                      =Melding'
     A                                      COLOR(BLU)
7.0x A                                  7  2'10=Overf. lagerv.  -
7.0x A                                      11=Overf. skaffev.  12=Utplukk -
7.0x A                                      for overf.  15=Vis ordre'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.0x A                                  8  5'Sys'
7.xx A                                      COLOR(WHT)
7.0x A                                  8  9'Referanse '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Lm'
7.xx A                                      COLOR(WHT)
7.0x A                                  8 23'Kundenavn      '
7.xx A                                      COLOR(WHT)
7.0x A                                  8 44'Kundeprosj'
7.xx A                                      COLOR(WHT)
7.0x A                                  8 55'Bruker-id'
7.xx A                                      COLOR(WHT)
7.0x A                                  8 66'Lev.dato'
7.xx A                                      COLOR(WHT)
7.0x A                                  8 75'M'
7.xx A                                      COLOR(WHT)
7.0x A                                  8 77'S'
7.0x A                                      COLOR(WHT)
7.0x A                                  8 79'O'
     A                                      COLOR(WHT)
7.0x A            B2FSYS    R        B  5  5REFFLD(NOFSYS)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
6.20 A**          B2NREF    R        B  4  8REFFLD(NONREF)
7.0x A            B2NREF        11A  B  5  9
     A                                      COLOR(YLW)
7.0x A**                                    CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KNAV        20A  B  5 24COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PNAV        11A  B  5 45COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2USER    R        B  5 57REFFLD(NOUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20051130  211152  HKO550      REL-V5R3M0  5722-WDS
7.08 A                                 22  2'F1=Filter -
7.0x A                                       F3=Gå tilbake  F5=Forny  F9=Vis a-
7.0x A                                      lle  F13=Status  F14=Hent kun-
7.0x A                                      deord'
7.08 A                                      COLOR(BLU)
7.17 A                                 23  2'F15=Systemoppsett  F16=Vis -
7.17 A                                      slettede'
7.08 A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20100305  132700  SSTASP      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CA12
     A                                      CF14
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ekstern ordre - hod-
     A                                      e'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Ekst. sys. .'
     A            NOFSYS    R        O  4 15REFFLD(NOHEPFR/NOFSYS *LIBL/NOHEPF)
     A                                      COLOR(YLW)
     A            NKBESK    R        O  4 20REFFLD(NKBESK)
     A                                      COLOR(YLW)
     A                                  4 52'Referansenr.'
     A            C2NREF    R        O  4 65REFFLD(NONREF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Avdeling . +'
     A            C2AVDE    R        B  5 15REFFLD(NOAVDE)
     A  40                                  ERRMSGID(FMA0140 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ATXT        18A  O  5 26COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Lager. . . +'
     A            C2LAGE    R        B  6 15REFFLD(NOLAGE)
     A  31                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2LTXT        18A  O  6 26COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV    R        O  6 46REFFLD(NOKNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Fors.måte. +'
6.20 A            C2LMAT    R        B  7 15REFFLD(NOLMAT)
6.20 A  41                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 41)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C2MTXT        18A  O  7 26COLOR(YLW)
6.20 A                                      OVRDTA
8.06 A            C2ELBE        15A  O  7 46COLOR(YLW)
8.06 A                                      OVRDTA
7.xx A                                  8  2'Lev.type . +'
6.20 A            C2LETY    R        B  8 15REFFLD(NOLETY)
     A  32                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.20 A            C2TTXT        18A  O  8 26COLOR(YLW)
     A                                      OVRDTA
6.20 A            C2PNAV    R        O  8 46REFFLD(NOPNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Bruker-id. .'
6.20 A            C2USER    R        B  9 15REFFLD(NOUSER)
     A  33                                  ERRMSGID(FMB0020 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMB0300 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.20 A            C2UTXT        18A  O  9 26COLOR(YLW)
     A                                      OVRDTA
6.20 A                                  9 46'Lev.dato . .'
6.20 A            C2LDAT         6Y 0B  9 59COLOR(YLW)
     A  38                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 38)
     A  39                                  ERRMSGID(FMD0205 *LIBL/QMFOFAK 39)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10  2'Kundenr. . +'
6.20 A            C2KUND    R        B 10 15REFFLD(NOKUND)
     A  35                                  ERRMSGID(FRK0010 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 36)
     A  50                                  ERRMSGID(FM00810 *LIBL/QMFOFAK 50)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KNA2        22A  O 10 22COLOR(YLW)
     A                                      OVRDTA
6.20 A                                 10 46'Kundens ref.'
6.20 A            C2DREF    R        B 10 59REFFLD(NODREF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.20 A                                 11  2'Kundeprosj..'
6.20 A            C2KPRO    R        B 11 15REFFLD(NOKPRO)
     A  37                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.20 A            C2PNA2        22A  O 11 22COLOR(YLW)
     A                                      OVRDTA
6.20 A                                 11 46'Rekv.nr. . .'
6.20 A            C2REKV    R        B 11 59REFFLD(NOREKV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Leveringsadresse:'
     A                                      COLOR(WHT)
     A                                 12 46'Opprettet:'
     A                                      COLOR(WHT)
     A                                 13  2'Navn/sted. .'
     A            C2NAVN    R        B 13 15REFFLD(NONAVN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 46'Dato . . . .'
     A            C2ODAT         6Y 0O 13 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2'Adresse-1. .'
     A            C2ADR1    R        B 14 15REFFLD(NOADR1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 46'Tidspunkt. .'
     A            C2OTIM         6Y 0O 14 59COLOR(YLW)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                 15  2'Adresse-2. .'
     A            C2ADR2    R        B 15 15REFFLD(NOADR1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Postnr . . .'
     A            C2PONR    R        B 16 15REFFLD(NOPONR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17  2'Poststed . .'
     A            C2STED    R        B 17 15REFFLD(NOSTED)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Registrert av:'
     A                                      COLOR(WHT)
     A                                 19  2'Navn . . . .'
     A            C2UNAV    R        O 19 15REFFLD(NOUNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Tlf/mob/fax.'
     A            C2UTLF    R        O 20 15REFFLD(NOUTLF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UMOB    R        O 20 31REFFLD(NOUMOB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UFAX    R        O 20 47REFFLD(NOUFAX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Mail . . . .'
     A            C2UMAI    R        O 21 15REFFLD(NOUMAI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22 46'Sist endret'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(NOEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Spørring  F6=Opp-
     A                                      r. kunde  F7=Oppr. kundeprosjekt  F-
     A                                      14=Melding'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030930  102835  MHA         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette nettbutikk-ordre '
     A                                      DSPATR(RI)
     A                                  4  2'Referansenr. . . .'
     A            D1NREF    R        O  4 21REFFLD(NONREF)
     A                                  7  2'Kunde. . . . . . .'
     A            NOKNAV    R        O  7 21
     A                                  8  2'Kundeprosjekt. . .'
     A            NOPNAV    R        O  8 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  2'Eksternt system. .'
     A            NOFSYS    R        O  5 21REFFLD(NOHEPFR/NOFSYS *LIBL/NOHEPF)
     A            NKBESK    R        O  5 26REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
      *
7.05 A          R H1WIN
7.05 A*%%TS  SD  20040910  133006  ASK530      REL-V5R2M0  5722-WDS
7.05 A                                      WINDOW(*DFT 12 55)
7.05 A                                      CA03
7.05 A**                                    CF04
7.05 A                                      CA12
7.05 A                                      PUTOVR
7.05 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
7.05 A            W_ARCD        10A  H
7.05 A            W_AFLD        10A  H
7.05 A                                  2  2'Bla alternativer'
7.05 A                                      COLOR(WHT)
7.05 A                                      DSPATR(UL)
7.05 A                                  4  2'Velg system . .:'
7.05 A            H1FSYS         3A  B  4 19
7.05 A                                      COLOR(YLW)
7.05 A                                      OVRDTA
7.05 A                                 11  2'F3=Gå tilbake'
7.05 A                                      COLOR(BLU)
      *
8.05 A          R B9WIN3
8.05 A*%%TS  SD  20081127  125855  ASP_MSX     REL-V5R4M0  5722-WDS
8.05 A                                      CA03
8.05 A                                      CA12
8.05 A                                      WINDOW(*DFT 13 45)
8.05 A                                  2  2' Feil i ordre innlesning!!!!!'
8.05 A                                      DSPATR(RI)
8.05 A                                      COLOR(RED)
8.05 A                                  5  2'En eller flere ordre feilet'
8.05 A                                  6  2'ved innlesing. Gå til loggen for'
8.05 A                                  7  2'å finne ut mer (F8)'
8.05 A                                 10  2'**** **** **** FEIL !!! **** **** -
8.05 A                                       ****'
8.05 A                                      COLOR(RED)
8.05 A                                 12  2'F3=Gå tilbake'
8.05 A                                      COLOR(BLU)
