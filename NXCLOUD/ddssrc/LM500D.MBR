     A*%%TS  SD  20070327  102941  ASK550      REL-V5R4M0  5722-WDS
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LM500D
     A* Beskrivelse . . : Bestillings logg
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  220316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.01 A                                      DSPSIZ(27 132 *WIDE -
7.01 A                                             24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070319  134652  AMD550      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1KODE    R        O  7 32REFFLD(FUKODE)
     A            B1DATE         6Y 0O  7  6EDTWRD('  .  .  ')
     A            B1TIME         6Y 0O  7 16EDTWRD('  .  .  ')
     A            B1JTXT    R        O  7 35REFFLD(VAJTXT)
     A            B1SUFF    R        O  7 26REFFLD(FUSUFF)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070327  102941  ASK550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
7.xx A                                  2  2' Best. - status  '
7.xx A                                      DSPATR(RI)
7.xx A                                  2 22'Best. :'
7.xx A            B2NUMM    R        O  2 31REFFLD(FUNUMM)
7.xx A                                  2 42'LM500B2'
7.xx A                                      COLOR(WHT)
7.xx A                                  3  8'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'5=Vise'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
     A                                      COLOR(WHT)
7.xx A                                  6  8'Dato'
     A                                      COLOR(WHT)
7.xx A                                  6 16'Klokken'
     A                                      COLOR(WHT)
7.xx A                                  6 25'Suff'
     A                                      COLOR(WHT)
7.xx A                                  6 30'Kode'
     A                                      COLOR(WHT)
7.xx A                                  6 35'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2DATE         6Y 0B  4  7COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20070319  112157  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 54)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20070319  131435  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 12 40)
     A                                  2  1' Spørring på ordrelogg '
     A                                      DSPATR(RI)
     A            C2VALG    R        B  3  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  4  2'Ordrenummer. . .'
     A            C2NUMM    R        O  4 19REFFLD(FUNUMM)
     A                                  5  2'Suffix . . . . .'
     A            C2SUFF    R        O  5 19REFFLD(FUSUFF)
     A                                  6  2'Dato . . . . . .'
     A            C2DATE         6Y 0O  6 19EDTWRD('  .  .  ')
     A                                  7  2'Klokken. . . . .'
     A            C2TIME         6Y 0O  7 19EDTWRD('  .  .  ')
     A                                  8  2'Kode . . . . . .'
     A            C2KODE    R        O  8 19REFFLD(FUKODE)
     A                                  9  2'Beskrivelse. . .'
     A            C2JTXT    R        O  9 19REFFLD(VAJTXT)
     A                                 10  2'Brukerid . . . .'
     A            C2USER    R        O 10 19REFFLD(FUUSER)
     A                                 11  2'Skjermid . . . .'
     A            C2WSID    R        O 11 19REFFLD(FUWSID)
     A          R ASUME                     ASSUME
     A                                 24 79' '
