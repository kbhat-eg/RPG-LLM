     A*%%TS  SD  20051030  130723  HKO550      REL-V5R3M0  5722-WDS
     A*
8.01 A* 8.xx  070422  bsa  Justeringer basert på ny GUI og regler
8.02 A* 8.xx  120224  bsa  Eksport av info
     A*
     A*
     A*-----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051020  130911  HKO530      REL-V5R3M0  5722-WDS
     A            B1ALFA        18A  H
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(3 7 9 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NAVN        30A  O  8  5
     A            B1LDOR    R        O  8 37REFFLD(ELBLEV)
     A            B1BONU         1Y 0O  8 47EDTCDE(Z)
     A            B1SBON    R        O  8 52REFFLD(ELSBON)
     A                                      EDTCDE(2)
     A            B1BDAT         6Y 0O  8 65EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051030  130723  HKO550      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
8.02 A                                      CA06
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandørbonus'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
8.01 A                                  4  5'Navn'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 37'Lev.nr'
8.01 A                                      COLOR(WHT)
8.01 A                                  6  2'3=Kopier  7=Bonus  9=Kalkuler bonu-
     A                                      s'
     A                                      COLOR(BLU)
8.01 A                                  7  2'V'
8.01 A                                      COLOR(WHT)
8.01 A                                  7  5'Navn                     '
8.01 A                                      COLOR(WHT)
8.01 A                                  7 37'Lev.nr'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 45'Bonus'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 53'Kalk.bonus'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 67'Dato'
8.01 A                                      COLOR(WHT)
8.01 A**                                6  2'V  Navn                           -
8.01 A**                                     Lev.nr  Bonus   Kalkulert     Dato'
8.01 A            B2ALFA        18A  B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.01 A            B2LDOR    R        B  5 37REFFLD(ELBLEV)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A**                                7 53'bonus'
8.01 A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051030  130602  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
8.02 A                                 23 28'F6=Eksport av avtaler'
     A                                      COLOR(BLU)
     A                                 23 62'F23=Alle/med bonus'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051027  211150  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
8.01 A                                  4  2'Leverandør . . . +'
     A            K1LDOR    R        B  4 21REFFLD(ELBLEV)
     A  31                                  ERRMSGID(FRL0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(EML0090 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Leverandør . . . .'
     A            ELBLEV    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R O1WIN
     A*%%TS  SD  20051027  211150  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 58)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kalkuler bonus for leverandør '
     A                                      DSPATR(RI)
     A                                  5  2'Leverandør . . . .'
     A            O1LDOR    R        O  5 21REFFLD(ELBLEV)
     A                                      OVRDTA
     A            O1LNAV        30A  O  5 28OVRDTA
     A                                  7  2'År . . . . . . . .'
     A            O1PAAR         4Y 0B  7 21COLOR(YLW)
     A  31                                  ERRMSGID(EML0300 *LIBL/QMFOFAK 31)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10  2'Jobben startes som bakgrunnsjobb.'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
