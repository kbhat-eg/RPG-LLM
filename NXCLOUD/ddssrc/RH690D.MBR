     A*%%TS  SD  20030314  000504  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH690D
     A* Beskrivelse . . : Saldooverføring, parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
6.20 A*       170611 NHO  Endret tekts
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A*
     A*  BILDE B1 - SUBFILE
     A*
     A          R B1WSF                     SFL
     A*%%TS  SD  19960406  202058  KB          REL-V3R1M0  5763-PW1
     A            B1SKOD    R        O  9  5REFFLD(RASKOD)
     A            B1STXT    R        O  9  8REFFLD(RASTXT)
     A            B1SSDK    R        O  9 52REFFLD(RASSDK)
     A*
     A*  BILDE B2 - SUBFILE KONTROLL
     A*
     A          R B2WCT                     SFLCTL(B1WSF)
     A*%%TS  SD  20020331  014948  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      KEEP
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Saldooverføring nytt regnskapsår'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Trykk enter for godkjenning'
     A                                      COLOR(BLU)
     A                                  8  5'Kl'
     A                                      COLOR(WHT)
     A                                  8  9'Kontoklasse'
     A                                      COLOR(WHT)
     A                                  8 47'Statuskode'
     A                                      COLOR(WHT)
     A*
     A*  BILDE B2 - FUNKSJONSTASTER
     A*
     A          R B2CMD
     A*%%TS  SD  20030314  000504  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  BILDE C2 - VINDU FOR VEDLIKEHOLD
     A*
     A          R C2WIN
     A*%%TS  SD  20011222  002250  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 15 65)
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2 19'Saldo-overføring nytt regnskapsår'
     A                                      COLOR(WHT)
6.20 A**                                4  3'Regnskapsår . . .:'
6.20 A                                  4  3'Nytt år . . . . .:'
     A            C2PRÅR    R        B  4 22REFFLD(RHPRÅR)
     A                                      COLOR(YLW)
     A                                  4 28'Bilagsdato ved overføring .:'
     A            C2PKDA    R        B  4 57REFFLD(RHPKDA)
     A                                      COLOR(YLW)
     A                                  6  3'Type overføring .:'
     A            C2POVR    R        B  6 22REFFLD(RHPOVR)
     A                                      COLOR(YLW)
     A                                  6 28'M = Midlertidig'
     A                                  7 28'P = Permanent'
     A                                  9  3'Ved midlertidig overføring overfør-
     A                                      es UB gammelt år til IB nytt'
     A                                      COLOR(RED)
     A                                 10  3'Ved permanent overføring genereres-
     A                                       posteringer som oppdaterer'
     A                                      COLOR(RED)
     A                                 11  3'regnskap for nytt år med periode =-
     A                                       00, dvs. IB oppdateres på'
     A                                      COLOR(RED)
     A                                 12  3'nytt.'
     A                                      COLOR(RED)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A*
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
