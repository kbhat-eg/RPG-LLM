     A*%%TS  SD  20160521  100106  ASP_G0X     REL-V7R1M0  5770-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
7.xx A*       281216 bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LPRG    R        O  8  5REFFLD(FÅPRGR)
     A            B1LREG         3   O  8  8
     A            B1LMAS    R        O  8 13REFFLD(FÅVALG)
     A            B1LANS    R        O  8 17REFFLD(FÅBESK_30)
     A            B1LKRA    R        O  8 60REFFLD(FÅVALG)
     A            B1LKSP    R        O  8 64REFFLD(FÅVALG)
     A            B1LKTI    R        O  8 68REFFLD(FÅVALG)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prisgr. kopiering'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'PG'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'Ansvarlig'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'PG'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  8'Reg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Mas'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'Ansvarlig             '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 59'Rab'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Sps'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'Til'
7.xx A**                                6  2'V  PG Reg. Mast  Ansvarlig        -
7.xx A**                                                           Rab Sps Til'
     A                                      COLOR(WHT)
7.xx A            B2LPRG    R        B  5  5REFFLD(FÅPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2LANS    R        B  5 17REFFLD(FÅBESK_30)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19971023  110452  HKO         REL-V3R7M0  5716-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prisgr. kopiering'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A            C1LPRG    R        B  5 31REFFLD(FÅPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Prisgruppe   finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20160521  100106  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prisgr. kopiering'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A            C1LPRG    R        O  5 31REFFLD(FÅPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Ansvarlig  . . . . . . . . .'
     A            C2LANS    R        B  7 31REFFLD(FÅBESK_30)
     A  31                                  ERRMSGID(FMC0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Master . . . . . . . . . . .'
     A            C2LMAS    R        B  8 31REFFLD(FÅVALG)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A  33                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Region . . . . . . . . . . .'
     A            C2LREG         3A  B  9 31COLOR(YLW)
     A  32                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 32)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Kopie Rab. Matrise  . . . . '
     A            C2LKRA    R        B 10 31REFFLD(FÅVALG)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Kopie Spes. Pris  . . . . . '
     A            C2LKSP    R        B 11 31REFFLD(FÅVALG)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Kopie Tilbudspris . . . . . '
     A            C2LKTI    R        B 12 31REFFLD(FÅVALG)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sis endret dato . . . . . . '
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endre av. . . . . . . . . . '
     A            C2EUSR    R        O 20 31REFFLD(FCEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=G tilbake '
     A                                      COLOR(BLU)
     A                                  9 37'MDT'
     A                                  9 41'VST'
     A                                  9 45'SVE'
     A                                  9 49'SOR'
     A                                  9 53'SOV'
     A                                  9 57'NVE'
     A          R D1WIN
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette recordformat '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgruppe . . . .'
     A            D1LPRG    R        O  4 21REFFLD(FÅPRGR)
     A                                  6  2'Ansvarlig  . . . .'
     A            D1LANS    R        O  6 21REFFLD(FÅBESK_30)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20160520  133540  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgruppe . . . .'
     A            K1LPRG    R        B  4 21REFFLD(FÅPRGR)
     A  31                                  ERRMSGID(FMC0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMC0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Prisgruppe . . . .'
     A            K2LPRG         1A  O  8 21OVRDTA
     A                                  9  2'Ansvarlig  . . . .'
     A            K2LANS        30A  O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
