     A*%%TS  SD  20041022  152604  AMD         REL-V5R2M0  5722-WDS
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041022  152604  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NAVN        23A  O  8  4
     A            B1ADR1        10A  O  8 28
     A            B1KPRO    R        O  8 39REFFLD(FLKPRO)
     A            B1EPRO    R        O  8 46REFFLD(FLEPRO)
     A            B1TDAT         6Y 0O  8 62EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041022  152346  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge kundeprosjekt '
     A                                      DSPATR(RI)
     A                                  4  2'Frisøk . . . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  4 17COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  6  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 28'Adresse-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 39'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 46'Ekst. prosj.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'Til-dato'
7.xx A                                      COLOR(WHT)
7.xx A*                                 7  2'V Navn                    Adresse--
7.xx A*                                     1  Prosj. Ekst. prosj.nr. Til-dato'
7.xx A*                                     COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990412  114018  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 18 70)
     A                                 17  2'F3=Gå tilbake  F5=Forny  F9=Gjenta-
     A                                       søk'
     A                                      COLOR(BLU)
     A N80                             17 43'F23=Vis utgåtte/ikke aktive'
     A                                      COLOR(BLU)
     A  80                             17 43'F23=Vis kun aktive'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
