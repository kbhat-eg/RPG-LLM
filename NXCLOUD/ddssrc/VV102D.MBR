     A*%%TS  SD  20100106  143633  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  020216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20100106  143633  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vare - bilde 2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr. . . . . . . . .'
     A            C2VARE    R   -7   O  4 26REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1    R        O  4 35REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Ikke bonusberegn. . . .'
     A            C2KBON    R        B  6 26REFFLD(VVKBON)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 29'1=Ja,0=Nei'
     A                                  6 42'Utelates fra etiketter.'
     A            C2KETI    R        B  6 66REFFLD(VVKETI)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 69'1=Ja,0=Nei'
     A                                  7  2'Ikke ordrerabatt. . . .'
     A            C2KORD    R        B  7 26REFFLD(VVKORD)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 29'1=Ja,0=Nei'
     A                                  7 42'Utelates fra prisbok. .'
     A            C2KPDI    R        B  7 66REFFLD(VVKPDI)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 69'1=Ja,0=Nei'
     A                                  8  2'Ikke godkj. for innkjøp'
     A            C2GODJ    R        B  8 26REFFLD(VVGODJ)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 29'1=Ja,0=Nei'
     A                                  8 42'Ikke prisjuster . . . .'
     A            C2KJUS    R        B  8 66REFFLD(VVKJUS)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 69'1=Ja,0=Nei'
     A                                  9  2'Restes ikke . . . . . .'
     A            C2KRES    R        B  9 26REFFLD(VVKRES)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 29'1=Ja,0=Nei'
     A                                  9 42'Ant. desimaler. . . . .'
     A            C2DESI    R        B  9 66REFFLD(VVDESI)
     A  37                                  ERRMSGID(VMV0410 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Kjedesortiment  . . . .'
     A            C2SORT    R   -9   O 18 26REFFLD(VVSORT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Avgiftskode . . . . . +'
     A            C2KMVA    R        B 11 26REFFLD(VVKMVA)
     A  34                                  ERRMSGID(VMV0450 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KMTX    R        O 11 28REFFLD(VAMTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'ABC-kode. . . . . . . .'
     A            C2KABC    R        B 12 26REFFLD(VVKABC)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 42'Struktur-kode . . . . .'
     A            C2KSTR    R        B 12 66REFFLD(VVKSTR)
     A  38                                  ERRMSGID(VMV0400 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13  2'Alt. varegruppe . . . +'
     A            C2AHGR    R        B 13 26REFFLD(VVAHGR)
     A  39                                  ERRMSGID(VMF0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13 29'+'
7.xx A            C2AUGR    R        B 13 31REFFLD(VVAUGR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 13 42'Alt. varenr . . . . . .'
     A            C2AVAR    R   -7   B 13 66REFFLD(VVAVAR)
     A  40                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Frie koder. . . . . . .'
     A            C2KFRI    R        B 14 26REFFLD(VVKFRI)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 42'Fri faktor. . . . . . .'
     A            C2FAKO    R        B 14 66REFFLD(VVFAKO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Standard DG . . . . . .'
     A            C2DEKN    R        B 15 26REFFLD(VVDEKN)
     A                                      RANGE(,00 100,00)
     A                                      CHKMSGID(FM00011 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 15 42'Nominell omr.faktor . .'
     A            C2OMRN    R        B 15 66REFFLD(VVOMRN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 16  2'Kontohenvisning . . . +'
     A            C2KHEV    R        B 16 26REFFLD(VVKHEV)
     A  47                                  ERRMSGID(VMA2020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KTXT    R        O 16 31REFFLD(VAKTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
