     A*%%TS  SD  20090821  134749  BOF610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JL110D
     A* Beskrivelse . . : Leverandører - kontaktpersoner vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       210809 bof  Nytt program
7.xx A*       040316 bsa  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090821  134105  BOF610      REL-V5R4M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1PTEL    R   -4   O  9  4REFFLD(JKPTEL)
7.xx A            B1PNAV    R  -10   O  9  7REFFLD(JKPNAV)
6.30 A*           B1PTTL    R  -24   O  8 33REFFLD(JKPTTL)
7.xx A            B1PROL    R   -6   O  9 33REFFLD(JKPROL)
7.xx A            B1PMOB    R   -7   O  9 43REFFLD(JKPMOB)
6.30 A*           B1PTLF    R        O  8 45REFFLD(JKPTLF)
7.xx A            B1PMAI    R  -24   O  9 52REFFLD(JKPMAI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090821  134105  BOF610      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Kontaktpersoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør : '
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 18REFFLD(JLLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV    R        O  4 26REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  4'Nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'Navn                   '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 33'Rolle'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'Mobil'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 54'E-post             '
7.xx A**                                6  2'V Nr Navn                      Rol-
7.xx A**                                    le       Mobil    E-post        '
     A                                      COLOR(WHT)
7.xx A            B2PTEL    R        B  6  4REFFLD(JKPTEL)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20090821  134105  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Kontaktpersoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør : '
     A                                      COLOR(BLU)
     A            C1LDOR    R        O  4 18REFFLD(JLLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1LNAV    R        O  4 26REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5  2'Løpenr . . . . . . . . . . .'
     A            C1PTEL    R        B  5 31REFFLD(JKPTEL)
     A  31                                  ERRMSGID(JM00110 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kontaktperson finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090821  134105  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Kontaktpersoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør : '
     A                                      COLOR(BLU)
     A            C1LDOR    R        O  4 18REFFLD(JLLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1LNAV    R        O  4 26REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5  2'Løpenr . . . . . . . . . . .'
     A            C1PTEL    R        O  5 31REFFLD(JKPTEL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Navn . . . . . . . . . . . .'
     A            C2PNAV    R        B  7 31REFFLD(JKPNAV)
     A  31                                  ERRMSGID(JMV0040 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Tittel   . . . . . . . . . .'
     A            C2PTTL    R        B  8 31REFFLD(JKPTTL)
     A  31                                  ERRMSGID(JMV0040 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Telefon  . . . . . . . . . .'
     A            C2PTLF    R        B  9 31REFFLD(JKPTLF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Mobil telefon  . . . . . . .'
     A            C2PMOB    R        B 11 31REFFLD(JKPMOB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Fax nr. . . .  . . . . . . .'
     A            C2PFAX    R        B 12 31REFFLD(JKPFAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Mail adresse . . . . . . . .'
     A            C2PMAI    R        B 13 31REFFLD(JKPMAI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Rolle  . . . . . . . . . . .'
     A            C2PROL    R        B 14 31REFFLD(JKPROL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2PODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2PEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2PUSR    R        O 20 31REFFLD(JKPUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  '
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20090821  134749  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Kontaktperson'
     A                                      DSPATR(RI)
     A                                  4  2'Løpenr . . . . . .'
     A            D1PTEL    R        O  4 21REFFLD(JKPTEL)
     A                                  5  2'Navn . . . . . . .'
     A            D1PNAV    R   -5   O  5 21REFFLD(JKPNAV)
     A                                  7  2'Tittel . . . . . .'
     A            D1PTTL    R  -15   O  7 21REFFLD(JKPTTL)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20090821  134749  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Løpenr . . . . . .'
     A            K1PTEL    R        B  4 21REFFLD(JKPTEL)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(JM00110 *LIBL/QMFVADM 31)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Løpenr . . . . . .'
     A            JKPTEL    R        O  9 21OVRDTA
     A                                 11  2'Navn . . . . . . .'
     A            K2PNAV    R        O 11 21REFFLD(JKPNAV)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
