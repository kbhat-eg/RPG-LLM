     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A*%%EC
     A*          220218 bsa  Nytt program
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021021  130850  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LINE    R        O  9  4REFFLD(NDLINE)
7.xx A            B1ANTA    R        O  9  9REFFLD(NDANTA)
7.xx A            B1ENHE    R        O  9 23REFFLD(NDENHE)
7.xx A            B1VARE         8A  O  9 27
7.xx A            B1TEK1        24A  O  9 36
7.xx A            B1STAT         1Y 0O  9 61EDTCDE(Z)
7.xx A            B1LDOR         6Y 0O  9 63EDTCDE(Z)
7.xx A            B1LDAT         6Y 0O  9 70EDTWRD('  .  .  ')
7.xx A            B1OVER         1A  O  9 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CF09
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av eksterne ordrelinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Eksternreferanse'
     A                                      COLOR(BLU)
7.xx A            B2NREF    R        O  4 19REFFLD(NONREF)
     A                                      COLOR(BLU)
7.xx A                                  4 35'Kundenr'
     A                                      COLOR(BLU)
7.xx A            B2KUND         6Y 0O  4 43COLOR(BLU)
7.xx A            B2KNAV        25A  O  4 51COLOR(BLU)
7.xx  *
7.xx A                                  5  4'Linnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'1=Velg'
     A                                      COLOR(BLU)
7.xx  *
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'Bestilt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 23'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 27'Varenr  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 36'Varetekst         '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Levnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 79'O'
7.xx A                                      COLOR(WHT)
7.xx  *
7.xx A            B2LINE    R        B  6  4REFFLD(NDLINE)
6.30 A  31                                  ERRMSG('Kan ikke overføre varer med-
6.30 A                                       status 1-3' 31)
7.xx A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20021001  150320  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny  F8=Ove-
     A                                      rf. linjer  F9=Vis alle  F13=Status-
     A                                      koder'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NREF    R        H      REFFLD(NDNREF)
     A            C2LINE    R        H      REFFLD(NDLINE)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av eksterne ordrelinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Varetekst. . . . . . . . . .'
     A            C2TEK1    R        O  6 31REFFLD(NDTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Nettopris. . . . . . . . . .'
     A            C2NTPR    R        O  7 31REFFLD(NDNTPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varenr . . . . . . . . . . +'
     A            C2VARE         8A  B  9 31COLOR(YLW)
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2VTXT    R        O  9 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Antall . . . . . . . . . . .'
     A            C2ANTA    R        B 10 31REFFLD(NDANTA)
     A  33                                  ERRMSGID(FMD0151 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 11  2'Enhet. . . . . . . . . . . +'
     A            C2ENHE    R        B 11 31REFFLD(NDENHE)
     A  34                                  ERRMSGID(FMD0160 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Lev.type . . . . . . . . . .'
     A            C2LETY    R        B 13 31REFFLD(NDLETY)
     A  35                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TTXT    R        O 13 34REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Lev.dato . . . . . . . . . .'
     A            C2LDAT         6Y 0B 14 31COLOR(YLW)
     A  36                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(FMD0205 *LIBL/QMFOFAK 37)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(NDEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                  4  2'Eksternt system. . . . . . .'
     A            NDFSYS    R        O  4 31REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A                                      COLOR(YLW)
     A            NKBESK    R        O  4 36REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
     A                                      COLOR(YLW)
     A          R C3BLD
     A*%%TS  SD  20030929  093955  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eksterne ordrelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Melding:'
     A            C3MELD        70A  O  6  2COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030929  132220  MHA         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Ekstern ordre - linje '
     A                                      DSPATR(RI)
     A                                  4  2'Referansenr. . . .'
     A            D1NREF    R        O  4 21REFFLD(NDNREF)
     A                                  5  2'Linjenummer. . . .'
     A            D1LINE    R        O  5 21REFFLD(NDLINE)
     A                                  8  2'Varenr . . . . . .'
     A            NDVARE    R        O  8 21
     A                                  9  2'Varetekst. . . . .'
     A            NDTEK1    R        O  9 21
     A                                 10  2'Antall . . . . . .'
     A            NDANTA    R        O 10 21
     A                                 11  2'Enhet. . . . . . .'
     A            NDENHE    R        O 11 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  6  2'Eksternt system. .'
     A            NDFSYS    R        O  6 21REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A            NKBESK    R        O  6 26REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
