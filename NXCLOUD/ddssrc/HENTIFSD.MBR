     A*%%TS  SD  20191028  122308  SST630      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . :
     A* Format. . . . . : XS500D
     A* Beskrivelse . . : Slettekoder, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       221116 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE +
     A                                             24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20170317  151950  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BANE        40A  O  7  5
     A            B1TYPE         4A  O  7 47
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20191028  122308  SST630      REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge Bane / fil'
     A                                      DSPATR(RI)
     A            B2BANE        49A  O  3  2
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V  Bane / fil                     -
     A                                                 Type'
     A                                      COLOR(WHT)
     A                                  4 18'Ny fil:'
     A                                      COLOR(BLU)
     A            B2NYFI        15A  I  4 26
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 19 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
