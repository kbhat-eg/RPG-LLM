     A*%%TS  SD  20020308  114808  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FC500D
     A* Beskrivelse . . : Pris-recordformat, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19971027  142808  HKO         REL-V3R7M0  5716-PW1
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1RFMT    R        O  7  5REFFLD(FCRFMT)
     A            B1BESK    R        O  7 17REFFLD(FCBESK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020308  114808  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge recordformat '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Rec.format'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Rec.format'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 17'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2RFMT    R        B  4  5REFFLD(FCRFMT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  4 17REFFLD(FCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19951103  132646  HK          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 17 47)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
