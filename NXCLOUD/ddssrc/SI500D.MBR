     A*%%TS  SD  19990515  000405  KOB         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : SI500D
     A* Beskrivelse . . : Spørring på kjøp
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  031016  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990515  000405  KOB         REL-V4R2M0  5769-PW1
     A*                                     WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Spørring på kjøp '
     A                                      DSPATR(RI)
     A                                  4  2'År . . . . . . . .'
     A            B1PAAR    R        B  4 21REFFLD(SFPAAR)
     A  31                                  ERRMSGID(SM00100 *LIBL/QMFSTAT 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  6  2'Dato . . . . . . .'
     A            B1BIDA         6Y 0B  6 21COLOR(YLW)
     A  32                                  ERRMSGID(SM00050 *LIBL/QMFSTAT 32)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
7.xx A                                  8  2'Leverandør . . . +'
     A            B1VALN    R        B  8 21REFFLD(SIVALN)
     A  33                                  ERRMSGID(SMF0100 *LIBL/QMFSTAT 33)
     A  34                                  ERRMSGID(SRK0020 *LIBL/QMFSTAT 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LNAV        30A  O  8 30COLOR(YLW)
7.xx A                                 10  2'Varenummer . . . +'
     A            B1VARE         8Y 0B 10 21COLOR(YLW)
     A  37                                  ERRMSGID(SVV0020 *LIBL/QMFSTAT 37)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1TEK1        30A  O 10 30COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
