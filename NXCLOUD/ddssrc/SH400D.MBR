     A*%%TS  SD  19990525  235836  KOB         REL-V4R2M0  5769-PW1
     A*%%EC
7.xx A*          160216 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990518  225706  KAI         REL-V4R2M0  5769-PW1
     A*                                     WINDOW(*DFT 14 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2' Spørring på fakturaregister '
     A                                      COLOR(WHT)
7.xx A                                  2 38'SH400B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'År . . . . . . . .'
     A            B1AARR    R        B  5 21REFFLD(SHAARR)
     A  31                                  ERRMSGID(FMO0300 *LIBL/QMFOFAX 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  7  2'Ordretype. . . . +'
     A            B1OTYP    R        B  7 21REFFLD(SHOTYP)
     A  32                                  ERRMSGID(FVA0220 *LIBL/QMFOFAX 32)
     A                                      COLOR(YLW)
7.xx A                                  9  2'Leverandør . . . +'
     A            B1LDOR    R        B  9 21REFFLD(SHLDOR)
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAX 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1NAVN        30A  O  9 29COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
