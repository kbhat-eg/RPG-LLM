     A*%%TS  SD  20051027  101609  AMD550      REL-V5R3M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : FO695D
     A* Beskrivelse . . : Utskrift av leverings-planleggingsliste
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      CF03
     A                                      CF12
     A          R B1BLD
     A*%%TS  SD  20051027  101609  AMD550      REL-V5R3M0  5722-WDS
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      KEEP
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av lev/planl. liste'
7.xx A**                                    -liste'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FP810B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Seleksjonskriterier :'
     A                                      COLOR(WHT)
7.xx A                                  7  2'Ordre-type . . . . . . . . +'
     A            B1OTYP    R        B  7 31REFFLD(FOOTYP)
     A  31                                  ERRMSGID(FMA0260 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A            B1OTXT    R        O  7 39REFFLD(VAOTXT)
     A                                      COLOR(GRN)
7.xx A                                  9  2'Forsendelsesmåte . . . . . +'
     A            B1LMAT    R        B  9 31REFFLD(FOLMAT)
     A  36                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A            B1LMTX    R        O  9 39REFFLD(FOLMTX)
     A                                      COLOR(GRN)
     A                                 12 32'Fra'
7.xx A                                 12 42'Til'
     A                                 13  2'Ordrenummer. . . . . . . . .'
     A            B1FONR    R        B 13 31REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1TONR    R        B 13 41REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 14  2'Kundenummer. . . . . . . . .'
     A            B1FKUN    R        B 14 31REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1TKUN    R        B 14 41REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 15  2'Leveringsdato. . . . . . . .'
     A            B1FDAT         6Y 0B 15 31COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(FM01160 *LIBL/QMFOFAK 35)
7.xx A            B1TDAT         6Y 0B 15 41COLOR(YLW)
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020312  205209  HKO         REL-V5R1M0  5722-WDS
     A                                      CF04
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av leverings-planleggings-
     A                                      -liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  2'Sorteringsrekkefølge . . . . . .'
     A            C1VAL1         1Y 0B  5 35VALUES(1 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  5 46'1 = Avdeling'
     A                                  6 46'2 = Leverings-dato'
     A                                  7 46'3 = Kundenummer'
     A                                  8 46'4 = Ordrenummer'
     A                                 10  2'Sideskift ved brudd i rekkefølge'
     A            C1VAL2         1Y 0B 10 35VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 10 46'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Jobbkø'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
