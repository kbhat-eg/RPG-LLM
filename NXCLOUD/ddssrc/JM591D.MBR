     A*%%TS  SD  20140220  085940  BOFBMP      REL-V7R1M0  5770-WDS
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140220  085940  BOFBMP      REL-V7R1M0  5770-WDS
     A            B1VKPR    R        H      REFFLD(JMVKPR)
     A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVADM)
     A            B1VKAM    R        O  9  4REFFLD(JMVKAM)
     A            B1ENHE    R        O  9 11REFFLD(JVPENH)
     A            B1VKAI    R        O  9 28REFFLD(JMVKAI)
     A            B1VKAA    R        O  9 16REFFLD(JMVKAA)
     A            B1VIDF         6Y 0O  9 40EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140220  085749  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Kampanje finnes på vare'
     A                                      DSPATR(RI)
     A                                  4  2'Varenr . .'
     A                                      COLOR(BLU)
     A            B2VVAR    R   -7   O  4 13REFFLD(JMVVAR)
     A                                      OVRDTA
     A                                      COLOR(BLU)
     A            B2VNAV        25A  O  4 22OVRDTA
     A                                      COLOR(BLU)
     A                                  5  2'Leverandør'
     A                                      COLOR(BLU)
     A            B2VLDO    R        O  5 13REFFLD(JMVLDO)
     A                                      OVRDTA
     A                                      COLOR(BLU)
     A            B2LNAV        25A  O  5 22OVRDTA
     A                                      COLOR(BLU)
     A                                  6  2'1=Velg innkjøpspris'
     A                                      COLOR(BLU)
     A                                  7  2'V Kamp. enhet Tilb.pris  Innkj.pri-
     A                                      s Innkj.dato'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20140130  075615  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 21 48)
     A                                 20  2'F3=Gå tilbake                     -
     A                                          '
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
