     A*%%TS  SD  20020730  130313  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : NXCLOUD
     A* Format. . . . . : RL120D
     A* Beskrivelse . . : Låste valutakurser pr. leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       040121 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A            B1UID         15Y 0H
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1TYPE         6A  O  7  8
     A            B1SCR1         3Y 0O  7 20EDTWRD('  0')
     A            B1GRN1         8Y 0O  7 26EDTWRD('       0')
     A            B1SCR2         3Y 0O  7 37EDTWRD('  0')
     A            B1GRN2         8Y 0O  7 43EDTWRD('       0')
     A            B1SCR3         3Y 0O  7 53EDTWRD('  0')
     A            B1GRN3         8Y 0O  7 59EDTWRD('       0')
     A            B1SCR4         3Y 0O  7 70EDTWRD('  0')
     A            B1GRN4         8Y 0O  7 76EDTWRD('       0')
     A            B1SCR5         3Y 0O  7 87EDTWRD('  0')
     A            B1GRN5         8Y 0O  7 93EDTWRD('       0')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 57'NXCLOUD'
     A                                      COLOR(WHT)
     A                                  1110'Kl. :'
     A                                      COLOR(WHT)
     A                                  1117TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av grenser for kreditt-
     A                                       vurdering'
     A                                      COLOR(WHT)
     A                                  2 91'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 99USER
     A                                      COLOR(WHT)
     A                                  2110'Dato:'
     A                                      COLOR(WHT)
     A                                  2117DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------- '
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  5'Selskapstype'
     A                                      COLOR(WHT)
     A                                  6 19'Score1'
     A                                      COLOR(WHT)
     A                                  6 27'Grense1'
     A                                      COLOR(WHT)
     A                                  6 36'Score2'
     A                                      COLOR(WHT)
     A                                  6 52'Score3'
     A                                      COLOR(WHT)
     A                                  6 44'Grense2'
     A                                      COLOR(WHT)
     A                                  6 60'Grense3'
     A                                      COLOR(WHT)
     A                                  6 69'Score4'
     A                                      COLOR(WHT)
     A                                  6 86'Score5'
     A                                      COLOR(WHT)
     A                                  6 77'Grense4'
     A                                      COLOR(WHT)
     A                                  6 94'Grense3'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake  F5=Forny  F6=Oppret-
     A                                      t '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  130313  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NXCLOUD'
     A                                      COLOR(WHT)
     A                                  1110'Kl. :'
     A                                      COLOR(WHT)
     A                                  1117TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av grenser for kreditt-
     A                                       vurdering'
     A                                      COLOR(WHT)
     A                                  2 91'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 99USER
     A                                      COLOR(WHT)
     A                                  2110'Dato:'
     A                                      COLOR(WHT)
     A                                  2117DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------- '
     A                                      COLOR(WHT)
     A                                  5  2'Selskapstype'
     A            C2TYPE         6A  B  5 26
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 33'Eks: AS, ASA, ENK, PRIVAT og blank-
     A                                      (''     '')'
     A                                      COLOR(WHT)
     A                                  7  2'Score over'
     A            C2SCR1         3Y 0B  7 14EDTWRD('  0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7 20'gir'
     A            C2GRN1         8Y 0B  7 25EDTWRD('       0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7 36'i kreditgrense'
     A            C2SCR2         3Y 0B  8 14EDTWRD('  0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2GRN2         8Y 0B  8 25EDTWRD('       0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2SCR3         3Y 0B  9 14EDTWRD('  0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2GRN3         8Y 0B  9 25EDTWRD('       0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2SCR4         3Y 0B 10 14EDTWRD('  0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2GRN4         8Y 0B 10 25EDTWRD('       0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2SCR5         3Y 0B 11 14EDTWRD('  0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2GRN5         8Y 0B 11 25EDTWRD('       0')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13 10'Definer kreditgrenser i stigende s-
     A                                      core rekkefølge'
     A                                      COLOR(RED)
     A                                 14 10'Selskapstype blank må defineres fo-
     A                                      r å fange opp andre selskapstyper'
     A                                      COLOR(RED)
     A                                 15 10'Selskapstype PRIVAT må defineres f-
     A                                      or å kunne søke på privatpersoner'
     A                                      COLOR(RED)
     A                                 19  2'Sist endret dato'
     A            C2EDAT         6Y 0O 19 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A            C2EUSR    R        O 20 21REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  '
     A                                      COLOR(BLU)
     A          R C2MSG1
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Selskapstype'
     A                                  3  2'Er definert fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG2
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Score intervall'
     A                                  3  2'Feil stigende rekkefølge!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Slette kredittgrense vurdering'
     A                                      DSPATR(RI)
     A                                  5  2'Selskapstype'
     A            D1TYPE         3A  O  5 20
     A                                  8 10'Bekreft at denne typen skal -
     A                                      slettes!'
     A                                      COLOR(RED)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
