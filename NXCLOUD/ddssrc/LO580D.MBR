     A*%%TS  SD  20051107  143508  HKO550      REL-V5R3M0  5722-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       170917 BKV  Endringer relatert til ny GUI
7.02 A* R6.30 111017 BSA  Legger ut varetype i skjermbildet
7.04 A* K000  050221 BSA  Rotete bilde.
     A*
8.01 A* K000  011221 bsa  Utvidet til bredt skjermbilde samt innført
8.01 A* K000              omregning mellom enheter.
8.04 A* K000  201222 bsa  Justert flagging av avvik.
8.06 A* K000  090123 bsa  Problemer med CLR av subfile
8.07 A* K000  100123 bsa  Flagger feil på antall/pris hvis enheter er like.
     A*
     A*
     A*
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
8.01 A**                                    DSPSIZ(24 80 *DS3 -
8.01 A**                                           27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20051025  123103  BOF540      REL-V5R3M0  5722-WDS
     A            B1NUMM    R        H      REFFLD(LONUMM)
     A            B1SUFF    R        H      REFFLD(LOSUFF)
     A            B1LINE    R        H      REFFLD(LOLINE)
     A            B1ONUM    R        H      REFFLD(LONUMM)
     A            B1OSUF    R        H      REFFLD(LOSUFF)
     A            B1OLIN    R        H      REFFLD(LOLINE)
     A            B1VALG    R        B  9  2REFFLD(LÅVALA)
     A                                      COLOR(YLW)
6.22 A**                                    VALUES('2' '5' '6' '7' '8' ' ')
6.22 A                                      VALUES('5' '6' '7' '8' ' ')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.02 A*           B1TEK1    R  -14   O  9  4REFFLD(LDTEK1)
8.01 A            B1TEK1    R  -12   O  9  4REFFLD(LDTEK1)
8.01 A            B1ANTA    R   -1   O  9 28REFFLD(LDANTA)
     A                                      EDTCDE(M)
8.01 A            B1ENH1    R        O  9 44REFFLD(LDENH1)
8.01 A            B1NETT    R        O  9 53REFFLD(LDIPNY)
8.01  *
8.01 A            B1DANT    R   -1   O  9 64REFFLD(LDANTA)
8.01 A                                      EDTCDE(M)
8.01 A  53                                  COLOR(RED)
8.01 A            B1DENH    R        O  9 78REFFLD(LDENH1)
8.01 A            B1DNET    R        O  9 86REFFLD(LDIPNY)
8.01 A                                      EDTCDE(M)
8.01 A  54                                  COLOR(RED)
8.01  *
8.01 A            B1OANT    R   -1   O  9 98REFFLD(LDANTA)
     A                                      EDTCDE(M)
8.07 A  52                                  COLOR(RED)
8.01 A            B1OENH    R        O  9113REFFLD(LDENH1)
8.04 A* 55                                  COLOR(RED)
8.01 A            B1VTYP         1A  O  9119
8.01 A            B1KPRI    R        O  9122REFFLD(LDIPNY)
8.07 A  51                                  COLOR(RED)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051107  143508  HKO550      REL-V5R3M0  5722-WDS
8.01 A                                      SFLSIZ(0017)
8.01 A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
6.11 A                                      CF14
     A                                      CF20
     A                                      PUTOVR
     A                                      SFLCSRRRN(&WCURRN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Matche bestilling mot ordre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Best.nr. . .'
     A            B2NUMM    R        O  4 15REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.02 A            B2SUFF    R        O  4 24REFFLD(LOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  4 46'Ordrenr. . .'
8.01 A            B2ORNR    R        O  4 59REFFLD(LOORNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Best.dato. .'
     A            B2BDAT         6Y 0O  5 15EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  5 46'Vår ref. . .'
8.01 A            B2VREF        30A  O  5 59COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Lev. dato. .'
     A            B2LDAT         6Y 0O  6 15EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6 46'Kundens ref.'
8.01 A            B2DREF    R        O  6 59REFFLD(LODREF)
8.06 A* 31                                  ERRMSG('Kan ikke endre da enheter e-
8.06 A*                                     r forskjellig ! ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.22 A**                                7  2'2=Endre ordrelinje'
6.22 A**                                    COLOR(BLU)
7.xx A                                  7  2'5=Vis  6=Oppd. m/antall  7=Oppd. m-
7.xx A                                      /pris  8=Oppd. m/avvik'
6.11 A                                      COLOR(BLU)
7.04 A                                  8  2'V'
7.04 A                                      COLOR(WHT)
7.04 A                                  8  4'Varetekst         '
7.04 A                                      COLOR(WHT)
8.01 A                                  8 28'Best antall'
7.04 A                                      COLOR(WHT)
8.01 A                                  8 41'Best enh'
7.04 A                                      COLOR(WHT)
8.01 A                                  8 50'Best kostpris'
7.04 A                                      COLOR(WHT)
8.01 A                                  8 65'Omr antall'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 76'Omr enh'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 84'Omr kostpris'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 99'Ord antall'
7.04 A                                      COLOR(WHT)
8.01 A                                  8111'Ord enh'
7.04 A                                      COLOR(WHT)
8.01 A                                  8119'L'
7.04 A                                      COLOR(WHT)
8.01 A                                  8121'Ord kostpris'
7.04 A                                      COLOR(WHT)
7.04 A*                                 8  2'V Varetekst             Best. anta-
7.04 A*                                     ll Enh   Pris     Ordre antall Enh -
7.04 A*                                     L Kostpris'
7.04 A*                                     COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19990602  000120  AMD         REL-V4R2M0  5769-PW1
8.01 A                                 26  2'F3=Gå tilbake  F14=Oppd. alle m-
6.11 A                                      /avvik                             -
     A                                               '
     A                                      COLOR(BLU)
      *
8.06 A*         R X1WIN
8.06 A*%%TS  SD  20020623  211603  HKO         REL-V5R1M0  5722-WDS
8.06 A*                                     WINDOW(*DFT 9 35)
8.06 A*                                     CA03
8.06 A*                                     CA12
8.06 A*                                     PUTOVR
8.06 A*                                 2  2'    V A R S E L    '
8.06 A*                                     DSPATR(RI)
8.06 A* 31                              4  2'Linjen kan ikke endres, da'
8.06 A* 31                              5  2'enhetene er forskjellig !'
8.06 A* 32                              4  2'Ikke alle linjenr er endret'
8.06 A* 32                              5  2'da enheter er forskjellig !'
8.06 A*                                 8  2'F3=Gå tilbake'
8.06 A*                                     COLOR(BLU)
