     A*%%TS  SD  20160606  130003  SST630      REL-V7R1M0  5770-WDS
     A*%%EC
     A*
7.xx A* 7.xx  191216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1SFL                     SFL
     A*%%TS  SD  20160606  130003  SST630      REL-V7R1M0  5770-WDS
     A            B1VALG         1A  B  6  2COLOR(YLW)
     A                                      VALUES('1' ' ')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1TEXT        10A  O  6  5
     A            B1BESK        30A  O  6 17
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160606  125803  SST630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  4  1' 1=Hente inn register'
     A                                      COLOR(BLU)
7.xx A                                  2  5'Register'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  5'Register'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 17'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2TEXT        10A  B  3  5OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B2BESK        30A  B  3 17OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  19951103  132646  HK          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                 14  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 14 14'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
