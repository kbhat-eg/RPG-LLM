     A*%%TS  SD  20050301  094206  BOF530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MM104D
     A* Beskrivelse . . : Prognose oversikt
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       020404 bø   Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050225  123853  BOF530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 4 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   O  8  4REFFLD(MOVARE)
     A            B1LAGE    R        O  8 16REFFLD(MOLAGE)
     A            B1TEXT    R        O  8 24REFFLD(MOTEXT)
     A            B1KABC    R        O  8 20REFFLD(MOKABC)
     A            B1VSOR    R        H      REFFLD(MOVSOR)
     A            B1OGRP    R        H      REFFLD(MOOGRP)
     A            B1HGRP    R        H      REFFLD(MOHGRP)
     A            B1UGRP    R        H      REFFLD(MOUGRP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050301  094206  BOF530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Prognose detaljer   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  3'4=Slett'
     A                                      COLOR(BLU)
     A                                  5 13'5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Varenr.   Lag.ABC  Tekst       -
     A                                                                         -
     A                                                '
     A                                      COLOR(WHT)
     A            B2VARE    R   -7   B  7  4REFFLD(MOVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2LAGE    R        B  7 16REFFLD(MOLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2KABC    R        B  7 20REFFLD(MOKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 13'F4'
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F8=Para-
                                            meter'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20041202  142747  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette prognose for vare'
     A                                      DSPATR(RI)
     A                                  4  2'Vare . . . . . . .'
     A            D1VARE    R   -7   O  4 21REFFLD(MMVARE)
     A                                  5  2'Lager  . . . . . .'
     A            D1LAGE    R        O  5 21REFFLD(MMLAGE)
     A                                  6  2'Tekst  . . . . . .'
     A            D1TEK1        35   O  6 21
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050131  183644  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg ABC-kode .:'
     A            H1KABC    R        B  4 19REFFLD(MBKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 26'Blank gir alle'
     A                                  5 26'Eks. AX,A , X'
     A                                  6  2'Bare låste  . .:'
     A            H1BARL    R        B  6 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6 26'1=Ja'
     A                                  7  2'Låst til ABC   :'
     A            H1LABC    R        B  7 19REFFLD(MBKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
