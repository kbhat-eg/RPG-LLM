     A*%%TS  SD  20090925  085004  NHO610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA101D
     A* Beskrivelse . . : Bilagskoder, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060302 KOB  Nytt program
6.10 A* R6.10 220909 NHO  Nye felt i forb med bilagsnummer og F4 i forbttet
6.10 A*                   med bilagsregistrering debet/kredit          tt og
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050803  185958  KOB540      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1AKOD    R        O  8  5REFFLD(RAAKOD)
     A            B1ATXT    R        O  8 11REFFLD(RAATXT)
     A            B1AKJØ    R        O  8 37REFFLD(RAAKJØ)
     A            B1ARNK    R        O  8 46REFFLD(RAARNK)
     A            B1AINK    R        O  8 55REFFLD(RFRFR/RAAINK *LIBL/RFRF)
     A            B1AFAC    R        O  8 64REFFLD(RFRFR/RAAFAC *LIBL/RFRF)
     A            B1ASUM    R        O  8 73REFFLD(RAASUM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050803  185958  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bilagskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse          '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 35'Kjøp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 44'Rente'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'Innbet'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'Factor'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Summer'
7.xx A                                      COLOR(WHT)
7.xx A**                                7  2'V  Kode  Beskrivelse             K-
7.xx A**                                    jøp     Rente   Innbet.  Factor.  S-
7.xx A**                                    ummer '
7.xx A**                                    COLOR(WHT)
7.xx A            B2AKOD    R        B  5  5REFFLD(RAAKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ATXT    R        B  5 11REFFLD(RAATXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bilagskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bilagskode . . . . . . . . .'
     A            C1AKOD    R        B  5 31REFFLD(RAAKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bilagskode finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090925  085004  NHO610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bilagskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bilagskode . . . . . . . . .'
     A            C1AKOD    R        O  4 31REFFLD(RAAKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse. . . . . . . . .'
     A            C2ATXT    R        B  5 31REFFLD(RAATXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
7.xx A                                  7  2'Bilagsnummer . . . . . . . +'
6.10 A            C2BINU    R        B  7 31REFFLD(RABINU)
6.10 A  34                                  ERRMSGID(AMN0020 *LIBL/QMFAGPL 34)
6.10 A                                      DSPATR(UL)
6.10 A                                      COLOR(YLW)
6.10 A                                      OVRDTA
6.10 A                                  8  2'Debet søk. . . . . . . . . .'
6.10 A            C2DEBE    R        B  8 31REFFLD(RADEBE)
6.10 A                                      DSPATR(UL)
6.10 A                                      COLOR(YLW)
6.10 A                                  8 34'(H,K,L)'
6.10 A                                  9  2'Kredit søk . . . . . . . . .'
6.10 A            C2KRED    R        B  9 31REFFLD(RAKRED)
6.10 A                                      DSPATR(UL)
6.10 A                                      COLOR(YLW)
6.10 A                                  9 34'(H,K,L)'
     A                                 10  2'Oppdatere kjøp i år. . . . .'
     A            C2AKJØ    R        B 10 31REFFLD(RAAKJØ)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '1')
     A                                 10 34'(1=Ja)'
     A                                 11  2'Renteberegnes ikke . . . . .'
     A            C2ARNK    R        B 11 31REFFLD(RAARNK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '1')
     A                                 11 34'(1=Ja)'
     A                                 12  2'Innbetalingskode rente . . .'
     A            C2AINK    R        B 12 31REFFLD(RAAINK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 34'(1=Ja)'
     A                                 14  2'Overføres til factoring. . .'
     A            C2AFAC    R        B 14 31REFFLD(RAAFAC)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14 34'(1=Ja)'
     A                                 16  2'Summer til hovedbok. . . . .'
     A            C2ASUM    R        B 16 31REFFLD(RAASUM)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '1')
     A                                 16 34'(1=Ja)'
     A                                 18  2'Opplysningstype arkiv. . . .'
     A            C2AOPT    R        B 18 31REFFLD(RAAOPT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bilagskode '
     A                                      DSPATR(RI)
     A                                  4  2'Bilagskode . . . .'
     A            D1AKOD    R        O  4 21REFFLD(RAAKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAATXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Bilagskode . . . .'
     A            K1AKOD    R        B  4 21REFFLD(RAAKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Bilagskode . . . .'
     A            RAAKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAATXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
