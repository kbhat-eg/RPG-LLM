     A*%%TS  SD  20080828  123934  ASP         REL-V5R4M0  5722-WDS
     A*%%EC
6.30 A*  070916  nho  2 rabattfelt endret til 5 posisjoner
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      MSGLOC(16)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070318  232303  KOB         REL-V5R4M0  5722-WDS
     A            B1VARE    R        H      REFFLD(HRVARE)
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1TEK1         6A  O  8  4
     A            B1ENHE    R        O  8 11REFFLD(HRENHE)
     A            B1ANTA         5Y 1O  8 15EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070318  232303  KOB         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2' EKSPEDISJON '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            B2SCA1        19A  B  4  2COLOR(YLW)
     A  31                                  ERRMSGID(HMR0120 *LIBL/QMFLAGR 31)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2SCA2         8A  B  5  2COLOR(YLW)
     A                                      OVRDTA
     A            B2ANTA         9Y 2O  5 11EDTCDE(4)
     A                                      OVRDTA
     A            B2ANTV         3Y 0O  6  2EDTCDE(Z)
     A                                      OVRDTA
     A            B2SAIM        10Y 2O  6 10EDTCDE(4)
     A                                      OVRDTA
     A                                  7  2'2=Endre  4=Slett'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20000927  084512  HKO         REL-V4R4M0  5769-PW1
     A                                 15  2'F4=Søk  F5=Forny'
     A                                      COLOR(WHT)
     A          R C2BLD
     A*%%TS  SD  20010509  110039  HKO         REL-V4R4M0  5769-PW1
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      PUTOVR
     A            C1ENHE    R        H      REFFLD(HRENHE)
     A                                  1  2' EKSPEDISJON '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            C1VARE    R        O  3  8REFFLD(HRVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1        19A  O  4  2COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK2        16A  O  5  2COLOR(YLW)
     A                                      OVRDTA
     A            C2LNAV        19A  O  6  2COLOR(YLW)
     A                                      OVRDTA
     A            C2ENH1    R        O  8  2REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH2    R        O  8  6REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  81                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH3    R        O  8 10REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  82                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Pris:'
     A            C2SAIM         9Y 2O  9  9COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C2KPRI         1A  O  9 20COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Rabatt:'
6.30 A**          C2RAB1         4Y 2O 10 10COLOR(YLW)
6.30 A            C2RAB1         5Y 2O 10 11COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      OVRDTA
6.30 A**          C2RAB2         4Y 2O 10 16COLOR(YLW)
6.30 A            C2RAB2         5Y 2O 10 18COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 12  2'Antall:'
     A            C2ANTA    R   -1   B 12 10REFFLD(HRANTA)
     A  31                                  ERRMSGID(HMR0130 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A  85                                  DSPATR(PR)
     A N85                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C2AKKU         9Y 3B 13 10COLOR(YLW)
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A  85                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 15  2'F1=Enh  F2=Kal  F6=Ak'
     A                                      COLOR(WHT)
     A          R D1BLD
     A*%%TS  SD  20000926  210827  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                  1  2' EKSPEDISJON '
     A                                      DSPATR(RI)
     A                                  3  2'Bekrefte sletting'
     A                                  4  2'av salg'
     A                                  6  2'Vare:'
     A            D1VARE    R        O  6  8REFFLD(HRVARE)
     A                                      COLOR(YLW)
     A            D1TEK1        19A  O  7  2COLOR(YLW)
     A            D1TEK2        16A  O  8  2COLOR(YLW)
     A                                 10  2'Enhet:'
     A            D1ENHE    R        O 10 10REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A                                 11  2'Antall:'
     A            D1ANTA    R   -1   O 11 10REFFLD(HRANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A          R E1BLD
     A*%%TS  SD  20070319  011559  KOB         REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                      PUTOVR
     A                                  1  2' EKSPEDISJON '
     A                                      DSPATR(RI)
     A                                  3  2'Tilleggsinformasjon'
     A                                  5  2'Selgernr:'
     A            E1SELG    R        B  5 12REFFLD(HOSELG)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(HMR0240 *LIBL/QMFLAGR 35)
     A  89                                  DSPATR(PR)
     A                                  6  2'Lev.adresse/navn:'
     A            E1NAV1        19A  B  7  2COLOR(YLW)
     A            E1NAV2        11A  B  8  2COLOR(YLW)
     A                                  9  2'Kundens ref.'
     A            E1DREF        19A  B 10  2COLOR(YLW)
     A                                      OVRDTA
     A  39                                  ERRMSGID(HMR0310 *LIBL/QMFLAGR 39)
     A                                 11  2'Rekv.nummer:'
     A            E1REKV        19A  B 12  2COLOR(YLW)
     A  34                                  ERRMSGID(HMR0320 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                 14  2'Trykk <Enter> for å'
     A                                 15  2'bekrefte salget.'
     A          R F1BLD
     A*%%TS  SD  20010919  143148  AMD         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      PUTOVR
     A                                  1  2' EKSPEDISJON '
     A                                      DSPATR(RI)
     A                                  3  2'Avslutting'
     A                                  5  2'Ekspedisjonen har'
     A                                  6  2'fått tildelt'
     A                                  7  2'referansenummer:'
     A            F1REFN    R        O  9  2REFFLD(HOREFN)
     A                                      COLOR(YLW)
     A                                 11  2'Kjøre pakkseddel'
     A                                 12  2'direkte ?'
     A            F1PAKS         1A  B 14  7COLOR(YLW)
     A                                      OVRDTA
     A  31                                  ERRMSGID(HMR0200 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(HMR0210 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(HMR0220 *LIBL/QMFLAGR 33)
     A  34                                  ERRMSGID(HMR0230 *LIBL/QMFLAGR 34)
     A  35                                  ERRMSGID(HMR0240 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 36)
     A  37                                  ERRMSGID(HMR0100 *LIBL/QMFLAGR 37)
     A  38                                  ERRMSGID(LM00011 *LIBL/QMFLAGR 38)
     A  41                                  ERRMSGID(HMR0330 *LIBL/QMFLAGR 41)
     A  42                                  ERRMSGID(HMR0340 *LIBL/QMFLAGR 42)
6.2B A  43                                  ERRMSGID(HMR0360 *LIBL/QMFLAGR 43)
     A                                 14 10'1=Ja'
     A          R M1MSG
     A*%%TS  SD  20080103  144410  BOF560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  5  2'Varen du har valgt '
     A                                  6  2'er en skaffevare.  '
     A                                  7  2'Tast <enter> for å '
     A                                  8  2'godkjenne dette.'
     A                                  9  2'F3=Tilbake'
6.24 A          R M2MSG
6.24 A*%%TS  SD  20080103  144410  NHO620      REL-V5R4M0  5722-WDS
6.24 A                                      CA03
6.24 A                                      CA05
6.24 A                                  1  2' SKAFFEVARE'
6.24 A                                      DSPATR(RI)
6.24 A                                  5  2'Varen du har valgt '
6.24 A                                  6  2'er en skaffevare.  '
6.24 A                                  7  2'og kan ikke godtas.'
6.24 A                                  8  2'F3=Tilbake'
