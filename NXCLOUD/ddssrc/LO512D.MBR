     A*%%TS  SD  20140530  121916  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
6.nu A* 300514 bof : Utvidet mtp. nummerutvidelse
     A*
7.01 A                                      DSPSIZ(27 132 *WIDE -
7.01 A                                             24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  121916  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  7  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A            B1SUFF    R        O  7 13REFFLD(LOSUFF)
     A            B1OTYP    R        O  7 16REFFLD(LOOTYP)
     A            B1ORSU    R        O  7 28REFFLD(LOORSU)
     A            B1ORTY    R        O  7 31REFFLD(LOOTYP)
     A            B1BDAT         6Y 0O  7 34EDTCDE(Y)
     A            B1LEVR        18A  O  7 43
     A            B1KUND        18A  O  7 62
     A            B1ORNR    R        O  7 19REFFLD(LOORNR)
     A            B1NUMM    R        O  7  4REFFLD(LONUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140530  121916  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      CF12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise bestillinger/ordrenummer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'V Bestnr         Ordrenr        Be-
     A                                      s.dato Leverandør         Kun'
     A                                      COLOR(WHT)
     A            B2NUMM    R        B  6  4REFFLD(LONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(LMG0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMG0030 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LMG0030 *LIBL/QMFLAGR 33)
     A            B2ORNR    R        B  6 19REFFLD(LOORNR)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20020923  083103  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Bla-alt'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A          R H1BLD
     A*%%TS  SD  20020925  091950  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Bla alternativer '
     A                                      DSPATR(RI)
     A                                  5  2'Blaing bakfra .'
     A            H1VALG    R        B  5 18REFFLD(LÅVALG)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 22'1=Ja,0=Nei'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
