     A*%%TS  SD  20020924  154120  HKO         REL-V5R1M0  5722-WDS
7.01 A*K00   290420  bof  Takler bredere skjermbilder
     A*%%EC
7.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R D1SFL                     SFL
     A*%%TS  SD  20020924  154120  HKO         REL-V5R1M0  5722-WDS
     A            D1FILG         3A  O  8  3
     A            D1LAGE    R        O  8 12REFFLD(VLLAGE)
     A                                      EDTWRD('0 ')
     A            D1LTEK        20A  O  8 15
     A            D1DISP         9Y 2O  8 36EDTWRD('       ,  -')
     A  41                                  COLOR(RED)
     A            D1BEHL         9Y 2O  8 48EDTWRD('       ,  -')
     A            D1BBES         9Y 2O  8 60EDTWRD('       ,  -')
     A            D1PAKK         9Y 2O  8 72EDTWRD('       ,  -')
     A            D1OORD         9Y 2O  8 84EDTWRD('       ,  -')
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20020924  154120  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise lagerinformasjon, andre lager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare:'
     A                                      COLOR(GRN)
     A            D2VARE    R   -7   O  5  8REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A            D2TEK1    R        O  5 17REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                  5 54'Lagerenhet'
     A            D2LENH    R        O  5 65REFFLD(VVENHL)
     A                                      COLOR(YLW)
     A                                  7  2'Filgr.    Lager                   -
     A                                      Disp.lager     I lager     I best. -
     A                                       For utlev.     I ordre'
     A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  20020924  153915  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
