     A*%%TS  SD  20070819  173755  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA103D
     A* Beskrivelse . . : Betalingsbetingelser, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060302 KOB  Nytt program
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 6.30  271217  bsa Setter på 16 if f-tast er trykket(VLDCMDKEY)
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1CKOD    R        O  8  5REFFLD(RACKOD)
     A            B1CTXT    R        O  8 11REFFLD(RACTXT)
     A            B1CDAG    R        O  8 36REFFLD(RACDAG)
     A            B1CJAN    R        O  8 45REFFLD(RACJAN)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  150257  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
7.01 A                                      VLDCMDKEY(16)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betalingsbetingelse-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 35'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 45'Fri'
7.xx A                                      COLOR(WHT)
7.xx A            B2CKOD    R        B  5  5REFFLD(RACKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2CTXT    R        B  5 11REFFLD(RACTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  150257  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betalingsbetingelse-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Betalingsbetingelse. . . . .'
     A            C1CKOD    R        B  5 31REFFLD(RACKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  002125  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Betalingsbetingelse finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070819  173755  KOB550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betalingsbetingelse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Betalingsbetingelse. . . . .'
     A            C1CKOD    R        O  5 31REFFLD(RACKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2CTXT    R        B  7 31REFFLD(RACTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2CTX2    R        B  8 31REFFLD(RACTX2)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  9  2'Antall dager kredit. . . . .'
     A            C2CDAG    R        B  9 31REFFLD(RACDAG)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 10  2'Fri leveringsmåned . . . . .'
     A            C2CJAN    R        B 10 31REFFLD(RACJAN)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      VALUES('JA ' 'NEI' '15 ' ' 15')
     A                                 11  2'Kontantrabatt. . . . . . . .'
     A            C2CRAB    R        B 11 31REFFLD(RACRAB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(1)
     A                                 12  2'Antall dager rabatt. . . . .'
     A            C2CDA1    R        B 12 31REFFLD(RACDA1)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 14  2'Antall dager kredit 2. . . .'
     A            C2CDA2    R        B 14 31REFFLD(RACDA2)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 15  2'Kontantrabatt. . . . . . . .'
     A            C2CRA2    R        B 15 31REFFLD(RACRA2)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      EDTCDE(1)
     A                                 16  2'Antall dager kredit 3. . . .'
     A            C2CDA3    R        B 16 31REFFLD(RACDA3)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 17  2'Kontantrabatt. . . . . . . .'
     A            C2CRA3    R        B 17 31REFFLD(RACRA3)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(1)
     A                                 19  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  002125  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette betalinsbetingelse '
     A                                      DSPATR(RI)
     A                                  4  2'Betalingsbetingelse'
     A            D1CKOD    R        O  4 22REFFLD(RACKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RACTXT    R        O  6 22
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020307  002125  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Betalingsbetingelse'
     A            K1CKOD    R        B  4 22REFFLD(RACKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Betalingsbetingelse'
     A            RACKOD    R        O  8 22
     A                                  9  2'Beskrivelse. . . .'
     A            RACTXT    R        O  9 22
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
