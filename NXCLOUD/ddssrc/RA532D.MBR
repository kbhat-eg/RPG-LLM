     A*%%TS  SD  20020327  202558  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA532D
     A* Beskrivelse . . : Kort, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       080910 bsa  Nytt program
7.xx A*       220120 bsa  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1SKTY         2S 0H
     A            B1SLAG         2S 0H
7.xx A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1TYPE        20   O  8  5
7.xx A            B1SBES    R        O  8 26REFFLD(RUSBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020327  202558  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0008)
7.xx A                                      SFLPAG(0007)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge kort '
     A                                      DSPATR(RI)
7.xx A                                  4  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 24'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 24'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2STYP        20   B  5  5
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2SBES    R        B  5 26REFFLD(RUSBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 60)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
