     A*%%TS  SD  20081023  112018  BOF570      REL-V5R4M0  5722-WDS
     A*%%EC
6.41 A*     BOF  120118    Påslag på kostpris
7.xx A*          260116 bsa  Justeringer basert på ny GUI og regler
8.01 A*          100622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
     A*-----------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081023  111755  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 12  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.01 A            B1PRGR    R        O 12  4REFFLD(FRPRGR)
     A            B1RKAT    R        O 12  7REFFLD(FRRKAT)
     A            B1KKAT    R        O 12 11REFFLD(FRKKAT)
     A            B1KUND    R        O 12 16REFFLD(FRKUND)
     A            B1KPRO    R        O 12 23REFFLD(FRKPRO)
     A            B1OGRP    R        O 12 30REFFLD(FROGRP)
     A            B1HGRP    R        O 12 33REFFLD(FRHGRP)
     A            B1UGRP    R        O 12 36REFFLD(FRUGRP)
     A            B1LDOR    R        O 12 40REFFLD(FRLDOR)
     A            B1MODN    R        O 12 47REFFLD(FRMODN)
     A            B1VARE    R   -7   O 12 56REFFLD(FRVARE)
     A            B1ENHE    R        O 12 65REFFLD(FRENHE)
     A            B1LETY    R        O 12 69REFFLD(FRLETY)
     A            B1RAB1         2Y 0O 12 71EDTCDE(4)
     A            B1RABN         2Y 0O 12 74EDTCDE(4)
     A            B1DEKN         3Y 0O 12 77EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081023  112018  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      CA23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  1 38'FR510B1'
7.xx A                                      COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise rabattmatrise for kunde/kunde-
     A                                      prosjekt'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rabattkategori. . .'
     A                                      COLOR(BLU)
     A            B2RKA1    R        O  4 22REFFLD(FRRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2RTXT        30A  O  4 30COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kundekategori . . .'
     A                                      COLOR(BLU)
     A            B2KKA1    R        O  5 22REFFLD(FRKKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KTXT        30A  O  5 30COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kunde . . . . . . .'
     A                                      COLOR(BLU)
     A            B2KUN1    R        O  6 22REFFLD(FRKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV        30A  O  6 30COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kundeprosjekt . . .'
     A                                      COLOR(BLU)
     A            B2KPR1    R        O  7 22REFFLD(FRKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PNAV    R        O  7 30REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'Rka'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Kka'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 16'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 23'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 30'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 33'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 36'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Lev.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 47'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 65'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 69'L'
     A                                      COLOR(WHT)
7.xx A**                                9  4'Pg Rka Kka  Kunde  Prosj. Og Hg -
7.xx A**                                    Ugr Lev.   Modulnr. Varenr.  Enh L'
7.xx A**                                    COLOR(WHT)
8.01 A            B2PRGR    R        B  9  4REFFLD(FRPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2RKAT    R        B  9  7REFFLD(FRRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2KKAT    R        B  9 11REFFLD(FRKKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KUND    R        B  9 16REFFLD(FRKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KPRO    R        B  9 23REFFLD(FRKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OGRP    R        B  9 30REFFLD(FROGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2HGRP    R        B  9 33REFFLD(FRHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2UGRP    R        B  9 36REFFLD(FRUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  9 40REFFLD(FRLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2MODN    R        B  9 47REFFLD(FRMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2VARE    R   -7   B  9 56REFFLD(FRVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ENHE    R        B  9 65REFFLD(FRENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2LETY    R        B  9 69REFFLD(FRLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A  80                              9 71'ds ds ds'
     A                                      COLOR(WHT)
7.xx A                                 11  2'V Pg Rka Kka  Kunde  Prosj. Og Hg -
7.xx A                                      Ugr Lev.   Modulnr. Varenr.  Enh L -
7.xx A                                      R1 Rn DG'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990519  102853  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny'
     A                                      COLOR(BLU)
     A N80                             23 66'F23=Datostyrte'
     A                                      COLOR(BLU)
     A  80                             23 66'F23=Ordinære'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20031116  125851  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise rabattmatrise-nivå'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgruppe . . . . . . . .'
     A            C2PRGR    R        O  4 29REFFLD(FRPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2DTXT        30A  O  4 39COLOR(YLW)
     A                                  5  2'Rabattkategori . . . . . .'
     A            C2RKAT    R        O  5 29REFFLD(FRRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RTXT        30A  O  5 39COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundekategori. . . . . . .'
     A            C2KKAT    R        O  6 29REFFLD(FRKKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KTXT        30A  O  6 39COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kunde. . . . . . . . . . .'
     A            C2KUND    R        O  7 29REFFLD(FRKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV        30A  O  7 39COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kundeprosjekt. . . . . . .'
     A            C2KPRO    R        O  8 29REFFLD(FRKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PNAV    R        O  8 39REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varegruppe . . . . . . . .'
     A            C2OGRP    R        O  9 29REFFLD(FROGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2HGRP    R        O  9 32REFFLD(FRHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UGRP    R        O  9 35REFFLD(FRUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2GTXT    R        O  9 39REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Leverandør . . . . . . . .'
     A            C2LDOR    R        O 10 29REFFLD(FRLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LNAV        30A  O 10 39COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Modulnr. . . . . . . . . .'
     A            C2MODN    R        O 11 29REFFLD(FRMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2MTXT        35A  O 11 39COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Varenr . . . . . . . . . .'
     A            C2VARE    R   -7   O 12 29REFFLD(FRVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2VTXT    R        O 12 39REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Enhet. . . . . . . . . . .'
     A            C2ENHE    R        O 13 29REFFLD(FRENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ETXT    R        O 13 39REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Leveringstype. . . . . . .'
     A            C2LETY    R        O 14 29REFFLD(FRLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LTXT    R        O 14 39REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.41 A                                 15  2'Påslag på kostpris . . . .'
6.41 A            C2PSLM    R        O 15 29REFFLD(FRPSLM)
6.41 A                                      COLOR(YLW)
6.41 A                                      OVRDTA
     A                                 15 38'Datostyrt'
     A                                      DSPATR(UL)
     A                                 15 50'Dato f.o.m'
     A                                      DSPATR(UL)
     A                                 15 62'Dato t.o.m'
     A                                      DSPATR(UL)
     A                                 16  2'Rabatt-1 . .(priskode '' '')'
     A            C2RAB1    R        O 16 29REFFLD(FRRAB1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RA1D    R        O 16 40REFFLD(FRRA1D)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RA1F         6Y 0O 16 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2RA1T         6Y 0O 16 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17  2'Rabatt-2 . .(priskode '' '')'
     A            C2RAB2    R        O 17 29REFFLD(FRRAB2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RA2D    R        O 17 40REFFLD(FRRA2D)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RA2F         6Y 0O 17 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2RA2T         6Y 0O 17 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 18  2'Rabatt-N . .(priskode ''N'')'
     A            C2RABN    R        O 18 29REFFLD(FRRABN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RAND    R        O 18 40REFFLD(FRRAND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RANF         6Y 0O 18 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2RANT         6Y 0O 18 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Rabatt-B . .(priskode ''B'')'
     A            C2RABB    R        O 19 29REFFLD(FRRABN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RABD    R        O 19 40REFFLD(FRRABD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RBUF         6Y 0O 19 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2RBUT         6Y 0O 19 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Rabatt-T . .(priskode ''T'')'
     A            C2RABT    R        O 20 29REFFLD(FRRABT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RATD    R        O 20 40REFFLD(FRRATD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RATF         6Y 0O 20 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2RATT         6Y 0O 20 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Dekningsgrad . . . . . . .'
     A            C2DEKN    R        O 21 29REFFLD(FRDEKN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2DEKD    R        O 21 40REFFLD(FRDEKD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2DEKF         6Y 0O 21 51COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2DEKT         6Y 0O 21 63COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 57'/'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(FREUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
