     A*%%TS  SD  20020930  134455  HKO         REL-V5R1M0  5722-WDS
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP600D
     A* Beskrivelse . . : Prosjekt, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       121110 NHO  Nytt program
7.01 A* K00   060520  bsa  Takler brede skjermer.
7.xx A* K00   080520  bsa  Skrevet om til cloud.
7.02 A* MGR   031017 sst  Mulig å opprette prosjekt
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020907  200411  KOB         REL-V5R1M0  5722-WDS
     A            B1ALFA    R        H      REFFLD(RPALFA)
     A            B1UPRS    R        H      REFFLD(RP1RUP)
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1PROS    R        O  7  4REFFLD(RÅPROA)
     A            B1TXT1    R        O  7 12REFFLD(RPALFA)
     A            B1NAVN        15   O  7 32
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020930  134455  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Spørring prosjekt '
     A                                      DSPATR(RI)
7.xx A                                  3  4'Prosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 12'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Prosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 12'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 32'Kundenavn      '
     A                                      COLOR(WHT)
7.xx A            B2PROS    R        B  4  4REFFLD(RÅPROA)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  4 12REFFLD(RPALFA)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020106  234033  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F5=Forny'
     A                                      COLOR(BLU)
     A                                 16 28'F6=Opprette'
     A          R ASUME                     ASSUME
     A                                 24 79' '
