     A*%%TS  SD  20210413  142830  ASP_NXC     REL-V7R3M0  5770-WDS
     A*%%190521 bof : utvidet med miljø
8.01 A*%%043035 bof : Endret ledetekster og tatt med beskrivelsefelt og spørring
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20210413  140940  ASP_NXC     REL-V7R3M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1GSRV    R        O  8  5REFFLD(AFGSRV)
     A            B1GFGR    R        O  8 20REFFLD(AFGFGR)
     A            B1GKJD    R        O  8 32REFFLD(AFGKJD)
     A            B1GMIL    R        O  8 40REFFLD(AFGMIL)
8.01 A            B1GVER    R        O  8 67REFFLD(AFGVER)
     A            B1GAKT    R        O  8 50REFFLD(AFGAKT)
8.01 A            B1GKO1    R        O  8 57REFFLD(AFGKO1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20210413  142830  ASP_NXC     REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av filgruppe pr. serve-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 38'AF120B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  6'Server'
     A                                      COLOR(WHT)
     A                                  4 18'Filegruppe'
     A                                      COLOR(WHT)
     A                                  5  2'+'
     A            B2GSRV    R        B  5  5REFFLD(AFGSRV)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 18'+'
     A            B2GFGR    R        B  5 20REFFLD(AFGFGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  6'Server'
     A                                      COLOR(WHT)
     A                                  7 16'Filegruppe'
     A                                      COLOR(WHT)
8.01 A                                  7 67'Versjon'
     A                                      COLOR(WHT)
     A                                  7 48'Aktiv'
     A                                      COLOR(WHT)
     A                                  7 31'Kjede'
     A                                      COLOR(WHT)
     A                                  7 38'Miljø'
     A                                      COLOR(WHT)
8.01 A                                  7 54'Spørring'
8.01 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020219  115427  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av filegrupper pr.-
     A                                      server'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Server . . . . . . . . . . .'
     A            C1GSRV    R        B  5 31REFFLD(AFGSRV)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(AM00010 *LIBL/QMFAGPL 32)
     A                                      OVRDTA
     A                                  6  2'Filegruppe . . . . . . . . .'
     A            C1GFGR    R        B  6 31REFFLD(AFGFGR)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(AMG0010 *LIBL/QMFAGPL 31)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Filegruppen finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090602  151844  ASP_OPT     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av filegrupper pr.-
     A                                      server'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Server . . . :'
     A            C1GSRV    R        O  5 17REFFLD(AFGSRV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Filegruppe . :'
     A            C1GFGR    R        O  6 17REFFLD(AFGFGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kjede  . . . :'
     A            C2GKJD    R        B  7 17REFFLD(AFGKJD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Versjon  . . :'
     A            C2GVER    R        B  8 17REFFLD(AFGVER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Miljø  . . . :'
     A            C2GMIL    R        B  9 17REFFLD(AFGMIL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Level  . . . :'
     A            C2GLEV    R        B 10 17REFFLD(AFGVER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Aktiv  . . . :'
     A            C2GAKT    R        B 11 17REFFLD(AFGAKT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 20'1=Ja,0=Nei'
8.01 A                                 12  2'Spørring . . :'
     A            C2GKO1    R        B 12 17REFFLD(AFGKO1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 20'1=Ja,0=Nei'
     A                                 13  2'Kode 2 . . . :'
     A            C2GKO2    R        B 13 17REFFLD(AFGKO2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 20'1=Ja,0=Nei'
     A                                 14  2'Kode 3 . . . :'
     A            C2GKO3    R        B 14 17REFFLD(AFGKO3)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14 20'1=Ja,0=Nei'
     A                                 15  2'Kode 4 . . . :'
     A            C2GKO4    R        B 15 17REFFLD(AFGKO4)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 20'1=Ja,0=Nei'
8.01 A                                 16  2'Kan fjernes. :'
     A            C2GKO5    R        B 16 17REFFLD(AFGKO5)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16 20'1=Ja,0=Nei'
8.01 A                                 17  2'Fri kode/Dato:'
     A            C2GKOD    R        B 17 17REFFLD(AFGKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20 51'Opprettet dato . .'
     A            C2GODA         6Y 0O 20 71COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 51'Sist endret dato .'
     A            C2GEDA         6Y 0O 21 71COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 51'Endret av. . . . .'
     A            C2GEUS    R        O 22 71REFFLD(AYEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
8.01 A                                 18  2'Beskrivelse'
8.01 A            C2GTXT        35A  B 18 17
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20090602  151844  ASP_OPT     REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette filegruppe'
     A                                      DSPATR(RI)
     A                                  4  2'Server . . . . . .'
     A            D1GSRV    R        O  4 21REFFLD(AFGSRV)
     A                                  5  2'Filegruppe . . . .'
     A            D1GFGR    R        O  5 21REFFLD(AFGFGR)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Server . . . . . .'
     A            K1GSRV    R        B  4 21REFFLD(AFGSRV)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(AM00010 *LIBL/QMFAGPL 32)
     A                                      OVRDTA
     A                                  5  2'Filegruppe . . . .'
     A            K1GFGR    R        B  5 21REFFLD(AFGFGR)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(AMG0010 *LIBL/QMFAGPL 31)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Server . . . . . .'
     A            AFGSRV    R        O  9 21
     A                                      OVRDTA
     A                                 10  2'Filegruppe . . . .'
     A            AFGFGR    R        O 10 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
