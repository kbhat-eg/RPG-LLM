     A*%%TS  SD  20020331  014019  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH672D
     A* Beskrivelse . . : MVA-oppgave, spesifikasjon- parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120207 HFL  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20010408  155452  KOB         REL-V4R4M0  5769-PW1
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av MVA-spesifikasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  8 10'Dato:'
     A            A2PKDA    R        B  8 16REFFLD(RHPKDA)
     A  33                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                  8 27'Regnskapsår . . .:'
     A            A2PRÅR    R        B  8 46REFFLD(RHPRÅR)
     A  34                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 34)
     A                                      COLOR(YLW)
     A                                 10 27'MVA-periode . . .:'
     A            A2PMVA    R        B 10 46REFFLD(RHPMVA)
     A                                      COLOR(YLW)
     A                                 12 10'Spesifikasjon pr. transaksjon. . .-
     A                                      :'
     A            A2PSPS    R        B 12 46REFFLD(RHPMV0)
     A                                      COLOR(YLW)
     A                                 12 50'(J=Ja, N=Nei)'
     A                                 14 10'Utskrift av poster med mva-kode=0.-
     A                                      :'
     A            A2PMV0    R        B 14 46REFFLD(RHPMV0)
     A                                      COLOR(YLW)
     A                                 14 50'(J=Ja, N=Nei)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Meldin10: '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
