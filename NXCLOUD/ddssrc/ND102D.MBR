     A*%%TS  SD  20100305  132700  SSTASP      REL-V5R4M0  5722-WDS
     A*%%EC
     A*          120318 bsa  Nytt program
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040202  104639  HKO         REL-V5R2M0  5722-WDS
     A            B1TELL    R        H      REFFLD(NOTELL)
     A            B1FSYS    R        H      REFFLD(NOFSYS)
     A            B1LEDT    R        H      REFFLD(NOLDAT)
     A            B1ODAT         6Y 0H
     A            B1LDOR    R        H      REFFLD(FDLDOR)
     A            B1VALG    R       0B  8  2REFFLD(FÅVALG)
     A                                      VALUES(0 5)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM    R        O  8  4REFFLD(FÅNUMM)
     A            B1SUFF    R        O  8 13REFFLD(FÅSUFF)
     A            B1OTYP    R        O  8 16REFFLD(FÅOTYP)
     A            B1LNAV        30A  O  8 19
     A            B1LDAT         6Y 0O  8 50EDTWRD('  .  .  ')
     A            B1BNUM    R        O  8 60REFFLD(FÅNUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051201  112514  HKO550      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  60                                  SFLMSGID(FMO0400 *LIBL/QMFOFAK 60)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis ordre etter referanse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A*                                 4  2'Ekst. system'
     A*                                     COLOR(BLU)
     A            B2FSYS    R        O  4  2REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A                                      COLOR(BLU)
     A            B2BESK        20   O  4  6
     A                                      COLOR(BLU)
     A                                  4 41'Ref'
     A                                      COLOR(BLU)
     A            B2NREF    R        O  4 45REFFLD(NONREF)
     A                                      COLOR(BLU)
     A            B2KNAV        30A  O  5  2
     A            B2PNAV        30A  O  5 41
      *
     A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'Ordrenr'
     A                                      COLOR(WHT)
     A                                  7 13'Sf'
     A                                      COLOR(WHT)
     A                                  7 16'OT'
     A                                      COLOR(WHT)
     A                                  7 19'Leverandør     '
     A                                      COLOR(WHT)
     A                                  7 50'Lev.dato'
     A                                      COLOR(WHT)
     A                                  7 61'Bestnr.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051130  211152  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny'
     A                                      COLOR(BLU)
