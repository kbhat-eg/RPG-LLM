     A*%%TS  SD  20050828  131459  KOB540      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL510D
     A* Beskrivelse . . : Leverandørposteringer, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       300702 KOB  Nytt program
7.xx A*       030516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010127  210556  KOB         REL-V4R4M0  5769-PW1
     A            B1RAAR    R        H      REFFLD(RORAAR)
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(ROBILN)
6.NU A            B1BIDA         6  0O  9 13
6.NU A            B1BILK    R        O  9 20REFFLD(ROBILK)
6.NU A            B1TEXT        14A  O  9 23
     A            B1BELØ        11Y 2O  9 38EDTCDE(J)
     A            B1LEVR    R        O  9 54REFFLD(ROLEVR)
     A            B1NAVN    R  -10   O  9 61REFFLD(RLNAVN)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050828  131459  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på leverandørposter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 23'Tekst/Lev.fakt'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 44'Beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis  9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  5 61'Regnskapsår. .:'
     A            B2RAAR    R        B  5 77REFFLD(RORAAR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      TEXT('Regnskapsår ........')
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
7.xx A                                  8 14'Dato'
     A                                      COLOR(WHT)
7.xx A                                  8 20'Bk'
     A                                      COLOR(WHT)
7.xx A                                  8 23'Tekst/Lev.fakt'
     A                                      COLOR(WHT)
7.xx A                                  8 44'Beløp'
     A                                      COLOR(WHT)
7.xx A                                  8 54'Lev.nr'
     A                                      COLOR(WHT)
7.xx A                                  8 61'Navn'
     A                                      COLOR(WHT)
7.xx A            B2BILN    R        B  6  4REFFLD(ROBILN)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
7.xx A            B2BDAT         6Y 0B  6 13CHECK(RZ)
7.xx A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2LFAK    R        B  6 23REFFLD(ROLFAK)
7.xx A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2BELØ    R        B  6 42REFFLD(ROBELØ)
7.xx A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020806  135217  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for visning
     A*
     A          R C2WIN
     A*%%TS  SD  20050519  232048  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
     A                                      CA03
     A                                      CA04
     A                                      PUTOVR
     A                                  2 29'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        O  6 19REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        O  7 19REFFLD(ROREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlebilag-nr. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        O  8 19REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        O  9 19REFFLD(ROAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(ROREST)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjon. . .:'
     A            C2ATTS    R        O 10 19REFFLD(ROATTS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2ATTU    R        O 10 30REFFLD(ROATTU)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2ADAT         6Y 0O 10 21DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        O 11 19REFFLD(ROBETK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        O 12 19REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        O 13 19REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        O 13 61REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        O 14 19REFFLD(RODECK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        O 15 17REFFLD(ROKIDI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 43'Lev.fakturanr. .:'
     A            C2LFAK    R        O 16 61REFFLD(ROLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
