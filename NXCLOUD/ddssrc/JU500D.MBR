     A*%%TS  SD  20050919  083540  HKO530      REL-V5R3M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
8.01 A*       120424 bsa  Tillater brede skjermbilder
     A*---------------------------------------------------------------
8.01 A                                      DSPSIZ(24 80 *DS3 -
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050918  212758  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5 6 7 8)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1MTE1    R        O  8  4REFFLD(JUMTE1)
     A            B1MODN    R        O  8 40REFFLD(JUMODN)
     A            B1LNAV        21A  O  8 49
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050919  083540  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge modul '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Fritekst . .'
     A                                      COLOR(BLU)
7.xx A            B2STEK        35A  B  3 15COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  4  4'Modultekst-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 40'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'1=Velg  5=Vis  6=Fritekst  7=Stikk-
     A                                      ord  8=Standarder'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Modultekst-1                    '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'Modulnr '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Leverandør'
     A                                      COLOR(WHT)
7.xx A            B2MTE1    R        B  5  4REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2MODN    R        B  5 40REFFLD(JUMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050918  213055  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 18 70)
     A                                 17  2'F3=Gå tilbake   F5=Forny   F9=Gjen-
     A                                      ta søk'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
