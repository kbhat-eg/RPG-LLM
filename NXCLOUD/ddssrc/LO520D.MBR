     A*%%TS  SD  20140530  122359  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
6.nu A* 300514 bof : Utvidet mtp. nummerutvidelse
7.01 A* 7.01  1202121 ask Endret skjermbildet med tanke på GU visning
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R D1SFL                     SFL
     A*%%TS  SD  20140530  122359  BOF630      REL-V7R1M0  5770-WDS
     A            D1SUFF    R        H      REFFLD(LDSUFF)
     A            D1LINE    R        H      REFFLD(LDLINE)
     A            D1KODE         1A  B  8  2COLOR(YLW)
     A                                      VALUES('1' '3' '4' '5' ' ')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            D1OTYP    R        O  8 13REFFLD(LOOTYP)
     A            D1LDOR    R        O  8 16REFFLD(LDLDOR)
     A            D1NAVN    R   -6   O  8 23REFFLD(LONAVN)
     A            D1DATO         6Y 0O  8 48EDTWRD('  .  .  ')
     A                                      COLOR(RED)
     A            D1ANTA    R   -2 03O  8 57REFFLD(LDANTA)
     A                                      EDTWRD('      ,   ')
     A            D1ENH1    R        O  8 68REFFLD(LDENH1)
     A            D1NUMM    R        O  8  4REFFLD(LDNUMM)
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20140530  122359  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN02         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise bestillingslinjer for varenr'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . .'
     A                                      COLOR(BLU)
     A            D2VARE    R        O  4 21REFFLD(LDVARE)
     A            D2TEK1    R        O  4 39REFFLD(LDTEK1)
7.01 A                                  7  2'K'
7.01 A                                      COLOR(WHT)
7.01 A                                  7  4'Bestnr'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 13'OT'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 16'Lev.dør'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 25'Navn'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 48'Lev.dato'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 59'Antall'
7.01 A                                      COLOR(WHT)
7.01 A                                  7 68'Enh'
7.01 A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  19970419  125640  AMD         REL-V3R7M0  5716-PW1
     A                                 23  2'F1=Bla-alternativer'
     A                                      COLOR(BLU)
     A                                 23 23'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 38'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
