     A*%%TS  SD  20110325  083451  ASP_MGR     REL-V6R1M0  5761-WDS
     A*%%EC
6.20 A*     nho  191214    Lagt inn feilmelding på filnavn, kun 5 pos.
6.20 A*                    da det er det som ligger på filen
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20110325  083451  ASP_MGR     REL-V6R1M0  5761-WDS
     A*                                     WINDOW(*DFT 16 54)
     A                                      CA03
     A                                      CA12
     A*                                     PUTOVR
     A                                  2  2' Innlesing av Kampanje Nobb Integ.-
     A                                      dat format  '
     A                                      DSPATR(RI)
     A                                  4  2'Mappenavn . . . . .'
     A            B1FLDR        20A  B  4 22COLOR(YLW)
     A                                  6  2'Filnavn   . . . . .'
     A            B1FILN        12A  B  6 22COLOR(YLW)
     A  31                                  ERRMSGID(JMR0110 *LIBL/QMFVADM 31)
     A*                                     OVRDTA
     A                                  8  2'Kampanjenavn  . . .'
     A            B1KAMP        10A  B  8 22COLOR(YLW)
     A  32                                  ERRMSGID(JMM0010 *LIBL/QMFVADM 32)
6.20 A  33                                  ERRMSGID(JMM0011 *LIBL/QMFVADM 31)
     A*                                     OVRDTA
9.01 A                                 10  2'CSV-format  . . . .'
9.01 A            B1CSVF    R        B 10 22REFFLD(JÅVALG)
9.01 A                                      COLOR(YLW)
9.01 A                                      VALUES(0 1)
9.01 A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
9.01 A*                                     OVRDTA
     A                                 10 25'1=ja'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1MSG
     A*%%TS  SD  20010904  091133  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 12 36)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Valgt mappenavn er ugyldig, filene'
     A                                  3  2'ble ikke funnet i mappen, eller du'
     A                                  4  2'mangler tilgang for behandling av'
     A                                  5  2'mapper.'
     A                                  7  2'Sjekk at filene finnes i mappen.'
     A                                  8  2'Dersom du mangler tilgang må du'
     A                                  9  2'kontakte systemansvarlig.'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
