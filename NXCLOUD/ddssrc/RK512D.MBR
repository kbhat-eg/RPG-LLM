     A*%%TS  SD  20110712  134604  ASK610      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK512D
     A* Beskrivelse . . : Kundeposteringer, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050802 KOB  Nytt program
7.01 A*       030120 bsa  Forberetdt for bredere skjermbilde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20040216  095743  KOB         REL-V5R2M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1RAAR    R        H      REFFLD(RNRAAR)
     A            B1RPER    R        H      REFFLD(RNRPER)
     A            B1INKO    R        H      REFFLD(RNINKO)
     A            B1ANTP    R        H      REFFLD(RNANTP)
     A            B1REKL    R        H      REFFLD(RNREKL)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(RNBILN)
     A  73                                  COLOR(YLW)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
6.NU A            B1BIDA         6Y 0O  9 13EDTCDE(Z)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
6.NU A            B1BILK    R        O  9 20REFFLD(RNBILK)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
6.NU A            B1TEXT        10A  O  9 23
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
6.NU A            B1REFN    R        O  9 34REFFLD(RNREFN)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1FDAT         6Y 0O  9 43EDTCDE(Z)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1BELØ        11Y 2O  9 50EDTCDE(J)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1REST        11Y 2O  9 66EDTCDE(J)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110712  134604  ASK610      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på kundeposteringer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde:'
     A            B2KUND    R        O  4  9REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                  4 58'Regnskapsår. .:'
     A            B2RAAR         4Y 0B  4 74COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  94        B2NOTB         8A  O  5 16DSPATR(RI)
     A                                      OVRDTA
     A                                  5 58'Periode. . . .:'
     A            B2RPER         2Y 0B  5 74COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2'1=Velg  5=Vis  6=Vis motpost  7=Vi-
     A                                      s bilag'
     A                                      COLOR(BLU)
     A                                  7  2'8=Vis faktura  9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  7 58'Saldo:'
     A            B2SALD    R        O  7 66REFFLD(RKSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  8 14'Dato'
     A                                      COLOR(WHT)
6.NU A                                  8 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  8 23'Tekst'
     A                                      COLOR(WHT)
     A                                  8 31'Val.'
     A                                      COLOR(WHT)
     A                                  8 37'Ref.'
     A                                      COLOR(WHT)
     A                                  8 43'Forfall'
     A                                      COLOR(WHT)
     A                                  8 56'Beløp'
     A                                      COLOR(WHT)
     A                                  8 68'Rest/Akkum'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20040317  200818  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F2=Notatark'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 30'F6=Åpne/Alle/Akkum'
     A                                      COLOR(BLU)
     A                                 23 50'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A                                 23 65'F8=Saldo/Kjøp'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20040507  093116  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 75)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 32'Kundeposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  73                                  COLOR(YLW)
6.NU A  73        C2NOTB         8   O  3 28
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(RNBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(RNTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6  0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        O  6 19REFFLD(RNREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        O  7 19REFFLD(RNREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Generalnota-nr. :'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        O  8 19REFFLD(RNANTP)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 24'Dato:'
     A            C2PDAT         6Y 0O  8 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        O  9 19REFFLD(RNRENK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 24'Dato:'
     A            C2RDAT         6  0O  9 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(RNREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Inkassokode. . .:'
     A            C2INKO    R        O 10 19REFFLD(RNINKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 24'Dato:'
     A            C2IDAT         6  0O 10 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            C2VALK    R        O 10 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            C2AVDE    R        O 11 19REFFLD(RNAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2GTXT    R   18   O 11 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valutabeløp. . .:'
     A            C2VALB    R        O 11 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12  1'Selger . . . . .:'
     A            C2SLGR    R        O 12 19REFFLD(RNSLGR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2ITXT    R   18   O 12 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            C2VALR    R        O 12 61REFFLD(RNVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 14 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E2WIN
     A*%%TS  SD  20070313  092324  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 60)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1 22'Reskontrostatistikk'
     A                                  2  1'Kjøp i år .:'
     A            E2KJØP    R        O  2 14REFFLD(RKKJØP)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  2 32'Saldo i år .:'
     A            E2SALD    R        O  2 46REFFLD(RKSALD)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3  1'Kjøp i fjor:'
     A            E2KJØF    R        O  3 14REFFLD(RKKJØF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3 32'Saldo i fjor:'
     A            E2SAIF    R        O  3 46REFFLD(RKSAIF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  4  1'Kred.grense:'
     A            E2GRNS    R        O  4 14REFFLD(RKGRNS)
     A                                      DSPATR(UL)
     A                                  4 32'Ordreverdi .:'
     A            E2MEMS    R        O  4 46REFFLD(RKMEMS)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  5  1'Kredittid .:'
     A            E2KVTI         5Y 0O  5 14EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  5 32'BRab akk.. .:'
     A            E2KJAL    R        O  5 46REFFLD(RKKJAL)
     A                                      DSPATR(UL)
     A                                  6 32'BRab i ordre:'
     A            E2MEMA    R        O  6 46REFFLD(RKMEMA)
     A                                      DSPATR(UL)
     A                                  7  1'Aldersfordeling:'
     A                                  7 38'Antall poster:'
     A                                  8  1'-Ikke forfalt :'
     A            E2ALD0    R        O  8 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  8 38'- Totalt. . . .:'
     A            E2POST         5Y 0O  8 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  9  1'-Ikke saldert'
     A                                  9 15':'
     A            E2ALDN    R        O  9 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  9 38'- Debet . . . .:'
     A            E2DEBE         5Y 0O  9 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 10  1'-   0 til'
     A            E23KA1    R        O 10 11REFFLD(RA3KA1)
     A                                      DSPATR(UL)
     A                                 10 15':'
     A            E2ALD1    R        O 10 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 10 38'- Kredit. . . .:'
     A            E2KRED         5Y 0O 10 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 11  1'-'
     A            E23KS2    R        O 11  3REFFLD(RA3KA1)
     A                                      DSPATR(UL)
     A                                 11  7'til'
     A            E23KA2    R        O 11 11REFFLD(RA3KA2)
     A                                      DSPATR(UL)
     A                                 11 15':'
     A            E2ALD2    R        O 11 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 11 38'- Åpne poster .:'
     A            E2REST         5Y 0O 11 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 12  1'-'
     A            E23KS3    R        O 12  3REFFLD(RA3KA2)
     A                                      DSPATR(UL)
     A                                 12  7'til'
     A            E23KA3    R        O 12 11REFFLD(RA3KA3)
     A                                      DSPATR(UL)
     A                                 12 15':'
     A            E2ALD3    R        O 12 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 12 38'- Purret. . . .:'
     A            E2PURR         5Y 0O 12 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 13  1'- Over'
     A            E23KA4    R        O 13 11REFFLD(RA3KA4)
     A                                      DSPATR(UL)
     A                                 13 15':'
     A            E2ALD4    R        O 13 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 13 38'- Renteberegnet:'
     A            E2RENT         5Y 0O 13 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 14  1'Sum. . . . . .:'
     A            E2SALT    R        O 14 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 14 38'- Inkasso . . .:'
     A            E2INKA         5Y 0O 14 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 15  1'-Herav inkasso:'
     A            E2INKB    R        O 15 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 15 38'- Reklamasjon .:'
     A            E2REKL         5Y 0O 15 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
