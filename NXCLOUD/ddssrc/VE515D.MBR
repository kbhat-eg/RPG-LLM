     A*%%TS  SD  20091019  192728  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A*          030216 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20091001  115908  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1PENH    R        O 10  5REFFLD(VEPENH)
     A            B1PLDO    R        O 10 10REFFLD(VEPLDO)
     A            B1PKLA    R        O 10 18REFFLD(VEPKLA)
     A            B1PGTI    R        O 10 21REFFLD(VEPGTI)
     A            B1PBES    R        O 10 37REFFLD(VEPBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20091001  124342  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  31                                  SFLMSGID(VME0310 *LIBL/QMFOFAK 31)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på GTIN-nr pr. prisgiver '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenr :'
     A                                      COLOR(BLU)
     A            B2PVAR    R   -7   O  5 11REFFLD(VEPVAR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2TEK1    R        O  5 20REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  8  2'5=Vis                            '
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 10'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 17'Pk.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 22'GTIN-nr      '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 37'B'
7.xx A**                                9  2'V  Enh  Lev.nr Pk.  GTIN-nr       -
7.xx A**                                     B           '
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20091001  120402  BOF610      REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny          -
     A                                         '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20091019  192728  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på enhet pr. prisgiver   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1PVAR    R   -7   O  5 31REFFLD(VEPVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Enhet  . . . . . . . . . . .'
     A            C1PENH    R        O  6 31REFFLD(VEPENH)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Prisgiver  . . . . . . . . .'
     A            C1PLDO    R        O  7 31REFFLD(VEPLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'GTIN-nr. . . . . . . . . . .'
     A            C2PGTI    R        B 10 31REFFLD(VEPGTI)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(VME0340 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Pakningsklasse . . . . . . .'
     A            C2PKLA    R        B 11 31REFFLD(VEPKLA)
     A                                      COLOR(YLW)
     A                                      VALUES('F' 'D' 'T' ' ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Bestillbar . . . . . . . . .'
     A            C2PBES    R        B 12 31REFFLD(VEPBES)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 12 34'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake              '
     A                                      COLOR(BLU)
     A            C2PODA         6Y 0O 23 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 57'/'
     A            C2PEDA         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2PEUS    R        O 23 70REFFLD(VEEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
