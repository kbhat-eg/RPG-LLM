     A*%%TS  SD  19991111  124645  HKO         REL-V4R4M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VV105D
     A* Beskrivelse . . : Parameterbilde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       010411 bof  Utvidet med prisgriver
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19991111  124645  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Jusering av priser på vare '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgiver . . . . . . . . .'
     A            B1LDOR    R        B  4 30REFFLD(VPLDOR)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(VMP0580 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                  5  2'Prisdato. . . . . . . . . .'
     A            B1DATO         6Y 0B  5 30COLOR(YLW)
     A  31                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMP0150 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'Justering av salgspris. . .'
     A            B1SPRO         5Y 2B  7 30COLOR(YLW)
     A  33                                  ERRMSGID(VMP0500 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(VMP0510 *LIBL/QMFOFAK 34)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                  7 38'%'
     A                                  8  2'Justering av kostpris . . .'
     A            B1KPRO         5Y 2B  8 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                  8 38'%'
     A                                  9  2'Justering av innkjøpspris .'
     A            B1IPRO         5Y 2B  9 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                  9 38'%'
     A                                 10  2'Justering av butikkpris . .'
     A            B1BPRO         5Y 2B 10 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                 10 38'%'
     A                                 12  2'Dekningsgrad (DG) . . . . .'
     A            B1DEKN         4Y 2B 12 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
