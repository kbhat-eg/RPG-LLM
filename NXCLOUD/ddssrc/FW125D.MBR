     A*%%TS  SD  20020312  223716  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FW125D
     A* Beskrivelse . . : Oppdatering av varer/priser
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20020312  223716  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppdatering av varer/priser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FW125B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Type oppdatering. . . . . .'
     A            B1OTYP         1Y 0B  4 30COLOR(YLW)
     A                                      VALUES(1 2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                  4 40'1=Kun prisoppdatering'
     A                                  5 40'2=Pris- og vareinformasjons-oppdat-
     A                                      ering'
     A                                  6  2'Utplukk:'
     A                                      DSPATR(UL)
7.xx A                                  7  2'Leverandør. . . . . . . . +'
     A            B1LDOR    R        B  7 30REFFLD(FWLDOR)
     A  31                                  ERRMSGID(FRL0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1NAVN        30A  O  7 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Varegruppe. . . . . . . . +'
     A            B1OGRP    R        B  8 30REFFLD(VVOGRP)
     A  33                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  8 33'+'
7.xx A            B1HGRP    R        B  8 35REFFLD(VVHGRP)
     A  34                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  8 38'+'
7.xx A            B1UGRP    R        B  8 40REFFLD(VVUGRP)
     A  35                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B1UTXT        30A  O  8 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Lev. varenummer fra-til . .'
     A            B1LVAF    R        B  9 30REFFLD(FWLVAR)
     A  36                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 52'-'
     A            B1LVAT    R        B  9 54REFFLD(FWLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Overstyringer:'
     A                                      DSPATR(UL)
     A                                 12  2'Prisdato. . . . . . . . . .'
     A            B1DATO         6Y 0B 12 30COLOR(YLW)
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 13  2'Påslag på innkjøpspris. . .'
     A            B1IPRO         5Y 2B 13 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 13 40'%'
     A                                 14  2'Påslag til kostpris . . . .'
     A            B1KPRO         5Y 2B 14 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 14 40'%'
     A                                 15  2'Påslag på salgspris . . . .'
     A            B1SPRO         5Y 2B 15 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 15 40'%'
     A                                 17  2'Ikke oppdat. innkjøpspris .'
     A            B1INIO         1Y 0B 17 30COLOR(YLW)
     A  38                                  ERRMSGID(FMW0130 *LIBL/QMFOFAK 38)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 17 40'1=Ja,0=Nei'
     A                                 18  2'Ikke oppdat. kostpris . . .'
     A            B1KOIO         1Y 0B 18 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 18 40'1=Ja,0=Nei'
     A                                 19  2'Ikke oppdat. salgspris. . .'
     A            B1SAIO         1Y 0B 19 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 19 40'1=Ja,0=Nei'
     A                                 20  2'Beholde nyere priser. . . .'
     A            B1NYPO         1Y 0B 20 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 20 40'1=Ja,0=Nei'
     A                                 21  2'Kun oppdat. leveringstype 0'
     A            B1LTYO         1Y 0B 21 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 21 40'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
