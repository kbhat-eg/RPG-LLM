     A*%%TS  SD  20220726  085217  SST_800     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL500D
     A* Beskrivelse . . : Spørring på histrorisk lagerbok, detaljer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.20 A*      020315  NHO  Lagt inn OVRDTA på div felter
     A*
7.xx A* 7.xx 200516  bsa  Justeringer basert på ny GUI og regler
7.02 A* K00  290420  bsa  Takler både brede og vanlige skjermer.
     A*
8.02 A* K00  040123  bsa  Legger ut ordre/best nummer ref.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20090805  150115  ASK610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1LAGE    R        O 10  5REFFLD(VHLAGE)
     A            B1DATO         6Y 0O 10  9EDTWRD('  .  .  ')
     A            B1TIME         4Y 0O 10 19EDTWRD('  .  ')
     A            B1SYST    R        O 10 26REFFLD(VHSYST)
     A            B1KODE    R        O 10 28REFFLD(VHKODE)
     A            B1TYPE    R        O 10 31REFFLD(VHSYST)
     A            B1ANTA         9Y 2O 10 33EDTWRD('     0 ,  -')
     A            B1BEHL        10Y 2O 10 45EDTWRD('      0 ,  -')
     A            B1REFR    R        O 10 58REFFLD(VHREFR)
     A            B1DATK    R        H      REFFLD(VHDATO)
     A            B1TIMK    R        H      REFFLD(VHTIME)
     A            B1SEKV    R        H      REFFLD(VHSEKV)
     A            B1USER    R        H      REFFLD(VHUSER)
     A            B1VARE    R        H      REFFLD(VHVARE)
8.01 A            B1ONUM    R        H      REFFLD(VHONUM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090805  155047  ASK610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på historisk lagerbok'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LH502B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare '
     A            B2VVAR    R   -7   O  4  8REFFLD(VHVARE)
     A            B2VTEK        30A  O  4 17
     A                                  5  2'Lager'
     A            B2VLAG         2A  O  5  8
     A            B2VLTK        30A  O  5 17
     A                                  5 54'Lagerenhet'
     A            B2ENHL         3A  O  5 65
7.xx A                                  7  5'LG'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'LG'
     A                                      COLOR(WHT)
7.xx A                                  9  9'Dato'
     A                                      COLOR(WHT)
7.xx A                                  9 20'Tid'
     A                                      COLOR(WHT)
7.xx A                                  9 35'Antall'
     A                                      COLOR(WHT)
7.xx A                                  9 45'Beholdning'
     A                                      COLOR(WHT)
7.xx A                                  9 58'Referanse'
     A                                      COLOR(WHT)
7.xx A                                  7  8'+'
7.xx A            B2LAGE         2A  B  7 10OVRDTA
     A  32                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
7.xx A            B2DATO         6Y 0B  7 19
     A  33                                  ERRMSGID(LM00250 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A                                  9  2'V'
     A                                      DSPATR(HI)
7.xx A                                  8  2'5=Detaljer'
     A                                      COLOR(BLU)
7.xx A                                  9 26'S'
7.xx A                                      DSPATR(HI)
7.xx A                                  9 28'OT'
7.xx A                                      DSPATR(HI)
7.xx A                                  9 31'B'
     A                                      DSPATR(HI)
     A          R B2CMD
     A*%%TS  SD  20020924  122105  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 32'F5=Forny'
     A                                      COLOR(BLU)
     A          R C1WIN
     A*%%TS  SD  20090903  210055  ASK610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            C1HEAD        30A  O  2  2OVRDTA
     A                                      DSPATR(RI)
     A                                  4  2'Lager:'
     A            C1LAGE    R        O  4  9REFFLD(VHLAGE)
     A                                      COLOR(WHT)
     A            C1VLTK        30A  O  4 12OVRDTA
     A                                      COLOR(WHT)
     A                                  5  2'Vare :'
     A            C1VVAR    R   -7   O  5  9REFFLD(VHVARE)
     A                                      COLOR(WHT)
     A            C1VTEK        30A  O  5 18COLOR(WHT)
     A                                  7  2'Dato :'
     A            C1DATO         6Y 0O  7  9EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
6.20 A                                      OVRDTA
     A                                  7 18'Kl:'
     A            C1TIME         6Y 0O  7 22EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
6.20 A                                      OVRDTA
     A                                  7 32'Bruker :'
     A            C1USER        10A  O  7 41COLOR(WHT)
6.20 A                                      OVRDTA
     A                                  9  2'Bevegelse:'
     A            C1ANTA         9Y 2O  9 13EDTWRD('     0 ,  -')
     A                                      COLOR(WHT)
6.20 A                                      OVRDTA
     A                                  9 32'Ny beh.:'
     A            C1BEHL        10Y 2O  9 41EDTWRD('      0 ,  -')
     A                                      COLOR(WHT)
6.20 A                                      OVRDTA
     A                                 11  2'INFO:'
     A            C1INF1        45A  O 11  8COLOR(WHT)
6.20 A                                      OVRDTA
     A            C1INF2        45A  O 12  8COLOR(WHT)
6.20 A                                      OVRDTA
     A            C1INF3        45A  O 13  8COLOR(WHT)
6.20 A                                      OVRDTA
     A            C1INF4        45   O 14  8COLOR(WHT)
6.20 A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
     A          R C2WIN
     A*%%TS  SD  20220726  085217  SST_800     REL-V7R3M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 17 55)
     A  *WIDE                               WINDOW(*DFT 17 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            C1HEAD        30A  O  2  2OVRDTA
     A                                      DSPATR(RI)
     A                                  4  2'Lager:'
     A            C1LAGE    R        O  4  9REFFLD(VHLAGE)
     A                                      COLOR(WHT)
     A            C1VLTK        30A  O  4 12OVRDTA
     A                                      COLOR(WHT)
     A                                  5  2'Vare :'
     A            C1VVAR    R   -7   O  5  9REFFLD(VHVARE)
     A                                      COLOR(WHT)
     A            C1VTEK        30A  O  5 18COLOR(WHT)
     A                                  6  2'Dato :'
     A            C1DATO         6Y 0O  6  9EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  6 18'Kl:'
     A            C1TIME         6Y 0O  6 22EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  6 32'Bruker :'
     A            C1USER        10A  O  6 41COLOR(WHT)
     A                                      OVRDTA
     A                                  7  2'Bevegelse:'
     A            C1ANTA         9Y 2O  7 13EDTWRD('     0 ,  -')
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7 32'Ny beh.:'
     A            C1BEHL        10Y 2O  7 41EDTWRD('      0 ,  -')
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  8  2'INFO:'
     A            C1INF1        45A  O  8  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF2        45A  O  9  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF3        45A  O 10  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF4        45   O 11  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF5        45   O 12  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF6        45   O 13  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF7        45   O 14  8COLOR(WHT)
     A                                      OVRDTA
     A            C1INF8        45   O 15  8COLOR(WHT)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
