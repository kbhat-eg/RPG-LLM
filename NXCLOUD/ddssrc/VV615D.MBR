     A*%%TS  SD  19990601  152621  HKO         REL-V4R2M0  5769-PW1
     A*%%EC
     A* 241018 : ENDRING I TEKST
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990601  152621  HKO         REL-V4R2M0  5769-PW1
7.xx A                                      WINDOW(03 04 16 67)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av vareregister med dekn-
     A                                      ingskontroll '
     A                                      DSPATR(RI)
6.33 A                                  4  2'Lager  . . . . . +'
6.33 A            B1LAGE    R        B  4 21REFFLD(FÅLAGE)
6.33 A  39                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 39)
6.33 A                                      COLOR(YLW)
7.xx A                                  5  2'Varegruppe . . . +'
     A            B1OGRP    R        B  5 21REFFLD(VVOGRP)
     A  31                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
7.xx A                                  5 25'+'
7.xx A            B1HGRP    R        B  5 27REFFLD(VVHGRP)
     A  32                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
7.xx A                                  5 30'+'
7.xx A            B1UGRP    R        B  5 32REFFLD(VVUGRP)
     A  33                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1UTXT    R   -4   O  5 36REFFLD(VGUTXT)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Rab.kat. fra-til +'
     A            B1RKAF    R        B  6 21REFFLD(FÅRKAT)
     A  35                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
7.xx A                                  6 25'+'
7.xx A            B1RKAT    R        B  6 27REFFLD(FÅRKAT)
     A  36                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Kunde  . . . . . +'
     A            B1KUND    R        B  7 21REFFLD(FÅKUND)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 37)
7.xx A                                  8  2'Kundeprosjekt. . +'
     A            B1KPRO    R        B  8 21REFFLD(FÅKPRO)
     A                                      COLOR(YLW)
     A  38                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 38)
7.xx A                                  9  2'Dato . . . . . . +'
     A            B1DATO         6Y 0B  9 21COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
7.xx A                                 11  2'Rapporten lagres på IFS /xxx/pcfil-
     A                                      er'
7.xx A                                 12  2'Det tar opptil 10 minutter å +
     A                                       lage rapporten.'
7.xx A            B1BMSG        50A  O 13 16
     A                                      COLOR(RED)
     A                                 15  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
