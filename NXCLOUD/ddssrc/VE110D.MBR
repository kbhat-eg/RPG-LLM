     A*%%TS  SD  20090930  145535  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  020216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20090929  144046  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 12  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 5 7 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1ENHE    R        O 12  5REFFLD(VEENHE)
     A            B1OMRE    R        B 12 10REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1SAEN    R        B 12 28REFFLD(VESAEN)
     A                                      COLOR(YLW)
     A            B1INEN    R        B 12 36REFFLD(VEINEN)
     A                                      COLOR(YLW)
     A            B1PBEN    R        B 12 45REFFLD(VEPBEN)
     A                                      COLOR(YLW)
     A            B1VOLU    R        O 12 52REFFLD(VEVOLU)
     A            B1VEKT    R        O 12 64REFFLD(VEVEKT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090929  144046  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF13
     A                                      CF15
     A                                      CF17
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  31                                  SFLMSGID(VME0100 *LIBL/QMFOFAK 31)
     A  32                                  SFLMSGID(VME0110 *LIBL/QMFOFAK 32)
     A  33                                  SFLMSGID(VME0150 *LIBL/QMFOFAK 33)
     A  40                                  SFLMSGID(VME0130 *LIBL/QMFOFAK 40)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av enheter'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VE110B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare. . . . . .'
     A            B2VARE    R   -7   O  5 18REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TEK1    R        O  5 28REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Basisenhet. . .'
     A            B2ENHE    R        O  6 18REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'2=Endre   5=Vis   7=Pr.Prisgiver'
     A                                      COLOR(BLU)
7.xx A**                                9 26'------ Rekkefølge ------'
7.xx A**                                    COLOR(WHT)
7.xx A                                 10  2'V'
     A                                      COLOR(WHT)
7.xx A                                 10  5'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 10'Omr.faktor'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 28'Salgs'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 36'Innkj.'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 44'Prisbok'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 52'Volum (m3)'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 64'Vekt (kg)'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 10'volum'
     A                                      COLOR(WHT)
7.xx A                                 11 28'enhet'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 36'enhet'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 44'enhet'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20030915  153117  HKO         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F13=Endre statistik-
     A                                      kenhet  F15=Endre lagerenhet  F17=B-
     A                                      ytt enhet'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090929  144046  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av enhet'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O  5 31REFFLD(VEENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Basisenhet . . . . . . . . .'
     A            B2ENHE    R        O  7 31REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Omregningsfaktor volum. . .'
     A            C2OMRE    R        B  9 31REFFLD(VEOMRE)
     A  31                                  ERRMSGID(VME0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VME0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Lengde (mm). . . . . . . . .'
     A            C2LENG    R        B 11 31REFFLD(VELENG)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Bredde (mm). . . . . . . . .'
     A            C2BRED    R        B 12 31REFFLD(VEBRED)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Høyde. (mm). . . . . . . . .'
     A            C2HOYD    R        B 13 31REFFLD(VEHOYD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Volum. (m3). . . . . . . . .'
     A            C2VOLU    R        B 14 31REFFLD(VEVOLU)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Vekt . (kg). . . . . . . . .'
     A            C2VEKT    R        B 15 31REFFLD(VEVEKT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Rekkefølge:'
     A                                      COLOR(WHT)
     A                                 19  2'Salgsenhet . . . . . . . . .'
     A            C2SAEN    R        O 19 31REFFLD(VESAEN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Innkjøpsenhet. . . . . . . .'
     A            C2INEN    R        O 20 31REFFLD(VEINEN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Prisbokenhet . . . . . . . .'
     A            C2PBEN    R        O 21 31REFFLD(VEPBEN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 58'/'
     A            C2EDAT         6Y 0O 22 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 69'/'
     A            C2EUSR    R        O 22 71REFFLD(VEEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=G tilbake '
     A                                      COLOR(BLU)
