     A*%%TS  SD  20021231  163514  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RE604D
     A* Beskrivelse . . : Overføre saldobalanse til Trioplan
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       301202 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20021231  163514  KOB         REL-V5R1M0  5722-WDS
     A                                      CA01
     A                                      CA03
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Saldo/Budsjett til Trioplan'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  4'Dato .:'
     A            A2PKDA    R        B  5 12REFFLD(RHPKDA)
     A  33                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 33)
     A                                      DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 22'Regnskapsår . . . .:'
     A            A2PRÅR    R        B  5 43REFFLD(RHPRÅR)
     A  34                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 34)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8  5'Rutinen legger ut member KLIxxx i -
     A                                      register RE01PF med tall i henhold -
     A                                      til'
     A                                      COLOR(RED)
     A                                  9  5'regnskapsår; alle bevegelser i år,-
     A                                       i fjor og budsjett i år(xxx=firma)-
     A                                      .'
     A                                      COLOR(RED)
     A                                 11  5'Etter at rutinen er kjørt kan RE01-
     A                                      PF overføres til PC for'
     A                                      COLOR(RED)
     A                                 12  5'bruk i Trioplan budsjett/likvidite-
     A                                      tsmodell.'
     A                                      COLOR(RED)
     A                                 14  5'Dersom mappe PCTRIO blir opprettet-
     A                                      , vil KLIxxx bli lagt ut som      '
     A                                 15  5'en PCFIL i mappen med dokumentnavn-
     A                                       = KLIxxx.                        '
     A                                 23  5'F3 tilbake   '
     A                                      COLOR(BLU)
