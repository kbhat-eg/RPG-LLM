     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  240216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 7 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1PASL    R        O  8  5REFFLD(VIPASL)
     A            B1BESK    R        O  8 17REFFLD(VIBESK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Påslag'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Behandle påslag'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Påslag    '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'Beskrivelse               '
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V  Påslag      Beskrivelse'
7.xx A**                                    COLOR(WHT)
7.xx A            B2PASL    R        B  5  5REFFLD(VIPASL)
     A  31                                  ERRMSGID(VMI0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMI0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  5 17REFFLD(VIBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Påslag . . . . . . . . . . .'
     A            C1PASL    R        B  5 31REFFLD(VIPASL)
     A  31                                  ERRMSGID(VMI0010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Påslag finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Påslag . . . . . . . . . . .'
     A            C1PASL    R        O  5 31REFFLD(VIPASL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2BESK    R        B  7 31REFFLD(VIBESK)
     A  31                                  ERRMSGID(VMI0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VIEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Slette påslag '
     A                                      DSPATR(RI)
     A                                  4  2'Påslag . . . . . .'
     A            D1PASL    R        O  4 21REFFLD(VIPASL)
     A                                  5  2'Beskrivelse. . . .'
     A            VIBESK    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Påslag . . . . . .'
     A            K1PASL    R        B  4 21REFFLD(VIPASL)
     A  31                                  ERRMSGID(VMI0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMI0030 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Påslag . . . . . .'
     A            VIPASL    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            VIBESK    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
