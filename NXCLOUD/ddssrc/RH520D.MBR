     A*%%TS  SD  20060403  150303  HFL540      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH520D
     A* Beskrivelse . . : Hovedboksaldi, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
6.30 A*       040719 BSA  Utvidet saldofelter
7.xx A*       030516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A                                      INDARA
     A*
     A*  Bilde B1 - subfile
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20011230  213328  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 8 0)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A            B1KONT    R        O 10  4REFFLD(RHKONT)
     A                                      EDTCDE(Z)
     A            B1SPES    R        O 10 10REFFLD(RHSPES)
     A                                      EDTCDE(Z)
     A            B1AVDE    R        O 10 15REFFLD(RHAVDE)
     A                                      EDTCDE(Z)
6.30 A            B1TEXT    R   -7   O 10 20REFFLD(RHTEXT)
6.30 A            B1DPER    R   -3   O 10 44REFFLD(RISA01)
6.30 A                                      EDTCDE(J)
6.30 A            B1HITT    R   -1   O 10 62REFFLD(RISA01)
     A                                      EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020411  235653  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på hovedboksaldi'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 12'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 19'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'Velg post og trykk enter'
     A                                      COLOR(BLU)
     A                                  5 54'Periode. . . .:'
     A                                      COLOR(WHT)
     A            B2RPER         2Y 0B  5 70EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2RAAR         4Y 0B  5 73EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  8  2'5=Vis  8=Vis poster'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 10'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 20'Tekst           '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 48'Denne periode'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 74'Hittil'
7.xx A**                                9  2'V Konto Bærer Avd Tekst           -
7.xx A**                                                Denne periode          -
7.xx A**                                       Hittil'
     A                                      COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2KONT    R        B  6  4REFFLD(RHKONT)
     A                                      DLTCHK
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(RMI0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMI0020 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMI0030 *LIBL/QMFOKON 33)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  6 10'+'
7.xx A            B2SPES    R        B  6 12REFFLD(RHSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  6 17'+'
7.xx A            B2AVDE    R        B  6 19REFFLD(RHAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020905  150726  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 40'F6=Summert/Detaljert'
     A                                      COLOR(BLU)
     A                                 23 62'F8=Poster'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2BLD
     A*%%TS  SD  20031218  204556  KOB         REL-V5R2M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      OVERLAY
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på hovedboksaldi'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Konto . . . .:'
     A                                      COLOR(WHT)
     A            C1KONT    R        O  4 17REFFLD(RIKONT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2TEXT    R        O  4 23REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  94        C2NOTB         8A  O  4 55DSPATR(RI)
     A                                  5  2'Kostnadsbærer:'
     A                                      COLOR(WHT)
     A            C1SPES    R        O  5 17REFFLD(RISPES)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2BTXT    R        O  5 22REFFLD(REBTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  2'Avdeling. . .:'
     A                                      COLOR(WHT)
     A            C1AVDE    R        O  6 17REFFLD(RIAVDE)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2GTXT    R        O  6 22REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2HED1        15A  O  7  8COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A            C2HED2        15A  O  7 27COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A            C2HED3        15A  O  7 46COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A            C2HED4        15A  O  7 65COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'IB'
     A            C2K100        12Y 2O  8  8OVRDTA
     A                                      TEXT('Bevegelse Periode 01')
     A                                      EDTCDE(L)
     A            C2K200    R        O  8 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K300    R        O  8 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K400         5Y 1O  8 67OVRDTA
     A                                      EDTCDE(L)
     A                                  9  2'01'
     A            C2K101    R        O  9  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K201    R        O  9 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K301    R        O  9 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K401    R        O  9 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 10  2'02'
     A            C2K102    R        O 10  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K202    R        O 10 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K302    R        O 10 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K402    R        O 10 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 11  2'03'
     A            C2K103    R        O 11  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K203    R        O 11 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K303    R        O 11 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K403    R        O 11 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 12  2'04'
     A            C2K104    R        O 12  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K204    R        O 12 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K304    R        O 12 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K404    R        O 12 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 13  2'05'
     A            C2K105    R        O 13  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K205    R        O 13 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K305    R        O 13 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K405    R        O 13 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 14  2'06'
     A            C2K106    R        O 14  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K206    R        O 14 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K306    R        O 14 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K406    R        O 14 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 15  2'07'
     A            C2K107    R        O 15  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K207    R        O 15 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K307    R        O 15 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K407    R        O 15 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 16  2'08'
     A            C2K108    R        O 16  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K208    R        O 16 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K308    R        O 16 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K408    R        O 16 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 17  2'09'
     A            C2K109    R        O 17  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K209    R        O 17 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K309    R        O 17 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K409    R        O 17 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 18  2'10'
     A            C2K110    R        O 18  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K210    R        O 18 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K310    R        O 18 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K410    R        O 18 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 19  2'11'
     A            C2K111    R        O 19  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K211    R        O 19 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K311    R        O 19 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K411    R        O 19 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 20  2'12'
     A            C2K112    R        O 20  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K212    R        O 20 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K312    R        O 20 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K412    R        O 20 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 21  2'Sum'
     A            C2K114    R        O 21  8REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K214    R        O 21 27REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K314    R        O 21 46REFFLD(C2K100 *SRC)
     A                                      OVRDTA
     A            C2K414    R        O 21 67REFFLD(C2K400 *SRC)
     A                                      OVRDTA
     A                                 22  2'F2=Notatark  F3=Gå tilbake  F5=For-
     A                                      rige år  F6=Neste år  F7=Saldo/beve-
     A                                      gelse'
     A                                      COLOR(BLU)
     A                                 23  2'F8=Posteringer  F9=Endre kolonne'
     A                                      COLOR(BLU)
     A*
     A* Bilde V1 - Vindu
     A* for valg av kolonner
     A*
     A          R V1WIN
     A*%%TS  SD  19941207  105050  KB          REL-V2R3M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 11 28)
     A                                      CA03
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2  2' Velg kolonne-innhold '
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  4 14'Innhold'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4 25'År'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5  2'Kolonne 1. .:'
     A            V1INH1    R        B  5 17REFFLD(RWVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('A' 'B' 'R')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            V1AAR1    R        B  5 23REFFLD(B2RAAR *SRC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  6  2'Kolonne 2. .:'
     A            V1INH2    R        B  6 17REFFLD(RWVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('A' 'B' 'R')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            V1AAR2    R        B  6 23REFFLD(B2RAAR *SRC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A* Dummybilde for asume
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
