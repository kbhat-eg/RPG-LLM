     A*%%TS  SD  20110531  131647  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
6.30 A* 6.30  241014  bsa  F tast for NOBBB visning
     A*
7.xx A* 7.xx  020216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20110531  130342  BOF620      REL-V7R1M0  5770-WDS
     A            B1VARE    R        H      REFFLD(VVVARE)
     A            B1NOBB    R        H      REFFLD(VVNOBB)
     A            B1LVAR    R        H      REFFLD(VVLVAR)
     A            B1VALG    R        B 11  2REFFLD(FÅVAL2)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 7 8 9 98 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1TEK1    R        O 11  5REFFLD(VVTEK1)
     A  50                                  COLOR(RED)
     A            B1NYPO        15A  O 11 41
6.33 A  51                                  COLOR(BLU)
6.33 A  52                                  COLOR(YLW)
     A            B1NAVN        18A  O 11 57
6.33 A  51                                  COLOR(BLU)
6.33 A  52                                  COLOR(YLW)
     A            B1KPRI    R        O 11 76REFFLD(VVKPRI)
     A            B1VTYP    R        O 11 78REFFLD(VVVTYP)
     A            B1NOTA         1A  O 11 80COLOR(YLW)
     A  83                                  DSPATR(RI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110531  131647  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CA21
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vareinformasjon'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VV100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varegruppe. . . +'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  4 20REFFLD(VVOGRP)
     A  31                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 23'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  4 25REFFLD(VVHGRP)
     A  32                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 28'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  4 30REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Frisøk. . . . . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  5 20COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  8  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Søketekster  8=GTIN-nr.    9=Spr-
     A                                      åktekster'
     A                                      COLOR(BLU)
7.xx A                                  9  2'98=Lageregister'
     A                                      COLOR(BLU)
7.xx A                                  6  5'Varetekst                         -
     A                                        Varenr   Nobb-nr  Lev. varenr'
     A                                      COLOR(WHT)
7.xx A            B2TEK1    R        B  7  5REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2VARE         8Y 0B  7 41
     A  33                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NOBB    R        B  7 50REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LVAR    R        B  7 59REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'V'
     A                                      COLOR(WHT)
     A                                 10  5'Tekst                             -
     A                                       '
     A                                      COLOR(WHT)
7.xx A  80                             10 41'Varenr         '
     A                                      COLOR(WHT)
     A  81                             10 41'Nobbnr         '
     A                                      COLOR(WHT)
     A  82                             10 41'Lev. varenr    '
     A                                      COLOR(WHT)
     A                                 10 57'Leverandør        '
     A                                 10 76'K'
     A                                 10 78'T'
     A                                 10 80'N'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031127  114914  HKO         REL-V5R2M0  5722-WDS
     A                                 22  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett   F9=Gjenta søk'
     A                                      COLOR(BLU)
     A                                 23  2'F13=Scanning'
     A                                      COLOR(BLU)
     A N84                             23 32'F21=Kun utgåtte'
     A                                      COLOR(BLU)
     A  84                             23 32'F21=Alle varer'
     A                                      COLOR(BLU)
     A                                 23 56'F23=Ny posisjonering'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  115428  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1VARE         8Y 0B  5 31COLOR(YLW)
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0050 *LIBL/QMFOFAK 32)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990112  100801  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Vare finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  19980324  131628  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 31)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varen er en tidligere slettet'
     A                                  3  2'vare.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051109  203556  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                      CF13
6.30 A                                      CF18
     A                                  2  2' Slette vare '
     A                                      DSPATR(RI)
     A                                  4  2'Varenr. . . . .'
     A            D1VARE    R        O  4 18REFFLD(VVVARE)
     A  31                                  ERRMSGID(VMV0060 *LIBL/QMFOFAK 31)
     A                                  5  2'Varetekst 1 . .'
     A            VVTEK1    R        O  5 18
     A                                  6  2'Varetekst 2 . .'
     A            VVTEK2    R        O  6 18
     A                                  7  2'Varegruppe. . .'
     A            VVOGRP    R        O  7 18
     A            VVHGRP    R        O  7 21
     A            VVUGRP    R        O  7 24
     A                                  8  2'Leverandør. . .'
     A            VVLDOR    R        O  8 18
     A                                  9  2'Lev. varenr . .'
     A            VVLVAR    R        O  9 18
     A                                 10  2'Nobbnr. . . . .'
     A            VVNOBB    R        O 10 18
     A                                 14  2'F3=Gå tilbake   F13=Ordrer med var-
6.30 A                                      en  F18=NOBB info'
     A                                      COLOR(BLU)
     A          R D1MSG
     A*%%TS  SD  20020113  222008  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 43)
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                      CA23
     A                                  2  2'Beholdningstall finnes på ett elle-
     A                                      r flere'
     A                                  3  2'lagre.'
     A                                  5  2'Sletting av vare må bekreftes med -
     A                                      F23 !!'
     A                                  8  2'F3=Gå tilbake  F13=Vis lager  F23=-
     A                                      Bekreft'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19980324  141527  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer. . .'
     A            K1VARE         8Y 0B  4 18
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0030 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMV0050 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Varenummer. . .'
     A            VVVARE    R        O  9 18OVRDTA
     A                                 10  2'Varetekst 1 . .'
     A            VVTEK1    R        O 10 18OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  19990428  224045  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Scanning '
     A                                      DSPATR(RI)
     A                                  4  2'GTIN-nummer .'
6.10 A            E1EANN        14A  B  4 16COLOR(YLW)
     A  31                                  ERRMSGID(VMX0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A                                      CHECK(ER)
     A                                      OVRDTA
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
