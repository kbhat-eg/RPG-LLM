     A*%%TS  SD  19971012  124142  HKO         REL-V3R7M0  5716-PW1
     A*%%EC
     A**--------------------------------------------------------
     A*
7.xx A*       171116 bsa  Endringer relatert til ny GUI
     A*
     A**--------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19971010  150025  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 21 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Gruppebehandling varer - prisbere-
     A                                      gning '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgivende leverandør . . +'
     A            B1PLDO    R        B  4 31REFFLD(JXLDOR)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1PLNA    R  -13   O  4 38REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Rabatter/påslag -inn:'
     A                                      DSPATR(UL)
     A                                  7  2'Rabatt-1 (på Nobb-pris). . .'
     A            B1RAB1         4Y 2B  7 31COLOR(YLW)
     A  39                                  ERRMSGID(JMM1200 *LIBL/QMFVADM 39)
     A  40                                  ERRMSGID(JMM1210 *LIBL/QMFVADM 40)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  7 37'%'
     A                                  8  2'Rabatt-2 (på Nobb-pris). . .'
     A            B1RAB2         4Y 2B  8 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  8 37'%'
     A                                  9  2'Påslag til kostpris. . . . .'
     A            B1PSLK         4Y 2B  9 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  9 37'%'
     A                                 11  2'Rabatt/påslag -ut:'
     A                                      DSPATR(UL)
     A                                 12  2'Rabatt (på Nobb-pris). . . .'
     A            B1RABA         4Y 2B 12 31COLOR(YLW)
     A  41                                  ERRMSGID(JMM1220 *LIBL/QMFVADM 41)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 12 37'%'
     A                                 13  2'Påslag (fra kostpris). . . .'
     A            B1PSLS         4Y 2B 13 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 13 37'%'
     A                                 15  2'Beløp:'
     A                                      DSPATR(UL)
     A                                 16  2'Innkjøpspris . . . . . . KAI'
     A            B1VKAI    R        B 16 31REFFLD(JMVKAI)
     A  42                                  ERRMSGID(JMM1100 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'Kostpris . . . . . . . . . .'
     A            B1VKPR    R        B 17 31REFFLD(JMVKPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 18  2'Egen/anbefalt kamp.pris. KAA'
     A            B1VKAA    R        B 18 31REFFLD(JMVKAA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 20  2'F3=Gå tilbake   F4=Spørring       -
     A                                               F13=Betingelser'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
