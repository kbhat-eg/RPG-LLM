     A*%%TS  SD  20160602  095258  SST620      REL-V7R1M0  5770-WDS
     A*%%EC
     A*
7.xx A* 7.xx  131216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SLOGPF SLOGPFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20160602  095258  SST620      REL-V7R1M0  5770-WDS
7.xx A            B1VALG         1A  B 10  2
7.xx A            B1REGI         6A  O 10  4
7.xx A            B1FORE         8A  O 10 11
7.xx A            B1FELT         6A  O 10 20
7.xx A            B1DATO         8A  O 10 27
7.xx A            B1USER         8A  O 10 36
7.xx A            B1FRTI        36A  O 10 45
     A            B1RDAT    R        H      REFFLD(ELRDAT)
     A            B1RTIM    R        H      REFFLD(ELRTIM)
     A            B1FORT    R        H      REFFLD(ELFORT)
     A            B1FELX        10A  H
     A            B1FORX         8A  H
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160602  080637  SST620      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CA10
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Endringslogg     '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på endringslogg  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Register. . . . . +'
7.xx A            B2REGI        10A  B  4 22OVRDTA
7.xx A                                  4 35'Forekomst . . . . .'
7.xx A            B2FORE        10A  B  4 55OVRDTA
7.xx A                                  5  2'Felt. . . . . . . +'
7.xx A            B2FELT        10A  B  5 22OVRDTA
7.xx A                                  5 35'Bruker  . . . . . .'
7.xx A            B2USER        10A  B  5 55OVRDTA
7.xx A            B2FUSR         8A  B  7 36COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      OVRDTA
7.xx A**          B2HEAD        35A  O  8 45COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A            B2FREG         6A  B  7  4COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2FFOR         6A  B  7 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A  22                                  ERRMSGID(RMS0011 *LIBL/QMFOKON 22)
     A                                      OVRDTA
7.xx A                                  7 18'+'
7.xx A            B2FFEL         6A  B  7 20COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2FDAT         8A  B  7 27COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Regist'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Forek.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 21'Felt'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 28'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 36'Bruker'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V Regist Forek.   Felt   Dato     -
7.xx A**                                    Bruker'
7.xx A**                                    COLOR(WHT)
7.xx A**                                4  2'2=Endre  3=Kopier  4=Slett '
7.xx A**                                5  2'5=Vis    6=Skriv   8=Vis poster'
7.xx A                                  8  2'5=Vis'
7.xx A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Regist'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Forek.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 20'Felt'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 28'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 45'Fra verdi'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'Til verdi'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20160602  082311  SST620      REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny           -
     A                                                                         -
     A                                           '
     A                                      COLOR(BLU)
     A**                               23 43'F10=Skriv Rapport'
     A          R C1BLD
     A*%%TS  SD  20160602  082311  SST620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Endringslogg     '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprette Endringslogg'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Register . . . . . . . . . .'
     A            C1REGI        10A  B  5 31COLOR(YLW)
     A                                      OVRDTA
     A            C1FORE        10A  B  6 31COLOR(YLW)
     A            C1FELT        10A  B  7 31COLOR(YLW)
     A            C1DATO        10A  B  8 31COLOR(YLW)
     A            C1USER        10A  B  9 31COLOR(YLW)
     A            C1RDAT        10A  B 10 31COLOR(YLW)
     A            C1RTIM        10A  B 11 31COLOR(YLW)
     A            C1FIRM    R        H      REFFLD(ELFIRM)
     A            C1FORT    R        H      REFFLD(ELFORT)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20130215  111700  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Logg finnes fra før.       '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20150310  115259  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Endringslogg     '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prosjekt   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  5  2'Register . . . . . . . . . .'
     A            C2REGI        10A  B  5 31
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                  6  2'Forekomst. . . . . . . . . .'
     A            C2FORE        10A  B  6 31
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  23                                  ERRMSGID(RMP0400 *LIBL/QMFOKON 23)
     A                                      OVRDTA
     A                                  7  2'FOREKOMST(2)  (SUFIX)  . . .'
     A            C2FORT        10A  B  7 31COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  23                                  ERRMSGID(RMP0400 *LIBL/QMFOKON 23)
     A                                      OVRDTA
     A                                  8  2'Felt . . . . . . . . . . . .'
     A            C2FELT        10A  B  8 31COLOR(YLW)
     A  24                                  ERRMSGID(RMP0410 *LIBL/QMFOKON 24)
     A  25                                  ERRMSGID(RMP0420 *LIBL/QMFOKON 25)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 10  2'Endringsdato . . . . . . . .'
     A            C2DATO        10A  B 10 31COLOR(YLW)
     A  26                                  ERRMSGID(RMP0430 *LIBL/QMFOKON 26)
     A  27                                  ERRMSGID(RMP0440 *LIBL/QMFOKON 27)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 11  2'Bruker. . . . .  . . . . . .'
     A            C2USER        10A  B 11 31
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(RMP0490 *LIBL/QMFOKON 37)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A            C2USRX        30A  O 11 42
     A                                      OVRDTA
     A                                 12  2'Fra verdi. . . . . . . . . .'
     A            C2FRAV        40A  B 12 31
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 13  2'Til verdi. . . . . . . . . .'
     A            C2TILV        40A  B 13 31
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  28                                  ERRMSGID(RMP0450 *LIBL/QMFOKON 28)
     A  29                                  ERRMSGID(RMP0460 *LIBL/QMFOKON 29)
     A  30                                  DSPATR(PR)
     A                                 14  2'Tidspunkt. . . . . . . . . .'
     A            C2TIME         8A  O 14 31
     A                                      OVRDTA
     A            C2RTIM    R        H      REFFLD(ELRTIM)
     A            C2FIRM    R        H      REFFLD(ELFIRM)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R CCMSG
     A*%%TS  SD  20130218  084604  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Feilmelding:               '
     A            CCMSG1        30   O  4  1
     A            CCMSG2        30   O  5  1
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
