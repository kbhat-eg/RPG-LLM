     A*%%TS  SD  20051110  104501  SST540S     REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* Beskrivelse . . : Vise varer som er linket opp mot varegr for
     A*                   transport
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*      090311  NHO  Nytt program
7.xx A* 7.00 220916  bsa  Justeringer basert på ny GUI og regler
7.01 A* K000 140121  bsa  Kan ikke avslutte uten å velge transportlinje
7.01 A* K000              Er kodet i rpg prog og styrt av switch VR743_701.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
7.xx A            B1VALG    R        B  6  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFUMAL)
7.xx A            B1VARE    R        B  6  5REFFLD(VVVARE)
7.xx A            B1TEK1    R        B  6 23REFFLD(VVTEK1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020228  092906  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
7.xx A                                      SFLSIZ(0010)
7.xx A                                      SFLPAG(0009)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Vise vare mot transport'
6.20 A                                  4  2'1=Velg'
6.20 A                                      COLOR(BLU)
7.xx A                                  5  2'V'
7.xx A                                      COLOR(WHT)
     A                                  5  5'Varenummer'
     A                                      COLOR(WHT)
     A                                  5 23'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2VARE        15A  H
     A            B2TEK1        35A  H
     A          R B2CMD
     A*%%TS  SD  20051110  104501  SST540S     REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 58)
     A                                 16  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
