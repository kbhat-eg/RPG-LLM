     A*%%TS  SD  20020401  164215  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH400D
     A* Beskrivelse . . : Kontoplan, sletting av poster/saldi
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       010402 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1BLD
     A*%%TS  SD  20020401  164215  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Sletting av posteringer/saldi'
     A                                      COLOR(WHT)
     A                                  4  2'Regnskapsår . . .:'
     A            B1RAAR    R        B  4 21REFFLD(RFRFR/RNRAAR *LIBL/RFRF)
     A  31                                  ERRMSGID(RMH0050 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMH0060 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMH0440 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                  4 28'Saldi . . .:'
     A            B1VALG    R        B  4 41REFFLD(RFRFR/RWVALG *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  4 44'J=Ja'
     A                                  6  2'Rutinen sletter alle poster eldre -
     A                                      enn angitt'
     A                                      COLOR(RED)
     A                                  7  2'regnskapsår i hovedbok. Dvs. at po-
     A                                      ster med  '
     A                                      COLOR(RED)
     A                                  8  2'regnskapsår = angitt år  -1 slette-
     A                                      s. Dersom du'
     A                                      COLOR(RED)
     A                                  9  2'har angitt 1997, slettes alle post-
     A                                      er fram til og'
     A                                      COLOR(RED)
     A                                 10  2'med 1996 fra posteringsregisteret.-
     A                                       Dersom feltet'
     A                                      COLOR(RED)
     A                                 11  2'for saldi er utfylt med J, slettes-
     A                                       alle saldi og'
     A                                      COLOR(RED)
     A                                 12  2'budsjetter eldre enn angitt regnsk-
     A                                      apsår'
     A                                      COLOR(RED)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
