     A*%%TS  SD  20120425  145928  SST620      REL-V7R1M0  5770-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.20 A* R6.20 151010 NHO  Prosjekt,aktivitet, konto og spes. fjernet fra
     A*                   bildet.  Underprosjekt legges til.
6.33 A* R6.30 040615 BSA  Fjernet arbeidsordre fra bildet F10
6.34 A* R6.30 211116 BSA  Feil feilmedling + overlappende info
7.xx A*  7.00 030316 bsa  Endringer relatert til ny GUI
8.01 A*  800  081121 ask  Lagt inn "NewLook kode" for skjermbilde
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A*
     A          R C2BLD
     A*%%TS  SD  20030430  160437  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A**                                    CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innlegging av faste opplysninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Avgiftskode  . . . . . . . . .'
     A            C2MKOD    R        B  6 33REFFLD(LOMKOD)
     A  31                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  6 35'(k)'
     A                                  6 39'0=Pliktig'
     A                                  7 39'1=Fri'
7.xx A                                  8  2'Avdeling . . . . . . . . . . +'
     A            C2AVDE    R        B  8 33REFFLD(LOAVDE)
     A  32                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 32)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2AVTX        20A  O  8 39OVRDTA
7.xx A                                  9  2'Innkjøper  . . . . . . . . . +'
     A            C2INNK    R        B  9 33REFFLD(LOINNK)
     A  33                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2INTX        20A  O  9 39OVRDTA
7.xx A                                 10  2'Lagernummer  . . . . . . . . +'
     A            C2LAGE    R        B 10 33REFFLD(LOLAGE)
     A  34                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LATX        20A  O 10 39OVRDTA
7.xx A                                 12  2'Betalingsbetingelser . . . . +'
     A            C2BETB    R        B 12 33REFFLD(LOBETB)
     A  35                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2BTX1        20A  O 12 39OVRDTA
     A            C2BTX2        20A  O 12 60OVRDTA
7.xx A                                 14  2'Valuta . . . . . . . . . . . +'
     A            C2VALK    R        B 14 33REFFLD(LOVALK)
     A  36                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 36)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2VATX        20A  O 14 39OVRDTA
7.xx A                                 16  2'Speditør . . . . . . . . . . +'
     A            C2SPED    R        B 16 33REFFLD(LOSPED)
     A  37                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 37)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2SPTX        20A  O 16 39OVRDTA
7.xx A                                 17  2'Forsendelsemåte  . . . . . . +'
     A            C2LMAT    R        B 17 33REFFLD(LOLMAT)
     A  38                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 38)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LMTX    R        B 17 39REFFLD(LOLMTX)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
7.xx A                                 18  2'Leveringsbetingelser . . . . +'
     A            C2LBET    R        B 18 33REFFLD(LOLBET)
     A  39                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 39)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LTX1        25A  B 18 39CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LTX2        25A  B 19 39CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LTX3        25A  B 20 39CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20120425  145928  SST620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A**                                    CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innlegging av faste opplysninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Bilags -nummer/-tekst  . . . .'
     A            C3BINR    R        B  6 33REFFLD(LOBINR)
     A  30                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 30)
     A  38                                  ERRMSGID(LSO0420 *LIBL/QMFLAGR 38)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C3BITX        25A  B  6 42COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kididentifikasjon  . . . . . .'
     A            C3BKID        25A  B  7 33COLOR(YLW)
     A                                  8  2'Fakturaperiode . . . . . . . .'
     A            C3PERI         4Y 0B  8 33
     A  31                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('    ')
     A                                  9  2'Fakturadato. . . . . . . . . .'
     A            C3FKDA         6Y 0B  9 33
     A  31                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                 10  2'Forfallsdato   . . . . . . . .'
     A            C3FFDA         6Y 0B 10 33
     A  32                                  ERRMSGID(LM09990 *LIBL/QMFLAGR 32)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                 11  2'Faktura-beløp  . . . . . . . .'
     A            C3TOTF    R        B 11 33REFFLD(LOTOTF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('       0 ,  ')
     A                                 12  2'Kredittreferanse . . . . . . .'
     A            C3KREF    R        B 12 33REFFLD(LOKREF)
     A  33                                  ERRMSGID(LSO0260 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 13  2'Fakturautsteder  . . . . . . +'
     A            C3FLEV    R        B 13 33REFFLD(LOLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C3FLNA        30A  O 13 40
     A                                 14  2'Leverandørens fakturanummer. .'
     A            C3SFAK        15A  B 14 33COLOR(YLW)
     A                                      DSPATR(UL)
     A  55                                  ERRMSG('Samme fakturanummer er regi-
     A                                      strert tidliger i år' 55)
     A                                 15  2'Workflow . . . . . . . . . . .'
     A            C3FLOW        10A  B 15 33COLOR(YLW)
     A                                      DSPATR(UL)
6.20 A**                               17  2'Prosjektnummer . . . . . . . .'
     A                                 17  2'Arbeidsordre . . . . . . . . .'
6.20 A**          C3PROJ    R        B 17 33REFFLD(LOPROJ)
6.33 A**          C3ARBO    R        B 17 33REFFLD(LOARBO)
6.33 A**34                                  ERRMSGID(LSO0271 *LIBL/QMFLAGR 34)
6.33 A**                                    OVRDTA
6.33 A**                                    COLOR(YLW)
6.33 A            C3PRTX        30A  O 18 42OVRDTA
7.xx A                                 18  2'Prosjekt . . . . . . . . . . +'
6.32 A            C3PROJ    R        B 18 33REFFLD(LOPROJ)
6.32 A                                      COLOR(YLW)
     A                                      OVRDTA
6.34 A**          C3PRT1        30A  O 18 42OVRDTA
     A                                 19  2'Underprosjekt. . . . . . . . .'
     A            C3UPRO         6A  O 19 33OVRDTA
     A                                 20  2'Aktivitet. . . . . . . . . . .'
     A            C3AKTI    R        O 20 33REFFLD(LOAKTI)
     A                                      OVRDTA
