     A*%%TS  SD  20071213  194730  BOF550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MC100D
     A* Beskrivelse . . : Parameter for leveringstid
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       081204 bø   Nytt program
6.30 A* 6.30  260614 BKV  Krav at leverandør må være angitt
6.32 A* 6.30  050615 BSA  Krav at leverandør må være gyldig
7.xx A* 7.xx  160316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20071213  194730  BOF550      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1OGRP    R        O  8  7REFFLD(MCOGRP)
     A            B1HGRP    R        O  8 11REFFLD(MCHGRP)
     A            B1UGRP    R        O  8 15REFFLD(MCUGRP)
     A            B1LDOR    R        O  8 19REFFLD(MCLDOR)
     A            B1CRDO    R        O  8 28REFFLD(MCCRDO)
     A                                      EDTCDE(Z)
     A            B1VARE    R   -7   O  8 31REFFLD(MCVARE)
     A            B1LAGE    R        O  8 40REFFLD(MCLAGE)
     A            B1LTID    R        O  8 43REFFLD(MCLTID)
     A            B1KDAT    R        O  8 73REFFLD(LÅDATR)
     A                                      EDTWRD('  .  .  ')
     A            B1NAVN        25   O  8 47
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20071213  194730  BOF550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Ledetidsparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 20'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 27'Cr.d'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 33'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 43'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       9=Legg ut l.tid                   -
     A                                                '
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  7'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 26'Cr.d'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 31'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 39'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 44'Lt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 47'Leverandørnavn     '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 73'Kj.dato'
     A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2OGRP    R        B  5  4REFFLD(MCOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  7'+'
7.xx A            B2HGRP    R        B  5  9REFFLD(MCHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 12'+'
7.xx A            B2UGRP    R        B  5 14REFFLD(MCUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 18'+'
7.xx A            B2LDOR    R        B  5 20REFFLD(MCLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2CRDO    R        B  5 29REFFLD(MCCRDO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  5 31'+'
7.xx A            B2VARE    R   -7   B  5 33REFFLD(MCVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 42'+'
7.xx A            B2LAGE    R        B  5 44REFFLD(MCLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett  F9=Oppd. alle'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050308  103134  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ledetidsparameter'
     A**                                    er'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        B  5 31REFFLD(MCOGRP)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(MMA0050 *LIBL/QMFLAGR 31)
     A  37                                  ERRMSGID(MMC0010 *LIBL/QMFLAGR 37)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 46
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe  . . . . . . . .'
     A            C1HGRP    R        B  6 31REFFLD(MCHGRP)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(MMA0060 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 46
     A                                      OVRDTA
     A                                  7  2'Undergruppe  . . . . . . . .'
     A            C1UGRP    R        B  7 31REFFLD(MCUGRP)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(MMA0070 *LIBL/QMFLAGR 33)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 46
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        B  8 31REFFLD(MCLDOR)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(HMR0280 *LIBL/QMFLAGR 34)
     A  39                                  ERRMSGID(LMA0610 *LIBL/QMFLAGR 39)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 46
     A                                      OVRDTA
     A                                  9  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R   -7   B  9 31REFFLD(MCVARE)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(HMT0030 *LIBL/QMFLAGR 35)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 46
     A                                      OVRDTA
     A                                 10  2'Lager    . . . . . . . . . .'
     A            C1LAGE    R        B 10 31REFFLD(MCLAGE)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(HMT0020 *LIBL/QMFLAGR 36)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LANA        30A  O 10 46
     A                                      OVRDTA
     A                                 11  2'Cross docking  . . . . . . .'
     A            C1CRDO    R        B 11 31REFFLD(MCCRDO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  38                                  ERRMSGID(MMC0020 *LIBL/QMFLAGR 38)
     A                                      EDTCDE(Z)
     A                                 11 34'1=Ja'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5 35'F4'
     A                                  6 35'F4'
     A                                  7 35'F4'
     A                                  8 38'F4'
     A                                 10 35'F4'
     A                                  9 40'F4'
     A                                 10 38'bl=Alle'
     A          R C1MSG
     A*%%TS  SD  20040825  140505  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Matrise       finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050125  103618  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ledetidsparameter'
     A**                                    er'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe   . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(MCOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 43
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe  . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(MCHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 43
     A                                      OVRDTA
     A                                  7  2'Undergruppe  . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(MCUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 43
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(MCLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 43
     A                                      OVRDTA
     A                                  9  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(MCVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 43
     A                                      OVRDTA
     A                                 10  2'Lager  . . . . . . . . . . .'
     A            C1LAGE    R        O 10 31REFFLD(MCLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LANA        30A  O 10 43
     A                                      OVRDTA
     A                                 11  2'Cross docking  . . . . . . .'
     A            C1CRDO    R        O 11 31REFFLD(MCCRDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 34'1=Ja'
     A                                 13  2'Ledetid (i dager). . . . . .'
     A            C2LTID    R        B 13 31REFFLD(MCLTID)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 20 41'Sist kjørt dato .'
     A            C2KDAT         6Y 0O 20 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Opprettet dato  . . . . . . '
     A            C2ODAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Sist endret dato  . . . . . '
     A            C2EDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Endret av . . . . . . . . . '
     A            C2EUSR    R        O 22 31REFFLD(MCUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring '
     A                                      COLOR(BLU)
     A                                 11 80' '
     A          R D1WIN
     A*%%TS  SD  20041202  142747  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Matrise'
     A                                      DSPATR(RI)
     A                                  5  2'Overgruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(MCOGRP)
     A                                  6  2'Hovedgruppe  . . .'
     A            D1HGRP    R        O  6 21REFFLD(MCHGRP)
     A                                  7  2'Undergruppe  . . .'
     A            D1UGRP    R        O  7 21REFFLD(MCUGRP)
     A                                  8  2'Leverandør . . . .'
     A            D1LDOR    R        O  8 21REFFLD(MCLDOR)
     A                                  9  2'Varenr.  . . . . .'
     A            D1VARE    R   -7   O  9 21REFFLD(MCVARE)
     A                                 10  2'Lager .  . . . . .'
     A            D1LAGE    R        O 10 21REFFLD(MCLAGE)
     A                                 11  2'Cross docking  . .'
     A            D1CRDO    R        O 11 21REFFLD(MCCRDO)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20041222  122439  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 21 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Overgruppe . . . .'
     A            K1OGRP    R        B  4 21REFFLD(MCOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Hovedgruppe  . . .'
     A            K1HGRP    R        B  5 21REFFLD(MCHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Undergruppe  . . .'
     A            K1UGRP    R        B  6 21REFFLD(MCUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Leverandør . . . .'
     A            K1LDOR    R        B  7 21REFFLD(MCLDOR)
6.32 A  40                                  ERRMSGID(RML0020 *LIBL/QMFOKON 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  8  2'Varenr.  . . . . .'
     A            K1VARE    R   -7   B  8 21REFFLD(MCVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lager  . . . . . .'
     A            K1LAGE    R        B  9 21REFFLD(MCLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 10  2'Cross Docking  . .'
     A            K1CRDO    R        B 10 21REFFLD(MCCRDO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 10 24'1=Ja'
     A                                 12  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 13  2'Overgruppe . . . .'
     A            MCOGRP    R        O 13 21OVRDTA
     A                                 14  2'Hovedgruppe  . . .'
     A            MCHGRP    R        O 14 21OVRDTA
     A                                 15  2'Undergruppe  . . .'
     A            MCUGRP    R        O 15 21OVRDTA
     A                                 16  2'Leverandør . . . .'
     A            K2LDOR    R        O 16 21REFFLD(MCLDOR)
     A                                 17  2'Varenr.  . . . . .'
     A            K2VARE    R   -7   O 17 21REFFLD(MCVARE)
     A                                      OVRDTA
     A                                 18  2'Lager  . . . . . .'
     A            K2LAGE    R        O 18 21REFFLD(MCLAGE)
     A                                      OVRDTA
     A                                 19  2'Cross docking  . .'
     A            K2CRDO    R        O 19 21REFFLD(MCCRDO)
     A                                      OVRDTA
     A                                 20  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5 24'F4'
     A                                  4 24'F4'
     A                                  6 25'F4'
     A                                  8 30'F4'
     A                                  7 28'F4'
     A                                  9 24'F4'
     A          R M1WIN
     A*%%TS  SD  20050308  102757  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Oppdater ledetid'
     A                                      DSPATR(RI)
     A                                  6  2'Overgruppe . . . .'
     A            M1OGRP    R        O  6 21REFFLD(MCOGRP)
     A            M1OGNA        25A  O  6 30
     A                                  7  2'Hovedgruppe  . . .'
     A            M1HGRP    R        O  7 21REFFLD(MCHGRP)
     A            M1HGNA        25A  O  7 30
     A                                  8  2'Undergruppe  . . .'
     A            M1UGRP    R        O  8 21REFFLD(MCUGRP)
     A            M1UGNA        25A  O  8 30
     A                                  9  2'Leverandør . . . .'
     A            M1LDOR    R        O  9 21REFFLD(MCLDOR)
     A            M1LENA        25A  O  9 30
     A                                 10  2'Varenummer . . . .'
     A            M1VARE    R   -7   O 10 21REFFLD(MCVARE)
     A            M1VANA        25A  O 10 30
     A                                 11  2'Lager  . . . . . .'
     A            M1LAGE    R        O 11 21REFFLD(MCLAGE)
     A            M1LANA        25A  O 11 30
     A                                 12  2'Cross docking  . .'
     A            M1CRDO    R        O 12 21REFFLD(MCCRDO)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  4  2'Du oppdatere nå lagerregister med -
     A                                      nye ledetider.'
     A          R N1WIN
     A*%%TS  SD  20050308  115258  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 9 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Oppdater ledetid '
     A                                      DSPATR(RI)
     A                                  3  2'     Alle parameter    '
     A                                      DSPATR(RI)
     A                                  5  2'Du oppdatere nå lagerregister med -
     A                                      nye ledetider.'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
