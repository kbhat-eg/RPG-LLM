     A*%%TS  SD  20150720  143649  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV118D
     A* Beskrivelse . . : Vare - Leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       170714 bof  Nytt program
6.30 A* 6.30  211014 BSA  Justert pga jacada
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20150720  143649  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1LLDO    R        O  8  5REFFLD(JVLLDO)
     A            B1LNAV    R        O  8 12REFFLD(JVTEK1)
     A            B1LVEI    R        O  8 48REFFLD(JVLVEI)
     A            B1LLVA    R        O  8 50REFFLD(JVLLVA)
     A            B1LUDA         6Y 0O  8 72EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20150720  143649  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring Vare Leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr.  . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2LVNR    R   -7   O  4 31REFFLD(JVLVNR)
     A                                      COLOR(YLW)
     A            B2VTE2    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2' 5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Leverandør                     -
     A                                                 VE  Lev.varenummer      -
     A                                       Utg.dato '
     A                                      COLOR(WHT)
     A            B2LLDO    R        B  7  5REFFLD(JVLLDO)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140717  125348  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring Vare Leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . . . . . . .'
     A            C1LVNR    R   -7   O  4 31REFFLD(JVLVNR)
     A                                      COLOR(YLW)
     A            C1VTE2    R        O  4 40REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1LLDO    R        O  5 31REFFLD(JVLLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  5 40REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Er Vareeier. . . . . . . . .'
     A            C2LVEI    R        O  8 31REFFLD(JVLVEI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Leverandørs varenummer . . .'
     A            C2LLVA    R        O  9 31REFFLD(JVLLVA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Utgått dato  . . . . . . . .'
     A            C2LUDA         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12  2'Merking 1. . . . . . . . . .'
     A            C2LMR1    R        O 12 31REFFLD(JVLMR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Merking 2. . . . . . . . . .'
     A            C2LMR2    R        O 13 31REFFLD(JVLMR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Merking 3. . . . . . . . . .'
     A            C2LMR3    R        O 14 31REFFLD(JVLMR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2NODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2NEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2NUSR    R        O 20 31REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
