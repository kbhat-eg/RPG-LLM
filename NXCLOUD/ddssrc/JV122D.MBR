     A*%%TS  SD  20140714  093510  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A* 040316 bsa Endringer relatert til ny GUI
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20140714  093510  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF20
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring til eget vareregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  9  2'Varegruppe  . . . . . . .'
     A            B1OGRP    R        O  9 28REFFLD(JVOGRP)
     A  33                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 33)
     A  38                                  ERRMSGID(JVG1040 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 31'+'
     A            B1HGRP    R        B  9 33REFFLD(JVHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 36'+'
7.xx A            B1UGRP    R        B  9 38REFFLD(JVUGRP)
     A  35                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1UTXT        35A  O  9 43COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Varenr  (kun tjeneste)  .'
7.xx A            B1VARE    R        B 11 28REFFLD(JVVARE)
     A  36                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 36)
     A  37                                  ERRMSGID(JMV0410 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B1VTXT        35A  O 12 28COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 15  2'Ant overført  . . . . . .'
7.xx A            B1ANTO         5Y 0O 15 28COLOR(YLW)
     A                                      OVRDTA
7.xx A            B1OFER        35A  O 15 43COLOR(YLW)
     A                                      OVRDTA
     A            B1ENHM        60A  O 18  7COLOR(RED)
     A                                      OVRDTA
     A                                 22  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23  2'F20=Overføring'
     A                                      COLOR(BLU)
