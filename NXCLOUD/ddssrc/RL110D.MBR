     A*%%TS  SD  20050519  233418  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL110D
     A* Beskrivelse . . : Leverandrposteringer, vedlikehold bilag/lever.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130802 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19960228  123916  KB          REL-V3R1M0  5763-PW1
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(ROBILN)
6.NU A            B1BIDA         6Y 0O  9 13
6.NU A            B1BILK    R        O  9 20REFFLD(ROBILK)
6.NU A            B1TEXT        16A  O  9 23
     A            B1BELØ         9Y 2O  9 40EDTCDE(J)
     A            B1LEVR    R        O  9 54REFFLD(RLLEVR)
     A            B1NAVN    R  -10   O  9 61REFFLD(RLNAVN)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020814  002640  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandørpostering-
     A                                      er'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  4 61'Regnskapsår. .:'
     A                                      COLOR(WHT)
7.xx A            B2RAAR    R        B  4 77REFFLD(RORAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
7.xx A                                  8 14'Dato'
     A                                      COLOR(WHT)
7.xx A                                  8 20'Bk'
     A                                      COLOR(WHT)
7.xx A                                  8 23'Tekst'
     A                                      COLOR(WHT)
7.xx A                                  8 44'Beløp'
     A                                      COLOR(WHT)
7.xx A                                  8 55'Levr.'
     A                                      COLOR(WHT)
7.xx A                                  8 61'Navn'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A            B2BILN    R        B  6  4REFFLD(ROBILN)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5 55'Levr.'
7.xx A                                      COLOR(WHT)
7.xx A            B2LEVR    R        B  6 54REFFLD(RLLEVR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020810  231330  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20050519  233418  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 75)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 29'Leverandørposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PC)
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(ROBILK)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  4 22REFFLD(ROTEXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0B  5 19
     A  37                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        B  6 19REFFLD(ROBETB)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01030 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2CTXT    R        O  6 22REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        B  7 19REFFLD(ROREKL)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 43'Samlebilag-nr. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        B  8 19REFFLD(ROREFN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        B  9 19REFFLD(ROAVDE)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2GTXT    R   18   O  9 24REFFLD(RAGTXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        B  9 61REFFLD(ROREST)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjon. . .:'
     A            C2ATTS    R        B 10 19REFFLD(ROATTS)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ADAT         6Y 0O 10 21DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A            C2ATTU    R        O 10 30REFFLD(ROATTU)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        B 11 19REFFLD(ROBETK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        B 12 19REFFLD(RORRF1)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        B 13 19REFFLD(RORRF2)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        B 13 61REFFLD(ROVALR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        B 14 19REFFLD(RODECK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        B 15 17REFFLD(ROKIDI)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
