     A*%%TS  SD  20040118  130951  HKO         REL-V5R2M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       020816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20040118  130951  HKO         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(*DFT 19 70)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Matching av egne varer mot varead-
     A                                      ministrasjon '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Eget leverandørnr+'
     A            B1LDOR         6A  B  4 21COLOR(YLW)
     A  31                                  ERRMSGID(JML0210 *LIBL/QMFVADM 31)
     A                                  4 32'Blank=alle'
     A                                  5 32'*BL=varer uten lev.'
7.xx A                                  7  2'Egen varegruppe. +'
     A            B1OGRP    R        B  7 21REFFLD(JOOGRP)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
7.xx A                                  7 24'+'
7.xx A            B1HGRP    R        B  7 26REFFLD(JOHGRP)
     A  33                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
7.xx A                                  7 29'+'
7.xx A            B1UGRP    R        B  7 31REFFLD(JOUGRP)
     A  34                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  9  2'Eget varenummer. .'
     A            B1VA1F         8Y 0B  9 21COLOR(YLW)
     A  35                                  ERRMSGID(JMV0100 *LIBL/QMFVADM 35)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                  9 30'-'
     A            B1VA1T         8Y 0B  9 32COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                 11  2'Matche-status. . .'
     A            B1KODE    R        B 11 21REFFLD(JOKODE)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 4)
     A                                      EDTCDE(3)
     A                                 11 32'0 = Ikke matchet'
     A                                 12 32'1 = Manuelt matchet'
     A                                 13 32'2 = Maskinelt matchet mot lev. var-
     A                                      enr.'
     A                                 14 32'3 = Maskinelt matchet mot EAN-nr.'
     A                                 15 32'4 = Maskinelt matchet mot Nobbnr.'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
