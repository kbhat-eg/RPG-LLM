     A*%%TS  SD  20140929  081507  NHO630      REL-V7R1M0  5770-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.20 A* R6.20 151010 NHO  Prosjekt og aktivitet fjernet fra bildet.    ttet
6.24 A* R6.20 050411 NHO  Sjekk om skaffevare                          ttet
6.25 A* R6.20 240913 BOF  Ved endring levtype,sjekk om best.er laget
6.2F A* R6.20 030114 BOF  Ved endring levtype og akktyp>= 2,så feil
6.30 A* R6.30 090114 BOF  BM Pakke - nytt popup Oppsalg/Mersalg
6.2J A* R6.20 110414 BSA  Kan oppgi negativ dekningsgrad
6.31 A* R6.30 160318 BKV  Nye koder på i FFRR FDKPRI
7.xx A* 7.xx  190216 bsa  Endringer relatert til ny GUI
7.xx A* 7.xx  270117 bkv  Popup til window
7.04 A* 7.00  160919 nho  2 nye felt på bildet + G1WIN
7.13 A* K000  190521 bsa  Legger ut dato for disponibelsituasjon,
7.13 A* K000              samt F tast for overgang til disponibelsituasjon.
     A*
8.01 A*PGR2   080622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02 A*       050722 BOF  Feilretting vedr. å vise Bruksrettrabatt
8.03 A* K000  090322 bsa  Skal ikke kune endre linjetype
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R C1WIN
     A*%%TS  SD  20140304  123251  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A*                                     WINDOW(*DFT 22 70)
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
7.13 A                                      CF11
     A                                      CA12
7.03 A                                      CA14
     A                                      CF16
6.36 A                                      CF18
     A                                      CA23
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1ALME    R        H      REFFLD(FDALME)
     A            C1OMVA    R        H      REFFLD(FDOMVA)
7.xx A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  1 46'Ordre/Fakturering'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 72TIME
7.xx A                                      COLOR(WHT)
     A                                  2  2' Reg./vedlikehold av varel-
     A                                      inje '
     A                                      DSPATR(RI)
7.xx A                                  2 38'FD105C1'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 54USER
7.xx A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 72DATE
7.xx A                                      EDTCDE(Y)
7.xx A                                      COLOR(WHT)
7.xx A                                  3  1'-----------------------------------
7.xx A                                      ------------------------------------
7.xx A                                      -----------'
7.xx A                                      COLOR(WHT)
7.xx A            C1KUND    R        O  4 73REFFLD(FOKUND)
7.xx A                                      OVRDTA
     A                                  4  2'Antall        Enh  Varenr  T  Nobb-
     A                                      nr  P.kl  '
     A                                      COLOR(WHT)
7.xx A            C1ODEK    R        O  4 61REFFLD(FDPASL)
     A                                      COLOR(BLU)
     A N86                                  DSPATR(ND)
     A                                      OVRDTA
6.3b A                                      EDTCDE(M)
7.xx A            C1ANT1    R        B  5  2REFFLD(FDANTA)
     A  33                                  ERRMSGID(FMD0150 *LIBL/QMFOFAK 33)
     A  49                                  ERRMSGID(FM00630 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
7.xx A            C1ENH1    R        B  5 16REFFLD(FDENH1)
     A  31                                  ERRMSGID(VMA1010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 32)
     A  81                                  COLOR(YLW)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C1VARE    R   -7   O  5 20REFFLD(FDVARE)
     A                                      OVRDTA
7.xx A            C1VTYP    R        O  5 29REFFLD(VVVTYP)
     A                                      OVRDTA
7.xx A            C1NOBB    R        O  5 31REFFLD(FDNOBB)
     A                                      OVRDTA
7.xx A            C1PKLA    R        O  5 41REFFLD(VEPKLA)
     A                                      OVRDTA
7.xx A            C1NTSU        11Y 2O  5 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
7.xx A            C1NTPR    R        O  5 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            C1ANT2    R        B  6  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  82                                  DSPATR(ND)
     A  82                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C1ENH2    R        O  6 16REFFLD(FDENH1)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A            C1NISU        11Y 2O  6 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
7.xx A            C1NTIM    R        O  6 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            C1ANT3    R        B  7  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  83                                  DSPATR(ND)
     A  83                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C1ENH3    R        O  7 16REFFLD(FDENH1)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                  8 21'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Rab-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Rab-2'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'K'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 75'LG'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 78'LT'
     A                                      COLOR(WHT)
7.xx A            C1ANTA    R        O  9  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
7.xx A            C1ENHE    R        O  9 16REFFLD(FDENH1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1SAPR         9Y 2B  9 20EDTWRD('     0 ,  ')
     A  39                                  ERRMSGID(FMD0170 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            C1RAB1    R        B  9 31REFFLD(FDRAB1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
7.xx A            C1RAB2    R        B  9 39REFFLD(FDRAB2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
7.xx A                                  9 47'+'
7.xx A            C1LETY    R        B  9 49REFFLD(FDLETY)
     A  34                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 34)
     A  45                                  ERRMSGID(VMY0200 *LIBL/QMFOFAK 45)
     A  46                                  ERRMSGID(FMO0740 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1KPRI    R        B  9 51REFFLD(FDKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 53'(prisk)'
7.xx A            C1KOPR    R        B  9 61REFFLD(FDKOPR)
     A                                      COLOR(YLW)
7.02 A*                                      EDTWRD('       ,  ')
7.02 A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  9 73'+'
7.xx A            C1LAGE    R        B  9 75REFFLD(FDLAGE)
     A  38                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.03 A**          C1LTYP    R        B  9 78REFFLD(FDLTYP)
8.03 A            C1LTYP    R        O  9 78REFFLD(FDLTYP)
8.03 A**42                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 42)
8.03 A**43                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 43)
8.03 A**44                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
8.03 A**                                    COLOR(YLW)
8.03 A**                                    OVRDTA
7.xx A            C1TEK1    R        B 10 20REFFLD(FDTEK1)
     A  35                                  ERRMSGID(FMD0180 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
8.02 A                                 10 59'Br.'
8.02 A            C1BRTY    R        O 10 63REFFLD(FDBRTY)
8.02 A            C1BRR1    R        O 10 65REFFLD(FDBRR1)
8.02 A                                      EDTCDE(4)
7.xx A            C1TEK2    R        B 11 20REFFLD(FDTEK2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A            C1BRR2    R        O 11 65REFFLD(FDBRR2)
     A                                      EDTCDE(4)
7.xx A  50        C1SKAF        10A  O 11  2OVRDTA
     A                                      COLOR(RED)
7.xx A                                 12  2'Salgspris inkl. .'
7.xx A            C1SAIM         9Y 2B 12 20EDTWRD('       ,  ')
     A  36                                  ERRMSGID(FMD0210 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12 43'Lagerbeholdn.'
     A N87                                  DSPATR(ND)
7.xx A            C1BEHL    R        O 12 57REFFLD(VLBEHL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                 13  2'Dekningsgrad (DG)'
6.2J A**          C1DEKN    R        B 12 20REFFLD(FDPASL)
7.xx A            C1DEKN    R   -6   B 13 20REFFLD(FDRKR1)
     A  37                                  ERRMSGID(FMD0220 *LIBL/QMFOFAK 37)
7.xx A                                      COLOR(YLW)
     A                                      CHECK(RB)
6.2J A                                      EDTCDE(M)
6.2J A**                                    EDTWRD('   ,  ')
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13 43'I bestilling.'
     A N87                                  DSPATR(ND)
7.xx A            C1BBES    R        O 13 57REFFLD(VLBBES)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                 14  2'Påslag. . . . . .'
7.xx A            C1PASL         5Y 2B 14 20COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 14 43'I ordre . . .'
     A N87                                  DSPATR(ND)
7.xx A            C1PAKK    R        O 14 57REFFLD(VLBEHL)
     A                                      DSPATR(UL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
7.01 A  51                             15  2'Returgebyr. . . .'
7.01 A  51        C1SATS    R        B 15 20REFFLD(FGSATS)
7.01 A                                      COLOR(YLW)
7.01 A                                      EDTCDE(M)
7.01 A                                      OVRDTA
7.01 A**
7.01 A**          C1GEBY    R        B 15 29REFFLD(FÅSVA2)
7.01 A  51        C1GEBY    R        B 15 29REFFLD(FÅSVA2)
7.01 A                                      OVRDTA
7.01 A                                      VALUES(0 1)
7.01 A                                      COLOR(YLW)
7.01 A**                               15 32'0=Nei,1=Ja'
7.01 A  51                             15 32'1/0'
7.01 A**
7.xx A** 7.01                          15  2'Lassnummer. . . .'
7.xx A** 7.01     C1LASS    R        B 15 20REFFLD(FDLASS)
     A                                 19 43'Lassnummer. .'
     A            C1LASS    R        B 19 57REFFLD(FDLASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 15 43'Disp.beholdn.'
     A N87                                  DSPATR(ND)
7.xx A            C1DISP    R        O 15 57REFFLD(VLBEHL)
     A N87                                  DSPATR(ND)
7.13 A  87 89                               COLOR(RED)
     A                                      OVRDTA
7.13 A                                 15 71'pr'
7.13 A N87                                  DSPATR(ND)
7.13 A            C1DDAT         6Y 0O 15 74
7.13 A N87                                  DSPATR(ND)
7.13 A                                      OVRDTA
7.xx A** 7.01                          16  2'Prisgruppe  . . .'
7.xx A** 7.01     C1PRGR    R        O 16 20REFFLD(FDPRGR)
     A                                 17  2'Prisgruppe  . . .'
     A            C1PRGR    R        O 17 20REFFLD(FDPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16 43'Modulnr . . .'
7.xx A            C1NMOD    R        O 16 57REFFLD(VVNMOD)
     A                                      OVRDTA
7.xx A** 7.01                          17  2'Lev.dato  . . . .'
7.xx A** 7.01     C1LDAT         6Y 0B 17 20COLOR(YLW)
     A                                 18  2'Lev.dato  . . . .'
     A            C1LDAT         6Y 0B 18 20COLOR(YLW)
     A  40                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(FMD0200 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A** 7.01                          18  2'Utgår dato  . . .'
     A                                 19  2'Utgår dato  . . .'
     A N84                                  DSPATR(ND)
7.xx A** 7.01     C1UDAT         6Y 0O 18 20EDTWRD('  .  .  ')
     A            C1UDAT         6Y 0O 19 20EDTWRD('  .  .  ')
     A N84                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                 17 43'GTIN-nr . . .'
7.xx A            C1EANN    R        O 17 57REFFLD(VXEANN)
     A                                      OVRDTA
7.xx A                                 18 43'Erstattes av.'
7.xx A            C1ENOB    R        O 18 57REFFLD(VVNOBB)
     A                                      OVRDTA
     A*                                18  2'Mersalg/Oppsalg'
     A*N29                                  DSPATR(ND)
7.xx A** 7.01     C1OPP1        69A  O 19  2
     A            C1OPP1        69   O 21  2
     A N29                                  DSPATR(ND)
     A                                      COLOR(RED)
     A                                      OVRDTA
7.01 A  51                             16  2'Returkode . . .+'
7.01 A  51        C1DKOD         1A  B 16 20COLOR(YLW)
7.01 A                                      OVRDTA
7.05 A  51        C1TEXT        20   O 16 22COLOR(YLW)
7.01 A                                      OVRDTA
7.xx A            C1MER1        69A  O 20  2
     A N29                                  DSPATR(ND)
     A                                      COLOR(RED)
     A                                      OVRDTA
7.xx A                                 22  2'F2=Kostp. F3=Gå tilbake F4=Spørrin-
7.13 A                                      g F6=Lengde F7=Pakke F8=ProInfo F9=-
7.13 A                                      LagInfo'
7.13 A**                                    g F6=Lengde F7=Pakke F18=NOBB Info'
     A                                      COLOR(BLU)
7.13 A*                                23  2'F8=ProInfo F9=LagInfo F10=Notat F1-
7.13 A*                                     4=VA Info F23=Vis kost'
7.13 A                                 23  2'F10=Notat F11=Disp F14=VA info'
     A                                      COLOR(BLU)
7.13 A                                 23 33'F16=BM info'
     A N29                                  DSPATR(ND)
     A                                      COLOR(BLU)
7.13 A                                 23 45'F18=NOBB info F23=Vis kost'
7.13 A                                      COLOR(BLU)
7.13 A N29                                  DSPATR(ND)
     A          R C1MSG
     A*%%TS  SD  20020530  172007  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Salgspris er lavere enn kostpris !'
     A                                  5  2'Bekreft salget med Enter eller <OK-
     A                                      >, eller'
     A                                  6  2'gå tilbake til registrering ved å -
     A                                      velge'
     A                                  7  2'<Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG2
     A*%%TS  SD  20081112  145607  BOF570      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Endret lager medførte ny prisgrupp-
     A                                      e'
     A                                  5  2'Bekreft endret pris med <OK>,eller'
     A                                  6  2'beholde priser ved å velge '
     A                                  7  2'<Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20020530  172007  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Lagerbeholdning er for lav for å  '
     A                                  4  2'gjennomføre dette salget !'
     A                                  5  2'Bekreft salget med Enter eller <OK-
     A                                      >, eller'
     A                                  6  2'gå tilbake til registrering ved å -
     A                                      velge'
     A                                  7  2'<Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20140929  081507  NHO630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 10 28)
     A                                      CA03
     A                                      CF07
     A                                  2  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  4  2'Varen er skaffevare'
     A                                  5  2'på valgt lager.    '
     A                                  6  2'Tast <enter> for å '
     A                                  7  2'godkjenne dette.'
     A                                  9  2'F3=Tilbake'
     A                                  9 14'F7=List Lager'
     A          R C4MSG
     A*%%TS  SD  20110526  143357  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 10 28)
     A                                      CA03
     A                                      CF07
     A                                  2  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  4  2'Varen er skaffevare'
     A                                  5  2'på valgt lager,    '
     A                                  6  2'og kan ikke godtas.'
     A                                  7  2'Tast enter'
     A                                  9  2'F7=List Lager'
     A          R C5MSG
     A*%%TS  SD  20120109  124421  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 10 28)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Lagerbeholdning er for '
     A                                  4  2'lav for å gjennomføre  '
     A                                  5  2'dette salget !'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C6MSG
     A*%%TS  SD  20120222  085139  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 10 28)
     A                                      CA03
     A                                      CA12
     A                                  3  2'En lagerfeil har oppstått'
     A                                  4  2'med varen, kontakt vare-'
     A                                  5  2'ansvarlig !   '
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C7WIN
     A*%%TS  SD  20140327  133502  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 12 65)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Oppsalg:'
     A            C7OPP1        50A  O  2 11
     A            C7OPP2        50A  O  3 11
     A                                  4  2'Mersalg:'
     A            C7MER1        50A  O  4 11
     A            C7MER2        50A  O  5 11
     A                                  6  2'Sortiment'
     A                                  7  2'S.leverandør'
     A            C7SORT         1A  O  6 19
     A            C7SLDO    R        O  7 19REFFLD(FÅLDOR)
     A            C7SLNA        30A  O  7 26
     A                                  8  2'Utstillingskode '
     A            C7UTKO        30A  O  8 19
     A                                  9  2'Brand . . . . . '
     A            C7BRAN        35A  O  9 19
     A                                 10  2'Kategoriansvar.'
     A            C7KNAV        30A  O 10 19
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.01 A          R G1WIN
7.01 A*%%TS  SD  20020623  204937  NHO         REL-V5R1M0  5722-WDS
7.01 A                                      WINDOW(*DFT 14 34)
7.01 A                                      CA03
7.01 A                                      CA12
7.01 A                                      PUTOVR
7.01 A                                  2  2' Slette returgebyr'
7.01 A                                      DSPATR(RI)
7.01 A                                  6  2'Kommentar til slettingen:'
7.01 A            G1KOM1    R        B  7  2REFFLD(FYKOM1)
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A            G1KOM2    R        B  8  2REFFLD(FYKOM2)
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A                                 13  2'F3=Gå tilbake'
7.01 A                                      COLOR(BLU)
7.xx A*         R ASUME                     ASSUME
7.xx A*                                24 79' '
