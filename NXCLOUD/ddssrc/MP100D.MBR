     A*%%TS  SD  20211109  084910  G01EGSVSTR  REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPMAL
     A* Format. . . . . : MP100D
     A* Beskrivelse . . : Parameter pr. lev. - best.forslag - vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200404 bø   Nytt program
7.04 A*       091019 nho  Mulighet til å filtrere på levnr,selger og lager
7.05 A*K0000  110920 bsa  F-tast for overgang til vedlikeholde leveranører
7.05 A*K0000              hvis best.forlslag kode er sat til 2.(div koder)
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080519  145400  ASP_NOR     REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1LDOR    R        O  8  5REFFLD(MPLDOR)
     A            B1LNUM    R        O  8 12REFFLD(MPLDOR)
     A            B1LNAV        20A  O  8 19
     A            B1SELG    R        O  8 40REFFLD(MPSELG)
     A            B1BESK    R  -15   O  8 50REFFLD(MPBESK)
     A            B1SDAT    R        O  8 72REFFLD(LÅDATR)
     A                                      EDTWRD('  .  .  ')
     A            B1LAGE         2Y 0O  8 46EDTCDE(Z)
     A* 5.51 220508 TSV  LISER LAGER PÅ SUBFILA
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080522  134438  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
7.04 A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CF06
7.05 A  50                                  CF07
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Parameter for bestillingsforslag'
     A                                      COLOR(WHT)
7.xx A                                  2 38'MP100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'  Lev.nr        Lev.navn          -
7.xx A                                        Selger     Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Kjør nå'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Løpnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Lev.navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'Selg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 46'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 50'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Sist kjørt'
7.xx A                                      COLOR(WHT)
7.xx A            B2LDOR    R        B  5  4REFFLD(MPLDOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LSOR    R        B  5 18REFFLD(MPLSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2SELG    R        B  5 39REFFLD(MPSELG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2BESK    R  -15   B  5 49REFFLD(MPBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
7.04 A                                 23  2'F1=Filter  F3=Gå tilbake  F5=Forny-
7.04 A                                        F6=Opprett'
7.04 A                                      COLOR(BLU)
7.05 A  50                             23 50'F7=Lev. til best.forslag'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040525  120457  BOF         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Parameter for bestillingsforslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør   . . . . .'
     A            C1LDOR    R        B  4 27REFFLD(MPLDOR)
     A  31                                  ERRMSGID(LMH0050 *LIBL/QMFLAGR 31)
6.30 A  33                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4 35'F4'
     A                                  5  2'Parameternummer  . . .'
     A            C1LNUM    R        B  5 27REFFLD(MPLNUM)
     A  32                                  ERRMSGID(LMH0090 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Parameter for leverandør   '
     A                                  3  2'finnes fra før.            '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090904  141658  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF09
6.20 A                                      CF10
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Parameter for bestillingsforslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør   . . . . .'
     A            C1LDOR    R        O  4 27REFFLD(MPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Parameternummer  . . .'
     A            C1LNUM    R        O  5 27REFFLD(MPLNUM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  4 36COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 52'Opprettet dato   :'
7.xx A            C2ODAT         6Y 0O  9 71COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                 10 52'Sist endret dato :'
7.xx A            C2EDAT         6Y 0O 10 71COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                 11 52'Endret av. . . . :'
7.xx A            C2USER    R        O 11 71REFFLD(MPUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Selger  / Mail   . . .'
7.xx A            C2SELG    R        B  6 27REFFLD(MPSELG)
     A  44                                  ERRMSGID(HMR0240 *LIBL/QMFLAGR 44)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  6 32'F4'
7.xx A            C2MAIL    R   -5   B  6 35REFFLD(MPMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  7  2'Beskrivelse. . . . . .'
7.xx A            C2BESK    R        B  7 27REFFLD(MPBESK)
     A  31                                  ERRMSGID(LMH0100 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  8  2'Produktansvarlig . . .'
7.xx A            C2PRAN    R        B  8 27REFFLD(MPPRAN)
     A  32                                  ERRMSGID(LMH0110 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  9  2'Overgruppe  fra - til '
7.xx A            C2OGRF    R        B  9 27REFFLD(MPOGRF)
     A  33                                  ERRMSGID(LMH0120 *LIBL/QMFLAGR 33)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2OGRT    R        B  9 36REFFLD(MPOGRT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10  2'Hovedgruppe fra - til '
7.xx A            C2HGRF    R        B 10 27REFFLD(MPHGRF)
     A  34                                  ERRMSGID(LMH0130 *LIBL/QMFLAGR 34)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2HGRT    R        B 10 36REFFLD(MPHGRT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 11  2'Undergruppe fra - til '
7.xx A            C2UGRF    R        B 11 27REFFLD(MPUGRF)
     A  35                                  ERRMSGID(LMH0150 *LIBL/QMFLAGR 35)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2UGRT    R        B 11 36REFFLD(MPUGRT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 12  2'Varenummer  fra - til '
7.xx A            C2VARF    R        B 12 27REFFLD(MPVARF)
     A  36                                  ERRMSGID(LMH0160 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2VART    R        B 12 46REFFLD(MPVART)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13  2'Tidshorisont . . . . .'
7.xx A            C2THOR    R        B 13 27REFFLD(MPTHOR)
     A  37                                  ERRMSGID(LMH0170 *LIBL/QMFLAGR 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13 32'Oppgis i uker'
7.xx A                                 14  2'Lager  . . . . . . . .'
7.xx A            C2LAGE    R        B 14 27REFFLD(MPLAGE)
     A  38                                  ERRMSGID(LMH0180 *LIBL/QMFLAGR 38)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 14 32'F4'
7.xx A                                 15  2'Valg av varer  . . . .'
7.xx A            C2VVAL    R        B 15 27REFFLD(MPVVAL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3)
     A                                      CHKMSGID(LMH0190 *LIBL/QMLAGR)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 15 30'0=Alle 0 eller neg. beholdning,1=A-
7.xx A                                      lle u/ min.lager'
7.xx A                                 16 30'2=Alle u/ best.punkt,3=Alle'
7.xx A                                 20 55'Sats (%)'
7.xx A            C2SATS    R        B 20 64REFFLD(MPSATS)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 17  2'Reberegn bestilling. .J'
7.xx A            C2REBE    R        B 17 27REFFLD(MPREBE)
     A                                      COLOR(YLW)
     A  41                                  ERRMSGID(LMH0200 *LIBL/QMFLAGR 41)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A*                                17 31'1=Ja'
7.xx A                                 18  2'Bestemme antall  . . .'
7.xx A            C2AVAL    R        B 18 27REFFLD(MPAVAL)
     A                                      COLOR(YLW)
     A                                      VALUES(1 2 3)
     A                                      CHKMSGID(LMH0210 *LIBL/QMFLAGR)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 18 30'1=Fast best.kvantum,2=Best. til ma-
7.xx A                                      ks beholdning'
7.xx A                                 19 30'3=Korrigert mengde         '
7.xx A                                 20  2'Korrigere mot anbrekk J'
7.xx A            C2KORA    R        B 20 27REFFLD(MPKORA)
     A                                      COLOR(YLW)
     A  43                                  ERRMSGID(LMH0200 *LIBL/QMFLAGR 43)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                20 31'1=Ja'
     A                                 21  2'Grense vekt / beløp  .'
     A            C2GVEK    R        B 21 27REFFLD(MPGVEK)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2GBEL    R        B 21 47REFFLD(MPGBEL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(M)
     A                                 21 41'kilo'
     A                                 21 61'kr.'
     A                                      COLOR(GRN)
     A                                 22  2'Kjede Sortiment  . . .'
     A            C2KSOR    R        B 22 27REFFLD(MPKSOR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 31'1/0'
     A                                 22 52'Sist kjørt  :'
     A            C2SDAT    R        B 22 66REFFLD(LÅDATR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  42                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 42)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8 33'F4'
     A                                  9 31'F4'
     A                                 10 31'F4'
     A                                 11 31'F4'
     A                                  9 41'F4'
     A                                 10 41'F4'
     A                                 11 41'F4'
     A                                 23  2'F3=Gå tilbake   F4=Spøring '
     A                                      COLOR(BLU)
6.20 A                                 23 33'F9=Kjøremønster   F10=Varsling'
     A                                      COLOR(BLU)
     A          R C3WIN
     A*%%TS  SD  20090908  120440  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  3'Kjøremønster'
     A                                      DSPATR(RI)
     A                                  3  3'Frekvens '
7.xx A            C3HYPP    R        B  3 16REFFLD(MPHYPP)
     A  39                                  ERRMSGID(LMH0230 *LIBL/QMFLAGR 39)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  6 16'Man.'
7.xx A                                  6 22'Tir.'
7.xx A                                  6 28'Ons.'
7.xx A                                  6 34'Tor.'
7.xx A                                  6 40'Fre'
7.xx A                                  6 46'Lør.'
7.xx A                                  6 52'Søn.'
     A                                  7  3'Dager . .'
     A            C3DAG1    R        B  7 16REFFLD(MPDAG1)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG2    R        B  7 22REFFLD(MPDAG2)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG3    R        B  7 28REFFLD(MPDAG3)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG4    R        B  7 34REFFLD(MPDAG4)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG5    R        B  7 40REFFLD(MPDAG5)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG6    R        B  7 46REFFLD(MPDAG6)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C3DAG7    R        B  7 52REFFLD(MPDAG7)
     A                                      COLOR(YLW)
7.xx A                                      VALUES(' ' 'J')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  7 18'J/N'
7.xx A                                  7 24'J/N'
7.xx A                                  7 30'J/N'
7.xx A                                  7 36'J/N'
7.xx A                                  7 42'J/N'
7.xx A                                  7 48'J/N'
7.xx A                                  7 54'J/N'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  4  6'DG=Daglig,UK=Ukentlig,14=Hver 14.d-
     A                                      ag,MD=Månedlig'
     A                                      COLOR(WHT)
     A                                 10  3'Tidspunkt .'
     A            C3_TTT         2  0B 10 17VALUES(09 10 11 12 13 +
     A                                             14 15 16 17 18 19)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 21':'
     A            C3_TMM         2  0B 10 23VALUES(00 05 10 15 20 25 30 +
     A                                             35 40 45 50 55)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 27'(09.00 - 19.00)'
6.20 A          R C4WIN
6.20 A*%%TS  SD  20090908  120440  BOF610      REL-V5R4M0  5722-WDS
6.20 A                                      WINDOW(*DFT 15 56)
6.20 A                                      CA03
6.20 A                                      CA12
6.20 A                                      PUTOVR
6.20 A                                  2  3'Varslingsparametre'
6.20 A                                      DSPATR(RI)
6.20 A                                  5  3'Aktiv . . .'
6.20 A            C4RAPP    R        B  5 16REFFLD(MPRAPP)
6.20 A                                      COLOR(YLW)
6.20 A                                      VALUES(' ' '1')
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A                                  5 18'1=Ja'
6.20 A                                  7  3'Kjøres for ABC koder . .(AX til +
6.20 A                                       CZ) Ingen = Alle'
6.20 A            C4AB01    R        B  8  3REFFLD(MPAB01)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB02    R        B  8  7REFFLD(MPAB02)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB03    R        B  8 11REFFLD(MPAB03)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB04    R        B  8 15REFFLD(MPAB04)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB05    R        B  8 19REFFLD(MPAB05)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB06    R        B  8 23REFFLD(MPAB06)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB07    R        B  8 27REFFLD(MPAB07)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB08    R        B  8 31REFFLD(MPAB08)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A            C4AB09    R        B  8 35REFFLD(MPAB09)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A                                 14  2'F3=Gå tilbake'
6.20 A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040422  140317  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Parameter    '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør   . . .'
     A            D1LDOR    R        O  4 21REFFLD(MPLDOR)
     A                                  5  2'Parameternummer  .'
     A            D1LNUM    R        O  5 21REFFLD(MPLNUM)
     A                                  7  2'Beskrivelse  . . .'
     A            MPBESK    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
7.02 A                                      CF04
7.02 A                                      CA12
     A                                      PUTOVR
7.02 A                                      CSRLOC(W_AROW     W_ACOL)
7.02 A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
7.02 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
7.02 A            W_AROW         3S 0H
7.02 A            W_ACOL         3S 0H
7.02 A            W_ARCD        10A  H
7.02 A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør . . . .'
     A            K1LDOR    R        B  4 21REFFLD(MPLDOR)
     A  31                                  ERRMSGID(LMH0050 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.02 A                                  4 29'F4'
     A                                  5  2'Parameternummer  .'
     A            K1LNUM    R        B  5 21REFFLD(MPLNUM)
     A  32                                  ERRMSGID(LMH0090 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Leverandør . . . .'
     A            MPLDOR    R        O  9 21OVRDTA
     A                                 10  2'Parameternummer  .'
     A            MPLNUM    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.04 A          R H1WIN
7.04 A*%%TS  SD  20100324  091556  BOF560      REL-V5R4M0  5722-WDS
7.04 A                                      WINDOW(*DFT 14 50)
7.04 A                                      CA03
7.04 A                                      CF04
7.04 A                                      RTNCSRLOC(&WACTRE &WACTFE)
7.04 A                                      PUTOVR
7.04 A            WACTRE        10A  H
7.04 A            WACTFE        10A  H
7.04 A                                  2  2'Bla alternativer'
7.04 A                                      COLOR(WHT)
7.04 A                                      DSPATR(UL)
7.04 A                                  6  2'Velg Selger  . .:'
7.04 A                                  6 27'F4'
7.04 A            P1SELG    R        B  6 20REFFLD(MPSELG)
7.04 A                                      DLTCHK
7.04 A                                      OVRDTA
7.04 A                                      DSPATR(PC)
7.04 A                                      COLOR(YLW)
7.04 A                                  4  2'Velg leverandør :'
7.04 A                                  4 27'F4'
7.04 A            P1LDOR    R        B  4 20REFFLD(MPLDOR)
7.04 A                                      DLTCHK
7.04 A                                      OVRDTA
7.04 A                                      DSPATR(PC)
7.04 A                                      COLOR(YLW)
7.04 A                                  8  2'Velg Lager . . .:'
7.04 A            P1LAGE         2Y 0B  8 20COLOR(YLW)
7.04 A                                      RANGE(0 99)
7.04 A                                      EDTCDE(Z)
7.04 A                                      OVRDTA
7.04 A                                  8 27'F4'
