     A*%%TS  SD  20040207  225402  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH108D
     A* Beskrivelse . . : Kontoplan, flytting av konti
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
     A*
8.01 A* 8.xx  150222 bsa  Justert mtp cloud og forskyvning av kolonner
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A            B1ALFA    R        H      REFFLD(RHALFA)
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        O  8  5REFFLD(RHKONT)
     A                                      EDTCDE(Z)
     A            B1SPES    R        O  8 11REFFLD(RHSPES)
     A                                      EDTCDE(Z)
     A            B1AVDE    R        O  8 16REFFLD(RHAVDE)
     A                                      EDTCDE(Z)
     A            B1TEXT    R        O  8 21REFFLD(RHTEXT)
     A            B1SPRK    R        O  8 53REFFLD(RHSPRK)
     A            B1MVAK    R        O  8 60REFFLD(RHMVAK)
     A            B1MVAO    R        O  8 63REFFLD(RHMVAO)
     A            B1MVAG    R        O  8 66REFFLD(RHMVAG)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030106  161115  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Flytting av konti'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
8.01 A                                  4  5'Konto'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 11'Bærer'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 17'Avd'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 21'Tekst                      '
8.01 A                                      COLOR(WHT)
8.01 A            B2KONT    R        B  5  5REFFLD(RHKONT)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
8.01 A            B2SPES    R        B  5 11REFFLD(RHSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
8.01 A            B2AVDE    R        B  5 16REFFLD(RHAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
8.01 A            B2ALFA    R        B  5 21REFFLD(RHALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
8.01 A                                  6 11'7=Flytt'
     A                                      COLOR(BLU)
8.01 A                                  7  2'V'
8.01 A                                      COLOR(WHT)
8.01 A                                  7  5'Konto'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 11'Bærer'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 17'Avd'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 21'Tekst                      '
8.01 A                                      COLOR(WHT)
8.01 A                                  7 52'Sperr'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 60'MK'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 63'O'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 66'Koder'
     A                                      COLOR(WHT)
8.01 A*                                 6  2'V  Konto Bærer Avd Tekst          -
8.01 A*                                                    Sperr. <----- MVA ---
8.01 A*                                     --->'
8.01 A*                                 7 52'kode'
8.01 A*                                     COLOR(WHT)
8.01 A*                                 7 58'kode overstyring'
8.01 A*                                     COLOR(WHT)
8.01 A**                                7 51'kode   kode overstyring'
     A          R B2CMD
     A*%%TS  SD  20030106  161115  KOB         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040122  003802  KOB         REL-V5R2M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Flytting av konti'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontonummer. . . . . . . . .'
     A            C1KONT    R        O  5 31REFFLD(RHKONT)
     A                                      EDTCDE(Z)
     A            C1SPES    R        O  5 37REFFLD(RHSPES)
     A                                      EDTCDE(Z)
     A            C1AVDE    R        O  5 42REFFLD(RHAVDE)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2TEXT    R        O  7 31REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2TXT2    R        O  8 31REFFLD(RHTXT2)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  9  2'Alfabetisk sortering . . . :'
     A            C2ALFA    R        O  9 31REFFLD(RHALFA)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 10  2'MVA-kode . . . . . . . . . :'
     A            C2MVAK    R        O 10 31REFFLD(RHMVAK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2ETXT    R        O 10 33REFFLD(RAETXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 11  2'Overstyring. . . . . . . . :'
     A            C2MVAO    R        O 11 31REFFLD(RHMVAO)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2MVAG    R        O 11 33REFFLD(RHMVAG)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 12  2'Budsjett-nøkkel. . . . . . :'
     A            C2BNØK    R        O 12 31REFFLD(RHBNØK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2MTXT    R        O 12 34REFFLD(RAMTXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 13  2'Valutakode . . . . . . . . :'
     A            C2VALK    R        O 13 31REFFLD(RHVALK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2PMNA    R        O 13 35REFFLD(RAPMNA)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 14  2'Mengdekode . . . . . . . . :'
     A            C2MNGD    R        O 14 31REFFLD(RHMNGD)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2OTXT    R        O 14 34REFFLD(RAOTXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 15  2'Prosjektkode . . . . . . . :'
     A            C2PROJ    R        O 15 31REFFLD(RHPROJ)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 16  2'Kostnadsbærerkode. . . . . :'
     A            C2BÆRK    R        O 16 31REFFLD(RHBÆRK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 17  2'Avdelingskode. . . . . . . :'
     A            C2AVDK    R        O 17 31REFFLD(RHAVDK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 18  2'Nøkkeltall-kode. . . . . . :'
     A            C2NØKK    R        O 18 31REFFLD(RHNØKK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 19  2'Sperret for postering. . . :'
     A            C2SPRK    R        O 19 31REFFLD(RHSPRK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 21  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 21 31COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 22  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 22 31REFFLD(RÅEUSR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A*  Bilde M1 - vindu for flytting av konto
     A*
     A          R M1WIN
     A*%%TS  SD  20040207  225402  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 15 55)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2 18'Flytt konto'
     A                                      COLOR(WHT)
     A                                  4  2' Fra .... '
     A                                  4 22' Beskrivelse '
     A                                  5  2'Konto. . . . :'
     A            M1FKTO    R        O  5 17REFFLD(RHKONT)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            M1FTX1    R        B  5 23REFFLD(RHTEXT)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2'Kostnadsbærer:'
     A            M1FSPS    R        B  6 17REFFLD(RHSPES)
     A  41                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            M1FTX2    R        B  6 23REFFLD(RHTXT2)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Avdeling . . :'
     A            M1FAVD    R        B  7 17REFFLD(RHAVDE)
     A  42                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            M1FATX    R        B  7 23REFFLD(RAGTXT)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2' Til .... '
     A                                 10  2'Konto  . . . :'
     A            M1TKTO    R        B 10 17REFFLD(RHKONT)
     A  31                                  DSPATR(PC)
     A  31 32                               ERRMSGID(RMA0020 *LIBL/QMFOKON 32)
     A  31 33                               ERRMSGID(RMH0410 *LIBL/QMFOKON 33)
     A  31 34                               ERRMSGID(RMH0420 *LIBL/QMFOKON 34)
     A  31 35                               ERRMSGID(RMH0430 *LIBL/QMFOKON 35)
     A  31 37                               ERRMSGID(RMH0450 *LIBL/QMFOKON 37)
     A  31 38                               ERRMSGID(RMH0460 *LIBL/QMFOKON 38)
     A  31 35                               DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            M1TTX1    R        B 10 23REFFLD(RHTEXT)
     A                                      DSPATR(PR)
     A                                 11  2'Kostnadsbærer:'
     A            M1TSPS    R        B 11 17REFFLD(RHSPES)
     A  43                                  DSPATR(PC)
     A  31 35                               DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            M1TTX2    R        B 11 23REFFLD(RHTXT2)
     A                                      DSPATR(PR)
     A                                 12  2'Avdeling . . :'
     A            M1TAVD    R        B 12 17REFFLD(RHAVDE)
     A  31 35                               DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  36                                  ERRMSGID(RM01070 *LIBL/QMFOKON 35)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            M1TATX    R        B 12 23REFFLD(RAGTXT)
     A                                      DSPATR(PR)
     A                                 14  2'F3=Tilbake  F4=Spørring'
     A                                      COLOR(BLU)
