     A*%%TS  SD  20020312  200904  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx A*          260116 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20020312  200904  HKO         REL-V5R1M0  5722-WDS
     A                                      CF01
     A                                      CF03
     A                                      CF04
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise opplysninger'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO526B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Kundenummer. +'
     A                                      COLOR(BLU)
     A            B1KUND    R        B  4 17REFFLD(FOKUND)
     A  31                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A N41                                  DSPATR(PC)
     A                                      CHECK(ER)
     A            B2NAVN        30A  O  4 27
     A            B2GATE        30A  O  5 27
     A            B2ADR2        30A  O  6 27
     A            B2STED        30A  O  7 27
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  9  2'Velg oppgave . . .'
     A                                      COLOR(BLU)
     A            B1VALG    R        B  9 22REFFLD(FÅSVAR)
     A                                      COLOR(YLW)
     A  41                                  DSPATR(PC)
     A                                      VALUES(0 1 2)
     A                                  9 27'1=Vise ordre med bestilt kvantum-
     A                                       pr.ordre'
     A                                 10 27'2=Vise ordre med bestilt kvantum-
     A                                       pr.varenummer'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
