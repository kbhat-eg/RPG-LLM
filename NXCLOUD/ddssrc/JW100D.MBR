     A*%%TS  SD  20140213  183444  BOFBMP      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JW100D
     A* Beskrivelse . . : Pakninger, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       310511 bof  Utvidet med skrivebeskyttelse på felter
     A*       130214 bof  Utvidet med prisgiver i nøkkel
6.30 A*       260216 bsa  "Klemmer" samme feltene pga scrolling i jacada
6.32 A*       220415 bof  Ved oppretting og kopiering må man få overstyre prisgi
7.xx A*       060616 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140213  152812  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1PAKN    R        O  9  4REFFLD(JWPAKN)
7.xx A            B1ENHE    R        O  9  8REFFLD(JWENHE)
7.xx A            B1OFAK    R        O  9 12REFFLD(JWOFAK)
7.xx A            B1OFAP    R        O  9 28REFFLD(JWOFAP)
7.xx A*            B1SAEN         1Y 0B  9 43VALUES(0 1 2 3)
7.xx A*                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1SAEN    R        B  9 43REFFLD(JÅENRK)
7.xx A                                      COLOR(YLW)
7.xx A                                      VALUES(0 1 2 3)
7.xx A*            B1INEN         1Y 0B  9 47VALUES(0 1 2 3)
7.xx A*                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1INEN    R        B  9 47REFFLD(JÅENRK)
7.xx A                                      COLOR(YLW)
7.xx A                                      VALUES(0 1 2 3)
7.xx A*            B1PBEN         1Y 0B  9 51VALUES(0 1 2 3)
7.xx A*                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1PBEN    R        B  9 51REFFLD(JÅENRK)
7.xx A                                      COLOR(YLW)
7.xx A                                      VALUES(0 1 2 3)
7.xx A            B1PSEN    R        O  9 55REFFLD(JWPSEN)
7.xx A            B1PKLA    R        O  9 58REFFLD(JWPKLA)
7.xx A            B1BEST    R        O  9 61REFFLD(JWBEST)
7.xx A            B1GTIN    R        O  9 63REFFLD(JWGTIN)
7.xx A            B1RSTS         1A  O  9 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140213  152812  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  31                                  SFLMSGID(JMZ0110 *LIBL/QMFVADM 31)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av enhet'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JW100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr . . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2VARE    R   -7   O  4 31REFFLD(JWVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2TEK1    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5  2'Prisgiver  . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  5 31REFFLD(JWLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV    R        O  5 41REFFLD(JVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kod'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  8'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 12'   Omr.fakt    '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 28'  Omr.fakt  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 41'Salg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 46'Ink'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'PB'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 54'PSE'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 58'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 60'Bs'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'GTIN          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 78'R'
7.xx A**                                8  2'V Kod Enh    Omr.fakt       Omr.fa-
7.xx A**                                    kt   Salg Ink  PB   PSE P Best.  GT-
7.xx A**                                    IN       R   '
     A                                      COLOR(WHT)
7.xx A**                                9 15'volum          pris       enh. enh-
7.xx A**                                    . enh.     K bar                '
7.xx A**                                    COLOR(WHT)
     A**                                9 63'bar'
     A          R B2CMD
     A*%%TS  SD  20020819  142545  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20140213  150544  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av enhet'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  4 31REFFLD(JWVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prisgiver  . . . . . . . . .'
     A            C1LDOR    R        B  5 31REFFLD(JWLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C1LNAV    R        O  5 38REFFLD(JVTEK1)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Enhetskode . . . . . . . . .'
     A            C1PAKN    R        B  6 31REFFLD(JWPAKN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020820  115304  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Enhetskode finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140213  183444  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av enhet'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  4 31REFFLD(JWVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prisgiver  . . . . . . . . .'
     A            C1LDOR    R        O  5 31REFFLD(JWLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C1LNAV    R        O  5 38REFFLD(JVTEK1)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Enhetskode . . . . . . . . .'
     A            C1PAKN    R        O  6 31REFFLD(JWPAKN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Pakningsklasse . . . . . . .'
     A            C2PKLA    R        O  7 31REFFLD(JWPKLA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Enhet. . . . . . . . . . . +'
     A            C2ENHE    R        B  8 31REFFLD(JWENHE)
     A  31                                  ERRMSGID(JVA1020 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A  60
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ETXT        35A  O  8 36COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Omregningsfaktor volum . . .'
     A            C2OFAK    R        B 10 31REFFLD(JWOFAK)
     A  32                                  ERRMSGID(JMW0100 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  61
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 50'Palleinfo:'
     A                                 11  2'Omregningsfaktor pris. . . .'
     A            C2OFAP    R        B 11 31REFFLD(JWOFAP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  62
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 50'Ant. Pakker pr lag '
     A            C2PAPP    R        O 11 70REFFLD(JWPAPP)
     A                                      COLOR(YLW)
     A                                 12  2'Bredde (mm). . . . . . . . .'
     A            C2BRED    R        B 12 31REFFLD(JWBRED)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  63
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 50'Max. Stablevekt . .'
     A            C2PMSB    R        O 12 70REFFLD(JWPMSB)
     A                                      COLOR(YLW)
     A                                 13  2'Lengde (mm). . . . . . . . .'
     A            C2LENG    R        B 13 31REFFLD(JWLENG)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  64
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Høyde. (mm). . . . . . . . .'
     A            C2HOYD    R        B 14 31REFFLD(JWHOYD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  65
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 50'Lagerfort . . . . .'
     A            C2LFOR    R        O 14 70REFFLD(JWLFOR)
     A                                      COLOR(YLW)
     A                                 15  2'Vekt . (gram). . . . . . . .'
     A            C2VEKT    R        B 15 31REFFLD(JWVEKT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  66
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Volum. (m3). . . . . . . . .'
     A            C2VOLU    R        B 16 31REFFLD(JWVOLU)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  67
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 50'Fra dato  . . . . .'
     A            C2FDAT         6Y 0O 16 70COLOR(YLW)
     A                                 17 50'Til dato  . . . . .'
     A            C2TDAT         6Y 0O 17 70COLOR(YLW)
     A                                 18  2'Sammensatt av enhet. . . . .'
     A            C2PAKS    R        B 18 31REFFLD(JWPAKS)
     A                                      COLOR(YLW)
     A  68
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Antall i pakning . . . . . .'
     A            C2ANTP    R        B 19 31REFFLD(JWANTP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  69
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Overstyrt PSE. . . . . . . .'
     A            C2PSEN    R        B 20 31REFFLD(JWPSEN)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      COLOR(YLW)
     A  70
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20 34'1=Ja,0=Nei'
     A                                 22  2'GTIN . . . . . . . . . . . .'
     A            C2GTIN    R        B 22 31REFFLD(JWGTIN)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(JMV0090 *LIBL/QMFVADM 33)
     A  34                                  ERRMSGID(JMV0080 *LIBL/QMFVADM 34)
     A  71
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 57'/'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(JWEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20020820  115304  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette enhet '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer. . .'
     A            D1VARE    R        O  4 18REFFLD(JWVARE)
     A                                  5  2'Prisgiver . . .'
     A            D1LDOR    R        O  5 18REFFLD(JWLDOR)
     A                                  6  2'Enhetskode. . .'
     A            D1PAKN    R        O  6 18REFFLD(JWPAKN)
     A                                  7  2'Enhet . . . . .'
     A            JWENHE    R        O  7 18
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20140213  145845  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 16 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer. . .'
     A            K1VARE    R        O  4 18REFFLD(JWVARE)
     A                                      DSPATR(UL)
     A                                  5  2'Prisgiver . . .'
     A            K1LDOR    R        B  5 18REFFLD(JWLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMX0030 *LIBL/QMFVADM 32)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      DSPATR(UL)
     A                                  6  2'Enhetskode. . .'
     A            K1PAKN    R        B  6 18REFFLD(JWPAKN)
     A  31                                  ERRMSGID(JMW0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMW0030 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Varenummer. . .'
     A            JWVARE    R        O 10 18OVRDTA
     A                                 11  2'Prisgiver . . .'
     A            JWLDOR    R        O 11 18OVRDTA
     A                                 12  2'Enhetskode. . .'
     A            JWPAKN    R        O 12 18OVRDTA
     A                                 13  2'Enhet . . . . .'
     A            JWENHE    R        O 13 18OVRDTA
     A                                 15  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
