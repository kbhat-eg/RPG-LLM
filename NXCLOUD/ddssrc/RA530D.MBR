     A*%%TS  SD  20051030  111025  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA528D
     A* Beskrivelse . . : Prisgrupper   , spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       021008 bof  Nytt program
7.xx A* 040316 bsa Endringer relatert til ny GUI
7.01 A* 060122 bof Tåle brede skjermbilder
8.01 A*PRG2   060722  BOF  Prisgruppe skal være venstrestilt
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
7.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051030  111025  KOB         REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1DKOD    R        O  7  5REFFLD(REDKOD)
     A            B1DTXT    R        O  7  9REFFLD(REDTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051030  111025  KOB         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge Prisgruppe    '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Priskode'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 20'(kode)'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  5'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Kd'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  9'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2DKOD    R        B  3 17REFFLD(REDKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
8.01 A*                                     CHECK(RB)
     A                                      OVRDTA
7.xx A            B2DTXT    R        B  4 17REFFLD(REDTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
