     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EK600D
     A* Beskrivelse . . : Kunder, utskrift av bonusgrunnlag, parametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160911 BSA  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Bonus  '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av bonusgrunnlag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 11'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5 11'Bonusår .:'
     A            A2BRÅR    R        B  5 22REFFLD(RKPRÅR)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6 11'Utvalg:'
     A                                      COLOR(WHT)
     A                                  6 34'Fra - Til'
     A                                  7 13'Bonustype . . . . .:'
     A                                  7 45'Blank gir alle'
     A            A2BONU         2A  B  7 34
     A  32                                  ERRMSG('Bonustype finnes ikke')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 13'Kundekategori . . .:'
     A            A2PKAF    R        B  8 34REFFLD(RKPKAF)
     A  53                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMK0400 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 41'-'
     A            A2PKAT    R        B  8 43REFFLD(RKPKAT)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 13'Kundenr.. . . . . .:'
     A            A2PKNF    R        B  9 34REFFLD(RKPKNR)
     A  57                                  DSPATR(PC)
     A  40                                  ERRMSGID(RMH0140 *LIBL/QMFOKON 40)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 41'-'
     A            A2PKNT    R        B  9 43REFFLD(RKPKNR)
     A  58                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 11 11'Skriv :'
     A                                      COLOR(WHT)
     A                                 12 13'Summeringsliste . .:'
     A            A2ABON         1A  B 12 34
     A  33                                  ERRMSG('Gyldig verdier er 1')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 41'(1=Ja)'
6.10 A                                 13 13'Kundebrev . . . . .:'
6.10 A            A2KBRV         1A  B 13 34
6.10 A  41                                  ERRMSG('Gyldig verdier er 1')
6.10 A                                      CHECK(RB)
6.10 A                                      DSPATR(UL)
6.10 A                                      COLOR(YLW)
6.10 A                                 13 41'(1=Ja)'
      *
     A*                                17 11'Sortering:'
     A*                                     COLOR(WHT)
     A*                                18 13'Kategori. . . . . .:'
     A*           A2PKTG    R        B 18 34REFFLD(RKPKTG)
     A* 36                                  DSPATR(PC)
     A* 36                                  ERRMSGID(RMK0420 *LIBL/QMFOKON 36)
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*                                     VALUES('J' ' ')
     A*                                18 41'(J=Ja, Blank)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
