     A*%%TS  SD  20020730  130313  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : NXCLOUD
     A* Format. . . . . : RL120D
     A* Beskrivelse . . : Låste valutakurser pr. leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       040121 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A            B1UID         15Y 0H
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1VALK         3A  O  9  6
     A            B1VALT        20A  O  9 12
     A            B1VFDA         6Y 0O  9 34EDTWRD('  .  .  ')
     A            B1VTDA         6Y 0O  9 44EDTWRD('  .  .  ')
     A            B1KURS        10Y 6O  9 55EDTCDE(1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NXCLOUD'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Låste valutakurser på leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  3'Leverandør'
     A                                      COLOR(WHT)
     A            B2LDOR         6Y 0O  4 14
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            B2LNAV        30A  O  4 21
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  5  3'Valuta'
     A                                      COLOR(WHT)
     A            B2VAKO         3A  O  5 14
     A                                      COLOR(WHT)
     A                                  5 44'Gjeldende kurs'
     A                                      COLOR(WHT)
     A            B2KURS        10Y 6O  5 60EDTCDE(1)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            B2VATX        20A  O  5 21
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  5'Valuta'
     A                                      COLOR(WHT)
     A                                  8 12'Beskrivelse     '
     A                                      COLOR(WHT)
     A                                  8 34'Dato fra'
     A                                      COLOR(WHT)
     A                                  8 44'Dato til'
     A                                      COLOR(WHT)
     A                                  8 59'Kurs'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake  F5=Forny  F6=Oppret-
     A                                      t '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  130313  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NXCLOUD'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av låst valuta'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Valutakode   '
     A            C2VAKO         3A  O  5 21
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2VATX        20A  O  5 26
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Gjeldende fra dato '
     A            C2VFDA         6Y 0B  7 23EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'Gjeldende til dato '
     A            C2VTDA         6Y 0B  8 23EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10  2'Kurs  '
     A            C2VKUR        10Y 6B 10 19
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 19  2'Sist endret dato'
     A            C2EDAT         6Y 0O 19 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A            C2EUSR    R        O 20 21REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C2MSG1
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Både fra og tildato'
     A                                  3  2'må fylles ut!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG2
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Overlapper et annet intervall'
     A                                  3  2'Sjekk dato!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG4
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kurs må angis!         '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette valuta periode '
     A                                      DSPATR(RI)
     A                                  4  2'Valutakode'
     A            D1VAKO         3A  O  4 22
     A                                  5  2'Beskrivelse'
     A            D1VATX        20A  O  5 22
     A                                  7  2'Gjeldende fra dato '
     A            D1VFDA         6Y 0O  7 22EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  8  2'Gjeldende til dato '
     A            D1VTDA         6Y 0O  8 22EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Kurs '
     A            D1VKUR        10Y 6O 10 18
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
