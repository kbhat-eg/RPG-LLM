     A*%%TS  SD  20051108  235715  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK501D
     A* Beskrivelse . . : Kunder, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
PDF  A*       080610 BSA  Foreløpig fjernes mulighet for oppslag mot arkiv.
6.22 A*       280711 BSA  Tilbakefører oppslag mot akriv.
6.23 A*       270213 NHO  Fjernet 'Utskrift F6' tydligvis lagt inn ved
     A*                   ny F9 tast
7.xx A*       020516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20050723  162828  KOB540      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O  9  4REFFLD(RKKUND)
     A            B1NAVN    R        O  9 11REFFLD(RKNAVN)
     A            B1STED        24   O  9 42
     A            B1SALD    R        O  9 67REFFLD(RKSALD)
     A                                      EDTWRD('      .   ,  -')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051108  235715  KOB         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA09
     A                                      CF11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på kunderegister'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RK501B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk .'
     A            B2STEK        35A  B  4 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Kundenavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 41'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis  6=Utskrift  7=Generalnota  -
PDF  A**6.21                                8=Vis poster'
6.23 A                                       8=Vis poster  9=Vis ark-
6.23 A****                                   6=Utskrift  8=Vis poster 9=Vis ark-
6.21 A                                      iv'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Kundenavn                    '
     A                                      COLOR(WHT)
7.xx A  80                              8 42'Postnr/poststed            '
     A                                      COLOR(WHT)
7.xx A  81                              8 42'Gateadresse                '
     A                                      COLOR(WHT)
7.xx A                                  8 70'Saldo'
     A                                      COLOR(WHT)
7.xx A  82                              8 42'Telefon       Mobil '
     A                                      COLOR(WHT)
7.xx A  83                              8 42'Foretaksnr.'
     A                                      COLOR(WHT)
7.xx A            B2KUND    R        B  6  4REFFLD(RKKUND)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  6 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PONR    R        B  6 42REFFLD(RKPONR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20050723  155147  KOB540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 22  2'F5=Aktiv/Alle  F6=Generalnota  F7=-
     A                                      Utvalg  F8=Posteringer  F9=Gjenta s-
     A                                      øk'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Gå tilbake  F11=Endre liste'
     A                                      COLOR(BLU)
     A*
     A* Bilde V1 - Vindu
     A* for utvalg
     A*
     A          R V1WIN
     A*%%TS  SD  19950511  183051  KB          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 11 28)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Foreta utvalg '
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  5  2'Saldo > enn :'
     A            V1SALD         9Y 0B  5 17EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
