     A*%%TS  SD  20050629  090549  AMD530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA016D
     A* Beskrivelse . . : Printer-styring, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       270605 AMD  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050627  143612  AMD530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1WSID    R        O  8  5REFFLD(AWWSID)
     A            B1RUTI    R        O  8 18REFFLD(AWRUTI)
     A            B1TEXT    R        O  8 29REFFLD(AWTEXT)
     A            B1OUTQ    R        O  8 60REFFLD(AWOUTQ)
     A            B1FORM    R        O  8 71REFFLD(AWFORM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050629  090007  AMD530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printerstyring'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  4'Skjerm/bruker'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 18'Rutine'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 29'Tekst'
     A                                      COLOR(WHT)
7.xx A                                  7 60'Printer'
     A                                      COLOR(WHT)
7.xx A                                  7 71'Formular'
     A                                      COLOR(WHT)
7.xx A            B2WSID    R        B  6  5REFFLD(AWWSID)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2RUTI    R        B  6 18REFFLD(AWRUTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050627  131452  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printer-styring'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skjerm eller Bruker. . . . .'
     A            C1WSID    R        B  5 31REFFLD(AWWSID)
     A  31                                  ERRMSGID(AMW0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                  6  2'Rutine . . . . . . . . . . .'
     A            C1RUTI    R        B  6 31REFFLD(AWRUTI)
     A  31                                  ERRMSGID(AMW0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20050627  132228  AMD530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Skjerm/bruker finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050629  090549  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av printer-styring'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skjerm eller bruker. . . . .'
     A            C1WSID    R        O  5 31REFFLD(AWWSID)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Rutine . . . . . . . . . . .'
     A            C1RUTI    R        O  6 31REFFLD(AWRUTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Tekst  . . . . . . . . . . .'
     A            C2TEXT    R        B  8 31REFFLD(AWTEXT)
     A  31                                  ERRMSGID(AMW0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Printer  (Utkø). . . . . . .'
     A            C2OUTQ    R        B  9 31REFFLD(AWOUTQ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(AM00100 *LIBL/QMFAGPL 33)
     A                                      OVRDTA
     A                                 10  2'Formular (Papirtype) . . . .'
     A            C2FORM    R        B 10 31REFFLD(AWFORM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Antall . (Eksemplarer) . . .'
     A            C2AKOP    R        B 11 31REFFLD(AWAKOP)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 13  2'Skrive firmaopplysninger ? .'
     A            C2PRFI    R        B 13 31REFFLD(AWPRFI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 13 35'0=Nei 1=Firma 2=Avdeling'
     A                                 14  2'Inn- skuff . . . . . . . . .'
     A            C2SKIN    R        B 14 31REFFLD(AWSKIN)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 15  2'Ut - skuff . . . . . . . . .'
     A            C2SKUT    R        B 15 31REFFLD(AWSKUT)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 16  2'Duplex . . . . . . . . . . .'
     A            C2DPLX    R        B 16 31REFFLD(AWDPLX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17  2'Print - kvalitet . . . . . .'
     A            C2PKVA    R        B 17 31REFFLD(AWPKVA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A***                              18  2'Opprettet dato . . . . . . .'
     A***         C2ODAT         6Y 0O 18 31COLOR(YLW)
     A***                                   EDTWRD('  .  .  ')
     A***                                   OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(AWEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050627  144416  AMD530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette printer-styring '
     A                                      DSPATR(RI)
     A                                  4  2'Skjerm/bruker. . .'
     A            D1WSID    R        O  4 21REFFLD(AWWSID)
     A                                  5  2'Rutine . . . . . .'
     A            D1RUTI    R        O  5 21REFFLD(AWRUTI)
     A                                  7  2'Tekst  . . . . . .'
     A            AWTEXT    R        O  7 21REFFLD(AWTEXT)
     A                                  8  2'Printer  (Utkø). .'
     A            AWOUTQ    R        O  8 21REFFLD(AWOUTQ)
     A                                  9  2'Formular . . . . .'
     A            AWFORM    R        O  9 21REFFLD(AWFORM)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Skjerm/bruker. . .'
     A            K1WSID    R        B  4 21REFFLD(AWWSID)
     A  31                                  ERRMSGID(AMW0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMW0030 *LIBL/QMFAGPL 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Rutine . . . . . .'
     A            K1RUTI    R        B  5 21REFFLD(AWRUTI)
     A  31                                  ERRMSGID(AMW0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMW0030 *LIBL/QMFAGPL 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Skjerm/bruker. . .'
     A            AWWSID    R        O  8 21
     A                                  9  2'Rutine . . . . . .'
     A            AWRUTI    R        O  9 21
     A                                 10  2'Tekst  . . . . . .'
     A            AWTEXT    R        O 10 21
     A                                 11  2'Printer  (Utkø). .'
     A            AWOUTQ    R        O 11 21
     A                                 12  2'Formular . . . . .'
     A            AWFORM    R        O 12 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
