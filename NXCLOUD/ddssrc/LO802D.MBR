     A*%%TS  SD  20140930  123851  NHO630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO802D
     A* Beskrivelse . . : Varemottak, Linjebehandling
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240402 ASK  Nytt program
6.10 A* R6.10 171109 BSA  Utvidet EAN koden fra 13 til 14 posisjoner
6.20 A* R6.20 230511 bsa  Håndtere avvikende enheter ved innmelding
6.21 A* R6.20 300511 bsa  Lagt inn F tast for loggkoder
6.30 A* R6.30 050214 bsa  Varsler hvis det finnes erstatninsgsvarer
6.NU A* R6.30 030614 BOF  Utvidelse vedr. nummerutvidelse
6.30 A* R6.30 120515 bkv  Flyttet felter fordi LVAR økt til 20
6.36 A* R6.30 080517 bsa  Mulighet for å skrive ut kollietiketter
7.xx A* 7.xx  090316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20140603  145152  BOF630      REL-V7R1M0  5770-WDS
     A            B1LINE    R        H      REFFLD(LDLINE)
     A            B1VALG    R        B 10  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VARE    R   -7   O 10  4REFFLD(LDVARE)
     A            B1TEK1    R        O 10 13REFFLD(LDTEK1)
     A            B1ANTA         8Y 2O 10 49EDTWRD('    0 ,  ')
     A            B1ENHE    R        O 10 59REFFLD(LDENH1)
     A            B1ANTL         8Y 2O 10 63EDTWRD('    0 ,  ')
     A            B1RKOD         1Y 0O 10 74
     A            B1EKOD         1Y 0O 10 76
     A            B1STAT         1A  O 10 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140603  145152  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA08
     A                                      CF09
     A                                      CF10
     A                                      CA12
     A                                      CA13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Varemottak - linjebehandling'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LO802B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bestilling'
     A            B1SUFF    R        O  4 22REFFLD(LOSUFF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1OTYP    R        O  4 26REFFLD(LOOTYP)
     A                                      COLOR(YLW)
     A                                  4 45'Innkjøper'
     A            B1INTX        20A  O  4 55COLOR(YLW)
     A                                  5  2'Leverandør'
     A            B1LENA    R        O  5 13REFFLD(LOLENA)
     A                                      COLOR(YLW)
     A                                  5 45'Etikett'
     A            B1ETTX        20A  O  5 55COLOR(YLW)
     A                                  6  2'Lager     '
     A            B1LATX        20A  O  6 13COLOR(YLW)
     A                                  6 45'Restes '
     A            B1RETX         5A  O  6 55COLOR(YLW)
     A                                  8  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Varenr  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 13'Varetekst                         -
7.xx A                                       '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 49'Bestilt  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 59'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'Levert    '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 74'R'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 76'E'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 78'S'
7.xx A                                      COLOR(WHT)
     A            B1NUMM    R        O  4 13REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20140930  123851  NHO630      REL-V7R1M0  5770-WDS
     A                                 23  2'F3=GåTilb'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 23'F6=Scanne'
     A                                      COLOR(BLU)
     A                                 23 34'F8=Hurtigreg'
     A                                      COLOR(BLU)
     A                                 23 48'F9=VisAvvik'
     A                                      COLOR(BLU)
     A                                 23 61'F10=Logg'
     A                                      COLOR(BLU)
     A                                 23 71'F13=Status'
     A                                      COLOR(BLU)
     A          R B13WIN
     A*%%TS  SD  20040525  213007  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 10 35)
     A                                  2  2' Statuskoder '
     A                                      DSPATR(RI)
     A                                  4  2'1 = Ikke fullevert     '
     A                                      COLOR(GRN)
     A                                  5  2'2 = Overlevert          '
     A                                      COLOR(GRN)
     A                                  6  2'3 = Nytt GTIN-nr lagt til'
     A                                      COLOR(GRN)
     A                                  7  2'4 = Varelinjen lagt til'
     A                                      COLOR(GRN)
6.30 A                                  8  2'E = Erstatningsvare'
6.30 A                                      COLOR(GRN)
     A                                  9  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R C1WIN
     A*%%TS  SD  20140305  120157  MHA620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(8 20 13 40)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                  2  2' Registrere varemottak '
     A                                      DSPATR(RI)
7.xx A                                  5  2'Varenr . . . . . . +'
7.xx A            C1VARI    R   -1   B  5 24REFFLD(LDVARE)
     A  40                                  ERRMSGID(LM00240 *LIBL/QMFLAGR 40)
     A                                      CHECK(ER)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  41                              7  2'GTIN-nr. . . . . . .'
     A  41        C1EANN        14S 0O  7 25COLOR(YLW)
     A  41                              9  2'Lagre scannet GTIN-nr:'
     A  41        C1SVAR         1Y 0B  9 25DSPATR(UL)
     A  42                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 34)
     A                                      CHECK(RB)
     A  41                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  41                              9 28'1=Ja,0=Nei'
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20070403  115059  KOB550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(8 20 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CF09
     A                                      CA12
6.20 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
6.20 A            W_ARCD        10A  H
6.20 A            W_AFLD        10A  H
     A                                  2  2' Registrere varemottak '
     A                                      DSPATR(RI)
     A                                  2 30'Nobb'
     A            C2NOBB         8S 0O  2 35COLOR(YLW)
     A                                  3 30'Lvn'
     A            C2LVAR    R        O  3 35REFFLD(LDLVAR)
     A                                      COLOR(YLW)
     A                                  4  2'Vare'
     A            C2VARE    R        O  4  7REFFLD(LDVARE)
     A                                      COLOR(YLW)
     A                                  4 30'GTIN'
     A            C2EANN        14Y 0O  4 35COLOR(YLW)
     A                                      EDTCDE(Z)
     A            C2TEK1    R        O  6  2REFFLD(LDTEK1)
     A                                      COLOR(YLW)
     A            C2TEK2    R        O  7  2REFFLD(LDTEK2)
     A                                      COLOR(YLW)
     A                                  8  2'Enh1'
     A            C2ENH1    R        O  8  7REFFLD(LDENH1)
     A                                      COLOR(YLW)
     A                                  8 11'Pris e/mva'
     A            C2PRIE    R        O  8 22REFFLD(LDIPNY)
     A                                      EDTWRD('     0 ,  ')
     A                                      COLOR(YLW)
     A                                  8 33'Pris i/mva'
     A            C2PRII    R        O  8 44REFFLD(LDIPNY)
     A                                      EDTWRD('     0 ,  ')
     A                                      COLOR(YLW)
     A                                 10  2'Bestilt'
     A            C2BEST    R        O 10 10REFFLD(LDANTA)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A            C2ENHB    R        O 10 23REFFLD(LDENH1)
     A                                      COLOR(YLW)
     A                                 10 27'Etikett'
     A            C2EKOD         1Y 0B 10 35COLOR(YLW)
     A                                      DSPATR(UL)
     A            C2ETTX        20   O 10 37COLOR(YLW)
     A                                 11  2'Levert '
     A            C2LEVT    R        O 11 10REFFLD(LDANTA)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A            C2ENHL    R        O 11 23REFFLD(LDENH1)
     A                                      COLOR(YLW)
     A                                 11 27'Restes '
     A            C2RKOD         1Y 0B 11 35COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 37'1=Ja,0=Nei'
     A  43                             12  2'Rest   '
     A  43        C2REST    R        O 12 10REFFLD(LDANTA)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A  43        C2ENHR    R        O 12 23REFFLD(LDENH1)
     A                                      COLOR(YLW)
     A                                 12 27'Levert '
     A            C2LEVI    R        B 12 35REFFLD(LDANTA)
     A  45                                  ERRMSGID(LRD0090 *LIBL/QMFLAGR 45)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
7.xx A                                 12 49'+'
7.xx A            C2ENHI    R        B 12 51REFFLD(LDENH1)
6.30 A            C2INFO        20   O 13  2
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 17'F4=Spørring'
     A                                      COLOR(BLU)
     A  44                             14 30'F9=Neste linje'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20140305  121259  MHA620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      WINDOW(*DFT 14 56)
     A                                  2  2' Avslutte varemottak '
     A                                      DSPATR(RI)
     A                                  4  2'Neste ordretype'
     A            D1OTYP         2A  B  4 18COLOR(YLW)
     A  40                                  ERRMSGID(LSO0010 *LIBL/QMFLAGR 40)
     A  41                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 41)
     A  42                                  ERRMSGID(LM00310 *LIBL/QMFLAGR 42)
     A                                  6  2'Skrive ut inngående pakkseddel. . -
     A                                      . .:'
     A            D1PAKK         1Y 0B  6 41COLOR(YLW)
     A  43                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 43)
     A                                  6 44'1=Ja,0=Nei'
     A                                  7  2'Skrive ut vareankomstmelding til k-
     A                                      unde'
     A  45                                  DSPATR(ND)
     A            D1MELD         1Y 0B  7 41COLOR(YLW)
     A  45                                  DSPATR(ND)
     A  45                                  DSPATR(PR)
     A  44                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 44)
     A                                  7 44'0=Nei,1=Ja'
     A  45                                  DSPATR(ND)
     A                                  8  2'Alt. utkø'
     A                                  8 12'Pakkdeddel'
     A            D1PAKQ        10A  B  8 23COLOR(YLW)
     A  46                                  ERRMSGID(LM06110 *LIBL/QMFLAGR 46)
     A                                  9 34'Melding'
     A  45                                  DSPATR(ND)
     A            D1MELQ        10A  B  9 42COLOR(YLW)
     A  45                                  DSPATR(ND)
     A  47                                  ERRMSGID(LM06110 *LIBL/QMFLAGR 47)
     A                                 10  2'Send sms til kunde  :'
     A            D1SMSK         1Y 0B 10 24COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A  48                                  DSPATR(ND)
     A                                 10 27'1=Ja,0=Nei'
     A  48                                  DSPATR(ND)
     A                                 11  2'Send sms til selger :'
     A  48                                  DSPATR(ND)
     A            D1SMSS         1Y 0B 11 24COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A  48                                  DSPATR(ND)
     A                                 11 27'1=Ja,0=Nei'
     A  48                                  DSPATR(ND)
6.36 A                                 12  2'Skriv kollietiketter:'
6.36 A            D1KOLE         1Y 0B 12 24COLOR(YLW)
6.36 A                                      VALUES(0 1)
6.36 A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.36 A                                 12 27'1=Ja,0=Nei'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  20140113  130037  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(8 20 13 47)
     A                                      CA03
     A                                      CA13
     A                                      CA14
     A                                  2  2' Sende SMS v/varemottak '
     A                                      DSPATR(RI)
     A                                  4  1'Kunde:'
     A            KUNMOB        11A  B  4 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 24'Selger:'
     A            SLGMOB        11A  B  4 32DSPATR(UL)
     A                                      COLOR(YLW)
     A            E1TXT1        35   O  5 11DSPATR(UL)
     A N48                                  DSPATR(PC)
     A            E1TXT2        35   O  6 11DSPATR(UL)
     A                                  7  1'Fritekst:'
     A            E1TXT3        35A  B  7 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  48                                  DSPATR(PC)
     A                                  8  1'Fritekst:'
     A            E1TXT4        35A  B  8 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A* 48                                  DSPATR(PC)
     A                                  9  1'Fritekst:'
     A            E1TXT5        35A  B  9 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            E1TXT6        35   O 10 11DSPATR(UL)
     A            E1TXT7        35   O 11 11DSPATR(UL)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R D8WIN
     A*%%TS  SD  20030516  102310  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 8 32)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'Antall levert settes likt'
     A                                  4  2'antall bestilt på alle varer'
     A                                  5  2'Bekreft at dette skal gjøres'
     A                                  1  2'Hurtig innmelding'
     A                                      DSPATR(RI)
     A*
     A          R E8WIN
     A*%%TS  SD  20040427  204323  ASK         REL-V5R2M0  5722-WDS
7.xx A                                      WINDOW(11 2 8 32)
     A                                      CA03
     A                                      CA12
     A                                  1  2' Avslutte varemottak '
     A                                      DSPATR(RI)
7.xx A                                  1 25'LO802E8'
7.xx A                                      COLOR(WHT)
     A                                  4  2'Velg behandling'
     A            E8VALG         1Y 0B  4 18COLOR(YLW)
     A  40                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 40)
     A                                  4 20'0=Lagre'
     A                                  5 20'1=Utplukk'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
