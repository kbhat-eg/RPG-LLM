     A*%%TS  SD  20080522  113951  ASP_NOR     REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MM511D
     A* Beskrivelse . . : Innnkjøpsprameter detaljer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       020404 bø   Nytt program
7.xx A* 7.xx  220316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080522  113951  ASP_NOR     REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   H      REFFLD(MOVARE)
     A            B1LAGE    R        H      REFFLD(MOLAGE)
     A            B1KABC    R        H      REFFLD(MOKABC)
     A            B1TEK1    R        H      REFFLD(MOTEXT)
     A            B1ENHL    R        H      REFFLD(LÅENHE)
     A            B1TEXT        36A  O  8  4
     A            B1BMEN    R   -3   O  8 53REFFLD(MOBMEN)
     A                                      EDTCDE(M)
     A            B1VSOR    R        H      REFFLD(MOVSOR)
     A            B1OGRP    R        H      REFFLD(MOOGRP)
     A            B1HGRP    R        H      REFFLD(MOHGRP)
     A            B1UGRP    R        H      REFFLD(MOUGRP)
     A            B1MINI    R   -3   O  8 64REFFLD(MOMINI)
     A                                      EDTCDE(M)
     A            B1BPUN    R   -3   O  8 42REFFLD(MOBPUN)
     A                                      EDTCDE(M)
     A*  5.51 TSV RETTET EDITKODE B2CTL-BPUN TIL M.
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080522  113951  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innkjøpsparameter detaljer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'MM511B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 42'Best.pkt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 53'B.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 64'Min.lager'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'7=Pr. uke'
     A                                      COLOR(BLU)
7.xx A                                  6 14'8=Lagervedl.'
     A                                      COLOR(BLU)
7.xx A                                  6 29'9=Lagerspørr.'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 13'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'Tekst         '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 42'Best.pkt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'B.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 64'Min.lager'
7.xx A**                                7  2'V  Varenr. Lag ABC  Tekst         -
7.xx A**                                          Best.pkt.  B.mengde   Min.lag-
7.xx A**                                    er        '
     A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2VARE    R   -7   B  5  4REFFLD(MOVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A**                                7 13'F4'
7.xx A            B2LAGE    R        B  5 16REFFLD(MOLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KABC    R        B  5 19REFFLD(MOKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2MINI    R   -3   B  5 64REFFLD(MOMINI)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(M)
7.xx A            B2BMEN    R   -3   B  5 53REFFLD(MOBMEN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(M)
7.xx A            B2BPUN    R   -3   B  5 42REFFLD(MOBPUN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(M)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F1=Bla-Alt  F3=Gå tilbake  F5=Fo-
     A                                      rny  F9=Oppdater  F11=Skift'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050306  142304  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  2 40'MM511H1'
7.xx A                                      COLOR(WHT)
     A                                  4  2'Velg ABC-kode .:'
     A            H1KABC    R        B  4 19REFFLD(MOKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 26'Blank gir alle'
     A                                  5 26'Eks. AX,A , X'
     A                                  6  2'Velg lager  . .:'
     A            H1LAGE    R        B  6 19REFFLD(MOLAGE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8 18'Over'
     A                                  9  2'Best.punkt  . .:'
     A            H1BPOV    R        B  9 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9 21'(1/0)'
     A            H1BPUN    R        B  9 29REFFLD(MOBPUN)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10  2'Best.mengde . .:'
     A            H1BMOV    R        B 10 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 21'(1/0)'
     A            H1BMEN    R        B 10 29REFFLD(MOBMEN)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  2'Varer m/innkj.p:'
     A            H1BVMI    R        B 11 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11 21'(1/0)'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
