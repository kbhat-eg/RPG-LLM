     A*%%TS  SD  20181102  121745  NHO630      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : VG205D
     A* Beskrivelse . . : Vedlikehold parametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       101218 ask  Nytt program                                 øves
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/VGKKST)
     A                                      PRINT
     A                                      HOME(21)
     A          R C2BLD
     A*%%TS  SD  20020730  125617  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av parametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Priskode    '
     A                                      COLOR(WHT)
     A            C2KPRK    R        B  5 25REFFLD(VGKPRK)
     A                                      VALUES('I' 'K')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 28'I=Innkjøpspris, K=Kostpris'
     A                                  7  2'Oppdatering ved'
     A                                      COLOR(WHT)
     A            C2OPPD    R        B  7 25REFFLD(VGOPPD)
     A                                      VALUES('M' 'F')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 28'M=Varemottak, F=Inng.faktura'
     A                                  9  2'Kostpris pr.lager'
     A                                      COLOR(WHT)
     A            C2LAGR    R        B  9 25REFFLD(VGPRLG)
     A                                      VALUES('J' 'N')
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  9 28'J=Ja, N=Nei'
     A                                 11  2'Bokføring        '
     A                                      COLOR(WHT)
     A            C2KRGN    R        B 11 25REFFLD(VGKRGN)
     A                                      VALUES('J' 'N')
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11 28'J=Ja, N=Nei'
     A                                 12  2'Bilagskode      +'
     A                                      COLOR(WHT)
     A            C2KBIK    R        B 12 25REFFLD(VGKBIK)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KBIT        20   O 12 29
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Bilagsserie      '
     A                                      COLOR(WHT)
     A            C2KBIS    R        B 13 25REFFLD(VGKBIS)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14  2'Kostkonto ekst. salg +'
     A                                      COLOR(WHT)
     A            C2KTOK    R        B 14 25REFFLD(VGKKOS)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KKTX        30   O 14 34
     A                                      OVRDTA
     A                                 18  2'Sist endret tid '
     A                                      COLOR(WHT)
     A            C2ETIM         6Y 0O 18 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato '
     A                                      COLOR(WHT)
     A            C2EDAT         6Y 0O 19 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A                                      COLOR(WHT)
     A            C2EUSR    R        O 20 21REFFLD(VGKUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring  F8=Av-
     A                                      viksprosenter'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  004349  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A  41                              2  2'Priskode må velges'
     A  42                              2  2'Ikke gyldig bilagskode'
     A  43                              2  2'Posteringskode må velges'
     A  44                              2  2'Bilagsserie må velges'
     A  45                              2  2'Oppdateringskode må velges'
     A  46                              2  2'Kostpris pr.lager må velges'
     A  47                              2  2'Kostkonto salg må velges'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
