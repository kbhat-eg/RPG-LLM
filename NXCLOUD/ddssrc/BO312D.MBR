     A*%%TS  SD  20060901  132950  BOF550      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO312D
     A* Beskrivelse . . : Kasseoppgjør Innstillinger, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       010906 bø   Nytt program
7.xx A* 7.xx  260516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20060901  132950  BOF550      REL-V5R3M0  5722-WDS
     A            B1VALG         1S 0B  8  2COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A            B1KASS    R        O  8  5REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A            B1BSTY    R        O  8 14REFFLD(BOT2ST/B2BSTY *LIBL/BOT2ST)
     A            B1GSTY    R        O  8 24REFFLD(BOT2ST/B2GSTY *LIBL/BOT2ST)
     A            B1FSOK    R        O  8 46REFFLD(BOT2ST/B2FSOK *LIBL/BOT2ST)
     A            B1GRUP    R        O  8 30REFFLD(BOT2ST/B2GRUP *LIBL/BOT2ST)
     A          R B3CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020311  105820  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av innstillinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Br.styrt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'Gr.styrt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 31'Gruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 39'Fjernstyrt OK'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V  Kasse  Br.styrt  Gr.styrt Grupp-
7.xx A**                                    e  Fjernstyrt OK  '
7.xx A**                                    COLOR(WHT)
7.xx A            B3KASS    R        B  5  5REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B3CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020219  115427  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av innstillinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kasse  . . . . . . . . . . .'
     A            C1KASS    R        B  5 31REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A  31                                  ERRMSGID(BMK0010 *LIBL/QMFSHOP 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Innstilling  finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20060901  132950  BOF550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av innstillinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kasse  . . . . . . . . . . .'
     A            C1KASS    R        O  5 31REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Brukerstyrt  . . . . . . . .'
     A            C2BSTY    R        B  7 31REFFLD(BOT2ST/B2BSTY *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  8  2'Gruppestyrt  . . . . . . . .'
     A            C2GSTY    R        B  8 31REFFLD(BOT2ST/B2GSTY *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(BMT0020 *LIBL/QMFSHOP 31)
     A  30                                  DSPATR(PR)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  9  2'Gruppe   . . . . . . . . . .'
     A            C2GRUP    R        B  9 31REFFLD(BOT2ST/B2GRUP *LIBL/BOT2ST)
     A  32                                  ERRMSGID(BMT0010 *LIBL/QMFSHOP 32)
     A  33                                  ERRMSGID(BMT0030 *LIBL/QMFSHOP 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Fjernstyrt OK  . . . . . . .'
     A            C2FSOK    R        B 10 31REFFLD(BOT2ST/B2FSOK *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2OTIM         6Y 0O 18 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ETIM         6Y 0O 19 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(BOT2ST/B2EUSR *LIBL/BOT2ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette innstilling  '
     A                                      DSPATR(RI)
     A                                  4  2'Kasse  . . . . . .'
     A            D1KASS    R        O  4 21REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A                                  6  2'Brukerstyrt  . . .'
     A            D1BSTY    R        O  6 21REFFLD(BOT2ST/B2BSTY *LIBL/BOT2ST)
     A                                  7  2'Gruppestyrt  . . .'
     A            D1GSTY    R        O  7 21REFFLD(BOT2ST/B2GSTY *LIBL/BOT2ST)
     A                                  8  2'Gruppe . . . . . .'
     A            D1GRUP    R        O  8 21REFFLD(BOT2ST/B2GRUP *LIBL/BOT2ST)
     A                                  9  2'Fjernstyrt OK  . .'
     A            D1FSOK    R        O  9 21REFFLD(BOT2ST/B2FSOK *LIBL/BOT2ST)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kasse  . . . . . .'
     A            K1KASS    R        B  4 21REFFLD(BOT2ST/B2KASS *LIBL/BOT2ST)
     A  31                                  ERRMSGID(BMK0010 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BMK0030 *LIBL/QMFSHOP 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kasse  . . . . . .'
     A            K2KASS         3A  O  8 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
