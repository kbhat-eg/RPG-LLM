     A*%%TS  SD  20210524  151553  ASK700      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : VG230D
     A* Beskrivelse . . : Vedlikehold av avviksprosenter ved
     A*                   beregning av gj.kostpris
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       210521 ask  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/VGKAST)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20210524  134018  ASK700      REL-V7R3M0  5770-WDS
     A            B1VALG         1Y 0B  8  2EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5 9)
     A            B1AOGR    R        O  8  5REFFLD(VGAOGR)
     A                                      EDTCDE(Z)
     A            B1AHGR    R        O  8  9REFFLD(VGAHGR)
     A                                      EDTCDE(Z)
     A            B1AUGR    R        O  8 12REFFLD(VGAUGR)
     A                                      EDTCDE(Z)
     A            B1ALDO    R        O  8 17REFFLD(VGALDO)
     A                                      EDTCDE(Z)
     A            B1AMOD    R        O  8 25REFFLD(VGAMOD)
     A                                      EDTCDE(Z)
     A  50                                  DSPATR(ND)
     A            B1AART    R        O  8 35REFFLD(VGAART)
     A                                      EDTCDE(Z)
     A  51                                  DSPATR(ND)
     A            B1APRO    R        O  8 45REFFLD(VGAPRO)
     A                                      EDTWRD('0 , ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20210524  134018  ASK700      REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kostpris avviks pro-
     A                                      senter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  4  1'Ogr'
     A                                      DSPATR(ND)
     A            B2AOGR    R        B  4  5REFFLD(VGAOGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  4  9'Hgr'
     A                                      DSPATR(ND)
     A            B2AHGR    R        B  4 13REFFLD(VGAHGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  4 17'Ugr'
     A                                      DSPATR(ND)
     A            B2AUGR    R        B  4 21REFFLD(VGAUGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  4 26'Levr.'
     A                                      DSPATR(ND)
     A            B2ALDO    R        B  4 32REFFLD(VGALDO)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  4 41'Modul'
     A  50                                  DSPATR(ND)
     A            B2AMOD    R        B  4 47REFFLD(VGAMOD)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                  4 57'Varenr'
     A  51                                  DSPATR(ND)
     A            B2AART    R        B  4 64REFFLD(VGAART)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  51                                  DSPATR(PR)
     A  51                                  DSPATR(ND)
     A                                  6  1'2=Endre  4=Slett  5=Vis  9=Test'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  5'Og'
     A                                      COLOR(WHT)
     A                                  7  9'Hg'
     A                                      COLOR(WHT)
     A                                  7 12'Ugr'
     A                                      COLOR(WHT)
     A                                  7 17'Lever.'
     A                                      COLOR(WHT)
     A                                  7 25'Modul'
     A                                      COLOR(WHT)
     A  50                                  DSPATR(ND)
     A                                  7 35'Varenr'
     A                                      COLOR(WHT)
     A  51                                  DSPATR(ND)
     A                                  7 44'Prosent'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  004349  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A  41                              2  2'Overgr. er ugyldig'
     A  42                              2  2'Hovedgr. er ugyldig'
     A  43                              2  2'Undergr. er ugyldig'
     A  44                              2  2'Leverandørnr. finnes ikke'
     A  45                              2  2'Modulnr. finnes ikke'
     A  46                              2  2'Varenummer finnes ikke'
     A  47                              2  2'Prosentsats må fylles ut'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  125617  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prosentsatser '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe     +'
     A                                      COLOR(WHT)
     A            C2AOGR    R        B  5 21REFFLD(VGAOGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2OTXT        35   O  5 30COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe    +'
     A                                      COLOR(WHT)
     A            C2AHGR    R        B  6 21REFFLD(VGAHGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2HTXT        35   O  6 30COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Undergruppe    +'
     A                                      COLOR(WHT)
     A            C2AUGR    R        B  7 21REFFLD(VGAUGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2UTXT        35   O  7 30COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør     +'
     A                                      COLOR(WHT)
     A            C2ALDO    R        B  8 21REFFLD(VGALDO)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2LTXT        30   O  8 30COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modul          +'
     A  50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            C2AMOD    R        B  9 21REFFLD(VGAMOD)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A* 32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2MTXT        35   O  9 30COLOR(YLW)
     A                                      OVRDTA
     A  50                                  DSPATR(ND)
     A                                 10  2'Varenummer     +'
     A  51                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            C2AART    R        B 10 21REFFLD(VGAART)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  51                                  DSPATR(PR)
     A  51                                  DSPATR(ND)
     A* 32                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2VTXT        35   O 10 30COLOR(YLW)
     A                                      OVRDTA
     A  51                                  DSPATR(ND)
     A                                 12  2'Prosentsats'
     A                                      COLOR(WHT)
     A            C2APRO    R        B 12 21REFFLD(VGAPRO)
     A                                      EDTCDE(4)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato '
     A                                      COLOR(WHT)
     A            C2EDAT         6Y 0O 19 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A                                      COLOR(WHT)
     A            C2EUSR    R        O 20 21REFFLD(VGAUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20210524  151553  ASK700      REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 15 70)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slett prosentsats '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  5  2'Overgruppe      '
     A            D1AOGR    R        O  5 21REFFLD(VGAOGR)
     A                                      EDTCDE(Z)
     A            D1OTXT        35A  O  5 30
     A            D1AHGR    R        O  6 21REFFLD(VGAHGR)
     A                                  6  2'Hovedgruppe     '
     A            D1HTXT        35A  O  6 30
     A                                  7  2'Undergruppe     '
     A            D1AUGR    R        O  7 21REFFLD(VGAUGR)
     A            D1UTXT        35A  O  7 30
     A                                  8  2'Leverandør      '
     A            D1ALDO    R        O  8 21REFFLD(VGALDO)
     A            D1LTXT        30A  O  8 30
     A                                  9  2'Modul           '
     A  50                                  DSPATR(ND)
     A            D1AMOD    R        O  9 21REFFLD(VGAMOD)
     A                                      EDTCDE(Z)
     A  50                                  DSPATR(ND)
     A            D1MTXT        35A  O  9 30
     A  50                                  DSPATR(ND)
     A                                 10  2'Varenummer      '
     A  51                                  DSPATR(ND)
     A            D1AART    R        O 10 21REFFLD(VGAART)
     A                                      EDTCDE(Z)
     A  51                                  DSPATR(ND)
     A            D1VTXT        35A  O 10 30
     A  51                                  DSPATR(ND)
     A                                 12  2'Prosentsats'
     A            D1APRO    R        O 12 21REFFLD(VGAPRO)
     A                                      EDTCDE(4)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20020312  190907  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(04 55 14 45)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Test av prosent beregning'
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  4  2'Velg parameter'
     A                                      COLOR(WHT)
     A                                  6  2' Ogr      '
     A            B9AOGR    R        B  6 14REFFLD(VGAOGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2' Hgr      '
     A            B9AHGR    R        B  7 14REFFLD(VGAHGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2' Ugr      '
     A            B9AUGR    R        B  8 14REFFLD(VGAUGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2' Lev      '
     A            B9ALDO    R        B  9 14REFFLD(VGALDO)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2' Prosent  '
     A            B9APRO    R        O 11 14REFFLD(VGAPRO)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
