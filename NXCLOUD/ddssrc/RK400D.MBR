     A*%%TS  SD  20060807  125722  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK400D
     A* Beskrivelse . . : Kunder, sletting
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
7.xx A* 7.xx  081220 bsa  Tilpasset newlook
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20060807  125722  KOB         REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Sletting av kunder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  7  7'Utvalg:'
7.xx A                                  8  7'Eldre enn dato. . . . . . :'
7.xx A            A2EDAT         6Y 0B  8 35
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Eldre enn dato.')
     A                                      COLOR(YLW)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A*                                      EDTCDE(Z)
7.xx A                                  8 45'(dd.mm.åå)'
7.xx A                                  9  7'Kunder lagt inn etter dato:'
7.xx A                                  9 45'blir ikke slettet'
7.xx A            A2NDAT         6Y 0B  9 35
     A  33                                  DSPATR(PC)
     A  33                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 33)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Eldre enn dato.')
     A                                      COLOR(YLW)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A*                                      EDTCDE(Z)
7.xx A                                 10  7'Test kjøring. . . . . . . :'
7.xx A            A2TYPE         1A  B 10 35
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Type liste. . . . .')
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
7.xx A                                 10 37'J/N'
7.xx A**                               11 45'J=Ja - Skriver liste over kunder'
7.xx A                                 11 45'Ja - Skriver liste over kunder'
7.xx A                                 12 45'som vil bli slettet'
     A                                 13  3'Rutinen sletter kunder som har sis-
     A                                      te transaksjonsdato før angitt dato-
     A                                      ,'
     A                                      COLOR(RED)
     A                                 14  3'saldo = 0, memosaldo = 0 (verdien -
     A                                      av inneliggende ordre) og det ikke'
     A                                      COLOR(RED)
     A                                 15  3'finnes posteringer i kundereskontr-
     A                                      o.'
     A                                      COLOR(RED)
     A                                 16  3'Når du angir J i feltet for testkj-
     A                                      øring skriver systemet bare ut list-
     A                                      e'
     A                                      COLOR(RED)
     A                                 17  3'over kunder som vil bli slettet ve-
     A                                      d ordinær kjøring av rutinen.'
     A                                      COLOR(RED)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
