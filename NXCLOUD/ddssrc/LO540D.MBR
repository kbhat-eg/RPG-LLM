     A*%%TS  SD  20140530  123840  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
6.nu A* 300514 bof : Utvidet mtp. nummerutvidelse
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*---------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20051025  090112  GRO550      REL-V5R3M0  5722-WDS
     A*                                     WINDOW(*DFT 15 45)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A*                                     PUTOVR
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2'Varsel-liste til leverandør:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  2 38'LO540A1'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  2'Oppgi leverandør . . . +'
     A            A1LDOR    R        B  4 27REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A*                                     OVRDTA
     A                                      CHECK(RB)
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(LMO0210 *LIBL/QMFLAGR 31)
     A                                  4 35'Blank=alle'
     A                                  6  2'Kjøre-dato . . . . . . .'
     A            A1DATO         6Y 0B  6 27COLOR(YLW)
     A                                      CHECK(RB)
     A                                      DSPATR(PC)
     A  32                                  ERRMSGID(LMO0240 *LIBL/QMFLAGR 32)
     A                                  8  2'Antall dager i tillegg .'
     A            A1DAGR         3Y 0B  8 27COLOR(YLW)
7.xx A                                 10  2'Ønsket/bekreftet . . . .'
     A            A1ONBE         1Y 0B 10 27COLOR(YLW)
     A  33                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 33)
7.xx A                                 10 35'1=Ønsket dato,2=Bekreftet dato'
7.xx A                                 12  2'Lager  . . . . . . . . +'
     A            A1LAGE    R        B 12 27REFFLD(LOLAGE)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 32)
     A*                                      OVRDTA
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  123840  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  6  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1LDAT         6Y 0O  6  4EDTWRD('  .  .  ')
     A            B1MODA         6Y 0O  6 13EDTWRD('  .  .  ')
     A            B1BDAT         6Y 0O  6 22EDTWRD('  .  .  ')
     A            B1SUFF    R        O  6 40REFFLD(LISUFF)
     A            B1LEVR         8A  O  6 46
     A            B1LADR        12A  O  6 55
     A            B1SALG    R        O  6 68REFFLD(LOTOTI)
     A            B1OTYP    R        O  6 43REFFLD(LOOTYP)
     A            B1NUMM    R        O  6 31REFFLD(LINUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140530  123840  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på ikke-mottatte bestilli-
     A                                      nger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'2=Endre  5=Se på'
     A                                      COLOR(BLU)
7.xx A                                  5  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  4'Ønsk.lev'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'Bekr.lev'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 22'Best.dat'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 31'Best.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 45'Leverandør'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 56'Vareadresse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 70'Best.verdi'
7.xx A**                                5  2'V Ønsk.lev Bekr.Lev Best.dat Best.-
7.xx A**                                    nr     O Leverandør Vareadresse  Be-
7.xx A**                                    st.verdi'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051107  142056  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F1=Filter   F3=Gå tilbake   -
     A                                       F5=Forny   F6=Utskrift'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
