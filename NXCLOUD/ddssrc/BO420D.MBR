     A*%%TS  SD  19960925  154931  AMD         REL-V3R6M0  5716-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO420D
     A* Beskrivelse . . : Vedlikehold av budsjett-register
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HLPTITLE('Hjelpetekst for BO430D')
     A                                      HLPPNLGRP(HELE BO430H)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A          R B1SFL                     SFL
     A*%%TS  SD  19950221  132614  AMD         REL-V2R3M0  5738-PW1
     A            B1VALG    R        B 11  2REFFLD(BÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '3' '4' '5' ' ')
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A            B1AARR    R        O 11  5REFFLD(BUAARR)
     A            B1TEXT    R        O 11 11REFFLD(BUTEXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  19960925  154931  AMD         REL-V3R6M0  5716-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A          H                           HLPPNLGRP(B2HELE BO430H)
     A                                      HLPARA(01 001 24 080)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
7.xx A**                                1 31'***  AS butikk  ***'
7.xx A                                  1 46'Butikk'
     A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
7.xx A                                  1 72TIME
     A                                      COLOR(WHT)
7.xx A                                  2  2'Vedlikehold av budsjett-register  -
     A                                        '
     A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
7.xx A                                  2 54USER
     A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
     A                                      COLOR(WHT)
7.xx A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A**                                5  2'V'
7.xx A**                                    COLOR(WHT)
7.xx A                                  5  6'År'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  9  2'1=Opprette'
     A                                      COLOR(BLU)
7.xx A                                  9 14'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  9 23'3=Kopiere'
     A                                      COLOR(BLU)
7.xx A                                  9 35'4=Slette'
     A                                      COLOR(BLU)
7.xx A                                  9 46'5=Vise'
     A                                      COLOR(BLU)
7.xx A                                  9 55'Blank=Vise fra'
     A                                      COLOR(BLU)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  6'År'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 11'Budsjettekst'
7.xx A**                               10  6'År'  Tekst budsjett           -
7.xx A**                                                                       -
7.xx A**                                             '
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  6  2REFFLD(BÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('1' '2' '3' '4' '5' ' ')
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A                                      OVRDTA
7.xx A            B2AARR    R        B  6  5REFFLD(BUAARR)
     A                                      DLTCHK
     A  31                                  ERRMSGID(BMU0010 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BMU0020 *LIBL/QMFSHOP 32)
     A  33                                  ERRMSGID(BMU0030 *LIBL/QMFSHOP 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19950221  101050  AMD         REL-V2R3M0  5738-PW1
     A                                 23  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Forny'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19960925  154931  AMD         REL-V3R6M0  5716-PW1
     A                                      CA03
     A                                      CA12
     A                                      CLRL(*ALL)
     A          H                           HLPPNLGRP(C1HELE BO430H)
     A                                      HLPARA(01 001 23 080)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
     A                                  1 31'***  AS Butikk  ***'
     A                                      COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 10USER
     A                                      COLOR(WHT)
     A                                  2 25'Vedlikehold av budsjett-register  -
     A                                        '
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Budsjett-år  . . . . . . . .'
     A            C1AARR    R        B  5 31REFFLD(BUAARR)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Annuller'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  19960925  154931  AMD         REL-V3R6M0  5716-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A          H                           HLPPNLGRP(C2HELE BO430H)
     A                                      HLPARA(01 001 23 080)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
     A                                  1 31'***  AS Butikk  ***'
     A                                      COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 10USER
     A                                      COLOR(WHT)
     A                                  2 25'Vedlikehold av budsjett-register  -
     A                                        '
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Budsjett-år. . . . . . . . .'
     A            C1AARR    R        O  5 31REFFLD(BUAARR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2NYEN    R        O  5 69REFFLD(BÅNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Budsjett-tekst . . . . . . .'
     A            C2TEXT    R        B  7 31REFFLD(BUTEXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  9  2'Virkedager pr. periode'
     A                                      DSPATR(UL)
     A                                 11  5'01'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 11'02'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 17'03'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 23'04'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 29'05'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 35'06'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 41'07'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 47'08'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 53'09'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 59'10'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 65'11'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 11 71'12'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            C2VI01    R        B 12  5REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI02    R        B 12 11REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI03    R        B 12 17REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI04    R        B 12 23REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI05    R        B 12 29REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI06    R        B 12 35REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI07    R        B 12 41REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI08    R        B 12 47REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI09    R        B 12 53REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI10    R        B 12 59REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI11    R        B 12 65REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2VI12    R        B 12 71REFFLD(BUVDAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A                                 15  2'Budsjett pr. periode  '
     A                                      DSPATR(UL)
     A                                 17  4'Periode 01'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 17 17'Periode 02'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 17 30'Periode 03'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 17 43'Periode 04'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 17 56'Periode 05'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 17 69'Periode 06'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            C2KR01    R        B 18  2REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR02    R        B 18 15REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR03    R        B 18 28REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR04    R        B 18 41REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR05    R        B 18 54REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR06    R        B 18 67REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A                                 20  4'Periode 07'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20 17'Periode 08'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20 30'Periode 09'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20 43'Periode 10'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20 56'Periode 11'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20 69'Periode 12'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            C2KR07    R        B 21  2REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR08    R        B 21 15REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR09    R        B 21 28REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR10    R        B 21 41REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR11    R        B 21 54REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A            C2KR12    R        B 21 67REFFLD(BUKRON)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Annuller'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19950221  132614  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A          H                           HLPPNLGRP(D1HELE BO430H)
     A                                      HLPARA(*RCD)
     A                                  2  4' Slette post '
     A                                      DSPATR(RI)
     A                                  4  4'Budsjettår. . .'
     A            BUAARR    R        O  4 20REFFLD(BUAARR)
     A                                      DSPATR(UL)
     A                                      TEXT('Konto')
     A                                  6  4'Tekst . . . . .'
     A            BUTEXT    R        O  6 20REFFLD(BUTEXT)
     A                                      DSPATR(UL)
     A                                      TEXT('Tekst 1')
     A                                 14  4'F12=Annuller'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19950221  132614  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A                                      PUTOVR
     A          H                           HLPPNLGRP(K1HELE BO430H)
     A                                      HLPARA(*RCD)
     A                                  2  4' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  4'Budsjett-år . .'
     A            K1AARR    R        B  4 20REFFLD(BUAARR)
     A  31                                  ERRMSGID(BMU0030 *LIBL/QMFSHOP 31)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6  4'Tekst . . . . .'
     A            K1TEXT    R        B  6 20REFFLD(BUTEXT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 10  4' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 12  4'Budsjett-år . .'
     A            K0AARR    R        O 12 20REFFLD(BUAARR)
     A                                      DSPATR(UL)
     A                                 14  4'F12=Annuller'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19950221  101050  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 15 50)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
