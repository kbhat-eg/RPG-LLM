     A*%%TS  SD  20080427  235322  KOB560      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A*          010216 bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*K0000     030123 bsa  Endret ledetekst
     A*
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20051027  101834  AMD550      REL-V5R3M0  5722-WDS
     A            B1SUFF    R        H      REFFLD(SDSUFF)
     A            B1KTXT    R        H      REFFLD(SDKTXT)
     A            B1LINE    R        H      REFFLD(SDLINE)
7.xx A            B1SUMN        12A  H
7.xx A            B1SUMK        12A  H
7.xx A            B1SUMB        12A  H
     A            B1VALG    R        B 14  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
6.23 A                                      VALUES(5 8 9 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM    R        O 14  4REFFLD(SDNUMM)
7.01 A            B1LETY    R        O 14 13REFFLD(SDLETY)
7.01 A            B1TEXT        65A  O 14 16
7.xx A            B1VARE         8A  H
7.xx A            B1ANTA        12A  H
7.xx A            B1ENHE    R        H      REFFLD(SDENH1)
7.xx A            B1SAPR    R        H      REFFLD(SDSAPR)
7.xx A            B1RABA         5A  H
7.xx A            B1KPRI         3A  H
7.xx A            B1TEX2        75A  H
7.xx A            B1NETO        12A  H
7.xx A            B1KOST        12A  H
7.xx A            B1LBEH        12A  H
7.xx A            B1DEKG         6A  H
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080427  235322  KOB560      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
7.xx A*                                     ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF15
     A                                      CF20
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise faktura'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SO510B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A            B2KUND    R        O  4 11REFFLD(SOKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV    R   -7   O  4 18REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 46'Faktura. . .'
     A            B2BINR    R        O  4 59REFFLD(SOBINR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KGAT    R        O  5 11REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 46'Dato . . . .'
     A            B2FKDA         6Y 0O  5 59EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KSTE    R        O  6 11REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 46'Forfall. . .'
     A            B2FFDA         6Y 0O  6 59EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 46'Bet.bet. . .'
     A            B2CTXT        20A  O  7 59COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'K.prosj.'
     A            B2NAVN    R        O  7 11REFFLD(SONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 46'Forsendmåte.'
     A            B2LMTX    R        O  8 59REFFLD(SOLMTX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ADR1    R        O  8 11REFFLD(SOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 46'Selger . . .'
     A            B2SELG    R        O  9 59REFFLD(SOSELG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2SNAV        15A  O  9 64COLOR(YLW)
     A                                      OVRDTA
     A            B2STED    R        O  9 11REFFLD(SOSTED)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A**                               10 46'Nettbut.ref.'
8.01 A                                 10 46'Eksternreferanse'
     A            B2NREF    R        O 10 64REFFLD(SONREF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Kid . .'
     A            B2KIDD    R   21   O 10 11REFFLD(SOKIDD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KIDR    R        O 10 34REFFLD(SOKIDR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTWRD('0         ')
     A                                 11 46'Fakturasum .'
     A            B2FSUM    R        O 11 59REFFLD(SOFSUM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.23 A                                 12  2'5=Vis  8=Vis ordrehode'
     A                                      COLOR(BLU)
6.23 A                                 12 25'  9=Vis arkiv(Pakkseddel)'
     A                                      COLOR(BLU)
     A            B2FATY        20A  O 11 11COLOR(RED)
     A                                      OVRDTA
7.01 A                                 13  2'V'
7.01 A                                      COLOR(WHT)
7.01 A                                 13  4'Ordre'
7.01 A                                      COLOR(WHT)
7.01 A                                 13 13'LT'
7.01 A                                      COLOR(WHT)
7.01 A                                 13 16'Varenr.  Varetekst                -
7.01 A                                         Antall Enh Rab      Beløp'
7.01 A                                      COLOR(WHT)
7.01 A*                                13 13'LT Varenr. Varetekst              -
7.01 A*                                          Antall  Enh  Rab     Beløp'
7.01 A*                                     COLOR(WHT)
7.01 A*                                13  2'V Ordre    LT  Varenr.  Varetekst -
7.01 A*                                                      Antall  Enh  Rab  -
7.01 A*                                       Beløp'
7.01 A*                                     COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  20020313  084410  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F15=Generer ordre -
     A                                      i ordresystem   F20=Skriv fakturako-
     A                                      pi'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20010620  101928  AMD         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      WINDOW(*DFT 13 40)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Generere ordre fra valgt faktura.'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Dette valg gir mulighet for å skap-
     A                                      e'
     A                                  5  2'en ordre i ordresystemet, på bakgr-
     A                                      unn'
     A                                  6  2'av en faktura (eller kreditnota) f-
     A                                      ra'
     A                                  7  2'historisk faktura-register.'
     A                                  9  2'Oppgi ordretype'
     A                                 10  2'som skal benyttes :'
     A            H1OTYP    R        B 10 23REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(VMA5010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMA5040 *LIBL/QMFOFAK 33)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
