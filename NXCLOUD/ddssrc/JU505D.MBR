     A*%%TS  SD  20050919  105215  HKO530      REL-V5R3M0  5722-WDS
     A*
7.xx A*   010816 bsa Endringer relatert til ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050917  143715  HKO530      REL-V5R3M0  5722-WDS
     A            B1OGRP    R        H      REFFLD(JUOGRP)
     A            B1HGRP    R        H      REFFLD(JUHGRP)
     A            B1UGRP    R        H      REFFLD(JUUGRP)
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 6 7 8 9 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
7.xx A            B1MTE1    R        O  9  4REFFLD(JUMTE1)
7.xx A            B1MODN    R        O  9 40REFFLD(JUMODN)
7.xx A            B1LDOR    R        O  9 49REFFLD(JULDOR)
7.xx A            B1LNAV        17A  O  9 56
7.xx A            B1VGRP         7A  O  9 74
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050919  105215  HKO530      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLFOLD(CF07)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Modulinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk. . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  4 14COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Modultekst-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 40'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 49'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis  6=Varer  7=Stikkord  8=Frit-
     A                                      ekst/produktinfo.  9=Standarder'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Modultekst-1          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'Navn           '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 74'Vgrp. '
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V Modultekst-1                    -
7.xx A**                                        Modulnr  Lev.   Navn           -
7.xx A**                                       Vgrp.'
7.xx A**                                    COLOR(WHT)
7.xx A            B2MTE1    R        B  6  4REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2MODN    R        B  6 40REFFLD(JUMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  6 49REFFLD(JULDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050918  210923  HKO530      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F9=Gjenta søk'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
