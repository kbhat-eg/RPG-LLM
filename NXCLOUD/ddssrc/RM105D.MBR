     A*%%TS  SD  20040104  214500  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RMM10D
     A* Beskrivelse . . : Eiendommer, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160303 KOB  Nytt program
7.xx A*       120516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030316  201449  KOB         REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
7.xx A            B1EGNR    R        O  9  5REFFLD(RMEGNR)
7.xx A            B1EBNR    R        O  9 11REFFLD(RMEBNR)
     A                                      EDTCDE(Z)
7.xx A            B1ENAV    R        O  9 17REFFLD(RMENAV)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20031209  222621  KOB         REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendommer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            B2ALME    R        O  4  2REFFLD(RMALME)
     A            B2NAVN    R        O  4  7REFFLD(RMNAVN)
7.xx A                                  5  5'G.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'B.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 17'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'G.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'B.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'Navn                       '
     A                                      COLOR(WHT)
7.xx A            B2EGNR    R        B  6  5REFFLD(RMEGNR)
     A  22                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
7.xx A            B2EBNR    R        B  6 11REFFLD(RMEBNR)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
7.xx A            B2ENAV    R        B  6 17REFFLD(RMENAV)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030316  201137  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendommer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gårdsnummer/Bruksnummer. . .'
     A            C1EGNR    R        B  5 31REFFLD(RMEGNR)
     A  31                                  ERRMSGID(RMM0040 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A            C1EBNR    R        B  5 37REFFLD(RMEBNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20030316  163742  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'G.rn./B.nr. finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040104  214500  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendommer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gårdsnummer/Bruksnummer. . :'
     A            C1EGNR    R        O  5 31REFFLD(RMEGNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C1EBNR    R        O  5 37REFFLD(RMEBNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2'Navn . . . . . . . :'
     A            C2ENAV    R        B  6 23REFFLD(RMENAV)
     A  31                                  ERRMSGID(RMM0100 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Adresse 1. . . . . :'
     A            C2EAD1    R        B  7 23REFFLD(RMEAD1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Adresse 2. . . . . :'
     A            C2EAD2    R        B  8 23REFFLD(RMEAD2)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Poststed . . . . . :'
     A            C2EPNR    R        B  9 23REFFLD(RMEPNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2STED    R        O  9 28REFFLD(AFRFR/APSTED *LIBL/AFRF)
     A                                      OVRDTA
     A                                 10  2'Driftsform . . . . :'
     A            C2DRIF    R        B 10 23REFFLD(RMDRIF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Dyrket mark i drift:'
     A            C2EARE    R        B 11 23REFFLD(RMEARE)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A                                 12  2'Beite. . . . . . . :'
     A            C2EDYR    R        B 12 23REFFLD(RMEDYR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A                                 12 36'Karakter av jordbruk:'
     A            C2KARA    R        B 12 58REFFLD(RMKARA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 60'(J/N)'
     A                                 13  2'Skog . . . . . . . :'
     A            C2EUTM    R        B 13 23REFFLD(RMEUTM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A                                 13 36'Bortleid . . . . . .:'
     A            C2BORT    R        B 13 58REFFLD(RMBORT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 60'(J/N)'
     A                                 14  2'Stemmerett 1 . . . :'
     A            C2EST1    R        B 14 23REFFLD(RMEST1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A            C2NAV1    R        O 14 30REFFLD(RFRFR/RKNAVN *LIBL/RFRF)
     A                                      OVRDTA
     A                                 15  2'Stemmerett 2 . . . :'
     A            C2EST2    R        B 15 23REFFLD(RMEST2)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A            C2ENA2    R        B 15 30REFFLD(RMENA2)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Bebyggelse'
     A                                 17  2'Hovedbygninger . . :'
     A            C2HBYG    R        B 17 23REFFLD(RMHBYG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17 27'Driftsbygninger.:'
     A            C2DBYG    R        B 17 45REFFLD(RMDBYG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17 50'Tinglyst. .:'
     A            C2TING    R        B 17 63REFFLD(RMTING)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17 65'(J/N)'
     A                                 18  2'Livørebygninger. . :'
     A            C2LBYG    R        B 18 23REFFLD(RMLBYG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 27'Andre hus. . . .:'
     A            C2AHUS    R        B 18 45REFFLD(RMAHUS)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 50'Særeie. . .:'
     A            C2SAER    R        B 18 63REFFLD(RMSAER)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 65'(J/N)'
     A                                 20  2'Sist endret dato . :'
     A            C2EDAT         6Y 0O 20 23COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . :'
     A            C2EUSR    R        O 21 23REFFLD(RMEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030316  195402  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette eiendom '
     A                                      DSPATR(RI)
     A                                  4  2'Gårdsnummer/Bruksnummer'
     A            D1EGNR    R        O  4 26REFFLD(RMEGNR)
     A                                  6  2'Navn. . . . . . . . . .'
     A            RMENAV    R        O  6 26
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            D1EBNR    R        O  4 32REFFLD(RMEBNR)
     A                                      EDTCDE(Z)
     A          R K1WIN
     A*%%TS  SD  20040104  210114  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Gårdsnummer/Bruksnummer'
     A            K1EGNR    R        B  4 26REFFLD(RMEGNR)
     A  31                                  ERRMSGID(RMM0040 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMM0060 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1EBNR    R        B  4 32REFFLD(RMEBNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Gårdsnummer/Bruksnummer'
     A            RMEGNR    R        O  8 26OVRDTA
     A            RMEBNR    R        O  8 32OVRDTA
     A                                  9  2'Navn. . . . . . . . . .'
     A            RMENAV    R        O  9 26OVRDTA
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
