     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
7.00 A*      260819  bsa  Nytt program basert på 132 posisjoners bilde
7.01 A*      040220  bsa  Justert hvilket felt som vises.
     A*
     A*----------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A            B1MNUM    R        H      REFFLD(LONUMM)
     A            B1FSNI        18A  H
     A            B1FNIV         6S 0H
     A            B1AARR         4S 0H
     A            B1LINR         6S 0H
     A            B1PVER         3S 0H
     A            B1VALG         2Y 0B  7  2
     A                                      VALUES(0 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            B1NOBB         8Y 0O  7  5
     A            B1NAVN        35A  O  7 14
     A            B1SSCC        18Y 0O  7 50
     A            B1PSDT         6Y 0O  7 70EDTWRD('  .  .  ')
7.09 A            B1BNUM         8Y 0O  7 79EDTCDE(Z)
     A            B1BLIN         6Y 0O  7 89EDTCDE(Z)
     A            B1BANT        10  3O  7 97EDTCDE(M)
     A            B1BENH         3A  O  7110
     A            B1PANT        10  3O  7114EDTCDE(M)
     A            B1PENH         3A  O  7127
     A            B1STAT         2A  O  7131
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  33                                  COLOR(GRN)
     A  34                                  COLOR(WHT)
      *
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A*                                     CA01
     A                                      CA03
     A                                      CF05
     A N79                                  CA10
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW     W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av pakkseddel - -
     A                                       linjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Pakkseddel ID:'
     A            B2PAID        17A  O  4 17
     A                                      COLOR(YLW)
     A                                  4 45'Leverandør:'
     A            B2LNAV        30A  O  4 58
     A                                      COLOR(YLW)
      *
     A                                  5  2'5=Se på'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  5'NOBB nr'
     A                                      COLOR(WHT)
     A                                  6 13'Beskrivelse'
     A                                      COLOR(WHT)
     A                                  6 50'SSCC kode'
     A                                      COLOR(WHT)
     A                                  6 70'Lev.dato'
     A                                      COLOR(WHT)
     A                                  6 80'Bestnr.'
     A                                      COLOR(WHT)
     A                                  6 90'Linr.'
     A                                      COLOR(WHT)
     A                                  6 98'Best. ant'
     A                                      COLOR(WHT)
     A                                  6110'Enh'
     A                                      COLOR(WHT)
     A                                  6115'Pakk. ant.'
     A                                      COLOR(WHT)
     A                                  6127'Enh'
     A                                      COLOR(WHT)
     A                                  6131'St'
     A                                      COLOR(WHT)
      *
     A          R B2CMD
     A*%%TS  SD  20041125  110702  GRO530      REL-V5R2M0  5722-WDS
     A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 26 17'F5=Forny'
     A                                      COLOR(BLU)
     A N79                             26 27'F10=Bestilling'
     A                                      COLOR(BLU)
     A*                                26 96'F13=Statuskoder'
     A*                                     COLOR(BLU)
     A          R C5BLD
     A*%%TS  SD  20040217  123227  BSA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av pakkseddel - -
     A                                       linjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Pakkseddel . . . . . .'
     A            C1PAID        17A  O  5 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PVER         3Y 0O  5 45EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 50'SSCC kode. . . . . . .'
     A            C1SSCC        18Y 0O  5 73EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 95'Linjenummer. . . . . .'
     A            C1LINR         6Y 0O  5118EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Status . . . . . . . .'
     A            C1STAT         2A  O  7 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SBES        30A  O  7 28
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'GTIN nummer. . . . . .'
     A            C1GTIN        14A  O  8 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'NOBB nummer. . . . . .'
     A            C1NOBB         8Y 0O  9 25
     A                                      COLOR(YLW)
     A                                 10  2'Leverandørens varenr .'
     A            C1LVAR        34A  O 10 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Artikkelnavn . . . . .'
     A            C1NAVN        70A  O 11 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Bestillingsnummer. . .'
     A            C1NUMM         8Y 0O 13 25EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SUFF         2Y 0O 13 35
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 38'Linjenr'
     A            C1BLIN         6Y 0O 13 47EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 70'Ordrenummer. . . . . .'
     A            C1ONUM         8S 0O 13 93COLOR(YLW)
     A                                      OVRDTA
     A            C1OSUF         2S 0O 13103COLOR(YLW)
     A                                      OVRDTA
     A                                 13107'Linjenr'
     A            C1OLIN         6Y 0O 13119EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Artikkelnavn . . . . .'
     A            C1BBES        70A  O 14 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Bestilt antall . . . .'
     A            C1BANT        11Y 3O 15 25COLOR(YLW)
     A                                      EDTWRD('      0 ,   ')
     A                                      OVRDTA
     A            C1BENH         3A  O 15 40COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Pakkseddel antall. . .'
     A            C1PANT        11Y 3O 16 25COLOR(YLW)
     A                                      EDTWRD('      0 ,   ')
     A                                      OVRDTA
     A            C1PENH         3A  O 16 40COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Ordreantall. . . . . .'
     A            C1OANT        11Y 3O 17 25COLOR(YLW)
     A                                      EDTWRD('      0 ,   ')
     A                                      OVRDTA
     A            C1OENH         3A  O 17 40COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Varemottatt antall . .'
     A            C1VANT        11Y 3O 18 25COLOR(YLW)
     A                                      EDTWRD('      0 ,   ')
     A                                      OVRDTA
     A            C1VENH         3A  O 18 40COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Restet antall. . . . .'
     A            C1RANT        11Y 3O 19 25COLOR(YLW)
     A                                      EDTWRD('      0 ,   ')
     A                                      OVRDTA
     A            C1RENH         3A  O 19 40COLOR(YLW)
     A                                      OVRDTA
     A                                 19 45'Restet dato. . . . . .'
     A            C1RDAT         6Y 0O 19 70COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20050523  130713  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(2 10 9 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Velg utskriftstype :'
     A                                      DSPATR(RI)
     A                                  5  2'Skriv dokumentkopi . .'
     A            B9DKOP    R        B  5 25REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                  5 28'1/0'
     A                                  6  2'Skriv avviksliste. . .'
     A            B9AVVK    R        B  6 25REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                  6 28'1/0'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN3
     A*%%TS  SD  20081127  125855  ASP_MSX     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 13 45)
     A                                  2  2' Feil i faktura innlesning!!!!!'
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A                                  5  2'En eller flere fakturaer feilet'
     A                                  6  2'ved innlesing. Gå til loggen for'
     A                                  7  2'å finne ut mer (F8)'
     A                                 10  2'**** **** **** FEIL !!! **** **** -
     A                                       ****'
     A                                      COLOR(RED)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B13WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Statuskoder '
     A                                      DSPATR(RI)
     A                                  3  2'E = Feil ved innlesing.'
     A                                      COLOR(RED)
     A                                  4  2'1 = Feil i bestillingsnummer.'
     A                                      COLOR(RED)
     A                                  5  2'2 = Sjekk leverandør'
     A                                      COLOR(RED)
     A                                  6  2'3 = Avvik fra bestilling'
     A                                      COLOR(RED)
     A                                  7  2'4 = Feil ordretype'
     A                                      COLOR(RED)
     A                                  8  2'5 = Avvik totalsum'
     A                                      COLOR(RED)
     A                                 10  2'7 = Slettemarkert'
     A                                      COLOR(BLU)
     A                                 11  2'8 = Manuelt godkjent'
     A                                      COLOR(YLW)
     A                                 12  2'9 = Oppdatert'
     A                                      COLOR(WHT)
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050915  092234  GRO540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 72)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Leverandørnr. +'
     A            H1SLDO         6Y 0B  4 18CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            H1LNAV        30A  O  4 26OVRDTA
     A                                  5  2'Leverandørnavn.'
     A            H1SNAV        20A  B  5 18COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Meldingsdato. .'
     A            H1SMDA         6Y 0B  6 18COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'Fakturanummer .'
     A            H1SFAK        20A  B  7 18COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Bestillingsnr..'
     A            H1SBES        20A  B  8 18COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Selger/innkj. .'
     A            H1INNK    R        B  9 18REFFLD(LOINNK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            H1INAV        20A  O  9 26OVRDTA
      *
     A                                 10  2'Lager . . . . +'
     A            H1LAGE    R        B 10 18REFFLD(LOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 23'+'
     A            H2LAGE    R        B 10 25REFFLD(LOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 30'+'
     A            H3LAGE    R        B 10 32REFFLD(LOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 37'+'
     A            H4LAGE    R        B 10 39REFFLD(LOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 44'+'
     A            H5LAGE    R        B 10 46REFFLD(LOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 11  2'Varemotatt. . .'
     A            H1VMOT         1A  B 11 18
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  33                                  ERRMSG('Kun blank, 1,2 og 3 er gy+
     A                                              ldige valg')
     A                                 11 21'0=Ikke valgt,1=Mottatt,2=Ikke mott+
     A                                      att, 3=Uten best.'
      *
     A                                 13  2'Direktelev. . .'
     A            H1DIRK         1A  B 13 18
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  32                                  ERRMSG('Kun blank og 1 er gyldig +
     A                                              valg')
     A                                 13 21'1=Kun'
     A                                 14  2'Fakt/Kreditnota'
     A            H1FAKR         1A  B 14 18
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  33                                  ERRMSG('Kun blank og F/K er gyldi+
     A                                              ge valg')
     A                                 14 21'0=Ikke valgt, F=Faktura, K=Kreditn-
     A                                      ota'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 18'F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 27132' '
