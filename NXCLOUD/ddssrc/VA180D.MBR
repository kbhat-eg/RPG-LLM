     A*%%TS  SD  20070403  084904  AMD550      REL-V5R4M0  5722-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A* R6.31 040314 BSA  Endringer pga forenklinger i jacada
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A* ----- ------ ---- -------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070403  084904  AMD550      REL-V5R4M0  5722-WDS
6.30 A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.30 A            B1ITYP    R        O  9  5REFFLD(VAITYP)
6.30 A            B1ITXT    R        O  9  9REFFLD(VAITXT)
6.30 A            B1IUSR    R        O  9 47REFFLD(VAIUSR)
6.30 A            B1IFAK    R        O  9 52REFFLD(VAIFAK)
6.30 A            B1ISEL    R        O  9 56REFFLD(VAISEL)
6.30 A            B1IFRG    R        O  9 62REFFLD(VAIFRG)
6.30 A            B1ISTI    R        O  9 70REFFLD(VAISTI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070403  084904  AMD550      REL-V5R4M0  5722-WDS
6.30 A                                      SFLSIZ(0014)
6.30 A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordreinformasjonsko-
     A                                      der'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7 46'Brk.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 51'St.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 56'Opf.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'Farge'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 69'Opf.'
6.30 A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Beskrivelse          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 46'kntr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'fakt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'selg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 69'tilb'
7.xx A                                      COLOR(WHT)
7.xx A            B2ITYP    R        B  5  5REFFLD(VAITYP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2ITXT    R        B  5  9REFFLD(VAITXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.30 A*                                 7 45'kntr fakt selg         tilb'
6.30 A*                                     COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020226  084800  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20060201  090121  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordreinformasjonsko-
     A                                      de'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Informasjonskode . . . . . .'
     A            C1ITYP    R        B  5 31REFFLD(VAITYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20060201  090121  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Info.kode finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070403  084904  AMD550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordreinformasjonsko-
     A                                      de'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Informasjonskode . . . . . .'
     A            C1ITYP    R        O  5 31REFFLD(VAITYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Beskrivelse. . . . . . . . .'
     A            C2ITXT    R        B  6 31REFFLD(VAITXT)
     A  31                                  ERRMSGID(VMA8100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Bruker-kontroll. . . . . . .'
     A            C2IUSR    R        B  8 31REFFLD(VAIUSR)
     A                                      VALUES(' ' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 34'1=Ja,Blank=Nei'
     A                                  9 34'Dersom valgt kontrolleres det mot -
     A                                      bruker om'
     A                                 10 34'kode kan oppheves'
     A                                 11  2'Stoppe fakturering . . . . .'
     A            C2IFAK    R        B 11 31REFFLD(VAIFAK)
     A                                      VALUES(' ' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 34'1=Ja,Blank=Nei'
     A                                 12 34'Dersom valgt og infokode er lagt i-
     A                                      nn på ordren,'
     A                                 13 34'blir ordren ikke tatt med til fakt-
     A                                      urering'
     A                                 14  2'Følges opp av selger . . . .'
     A            C2ISEL    R        B 14 31REFFLD(VAISEL)
     A                                      VALUES(' ' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 34'1=Ja,Blank=Nei'
     A                                 16  2'Overstyr skjermfarge . . . .'
     A            C2IFRG    R        B 16 31REFFLD(VAIFRG)
     A                                      VALUES(' ' 'H' 'G' 'B' 'R')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.30 A                                 16 34'H=Hvit,'
     A                                      COLOR(WHT)
6.30 A                                 16 42'G=Gul,'
     A                                      COLOR(YLW)
6.30 A                                 16 49'B=Blå,'
     A                                      COLOR(BLU)
6.30 A                                 16 56'R=Rød'
     A                                      COLOR(RED)
     A                                 18  2'Følges opp som tilbud  . . .'
     A            C2ISTI    R        B 18 31REFFLD(VAISTI)
     A                                      VALUES(' ' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 34'1=Ja,Blank=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C2IEDA         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2IEUS    R        O 23 70REFFLD(VAIEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20070403  084904  AMD550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 13 56)
     A                                  2  2' Slette informasjonskode '
     A                                      DSPATR(RI)
     A                                  4  2'Informasjonskode .'
     A            D1ITYP    R        O  4 21REFFLD(VAITYP)
     A                                  5  2'Beskrivelse. . . .'
     A            VAITXT    R        O  5 21
     A                                  7  2'Bruker-kontroll. .'
     A            VAIUSR    R        O  7 21
     A                                  8  2'Stoppe fakturering'
     A            VAIFAK    R        O  8 21
     A                                  9  2'Skjermfarge. . . .'
     A            VAIFRG    R        O  9 21
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20060201  092220  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Informasjonskode .'
     A            K1ITYP    R        B  4 21REFFLD(VAITYP)
     A  31                                  ERRMSGID(VMA8010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA8030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Informasjonskode .'
     A            VAITYP    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            VAITXT    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
