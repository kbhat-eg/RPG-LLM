     A*%%TS  SD  20140314  135643  BOFBMP      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO565D
     A* Beskrivelse . . : Vise depositum pr kunde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       040419 bsa  Skrevet om til cloud
7.xx A*       040419 bsa  Mulighet for bredere skjerm
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.xx A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140314  135328  BOFBMP      REL-V7R1M0  5770-WDS
     A            B1ALFA        18A  H
7.xx A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1NUMM    R        O  8  4REFFLD(SKNUMM)
7.xx A            B1DEPO    R        O  8 19REFFLD(SKDEPO)
7.xx A            B1DEPB    R        O  8 37REFFLD(SKDEPB)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140314  135643  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0012)
7.xx A                                      SFLPAG(0011)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Depositum på kunde'
     A                                      DSPATR(RI)
     A                                  4  2'Kunde  . .'
7.xx A*                                     COLOR(BLU)
     A            B2KUND    R        O  4 13REFFLD(FOKUND)
7.xx A*                                     COLOR(BLU)
     A            B2NAVN    R   -5   O  4 20REFFLD(FONAVN)
7.xx A*                                     COLOR(BLU)
     A                                  6 11'5=Vis ordre'
     A                                      COLOR(BLU)
     A                                  6  2'2=Velg'
     A                                      COLOR(BLU)
      *
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Ordrenummer'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 18'Depositumskrav'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 39'Innbetalt'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20140130  075615  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 21 70)
     A                                 20  2'F3=Gå tilbake                     -
     A                                          '
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 12 60)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Forskuddsbetaling'
     A                                      DSPATR(RI)
     A                                  4  2'Foreløpige ordretotaler . . .'
     A            S1TOTA    R        O  4 32REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Ikke oppdater ved innbetaling'
     A            S1IUPD    R        B  5 32REFFLD(SKIUPD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 34'1=Ja'
     A                                  6  2'Forhåndsbetaling 100 %    . .'
     A            S1100P    R        O  6 32REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Delbetaling . . . . . . . . .'
     A            S1DEPP    R        O  7 32REFFLD(SKDEPP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Depositum   innkl. mva. . . .'
     A            S1DEPO    R        O  8 32REFFLD(SKDEPO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Innbetalt . . . . . . . . . .'
     A            S1DEPB    R        B  9 32REFFLD(SKDEPB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                 10  2'Depositum til gode  . . . . .'
     A            S1TRUK    R        O 10 32REFFLD(SKDEPB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
