     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK630D
     A* Beskrivelse . . : Kunder, utskrift av saldoliste - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240702 KOB  Nytt program
6.10 A*       070711 NHO  Indikator for PC på kundenr ved F4
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av saldoliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 11'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5 11'Dato .:'
     A            A2PKDA    R        B  5 19REFFLD(RKPKDA)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A  37                                  ERRMSGID(RM02130 *LIBL/QMFOKON 37)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  5 27'Regnskapsår .:'
     A            A2PRÅR    R        B  5 42REFFLD(RKPRÅR)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  5 48'Periode .:'
     A            A2PPER    R        B  5 59REFFLD(RKPPER)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6 11'Utvalg:'
     A                                      COLOR(WHT)
     A                                  6 35'Fra - Til'
     A                                  7 13'Avdeling. . . . . .:'
     A            A2PAVF    R        B  7 34REFFLD(RKPAVF)
     A  51                                  DSPATR(PC)
     A  33                                  ERRMSGID(RMH0130 *LIBL/QMFOKON 33)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  7 39'-'
     A            A2PAVT    R        B  7 42REFFLD(RKPAVT)
     A  52                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 13'Kundekategori . . .:'
     A            A2PKAF    R        B  8 34REFFLD(RKPKAF)
     A  53                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMK0400 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 39'-'
     A            A2PKAT    R        B  8 42REFFLD(RKPKAT)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 13'Selger. . . . . . .:'
     A            A2PSEF    R        B  9 34REFFLD(RKPSEF)
     A  55                                  DSPATR(PC)
     A  35                                  ERRMSGID(RMK0410 *LIBL/QMFOKON 35)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 39'-'
     A            A2PSET    R        B  9 42REFFLD(RKPSET)
     A  56                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 10 13'Kundenr.. . . . . .:'
     A            A2PKNF    R        B 10 34REFFLD(RKPKNR)
     A**40                                  DSPATR(PC)
6.10 A  57                                  DSPATR(PC)
     A  40                                  ERRMSGID(RMH0140 *LIBL/QMFOKON 40)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 10 41'-'
     A            A2PKNT    R        B 10 43REFFLD(RKPKNR)
6.10 A  58                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 11 11'Skriv :'
     A                                      COLOR(WHT)
     A                                 12 13'Kontaktperson . . .:'
     A            A2PKPE    R        B 12 34REFFLD(RKPKPE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 41'(J=Ja)'
     A                                 13 13'Telefonnummer . . .:'
     A            A2PTLF    R        B 13 34REFFLD(RKPTLF)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 41'(J=Ja)'
     A                                 14 13'Kun konti m/saldo .:'
     A            A2PSAL    R        B 14 34REFFLD(RKPSAL)
     A N98                                  DFTVAL('J')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 41'(J=Ja)'
     A                                 15 13'Alle med kjøp i år.:'
     A            A2PKJO    R        B 15 34REFFLD(RKPSAL)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 41'(J=Ja)'
     A                                 16 13'Kun konti m/poster i kol.'
     A            A2PKOL         1A  B 16 39CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 41'(1-4)'
     A                                 17 11'Sortering:'
     A                                      COLOR(WHT)
     A                                 18 13'Kategori. . . . . .:'
     A            A2PKTG    R        B 18 34REFFLD(RKPKTG)
     A  36                                  DSPATR(PC)
     A  36                                  ERRMSGID(RMK0420 *LIBL/QMFOKON 36)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 18 41'(J=Ja, Blank)'
     A                                 19 13'Avdeling. . . . . .:'
     A            A2PAVD    R        B 19 34REFFLD(RKPAVD)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 19 41'(J=Ja, Blank)'
     A                                 20 13'Selger. . . . . . .:'
     A            A2PSLG    R        B 20 34REFFLD(RKPSLG)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 20 41'(J=Ja, Blank)'
     A                                 21 13'Alfabetisk. . . . .:'
     A            A2PALF    R        B 21 34REFFLD(RKPALF)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 21 41'(J=Ja, Blank)'
     A                                 22 13'Synkende på saldo .:'
     A            A2PSSA    R        B 22 34REFFLD(RKPKPE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 22 41'(J=Ja, Blank)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
