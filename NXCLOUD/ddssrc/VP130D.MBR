     A*%%TS  SD  20081126  133323  BOF570      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : MR100D
     A* Beskrivelse . . : Prisgruppe/avd knytning, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       251108 bof  Nytt program
7.xx A* 7.xx  130516 bsa  Justeringer basert på ny GUI og regler
7.xx A* 7.xx  300622 MST  Nye justeringer basert på ny GUI og regler
     A*
8.01 A*       240223 BOF  Feilretting vedr. plassering CS1173964
8.02 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081126  133323  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1GLAG    R        O  8  5REFFLD(VPGLAG)
     A            B1GAVD    R        O  8 10REFFLD(VPGAVD)
     A                                      EDTCDE(Z)
     A            B1GPRG    R        O  8 17REFFLD(VPGPRG)
8.02 A            B1PTXT        30A  O  8 20
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081126  122028  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre        '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Prisgruppe knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Lager'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Avd.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Lager'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Avd.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'PG'
     A                                      COLOR(WHT)
7.xx A            B2GLAG    R        B  5  5REFFLD(VPGLAG)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2GAVD    R        B  5 10REFFLD(VPGAVD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20081126  133323  BOF570      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre        '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Prisgruppe knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Lager  . . . . . . . . . . +'
     A            C1GLAG    R        B  5 31REFFLD(VPGLAG)
     A  31                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LTXT        30A  O  5 36OVRDTA
7.xx A                                  6  2'Avdeling . . . . . . . . . +'
     A            C1GAVD    R        B  6 31REFFLD(VPGAVD)
     A  32                                  ERRMSGID(FMA0140 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C1ATXT        30A  O  6 36OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Pris  gruppe finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20081126  133323  BOF570      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre        '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Prisgruppe knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager  . . . . . . . . . . .'
     A            C1GLAG    R        O  5 31REFFLD(VPGLAG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        30A  O  5 36OVRDTA
     A                                  6  2'Avdeling . . . . . . . . . .'
     A            C1GAVD    R        O  6 31REFFLD(VPGAVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C1ATXT        30A  O  6 36OVRDTA
8.01 A                                  8  2'Prisgruppe . . . . . . . . +'
8.01 A            C2GPRG    R        B  8 32REFFLD(VPGPRG)
     A  31                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.05 A                                      OVRATR
     A            C2PTXT        30A  O  8 36OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VPGEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Pris  gruppe '
     A                                      DSPATR(RI)
     A                                  4  2'Lager  . . . . . .'
     A            D1GLAG    R        O  4 21REFFLD(VPGLAG)
     A                                      EDTCDE(Z)
     A                                  5  2'Avdeling . . . . .'
     A            D1GAVD    R        O  5 21REFFLD(VPGAVD)
     A                                      EDTCDE(Z)
     A                                  7  2'Prisgruppe . . . .'
     A            VPGPRG    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20081126  103333  BOF570      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Lager  . . . . . .'
     A            K1GLAG    R        B  4 21REFFLD(VPGLAG)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Avdeling . . . . .'
     A            K1GAVD    R        B  5 21REFFLD(VPGAVD)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(FMA0140 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FMX0030 *LIBL/QMFOFAK 33)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Lager  . . . . . .'
     A            VPGLAG    R        O  8 21OVRDTA
     A                                  9  2'Avdeling . . . . .'
     A            VPGAVD    R        O  9 21OVRDTA
     A                                 10  2'Prisgruppe . . . .'
     A            VPGPRG    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
