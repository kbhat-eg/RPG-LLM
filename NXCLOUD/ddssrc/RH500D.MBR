     A*%%TS  SD  20030702  233847  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH500D
     A* Beskrivelse . . : Kontoplan, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
7.xx A* 7.xx  160316 bsa  Justeringer basert på ny GUI og regler
7.01 A* K00   080520 bsa  Takler kall fra brede skjermbilder
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020829  115940  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        O  7  4REFFLD(RHKONT)
     A                                      EDTCDE(Z)
     A            B1SPES    R        O  7 10REFFLD(RHSPES)
     A                                      EDTCDE(Z)
     A            B1AVDE    R        O  7 15REFFLD(RHAVDE)
     A                                      EDTCDE(Z)
     A            B1TEXT    R   28   O  7 20REFFLD(RHTEXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030702  233847  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 12'Spørring på kontoplan'
7.xx A                                      DSPATR(RI)
7.xx A                                  2  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  3  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 20'Tekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  5 10'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 20'Tekst'
     A                                      COLOR(WHT)
7.xx A            B2KONT    R        B  4  4REFFLD(RHKONT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2ALFA    R        B  4 20REFFLD(RHALFA)
     A                                      COLOR(YLW)
     A*                                     CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020829  115150  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 48)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
