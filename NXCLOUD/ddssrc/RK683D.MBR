     A*%%TS  SD  20050612  102206  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK653D
     A* Beskrivelse . . : Kunder, betalingsvarsel - paramter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150902 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20050612  102206  KOB         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Factoring - overføring av poster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  8'Kjøredato. . . . . . . .:'
     A            A2PKDA    R        B  5 34REFFLD(RKPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                  7  8'Ajourfør regnskap. . . .:'
     A            A2AREG    R        B  7 34REFFLD(RA5RPO)
     A  36                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A  36                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 36)
     A                                  7 37'(J=Ja)'
     A                                  9  8'Regnskapsperiode . . . .:'
     A            A2PRPE    R        B  9 34REFFLD(RKPPER)
     A  39                                  DSPATR(PC)
     A  39                                  ERRMSGID(RMH0040 *LIBL/QMFOKON 39)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            A2PRÅR    R        B  9 37REFFLD(RKPRÅR)
     A  40                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      COMP(GE 2000)
     A  40                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 40)
     A                                 11  8'Factoringselskap . . . .:'
     A            A2FORM    R        O 11 34REFFLD(RA9FMT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
