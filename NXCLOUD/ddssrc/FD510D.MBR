     A*%%TS  SD  20051030  111154  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FD510D
     A* Beskrivelse . . : Kopiere varelinjer fra en ordre til en annen
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       180919 bkv  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051030  111154  KOB         REL-V5R3M0  5722-WDS
     A            B1VANT    R   -4 -1B  6  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A            B1LINE    R        O  6 11REFFLD(FDLINE)
     A            B1VARE    R   -5   O  6 18REFFLD(FDVARE)
     A            B1TEKT        63A  O  6 30
     A  55                                  COLOR(BLU)
     A            B1LETY    R        O  6 95REFFLD(FDLETY)
     A            B1LMAT    R        O  6 98REFFLD(FOLMAT)
     A            B1ANTA    R        O  6102REFFLD(FDANTA)
     A            B1ENHE    R        O  6116REFFLD(FDENH1)
     A            B1SAPR    R        O  6120REFFLD(FDSAPR)
     A            B1SUFF    R        H      REFFLD(FDSUFF)
     A            B1NUMM    R        H      REFFLD(FDNUMM)
     A            B1AARR    R        H      REFFLD(SDAARR)
     A            B1BINR    R        H      REFFLD(SDBINR)
     A            B1KTXT    R        H      REFFLD(SDKTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051030  111154  KOB         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      ROLLUP(10)
     A*                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Hent varelinjer'
     A                                      COLOR(WHT)
     A                                  2 38'FD510B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A  51                              4 19'Søk:'
     A                                      COLOR(WHT)
     A*                                  5  2'1=Velg'
     A*                                      COLOR(BLU)
     A                                  5  2'Antall'
     A                                      COLOR(WHT)
     A                                  5 11'Ln'
     A                                      COLOR(WHT)
     A                                  5 18'Varenummer'
     A                                      COLOR(WHT)
     A                                  5 30'Tekst'
     A                                      COLOR(WHT)
     A                                  5 95'LT'
     A                                      COLOR(WHT)
     A                                  5 98'FM'
     A                                      COLOR(WHT)
     A                                  5102'Antall'
     A                                      COLOR(WHT)
     A                                  5116'Enh'
     A                                      COLOR(WHT)
     A                                  5120'Pris'
     A                                      COLOR(WHT)
     A  51        B2FRIS        30A  B  4 31
     A                                      COLOR(YLW)
     A*                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 26  3'F3=Gå tilbake   F5=Forny '
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
