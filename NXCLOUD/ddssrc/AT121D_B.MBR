     A*%%TS  SD  20080225  210242  ASK560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AT121D
     A* Beskrivelse . . : Kundetilpass konfig
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A* 7.00  090819  bkv  Nytt prg
7.01 A*K0000  160920  bsa  Må tillate både store og små bokstaver
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A*                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041102  191648  HKO530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A            B1TFGP    R        O  8  5REFFLD(AKTFGP)
     A            B1TFIR    R        O  8 12REFFLD(AKTFIR)
     A            B1TLAG    R        O  8 18REFFLD(AKTLAG)
     A            B1TNOK    R        O  8 23REFFLD(AKTNOK)
     A            B1TVD1    R        O  8 75REFFLD(AKTVD1)
     A            B1TBSK    R-1950   O  8 82REFFLD(AKTBSK)
     A            B1TUID    R        H      REFFLD(AKTUID)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041102  193557  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A                                      ROLLUP(10)
     A*                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'ADB'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'NX kundetilpasninger'
     A                                      COLOR(WHT)
     A                                  2 38'AT121B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  4  4'Filgrupppe'
     A                                      COLOR(WHT)
     A                                  4 15'Firma'
     A                                      COLOR(WHT)
     A                                  4 21'Lager'
     A                                      COLOR(WHT)
     A                                  4 28'Nøkkel'
     A                                      COLOR(WHT)
     A                                  4 82'Frisøk'
     A                                      COLOR(WHT)
     A                                  6  2'2=Endre 3=Kopier  4=Slett 5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  5'Fgr'
     A                                      COLOR(WHT)
     A                                  7 12'Fir'
     A                                      COLOR(WHT)
     A                                  7 18'Lg'
     A                                      COLOR(WHT)
     A                                  7 23'Nøkkel'
     A                                      COLOR(WHT)
     A                                  7 75'Verdi'
     A                                      COLOR(WHT)
     A                                  7 82'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2TFGR    R        B  5  4REFFLD(AKTFGP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TFIR    R        B  5 15REFFLD(AKTFIR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TLAG    R        B  5 21REFFLD(AKTLAG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TNOK    R        B  5 28REFFLD(AKTNOK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2FRIS        30A  B  5 82
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20041102  192054  HKO530      REL-V5R2M0  5722-WDS
     A                                 27  3'F3=Gå tilbake  F5=Forny  F6=Oppret-
     A                                      t'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20080225  210242  ASK560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'ADB'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Ny'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Filgrupppe'
     A            C1TFGP    R        B  5 17REFFLD(AKTFGP)
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                  6  2'Firma'
     A            C1TFIR    R        B  6 17REFFLD(AKTFIR)
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                  7  2'Lager'
     A            C1TLAG    R        B  7 17REFFLD(AKTLAG)
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                  8  2'Nøkkel'
     A            C1TNOK    R        B  8 17REFFLD(AKTNOK)
7.01 A                                      CHECK(LC)
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A          R C1MSG
     A*%%TS  SD  19990112  143848  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990112  150031  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 11 65)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette nokkel '
     A                                      DSPATR(RI)
     A                                  5  2'Filgrupppe'
     A            D1TFGP    R        O  5 13REFFLD(AKTFGP)
     A                                      COLOR(GRN)
     A                                  6  2'Firma'
     A            D1TFIR    R        O  6 13REFFLD(AKTFIR)
     A                                      COLOR(GRN)
     A                                  7  2'Lager'
     A            D1TLAG    R        O  7 13REFFLD(AKTLAG)
     A                                      COLOR(GRN)
     A                                  8  2'Nøkkel'
     A            D1TNOK    R        O  8 13REFFLD(AKTNOK)
     A                                      COLOR(GRN)
     A            D1TUID    R        H      REFFLD(AKTUID)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080225  210242  ASK560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'ADB'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'NX kundetilpasninger'
     A                                      COLOR(WHT)
     A N30                              2 23'Vise'
     A  30                              2 23'Endre'
     A                                      COLOR(WHT)
     A                                  2 38'AT121C2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Filgrupppe'
     A            C2TFGP    R        O  5 17REFFLD(AKTFGP)
     A* N30                                  DSPATR(PR)
     A* N30                                  COLOR(YLW)
     A*  30                                  COLOR(GRN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Firma'
     A            C2TFIR    R        O  6 17REFFLD(AKTFIR)
     A* N30                                  DSPATR(PR)
     A* N30                                  COLOR(YLW)
     A*  30                                  COLOR(GRN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Lager'
     A            C2TLAG    R        O  7 17REFFLD(AKTLAG)
     A* N30                                  DSPATR(PR)
     A* N30                                  COLOR(YLW)
     A*  30                                  COLOR(GRN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Nøkkel'
     A            C2TNOK    R        O  8 17REFFLD(AKTNOK)
     A* N30                                  DSPATR(PR)
     A* N30                                  COLOR(YLW)
     A*  30                                  COLOR(GRN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Verdi1 (av/på)'
     A            C2TVD1    R        B  9 17REFFLD(AKTVD1)
     A N30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      CHECK(LC)
     A                                 10  2'Verdi2'
     A            C2TVD2    R-1600   B 10 17REFFLD(AKTVD2)
     A N30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      CHECK(LC)
     A                                 14  2'Kjede'
     A            C2TKJD    R        B 14 17REFFLD(AKTKJD)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 15  2'System'
     A            C2TSYS    R        B 15 17REFFLD(AKTSYS)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 16  2'Modul'
     A            C2TMDL    R        B 16 17REFFLD(AKTMDL)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 17  2'Endringstype'
     A            C2TETY    R        B 17 17REFFLD(AKTETY)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 18  2'Pris'
     A            C2TPRS    R        B 18 17REFFLD(AKTPRS)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 19  2'Beskrivelse'
     A            C2TBSK    R-1300   B 19 17REFFLD(AKTBSK)
     A N30                                  DSPATR(PR)
     A N30                                  COLOR(YLW)
     A  30                                  COLOR(GRN)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2ODAT         6Y 0O 25 94COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 25105'/'
     A            C2EDAT         6Y 0O 25108COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 25120'/'
     A            C2EUSR    R        O 25122REFFLD(AKTEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
