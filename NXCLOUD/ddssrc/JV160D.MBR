     A*%%TS  SD   20140808  122839  BOF630      REL-V7R1M0  5770-WDS
6.20 A*           51124   NHO  Lagt inn PC på frisøk
7.xx A*      BSA  220116  Endringer relatert til ny GUI
7.01 A* K000 bsa  060720  Skrevet om til 132 bredde
7.02 A* K000 bsa  060720  Utvidet funksjonalitet rundt søk.
     A*
     A*
     A*
     A*%%EC
7.01 A**                                    DSPSIZ(24 80 *DS3 -
7.01 A                                      DSPSIZ(27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140808  122839  BOF630      REL-V7R1M0  5770-WDS
7.01 A            B1NYPO        15A  H
7.01 A            B1ANTA         7Y 2B 10  2COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
7.xx A N22                                  DSPATR(PC)
7.01 A            B1TEK1    R        O 10 11REFFLD(JVTEK1)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.01 A            B1TEK2    R  -15   O 10 47REFFLD(JVTEK2)
7.01 A  27                                  COLOR(BLU)
7.01 A  28                                  COLOR(YLW)
7.01 A            B1VARE    R        O 10 70REFFLD(JVVARE)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.01 A            B1NOBB    R        O 10 86REFFLD(JVNOBB)
7.01 A  27                                  COLOR(BLU)
7.01 A  28                                  COLOR(YLW)
7.01 A            B1LVAR    R   -5   O 10 95REFFLD(JVLVAR)
7.01 A  27                                  COLOR(BLU)
7.01 A  28                                  COLOR(YLW)
7.01 A            B1LNAV        17A  O 10111
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140808  122839  BOF630      REL-V7R1M0  5770-WDS
7.01 A                                      SFLSIZ(0016)
7.01 A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF14
     A                                      CF15
     A                                      CF16
7.01 A**                                    CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  60                                  SFLMSGID(JM90001 *LIBL/QMFVADM 60)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
7.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring/registrering av varelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 38'JD160B1'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre. .'
     A                                      COLOR(BLU)
     A            B2NUMM         8S 0O  4 11OVRDTA
7.02 A                                  4 27'Leverandør+'
7.02 A                                      COLOR(BLU)
7.02 A            B2LDOR    R        B  4 40REFFLD(JVLDOR)
7.02 A                                      COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A            B2LNVN        30A  O  4 47COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A                                  4 80'Kunde. .'
     A                                      COLOR(BLU)
7.02 A            B2KUND         6S 0O  4 89OVRDTA
7.02 A            B2KNAV        17A  O  4 96OVRDTA
7.xx A                                  5  2'Varegrp+'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  5 11REFFLD(JVOGRP)
     A  31                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 14'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  5 16REFFLD(JVHGRP)
     A  32                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 19'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  5 21REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.02 A                                  5 27'Nullstill filter'
7.02 A                                      COLOR(BLU)
7.02 A            B2NULL         1Y 0B  5 45
7.02 A                                      COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A                                  5 47'1/0'
7.02 A                                      COLOR(BLU)
7.02 A                                  5 80'K-prosj.'
     A                                      COLOR(BLU)
7.02 A            B2KPRO         6S 0O  5 89OVRDTA
7.02 A            B2PNAV        17A  O  5 96OVRDTA
     A                                  6  2'Søk. . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  6 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.02 A                                  6 80'Levtype.'
     A                                      COLOR(BLU)
7.02 A            B2LTXT        24A  O  6 89
     A                                  7  2'Frisøk .'
     A                                      COLOR(BLU)
     A            B2FTEK        35A  B  7 11COLOR(YLW)
6.20 A  23                                  DSPATR(PC)
     A                                      OVRDTA
7.01 A                                  7 80'Tot:'
     A                                      COLOR(WHT)
7.01 A            B2TOTS        11Y 2O  7 85EDTCDE(4)
7.01 A**                                8 11'Varetekst                         -
7.01 A**                                      Varenr   Nobbnr   Lev. varenr'
7.01 A**                                    COLOR(WHT)
7.01 A                                  8  2'Varetekst'
7.01 A                                      COLOR(WHT)
7.01 A            B2TEK1    R        B  8 12REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  8 56'Varenr'
7.01 A                                      COLOR(WHT)
7.01 A            B2VARE         8Y 0B  8 64COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.01 A                                  8 80'Nobbnr'
7.01 A                                      COLOR(WHT)
7.01 A            B2NOBB    R        B  8 88REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A                                  8100'Lev.varenr'
7.01 A                                      COLOR(WHT)
7.01 A            B2LVAR    R   -5   B  8112REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  9  3'Antall'
     A                                      COLOR(WHT)
7.01 A                                  9 11'Vartekst                        '
     A                                      COLOR(WHT)
7.01 A                                  9 47'Tekst 2            '
7.01 A                                      COLOR(WHT)
7.01 A                                  9 70'Varenr'
7.01 A                                      COLOR(WHT)
7.01 A                                  9 86'Nobbnr'
7.01 A                                      COLOR(WHT)
7.01 A                                  9 95'Lev. varenr'
7.01 A                                      COLOR(WHT)
7.01 A                                  9111'Leverandør'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20040809  094850  HKO         REL-V5R2M0  5722-WDS
7.01 A                                 26  2'F2=Tekstlinje  F3=Gå tilbake  F4=S-
7.01 A                                      pørring  F9=Gjenta søk  F14=Kundein-
7.01 A                                      rformasjon  F15=Rabattmatrise  F16=-
7.01 A                                      Spesialpriser'
     A                                      COLOR(BLU)
7.01 A**                               26  2'F14=Kundeinformasjon  F15=Rabattma-
7.01 A**                                    trise  F16=Spesialpriser      F23=B-
7.01 A**                                    ytt info.'
7.01 A**                                    COLOR(BLU)
     A          R ASUME                     ASSUME
7.01 A                                 27131' '
