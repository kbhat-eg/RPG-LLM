     A*%%TS  SD  20190814  114526  G0XEGSVSTR  REL-V7R3M0  5770-WDS
7.xx A*     BKV  030418    Endringer relatert til ny GUI
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20010504  132902  AMD         REL-V4R4M0  5769-PW1
     A            B1KUND         6S 0H
     A            B1SALG    R        H      REFFLD(FYTOTS)
     A            B1VALG         2Y 0B 10  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.10 A                                      VALUES(0 5 6 10)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1AARR    R        O 10  5REFFLD(FYAARR)
     A            B1NUMM    R        O 10 10REFFLD(FYNUMM)
6.NU A            B1SUFF    R        O 10 19REFFLD(FYSUFF)
6.NU A            B1OTYP    R        O 10 22REFFLD(FYOTYP)
6.NU A            B1KUNA        18A  O 10 25
     A            B1DATO         6Y 0O 10 44EDTWRD('  .  .  ')
7.xx A            B1NAVN    R  -14   O 10 53REFFLD(FYNAVN)
7.xx A            B1NAV2    R  -20   O 10 70REFFLD(FYNAVN)
     A            B1BDAT    R        H      REFFLD(FYBDAT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20190814  114526  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF15
     A                                      CF16
     A                                      CF22
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Slettede ordre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'NB Årstall må oppgis ved posisjone-
     A                                      ring !'
     A                                      COLOR(BLU)
     A                                  4 58'Ordretype :'
     A                                      COLOR(BLU)
     A            H1OTYP    R        O  4 70REFFLD(FOOTYP)
     A                                      COLOR(BLU)
     A                                  5  5'År'
     A                                      COLOR(WHT)
     A            B2AARR    R        B  5 10REFFLD(FYAARR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  5 15'Ordre'
     A                                      COLOR(WHT)
     A            B2NUMM    R        B  5 21REFFLD(FYNUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(FMO0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 32)
     A                                  5 31'Suff'
     A                                      COLOR(WHT)
     A            B2SUFF    R        B  5 37REFFLD(FYSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  5 41'Ot'
     A                                      COLOR(WHT)
     A            B2OTYP    R        B  5 44REFFLD(FYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 58'Bruker. . :'
     A                                      COLOR(BLU)
     A            H1USER    R        O  5 70REFFLD(FOUSER)
     A                                      COLOR(BLU)
     A                                  6  5'Alfa'
     A                                      COLOR(WHT)
     A            B2KUNA        18A  B  6 10COLOR(YLW)
     A                                      OVRDTA
     A                                  6 31'Dato'
     A                                      COLOR(WHT)
     A            B2DATO         6Y 0B  6 37COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  6 58'Kunde . . :'
     A                                      COLOR(BLU)
     A            H1KUND    R        O  6 70REFFLD(FOKUND)
     A                                      COLOR(BLU)
     A                                  7  5'Navn'
     A                                      COLOR(WHT)
     A            B2NAVN    R  -12   B  7 10REFFLD(FYNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  8  2'5=Vis ordre  6=Vis ordrelinjer'
     A                                      COLOR(BLU)
     A                                  9  2'V'
     A                                      COLOR(WHT)
     A                                  9  5'År'
     A                                      COLOR(WHT)
     A                                  9 10'Ordre'
     A                                      COLOR(WHT)
     A                                  9 19'S'
     A                                      COLOR(WHT)
     A                                  9 22'Ot'
     A                                      COLOR(WHT)
     A                                  9 25'Kundealfa'
     A                                      COLOR(WHT)
     A            B2HEDD         9A  O  9 43COLOR(WHT)
     A            B2HED1        11A  O  9 53COLOR(WHT)
     A            B2HED2        10A  O  9 70COLOR(WHT)
     A                                  8 35'10=Overfør til tilbud'
     A                                      COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  20020313  081720  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Bla-alt'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 70'F23-Vise 2'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20020313  080932  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 40)
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Velg ordretype.+'
     A            H1OTYP    R        B  4 19REFFLD(FYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A                                  4 22'+'
7.xx A            H1OTY2    R        B  4 24REFFLD(FYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  4 27'+'
7.xx A            H1OTY3    R        B  4 29REFFLD(FYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  4 32'Blank gir'
7.xx A                                  5 32'alle'
7.xx A                                  6  2'Velg bruker....'
     A            H1USER    R        B  6 19REFFLD(FYUSER)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  7  2'Velg kundenr...+'
     A            H1KUND    R        B  7 19REFFLD(FYKUND)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.10 A                                  8  2'Inkl. kasseordre'
6.10 A                                  8 21'1=Ja'
6.10 A            H1SHOP         1A  B  8 19
6.10 A                                      CHECK(RB)
6.10 A                                      OVRDTA
6.10 A                                      COLOR(YLW)
6.10 A                                  9  2'Inkl. faktura .:'
6.10 A                                  9 21'1=Ja'
6.10 A            H1FAKT         1A  B  9 19
6.10 A                                      CHECK(RB)
6.10 A                                      OVRDTA
6.10 A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
