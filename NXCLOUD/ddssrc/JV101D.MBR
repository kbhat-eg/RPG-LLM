     A*%%TS  SD  20140822  074823  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV100D
     A* Beskrivelse . . : Vedlikehold av vare 1:2
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 030616 bsa Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20140822  074823  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
6.39 A                                      CF23
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vare 1:2'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JV101C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr . . . . .'
     A            C2VARE    R        O  4 19REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2VTTX        12A  O  4 43OVRDTA
     A                                      COLOR(WHT)
     A  80                              4 65' Slettet i NOBB '
     A  80                                  COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                  5  2'NOBB-nr. . . . .'
     A            C2NOBB    R        B  5 19REFFLD(JVNOBB)
     A  31                                  ERRMSGID(JMV0070 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMV0060 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  60                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  5 29'Erstattet av '
     A            C2ENOB    R        B  5 43REFFLD(JVENOB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  6  2'Modulnr. . . . .'
     A            C2MODN    R        B  6 19REFFLD(JVMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  61                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2MTXT    R        O  6 29REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 65'Prisenhet'
7.xx A            C2PENH    R        B  5 76REFFLD(JVPENH)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(JFD0100 *LIBL/QMFVADM 31)
     A  60                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2OGRP    R        B  7 19REFFLD(JVOGRP)
     A  34                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2HGRP    R        B  7 22REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A  63                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2UGRP    R        B  7 25REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  64                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2UTXT        35A  O  7 29COLOR(YLW)
     A                                      OVRDTA
     A                                  6 65'Stat.enhet'
7.xx A            C2SENH    R        O  6 76REFFLD(JVPENH)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . .'
7.xx A                                  8  2'Egen varegr .  +'
7.xx A                                  8 22'+'
7.xx A                                  8 27'+'
     A            C2EOGR    R        B  8 19REFFLD(JVEOGR)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2EHGR    R        B  8 24REFFLD(JVEHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2EUGR    R        B  8 29REFFLD(JVEUGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            C2EUTX        35A  O  8 33COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 65'Kladdebok'
7.xx A            C2KLAD         1A  B  7 76COLOR(YLW)
     A  75                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Varetekst 1. . .'
     A            C2TEK1    R        B  9 19REFFLD(JVTEK1)
     A  37                                  ERRMSGID(JMV0040 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A  65                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Varetekst 2. . .'
     A            C2TEK2    R        B 10 19REFFLD(JVTEK2)
     A                                      COLOR(YLW)
     A  66                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 56'Lagerført  .'
7.xx A            C2LFOR    R        B  9 69REFFLD(JVLFOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 71'1=Ja,0=Nei'
     A                                 11  2'Egen varetekst 1'
     A            C2ETE1    R        B 11 19REFFLD(JVETE1)
     A  39                                  ERRMSGID(JMV0140 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 56'Holdb.h.dato'
7.xx A            C2HDAT    R        B 10 69REFFLD(JVHDAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 71'1=Ja,0=Nei'
     A                                 12  2'Egen varetekst 2'
     A            C2ETE2    R        B 12 19REFFLD(JVETE2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 56'Tåler Frost '
7.xx A            C2TFRO    R        B 11 69REFFLD(JVTFRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 71'1=Ja,0=Nei'
     A                                 13  2'Skaffevare . . .'
     A            C2DSKO    R        B 13 19REFFLD(JVDSKO)
     A  49                                  ERRMSGID(JM00010 *LIBL/QMFVADM 49)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 22'1=Ja,0=Nei'
     A                                 12 56'Har PSE  . .'
7.xx A            C2HPSE    R        B 12 69REFFLD(JVHPSE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 71'1=Ja,0=Nei'
     A                                 13 56'Under arbeid'
     A            C2UARB    R        B 13 69REFFLD(JVUARB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                               13 71'1=Ja,0=Nei'
     A                                 14  2'Priskode . . . .'
     A            C2DPKO    R        B 14 19REFFLD(JVDPKO)
     A  48                                  ERRMSGID(JVA6020 *LIBL/QMFVADM 48)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 14 21'(kode)'
     A                                 14 56'Godkj.status'
     A            C2GSTS    R        O 14 69REFFLD(JVGSTS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 71'1=Ja,0=Nei'
7.xx A                                 15  2'Produsent. . . +'
     A            C2PROD    R        B 15 19REFFLD(JVPROD)
     A  40                                  ERRMSGID(JMQ0020 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  67                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PNAV        13A  O 15 26COLOR(YLW)
     A                                      OVRDTA
     A            C2PVAR    R        B 15 40REFFLD(JVPVAR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  69                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 16  2'Leverandør . . +'
     A            C2LDOR    R        B 16 19REFFLD(JVLDOR)
     A  41                                  ERRMSGID(JML0010 *LIBL/QMFVADM 41)
     A  42                                  ERRMSGID(JML0020 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  68                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2LNAV        13A  O 16 26COLOR(YLW)
     A                                      OVRDTA
     A            C2LVAR    R        B 16 40REFFLD(JVLVAR)
     A  43                                  ERRMSGID(JMV0050 *LIBL/QMFVADM 43)
     A                                      COLOR(YLW)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17  2'Sortimentkode  .'
     A            C2SORT    R   -9   O 17 19REFFLD(JVSORT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Sort.leverandør'
     A            C2SLDO    R        O 18 19REFFLD(JVSLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Kategoriansvar.'
     A            C2KATE    R        O 19 19REFFLD(JVKATE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV    R        O 19 23REFFLD(JKNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Aktiv dato. . . '
     A            C2ADAT         6Y 0B 20 19COLOR(YLW)
     A  54                                  ERRMSGID(JM00250 *LIBL/QMFVADM 54)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Utgå dato . . . '
     A            C2UDAT         6Y 0B 21 19COLOR(YLW)
     A  55                                  ERRMSGID(JM00250 *LIBL/QMFVADM 55)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 21 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 57'/'
     A            C2EDAT         6Y 0O 21 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 68'/'
     A            C2EUSR    R        O 21 70REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'F3=Gå Tilbake  F4=Spørr  F14=Enh  -
     A                                       F15=Priser  F16=Søketekst  F17=Bet-
     A                                      ingelser'
     A                                      COLOR(BLU)
6.39 A                                 23  2'F18=Kampanjepris F19=ProdInfo F2-
6.39 A                                      0=Alt.varer F21=Avgifter F22=NRF -
6.39 A                                      F23=NOBB info'
     A                                      COLOR(BLU)
     A                                 14 40'Varenr'
     A                                      COLOR(WHT)
     A          R C3BLD
     A*%%TS  SD  20140211  093807  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vare 2:2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr . . . . .'
     A            C3VARE    R        O  4 19REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80                              4 65' Slettet i NOBB '
     A  80                                  COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                  5  2'NOBB-nr. . . . .'
     A            C3NOBB    R        O  5 19REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Modulnr. . . . .'
     A            C3MODN    R        O  6 19REFFLD(JVMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3MTXT    R        O  6 29REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . .'
     A            C3OGRP    R        O  7 19REFFLD(JVOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3HGRP    R        O  7 22REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3UGRP    R        O  7 25REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3UTXT        35A  O  7 29COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varetekst 1. . .'
     A            C3TEK1    R        O  8 19REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lang Varetekst .'
     A            C3LTX1        50A  O  9 19COLOR(YLW)
     A                                      OVRDTA
     A            C3LTX2        50A  O 10 19COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Fritekst :'
     A                                      COLOR(WHT)
     A            C3FTE1        78A  O 11  2COLOR(YLW)
     A                                      OVRDTA
     A            C3FTE2        78A  O 12  2COLOR(YLW)
     A                                      OVRDTA
     A            C3FTE3        78A  O 13  2COLOR(YLW)
     A                                      OVRDTA
     A* TRANSPORT
     A                                 14  2'Transportmerking:'
     A                                      COLOR(WHT)
     A                                 15  2'Beskrivelse .'
     A            C3FABS    R        O 15 16REFFLD(JVFABS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Farenr. . . .'
     A            C3FANR    R        O 18 16REFFLD(JVFANR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Embalasjemrk.'
     A            C3EMBM    R        O 16 16REFFLD(JVEMBM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'UNNR. . . . .'
     A            C3UNNR    R        O 17 16REFFLD(JVUNNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A* TOLLKODER
     A                                 17 30'Tollkode : '
     A                                      COLOR(WHT)
     A                                 18 30'Norsk '
     A            C3TONO    R        O 18 38REFFLD(JVTONO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19 30'EU . .'
     A            C3TOEU    R        O 19 38REFFLD(JVTOEU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A* Miljømerking
     A                                 17 60'Miljømerke 1 '
     A            C3MMK1    R        O 17 74REFFLD(JVMMK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19 60'Miljømerke 2 '
     A            C3MMK2    R        O 19 74REFFLD(JVMMK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20 60'Landkode 2 . '
     A            C3MLK2    R        O 20 74REFFLD(JVMLK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 60'Landkode 1 . '
     A            C3MLK1    R        O 18 74REFFLD(JVMLK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Aktiv dato . . .'
     A            C3ADAT         6Y 0O 20 19COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Utgår dato . . .'
     A            C3UDAT         6Y 0O 21 19COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C3ODAT         6Y 0O 21 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 57'/'
     A            C3EDAT         6Y 0O 21 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 68'/'
     A            C3EUSR    R        O 21 70REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
