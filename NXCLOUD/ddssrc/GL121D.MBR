     A*%%TS  SD  20030206  102013  RHO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL121D
     A* Beskrivelse . . : Felles sekvensteller
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R D1WIN
     A*%%TS  SD  19970522  102642  RHO         REL-V3R7M0  5716-PW1
     A                                      TEXT('Windowsversjon')
     A                                      CA03
     A                                      CA11
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 11 39)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  9'FELLES SEKVENSTELLER'
     A                                      COLOR(WHT)
     A                                  4  2'Foretaksidentifikasjon:'
     A            A1UFOR    R        O  4 26REFFLD(GDUFOR)
     A                                      EDTCDE(Z)
     A                                      TEXT('Foretaksidentifikasjon')
     A                                      DSPATR(UL)
     A                                  6  2'Sekvensteller. . . . .:'
     A            A1USB1    R        B  6 26REFFLD(GDUSB1)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      TEXT('Sekvensteller DIRREM')
     A                                      COLOR(YLW)
     A                                  7  2'Sekvensteller for DIRADD .:'
     A                                      DSPATR(ND)
     A            A1USB2    R        B  7 30REFFLD(GDUSB2)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      TEXT('Sekvensteller DIRADD')
     A                                      DSPATR(ND)
     A                                  8  4'Endret:'
     A                                      DSPATR(ND)
     A            A1UDAT    R        O  8 12REFFLD(GDUDAT)
     A                                      TEXT('Siste endret dato')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(ND)
     A            A1UTIM    R        O  8 25REFFLD(GDUTIM)
     A                                      TEXT('Siste endret tidspunkt')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(ND)
     A                                 10  2'F3=GÃ¥ tilbake'
     A                                      COLOR(BLU)
     A                                 10 17'F11=Sist brukte teller'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  20030206  093411  RHO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 6 27)
     A                                      TEXT('Sist brukte sekvensnummer')
     A                                      CA03
     A                                      KEEP
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Legg tilbake flg. tellere:'
     A                                      DSPATR(UL)
     A            E1DASE         6Y 0O  5 15TEXT('Sist brukte Daglig sekvenstel-
     A                                      ler')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  5  2'Daglig. . .:'
     A                                  4  2'Egen/Felles:'
     A            E1EFSE         4Y 0O  4 17TEXT('Sist brukte Egen/Felles sekve-
     A                                      nsteller')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A          R ASUME                     ASSUME
     A                                 24 79' '
