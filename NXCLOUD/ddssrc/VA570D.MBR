     A*%%TS  SD  20020513  120436  AMD         REL-V5R1M0  5722-WDS
     A*
7.xx A*   290716 bsa Endringer relatert til ny GUI
     A*
     A*%%EC
6.31 A                                      DSPSIZ(24 80 *DS3 -
6.31 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020226  092404  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VTYP    R        O  7  6REFFLD(VAVTYP)
     A            B1VTXT    R        O  7 11REFFLD(VAVTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020513  120436  AMD         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge varetype '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Varetype'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 19'(kode)'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  5'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2VTYP    R        B  3 17REFFLD(VAVTYP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2VTXT    R   -9   B  4 17REFFLD(VAVTXT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 46)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
