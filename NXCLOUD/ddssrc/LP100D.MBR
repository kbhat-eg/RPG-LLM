     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LP100D
     A* Beskrivelse . . : Vedlikehold av leverandørdistribusjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.10 A* 6.10  230112 BSA  Legger ut kode for kvalitetssikret EDI kontroll
6.10 A*                   Kalles via F-tast
6.20 A* 6.20  251110 BOF  Utvidet med OTYP
6.22 A* 6.20  241012 BSA  Reversert vedlikehold via F tast
7.xx A* 7.xx  150316 bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A* 8.xx  270422 bsa  Ny F-tast for ekstra EDI informasjon.
8.02 A* 8.xx  031022 bsa  Nytt felt for negativ ordretype.
8.06 A* 8.xx  071122 bsa  Nytt felt for ordreinfo type.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010702  094226  ASK         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1LDOR    R        O  8  4REFFLD(LPLDOR)
     A            B1LAGE    R        O  8 11REFFLD(LPLAGE)
     A            B1LNAV        11A  O  8 14
     A            B1TYPE    R        O  8 26REFFLD(LPTYPE)
     A            B1FORM    R        O  8 37REFFLD(LPFORM)
     A            B1MOTT    R        O  8 48REFFLD(LPMOTT)
     A            B1DATO         6Y 0O  8 64EDTWRD('  .  .  ')
     A            B1TIME         6Y 0O  8 73EDTWRD('  .  .  ')
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  084253  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WCURRN         5S 0H
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'+'
7.xx A                                  4  6'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'+'
7.xx A                                  4 15'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Navn      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 26'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 37'Format'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 48'Mottaker'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'Siste sending'
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  5  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 2 3 4 5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  5  6REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LAGE    R        B  5 15REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
      *
     A          R B2CMD
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny  F6=Opprett'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020924  153446  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CLRL(*ALL)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  5 31REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LNAV        30A  O  5 39COLOR(GRN)
7.xx A                                  6  2'Lager. . . (Blank gir alle)+'
     A            C1LAGE    R        B  6 31REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LAGN        20A  O  6 39COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1MSG
     A*%%TS  SD  19971028  093526  ASK         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  1  2'Feilmelding!'
     A                                  3  2'Distribusjonsliste finnes'
     A                                  4  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF08
8.01 A  50                                  CF09
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN        11A  O  4 69DSPATR(RI)
     A                                      OVRDTA
     A                                  5  2'Leverandør . . . .'
     A            C1LDOR    R        O  5 21REFFLD(LPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  5 29COLOR(GRN)
     A                                  6  2'Lager. . . . . . .'
     A            C1LAGE    R        O  6 21REFFLD(LPLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LAGN        20A  O  6 29COLOR(GRN)
     A                                      OVRDTA
7.xx A                                  8  2'Forsendelsestype +'
     A            C2TYPE    R        B  8 21REFFLD(LPTYPE)
     A  31                                  ERRMSGID(LMP0040 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMP0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  9  2'Format . . . . . +'
     A            C2FORM    R        B  9 21REFFLD(LPFORM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10  2'Mottaker . . . . +'
     A            C2MOTT    R        B 10 21REFFLD(LPMOTT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'FTP rutinenavn . .'
     A            C2FTPR    R   10   B 11 21REFFLD(LPFTPR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 12  2'OT intern lev.   +'
8.02 A            C2OTYP         2A  B 12 21
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(LMA0260 *LIBL/QMFLAGR 33)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 13  2'Retur OT int.lev.+'
8.02 A            C2NOTY         2A  B 13 21
8.02 A                                      COLOR(YLW)
8.02 A  33                                  ERRMSGID(LMA0260 *LIBL/QMFLAGR 33)
8.02 A  30                                  DSPATR(PR)
8.02 A                                      OVRDTA
     A                                 14  2'Kundenr. hos lev..'
     A            C2LKNR    R        B 14 21REFFLD(LPLKNR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 15  2'Faxnr. . . . . . .'
     A            C2FAXN    R        B 15 21REFFLD(LPFAXN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'E-Mail . . . . . .'
     A            C2MAIL    R        B 16 21REFFLD(LPMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.10 A                                 17  2'NEB godkjent . . .'
6.10 A                                 17 24'1=Ja'
6.22 A            C2NEBG         1A  B 17 21
6.22 A                                      COLOR(YLW)
6.22 A  30                                  DSPATR(PR)
6.10 A                                      OVRDTA
     A                                 18  2'Siste overføring .'
     A            C2DATO         6Y 0O 18 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            C2TIME         6Y 0O 18 30EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 20  2'Sist endret dato .'
     A            C2EDAT         6Y 0O 20 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 21  2'Endret av. . . . .'
     A            C2EUSR    R        O 21 21REFFLD(LPEUSR)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
6.22 A**                               23 32'F8=FTP rutiner   F9=NEB info'
6.22 A                                 23 32'F8=FTP rutiner'
     A                                      COLOR(BLU)
8.01 A  50                             23 49'F9=EDI info'
8.01 A  50                                  COLOR(BLU)
     A*
     A          R D1WIN
     A*%%TS  SD  19990527  123440  ASK         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette distibusjonsliste '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør.'
     A            D1LDOR    R        O  4 14REFFLD(LPLDOR)
     A            D1LNAV        30A  O  4 21
     A                                  5  2'Lager . . .'
     A            D1LAGE    R        O  5 14REFFLD(LPLAGE)
     A            D1LAGN        20A  O  5 21
     A                                  7  2'Type. . . .'
     A            D1TYPE    R        O  7 14REFFLD(LPTYPE)
     A                                  8  2'Format. . .'
     A            D1FORM    R        O  8 14REFFLD(LPFORM)
     A                                  9  2'Mottaker. .'
     A            D1MOTT    R        O  9 14REFFLD(LPMOTT)
     A                                 13  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20020924  104016  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 30)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Hent inn'
     A                                      DSPATR(RI)
     A            F1HEAD         8A  O  2 11DSPATR(RI)
     A                                  4  2'1=Valg'
     A                                      COLOR(BLU)
     A                                  5  2'V'
     A                                      COLOR(WHT)
     A            F1ALTH         8A  O  5  5COLOR(WHT)
     A            F1VAL1         1A  B  6  2COLOR(YLW)
     A            F1ALT1        15A  O  6  5
     A            F1VAL2         1A  B  7  2COLOR(YLW)
     A            F1ALT2        15A  O  7  5
     A            F1VAL3         1A  B  8  2COLOR(YLW)
     A            F1ALT3        15A  O  8  5
     A            F1VAL4         1A  B  9  2COLOR(YLW)
     A            F1ALT4        15A  O  9  5
     A            F1VAL5         1A  B 10  2COLOR(YLW)
     A            F1ALT5        15A  O 10  5
     A            F1VAL6         1A  B 11  2COLOR(YLW)
     A            F1ALT6        15A  O 11  5
     A            F1VAL7         1A  B 12  2COLOR(YLW)
     A            F1ALT7        15A  O 12  5
     A            F1VAL8         1A  B 13  2COLOR(YLW)
     A            F1ALT8        15A  O 13  5
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R K1WIN
     A*%%TS  SD  20000228  151422  ASK         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør'
     A            K1LDOR    R        B  4 13REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LMP0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LNAV        30A  O  4 21
     A                                      OVRDTA
     A                                  5  2'Lager. . .'
     A            K1LAGE    R        B  5 13REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LAGN        20A  O  5 21
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Leverandør'
     A            K2LDOR    R        O 10 13REFFLD(LPLDOR)
     A                                      OVRDTA
     A            K2LNAV        30A  O 10 21
     A                                      OVRDTA
     A                                 11  2'Lager. . .'
     A            K2LAGE    R        O 11 13REFFLD(LPLAGE)
     A                                      OVRDTA
     A            K2LAGN        20A  O 11 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
6.10 A          R I2WIN
6.10 A*%%TS  SD  20050526  100703  KOB         REL-V5R2M0  5722-WDS
8.06 A                                      WINDOW(&W_ALIN &W_ACOL 15 65)
6.10 A                                      CA03
8.06 A                                      CF04
6.10 A                                      CA15
8.06 A                                      RTNCSRLOC(&WACTRE &WACTFE)
6.10 A                                      PUTOVR
8.06 A            WACTRE        10A  H
8.06 A            WACTFE        10A  H
6.10 A            W_ALIN         2S 0P
6.10 A            W_ACOL         3S 0P
8.01 A                                  2 19'EDI parametre'
6.10 A                                      COLOR(WHT)
8.01 A                                  4  2'Automatisk OBKR.....:'
8.01 A                                  4 26'1=Ja/0=Nei (ikke aktiv)'
8.01 A            I2AOBK         1A  B  4 24
8.01 A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
6.10 A N30                                  COLOR(YLW)
6.10 A                                      DSPATR(UL)
6.10 A  30                                  DSPATR(PR)
6.10 A                                      OVRDTA
      *
8.01 A                                  5  2'Automatisk PAKK.....:'
8.01 A                                  5 26'1=Ja/0=Nei'
8.01 A            I2APAK         1A  B  5 24
8.01 A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      DSPATR(UL)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
      *
8.01 A                                  6  2'Automatisk FAKT.....:'
8.01 A                                  6 26'1=Ja/0=Nei (Ikke aktiv)'
8.01 A            I2AFAK         1A  B  6 24
8.01 A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      DSPATR(UL)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
      *
8.01 A                                  7  2'Bruke logistikknr...:'
8.01 A                                  7 26'1=Ja/0=Nei'
8.01 A            I2LOGN         1A  B  7 24
8.01 A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      DSPATR(UL)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
      *
8.06 A                                  8  2'Ordre infotype......+'
8.06 A            I2ITYP         2A  B  8 24
8.06 A  34                                  ERRMSGID(VMA8020 *LIBL/QMFOFAK 34)
8.06 A N30                                  COLOR(YLW)
8.06 A                                      DSPATR(UL)
8.06 A  30                                  DSPATR(PR)
8.06 A                                      OVRDTA
8.06 A            I2ITXT        35A  O  8 27
8.06 A                                      OVRDTA
      *
8.06 A                                 14  2'F3=Gå tilbake   F4=Spøring'
6.10 A                                      COLOR(BLU)
