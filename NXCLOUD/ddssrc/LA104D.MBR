     A*%%TS  SD  20021003  141247  ASK         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LA104D
     A* Beskrivelse . . : Statuskoder, lager koder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       260802 ASK  Nytt program
7.xx A*  7.xx 160316 bsa  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      CA03(03 'F3=Gå tilbake')
     A          R A1BLD
     A*%%TS  SD  20021003  141247  ASK         REL-V5R1M0  5722-WDS
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Lager koder'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LA104A1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Innkjøp/Lager:'
     A                                      COLOR(WHT)
     A                                  6  2'Kostprisjustering . . . . . . .'
     A            A1ALIK    R        B  6 34REFFLD(LFRFR/LAALIK *LIBL/LFRF)
     A  31                                  ERRMSGID(LMA0125 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  6 38' 0=Ingen justering av kostpris '
     A                                  7 38' 1=Direkte oppdatering '
     A                                  8 38' 2=Gjennomsnittsberegning '
     A                                  9 38' 3=Innkjøpspris og kostpris hvis n-
     A                                      ull'
     A                                 10 38' 4=Innkjøpspris og kostpris uanset-
     A                                      t'
     A                                 12  2'Lagertransaksjoner, overføring.'
     A            A1AOVF    R        B 12 34REFFLD(LFRFR/LAALIK *LIBL/LFRF)
     A  32                                  ERRMSGID(LMA0110 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                 12 38' 0=Med regnskapsoppdatering'
     A                                 13 38' 1=Uten regnskapsoppdatering'
     A                                 16  2'Kode for bilagsnummer . . . . .'
     A            A1ABIC    R        B 16 34REFFLD(LAABIC)
     A                                      COLOR(YLW)
     A                                 17  2'Bilagskode, Lagertransaksjoner.'
     A            A1ABIK    R        B 17 34REFFLD(LAABIK)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
