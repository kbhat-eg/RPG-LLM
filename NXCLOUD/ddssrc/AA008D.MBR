     A*%%TS  SD  20041103  231309  ASP         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA008D
     A* Beskrivelse . . : Firmaregister, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060904 AMD  Nytt program
7.xx A* 7.xx  090516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1FFIR    R        O  8  5REFFLD(AAFFIR)
     A            B1FNAV    R        O  8 11REFFLD(AAFNAV)
     A            B1FSTE    R        O  8 44REFFLD(AAFSTE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av firma-register '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Firma'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Firmanavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Firma'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Firmanavn           '
     A                                      COLOR(WHT)
7.xx A                                  7 44'Poststed'
     A                                      COLOR(WHT)
7.xx A            B2FFIR    R        B  5  5REFFLD(AAFFIR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FNAV    R        B  5 11REFFLD(AAFNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av firma-register '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firma. . . . . . . . . . . .'
     A            C1FFIR    R        B  5 31REFFLD(AAFFIR)
     A  31                                  ERRMSGID(AMA0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040906  142839  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Firma finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20041103  231309  ASP         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av firma-register '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
6.20 A                                  5  2'Firma. . . . . . . . .'
6.20 A            C1FFIR    R        O  5 25REFFLD(AAFFIR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.20 A                                  6  2'Firmanavn. . . . . . .'
6.20 A            C2FNAV    R        B  6 25REFFLD(AAFNAV)
     A  31                                  ERRMSGID(AMA0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
6.20 A                                  7  2'Adresse. . . . . . . .'
6.20 A            C2FAD1    R        B  7 25REFFLD(AAFAD1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
6.20 A                                  8  2'Mellomadresse. . . . .'
6.20 A            C2FAD2    R        B  8 25REFFLD(AAFAD2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
6.20 A                                  9  2'Poststed . . . . . . .'
6.20 A            C2FSTE    R        B  9 25REFFLD(AAFSTE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
6.20 A                                 10  2'Telefonnummer. . . . .'
6.20 A            C2FTLF    R        B 10 25REFFLD(AAFTLF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A                                 11  2'Telefaxnummer. . . . .'
6.20 A            C2FFAX    R        B 11 25REFFLD(AAFFAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A                                 12  2'Bankgironummer . . . .'
6.20 A            C2FBGI    R        B 12 25REFFLD(AAFBGI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A                                 13  2'Internas. bankkontonr.'
6.20 A            C2IBAN    R        B 13 25REFFLD(AAIBAN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A                                 14  2'Swift-kode - Bic-nr. .'
6.20 A            C2SWIF    R        B 14 25REFFLD(AAIBAN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
6.20 A                                 15  2'Foretaksnummer . . . .'
6.20 A            C2FFTN    R        B 15 25REFFLD(AAFFTN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A                                 16  2'MVA-pliktig ?  . . . .'
6.20 A            C2MVAP    R        B 16 25REFFLD(AAMVAP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A                                 16 36'0=Ja, 1=Nei'
6.20 A                                 16 48'(Styrer om MVA skal printes'
7.xx A                                      COLOR(WHT)
6.20 A                                 17 49'sammen med foretaksnummer)'
7.xx A                                      COLOR(WHT)
6.20 A                                 18  2'E-mail adresse . . . .'
6.20 A            C2MAIL    R        B 18 25REFFLD(AAMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
6.20 A                                 19  2'WEB adresse. . . . . .'
6.20 A            C2WEBA    R        B 19 25REFFLD(AAWEBA)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A                                      OVRATR
6.20 A                                      CHECK(LC)
6.20 A                                 20  2'Opprettet dato . . . .'
6.20 A            C2ODAT         6Y 0O 20 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.20 A                                 21  2'Sist endret dato . . .'
6.20 A            C2EDAT         6Y 0O 21 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.20 A                                 21 40'Endret av. . . . . . .'
6.20 A            C2EUSR    R        O 21 64REFFLD(AAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040907  100515  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette firma        '
     A                                      DSPATR(RI)
     A                                  4  2'Firma. . . . . . .'
     A            D1FFIR    R        O  4 21REFFLD(AAFFIR)
     A                                  6  2'Firmanavn. . . . .'
     A            AAFNAV    R        O  6 21
     A                                  7  2'Adresse. . . . . .'
     A            AAFAD1    R        O  7 21REFFLD(AAFNAV)
     A                                  8  2'Poststed . . . . .'
     A            AAFSTE    R        O  8 21REFFLD(AAFSTE)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040907  100515  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Firma. . . . . . .'
     A            K1FFIR    R        B  4 21REFFLD(AAFFIR)
     A  31                                  ERRMSGID(AMA0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMA0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Firma. . . . . . .'
     A            AAFFIR    R        O  8 21OVRDTA
     A                                  9  2'Firmanavn. . . . .'
     A            AAFNAV    R        O  9 21OVRDTA
     A                                 10  2'Adresse. . . . . .'
     A            AAFAD1    R        O 10 21OVRDTA
     A                                 11  2'Poststed . . . . .'
     A            AAFSTE    R        O 11 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
