     A*%%TS  SD  20140526  134036  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
6.NU A* 260514 Utvidelse ihht nummerserieutvidelse
6.30 A* 230914 Lev.varenr er utvidet til 20 posisjoner
7.xx A* 7.00  030316 bsa  Endringer relatert til ny GUI
     A*----------------------------------------------------------------
     A*
7.03 A                                      DSPSIZ(27 132 *WIDE)
7.03 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021119  143057  HKO         REL-V5R1M0  5722-WDS
     A            B1VARE    R        H      REFFLD(LDVARE)
     A            B1NOBB    R        H      REFFLD(LDNOBB)
     A            B1LVAR    R        H      REFFLD(LDLVAR)
7.03 A            B1TEK1    R        H      REFFLD(VVTEK1 *LIBL/FFRF)
     A            B1ANTA    R   -4 -1B  8  2REFFLD(LDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
7.03 A*            B1TEK1    R        O  9 11REFFLD(VVTEK1 *LIBL/FFRF)
7.03 A            B1TEXT        50A  O  8 11
     A  50                                  COLOR(RED)
7.03 A            B1NYPO        15A  O  8 62
6.31 A  51                                  COLOR(BLU)
6.31 A  52                                  COLOR(YLW)
7.03 A            B1ENHE    R        O  8 78REFFLD(VPENHE *LIBL/FFRF)
7.03 A            B1INPR    R        O  8 82REFFLD(VPINPR *LIBL/FFRF)
7.03 A            B1STAT         3A  O  8 93
7.03 A            B1BEHL         5Y 0O  8 97EDTCDE(L)
7.03 A            B1BBES         5Y 0O  8104EDTCDE(L)
7.03 A            B1MINI         5Y 0O  8111EDTCDE(3)
7.03 A            B1MAKS         5Y 0O  8117EDTCDE(3)
7.03 A            B1BMIN         5Y 0O  8123EDTCDE(3)
     A            B1VTYP    R        H      REFFLD(VVVTYP *LIBL/FFRF)
     A            B1SENT         1A  H
     A            B1SESS         1A  H
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140526  134036  BOF630      REL-V7R1M0  5770-WDS
7.03 A                                      SFLSIZ(0019)
7.03 A                                      SFLPAG(0018)
     A                                      ROLLUP(10)
7.03 A*                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
7.02 A                                      CA07
     A                                      CA09
     A                                      CA12
     A                                      CA21
     A                                      CA22
     A                                      CA23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
7.03 A*  15                                  SFLEND(*MORE)
7.03 A  15                                  SFLEND
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.03 A                                  1118'Kl. :'
7.03 A                                      COLOR(WHT)
7.03 A                                  1124TIME
7.03 A                                      COLOR(WHT)
     A                                  2  2'Spørring/Registrering av varelinje-
     A                                      r'
     A                                      COLOR(WHT)
7.XX A                                  2 38'LD102B1'
7.XX A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.03 A                                  2118'Dato:'
7.03 A                                      COLOR(WHT)
7.03 A                                  2124DATE
7.03 A                                      EDTCDE(Y)
7.03 A                                      COLOR(WHT)
7.03 A                                  3  1'-----------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ---------------------------'
7.03 A                                      COLOR(WHT)
7.xx A                                  4  2'Varegrp+'
     A                                      COLOR(BLU)
7.03 A            B2OGRP    R        B  4 12REFFLD(VVOGRP *LIBL/FFRF)
     A  31                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.03 A                                  4 15'+'
7.xx A                                      COLOR(BLU)
7.03 A            B2HGRP    R        B  4 17REFFLD(VVHGRP *LIBL/FFRF)
     A  32                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.03 A                                  4 20'+'
7.xx A                                      COLOR(BLU)
7.03 A            B2UGRP    R        B  4 22REFFLD(VVUGRP *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.03 A                                  4 48'Lev. . .'
     A                                      COLOR(BLU)
7.03 A            B2LDOR    R        O  4 57REFFLD(LOLDOR)
7.03 A            B2LNAV        17A  O  4 64
     A                                  5  2'Frisøk .'
     A                                      COLOR(BLU)
7.03 A            B2STEK        35A  B  5 12COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.03 A                                  5 48'Best.. .'
     A                                      COLOR(BLU)
7.03 A            LONUMM    R        O  5 57
7.03 A                                  5 66'Tot'
     A                                      COLOR(WHT)
7.03 A            B2TOTI    R        O  5 70REFFLD(LOTOTI)
     A                                      EDTCDE(4)
7.03 A N85                              4104'Viser for alle leverandører'
7.02 A                                      COLOR(WHT)
7.03 A  85                              4104'Viser for valgt leverandør'
7.02 A                                      COLOR(WHT)
7.03 A                                  6  2'Varetekst'
7.03 A                                      COLOR(BLU)
7.03 A                                  6 48'Varenr . '
7.03 A                                      COLOR(BLU)
7.03 A                                  6 68'Nobbnr . .'
7.03 A                                      COLOR(BLU)
7.03 A                                  6 90'Lev.varenr . .'
7.03 A                                      COLOR(BLU)
7.03 A            B2TEK1    R        B  6 12REFFLD(VVTEK1 *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.03 A            B2VARE         8Y 0B  6 58COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.03 A            B2NOBB    R        B  6 79REFFLD(VVNOBB *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.03 A            B2LVAR    R        B  6105REFFLD(VVLVAR *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.03 A                                  7  2'Antall  '
     A                                      COLOR(WHT)
7.03 A                                  7 11'Varetekst                         -
7.03 A                                                      '
7.03 A                                      COLOR(WHT)
7.03 A  80                              7 62'Varenr         '
     A                                      COLOR(WHT)
7.03 A  81                              7 62'Nobbnr'
     A                                      COLOR(WHT)
7.03 A  82                              7 62'Lev. varenr'
     A                                      COLOR(WHT)
7.03 A                                  7 78'Enh'
     A                                      COLOR(WHT)
7.03 A N84                              7 82'Innkj.pris'
     A                                      COLOR(WHT)
7.03 A  84                              7 83'inkl. mva'
     A                                      COLOR(WHT)
7.03 A                                  7 93'CPT'
     A                                      COLOR(WHT)
7.03 A                                  7 97'L beho'
7.03 A                                      COLOR(WHT)
7.03 A                                  7104'I best'
7.03 A                                      COLOR(WHT)
7.03 A                                  7111'Min. '
7.03 A                                      COLOR(WHT)
7.03 A                                  7117'Maks '
7.03 A                                      COLOR(WHT)
7.03 A                                  7123'Anbr.'
7.03 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031126  085928  HKO         REL-V5R2M0  5722-WDS
7.03 A                                 26  2'F2=Tekstlinje F3=Gå tilbake F4-
7.02 A                                      =Spørring'
     A                                      COLOR(BLU)
7.03 A  85                             26 42'F7=Vis alle'
7.02 A                                      COLOR(BLU)
7.03 A N85                             26 42'F7=Vis for leverandør'
7.02 A                                      COLOR(BLU)
7.03 A                                 26 64'F9=Gjenta søk'
7.02 A                                      COLOR(BLU)
7.03 A N83                             26 78'F21=Alle varetyper'
     A                                      COLOR(BLU)
7.03 A  83                             26 78'F21=Kun lagervarer'
     A                                      COLOR(BLU)
7.03 A  84                             26101'F22=Eksl.mva.'
     A                                      COLOR(BLU)
7.03 A N84                             26 97'F22=Inkl.mva.'
     A                                      COLOR(BLU)
7.03 A                                 26115'F23=Bytt info'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
