     A*%%TS  SD  20181102  121745  NHO630      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : VG205D
     A* Beskrivelse . . : Vedlikehold parametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       101218 ask  Nytt program                                 øves
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/VGKKST)
     A                                      PRINT
     A                                      HOME(21)
     A          R C2BLD
     A*%%TS  SD  20020730  125617  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Etablering av startverdi kostpris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Setter en startverdi basert på kal-
     A                                      kulert pris på angitt dato. '
     A                                      COLOR(RED)
     A                                  7  2'Startdato   '
     A                                      COLOR(WHT)
     A            C2SDAT         6Y 0B  7 22COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2'Priskode    '
     A                                      COLOR(WHT)
     A            C2KPRK         1   B  8 22
     A                                      VALUES('I' 'K')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 26'I=Innkjøpspris, K=Kostpris'
     A                                 10  2'Lager   +'
     A                                      COLOR(WHT)
     A  40                                  DSPATR(ND)
     A            C2LAGE         2Y 0B 10 22
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  40                                  DSPATR(ND)
     A            C2LANA        30   O 10 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  40                                  DSPATR(ND)
     A                                 13  2'Alle startpriser kan slettes!'
     A                                      COLOR(RED)
     A                                 14  2'Slette startpriser'
     A                                      COLOR(WHT)
     A            C2SLTA         1   B 14 22
     A                                      VALUES('J' 'N')
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14 25'J=Ja, N=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  004349  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A  41                              2  2'Startdato må velges'
     A  42                              2  2'Priskode må velges'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
