     A*%%TS  SD  20151029  101651  ASP_G0X     REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : EDI - LAGR
     A* Format. . . . . : LI202D
     A* Beskrivelse . . : Spesialbehandling av EDI meldinger
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150518 bsa  Nytt program
7.01 A* K000  201020 bsa  Utvidet funksjonaliteten rundt innlesing av
7.01 A* K000              EDI meldinger.
7.02 A* K000  270421 bsa  Endret ledetekst
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20151026  150945  ASP_G0X     REL-V7R1M0  5770-WDS
7.01 A            B1KTYP         1A  H
7.01 A            B1KKOD         1A  H
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
7.01 A            B1KMTY         1A  O  8  5
7.01 A            B1KTXT        20A  O  8 10
7.01 A            B1KID1        10A  O  8 31
7.01 A**          B1KID2        10A  O  8 27
7.01 A            B1KKTX        35A  O  8 42
7.01 A            B1KBES        35A  O  8 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20151026  150945  ASP_G0X     REL-V7R1M0  5770-WDS
7.01 A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'EDI meldinger'
     A                                      COLOR(WHT)
7.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spesialbehandling av EDI meldinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  5'Type'
     A                                      COLOR(WHT)
     A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
7.01 A                                  7  5'MTyp'
     A                                      COLOR(WHT)
7.01 A                                  7 10'Type'
     A                                      COLOR(WHT)
7.01 A                                  7 31'Identitet'
     A                                      COLOR(WHT)
7.01 A**                                7 27'Identitet2'
     A**                                    COLOR(WHT)
7.01 A                                  7 42'Kode'
     A                                      COLOR(WHT)
7.01 A                                  7 78'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2KTYP         1A  B  5  6COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20151029  101651  ASP_G0X     REL-V7R1M0  5770-WDS
7.01 A                                 26  2'F3=Gå tilbake  F5=Forny  F6=Oppre-
     A                                      tt'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20151028  122907  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'EDI meldinger'
     A                                      COLOR(WHT)
7.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spesialbehandling av EDI meldinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.01 A                                  5  2'Meldingstype . . .'
7.01 A            C1KMTY         1A  B  5 22COLOR(YLW)
7.01 A  31                                  ERRMSG('Kun kode F er gyldig!' 31)
7.01 A                                      OVRDTA
7.01 A                                  5 33'F=Faktura'
7.01 A                                  6  2'Identitetstype . .'
7.01 A            C1KTYP         1A  B  6 22COLOR(YLW)
7.01 A  32                                  ERRMSG('Intervall er 1-4 !' 32)
7.01 A  35                                  ERRMSG('Ugyldig kombinasjon !' 35)
     A                                      OVRDTA
7.01 A                                  6 33'1=Leverandør'
7.01 A                                  7 33'2=Kunder'
7.01 A                                  8 33'3=Leverandørkategori'
7.01 A                                  9 33'4=Kundekategori'
     A*                                 8 33'3=Konverteringsregel (IKKE AKTIVER-
     A*                                     T)'
7.01 A                                 11  2'Identitet. . . . +'
7.01 A            C1KID1        10A  B 11 22COLOR(YLW)
7.01 A  33                                  ERRMSG('Ugyldig kode !' 33)
7.02 A  36                                  ERRMSG('Feil lengde på verdi !' 36)
     A                                      OVRDTA
7.02 A                                 11 33'NB Husk å legge inn foranstilte -
7.02 A                                      0 er. Dvs kategori 40 = 0040'
7.01 A**                               12  2'Identitet 2. . . +'
7.01 A**          C1KID2        10A  B 12 22COLOR(YLW)
7.01 A**33                                  ERRMSG('Ugyldig kode !' 33)
     A**                                    OVRDTA
7.01 A                                 13  2'Kode . . . . . . .'
7.01 A            C1KKOD         1A  B 13 22COLOR(YLW)
7.01 A  34                                  ERRMSG('Intervall er 1-3 !' 34)
     A                                      OVRDTA
7.01 A                                 13 33'1=Utelate omregning av enheter'
7.01 A                                 14 33'2=Stoppes fra automatisk innlesing'
7.01 A                                 15 33'3=Utelates fra beløpskontroll'
7.01 A**                               13 33'2=Enhetskonvertering (IKKE AK-
7.01 A**                                    TIVERT)'
7.01 A                                 26  2'F3=Gå tilbake  F4=Søk'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20151026  125839  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Behandlingsregel finnes '
     A                                  3  2'fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20151026  135249  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'EDI meldinger'
     A                                      COLOR(WHT)
7.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spesialbehandling av EDI meldinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.01 A                                      ------------------------------------
7.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.01 A                                  5  2'Meldingstype . . . '
7.01 A            C1KMTY         1   O  5 22COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  6  2'Type . . . . . . . '
7.01 A            C1KTYP         1   O  6 22COLOR(YLW)
     A                                      OVRDTA
7.01 A            C1KTTX        30   O  6 33COLOR(YLW)
     A                                      OVRDTA
7.01 A                                  7  2'Identitet. . . . .'
7.01 A            C1KID1        10A  O  7 22COLOR(YLW)
     A                                      OVRDTA
7.01 A            C1D1TX        30   O  7 33COLOR(YLW)
     A                                      OVRDTA
7.01 A**                                8  2'Identitet 2. . . .'
7.01 A**          C1KID2        10A  O  8 22COLOR(YLW)
     A**                                    OVRDTA
7.01 A**          C1D2TX        30   O  8 33COLOR(YLW)
     A**                                    OVRDTA
7.01 A                                  9  2'Kode . . . . . . .'
7.01 A            C1KKOD         1A  O  9 22COLOR(YLW)
     A                                      OVRDTA
7.01 A            C1KKTX        30   O  9 33COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Beskrivelse. . . .'
     A            C2KBES        35A  B 12 22COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A*
7.01 A                                 22  2'Opprettet dato . . . . . . .'
7.01 A            C2KODA         6Y 0O 22 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 23  2'Sist endret dato . . . . . .'
7.01 A            C2KEDA         6Y 0O 23 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 24  2'Endret av. . . . . . . . . .'
7.01 A            C2KEUS    R        O 24 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20151023  145628  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette spesialbehandling'
     A                                      DSPATR(RI)
     A                                  4  2'Meldinsgtype'
7.01 A            D1KMTY         1A  O  4 15
7.01 A                                  5  2'Type . . . .'
7.01 A            D1KTYP         1A  O  5 15
7.01 A                                  6  2'Identitet. .'
7.01 A            D1KID1        10A  O  6 15
7.01 A**                                7  2'Identitet 2.'
7.01 A**          D1KID2        10A  O  7 15
7.01 A                                  8  2'Kode . . . .'
7.01 A            D1KKOD         1A  O  8 15
7.01 A                                  9  2'Beskrivelse.'
7.01 A            D1KBES        35A  O  9 15
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20151028  122907  ASP_G0X     REL-V7R1M0  5770-WDS
7.01 A                                      WINDOW(*DFT 18 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Ny EDI regel'
7.01 A            K1KMTY         1A  B  4 21COLOR(YLW)
7.01 A  31                                  ERRMSG('Kun kode F er gyldig!' 31)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A            K1KTYP         1A  B  5 21COLOR(YLW)
7.01 A  32                                  ERRMSG('Intervall er 1-4 !' 32)
7.01 A  35                                  ERRMSG('Ugyldig kombinasjon !' 35)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A            K1KID1        10A  B  6 21COLOR(YLW)
7.01 A  33                                  ERRMSG('Ugyldig kode !' 33)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A**          K1KID2        10A  B  7 21COLOR(YLW)
7.01 A**                                    CHECK(RB)
7.01 A**                                    OVRDTA
7.01 A            K1KKOD         1A  B  8 21COLOR(YLW)
7.01 A  34                                  ERRMSG('Intervall er 1-3 !' 34)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.01 A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
7.01 A                                 12  2'Gammel type       '
7.01 A            K2KMTY         1A  O 12 21COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A            K2KTYP         1A  O 13 21COLOR(YLW)
     A                                      OVRDTA
7.01 A            K2KID1        10A  O 14 21COLOR(YLW)
     A                                      OVRDTA
7.01 A**          K2KID2        10A  O 15 21COLOR(YLW)
7.01 A**                                    OVRDTA
7.01 A            K2KKOD         1A  O 16 21COLOR(YLW)
     A                                      OVRDTA
7.01 A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
