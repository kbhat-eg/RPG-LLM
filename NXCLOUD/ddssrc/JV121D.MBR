     A*%%TS  SD  20100430  133920  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
      * 020714 BOF: Utvidet med info om DG
8.01 A*PRG2 140622 BOF : utvidet prisgruppe fra 1 til 2 posisjoner
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090903  094926  BOF610      REL-V5R4M0  5722-WDS
     A            B1VARE    R        H      REFFLD(JVVARE)
     A            B1NOBB    R        H      REFFLD(JVNOBB)
     A            B1LVAR    R        H      REFFLD(JVLVAR)
     A            B1SKOD    R        H      REFFLD(JVDSKO)
6.3j A            B1LOGV         2S 0H
6.3j A            B1UDEV         2S 0H
     A            B1VALG         2Y 0B 12  2COLOR(YLW)
     A                                      EDTCDE(4)
6.3j A                                      VALUES(2 3 5 6 7 8 9 10 11 13 14  -
6.3j A                                             15 17 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1TEK1    R   -5   O 12  5REFFLD(JVTEK1)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1NYPO        13A  O 12 36
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1OGRP    R        O 12 50REFFLD(JVOGRP)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1HGRP    R        O 12 53REFFLD(JVHGRP)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1UGRP    R        O 12 56REFFLD(JVUGRP)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1S1PR    R        O 12 60REFFLD(JBS1PR)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1MIDL         1A  O 12 72
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1PKOD    R        O 12 74REFFLD(JVDPKO)
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1VTYP         1A  O 12 76
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1OSTS         1A  O 12 78
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A            B1HKOD         1A  O 12 80
6.3i A  50                                  COLOR(BLU)
6.3i A  51                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20100430  133920  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring til eget vareregister'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JV121B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk .'
     A                                      COLOR(BLU)
     A            B2STEK    R        B  4 11REFFLD(JSSTEK)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  4 49'Lev. . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 58REFFLD(JVLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2NAVN    R  -20   O  4 66REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5 49'Prisgiv.'
     A                                      COLOR(BLU)
     A            B2PGIV    R        O  5 58REFFLD(JVLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2PNAV    R  -20   O  5 66REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  2'Varegrp.'
     A                                      COLOR(BLU)
7.xx A            B2OGRP    R        O  5 11REFFLD(JVOGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2HGRP    R        O  5 14REFFLD(JVHGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2UGRP    R        O  5 17REFFLD(JVUGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 21'Sort.'
     A                                      COLOR(BLU)
7.xx A            B2OSOR    R   -3   O  5 27REFFLD(JVSORT)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  7  2'2=Endre 3=Hold tilbake 5=Vis  6=Fr-
7.xx A                                      igi'
7.xx A                                      COLOR(BLU)
7.xx A                                  7 40'11=Prodinfo 13=Dupl. GTIN 14=Logi.-
     A                                      vare'
     A                                      COLOR(BLU)
7.xx A                                  6 49'Lev.type'
     A                                      COLOR(BLU)
7.xx A            B2LETY         1S 0O  6 59COLOR(BLU)
7.xx A                                  6 67'Prisgruppe'
     A                                      COLOR(BLU)
7.xx A            B2PRGR         2A  O  6 78COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  8  2'7=Pris 8=Enhet 9=Søketekst 10=Beti-
7.xx A                                      ngelse'
7.xx A                                      COLOR(BLU)
7.xx A                                  8 43'15=Kalkulasjon 17=Slett midlert. p-
7.xx A                                      ris'
     A                                      COLOR(BLU)
7.xx A                                  6  2'Ant.F'
     A                                      COLOR(BLU)
7.xx A            B2ANTF         4Y 0O  6  8COLOR(BLU)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A  85                              6 13'Opprettes som skaffevarer'
     A                                      COLOR(BLU)
     A                                      DSPATR(RI)
     A                                  9  2'   Varetekst                      -
     A                                      Nobbnr   Lev. varenr     Varenr'
     A                                      COLOR(WHT)
     A            B2TEK1    R   -5   B 10  5REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2NOBB    R        B 10 36REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2LVAR    R   -5   B 10 45REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2VARE         8Y 0B 10 61COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  5'Varetekst                    '
7.xx A                                      COLOR(WHT)
     A  80                             11 36'Nobbnr'
     A  80                                  COLOR(WHT)
     A  81                             11 36'Lev. varenr'
     A  81                                  COLOR(WHT)
     A  82                             11 36'Varenr'
     A  82                                  COLOR(WHT)
7.xx A                                 11 50'OG'
     A                                      COLOR(WHT)
7.xx A                                 11 53'HG'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 56'UG'
7.xx A                                      COLOR(WHT)
     A  80                             11 61'Salgspris'
     A  80                                  COLOR(WHT)
     A  81                             11 61'   DG %'
     A  81                                  COLOR(WHT)
     A  82                             11 61'Salgspris'
     A  82                                  COLOR(WHT)
7.xx A                                 11 72'M'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 74'P'
     A                                      COLOR(WHT)
7.xx A                                 11 76'T'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 78'S'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 80'H'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031014  132820  HKO         REL-V5R2M0  5722-WDS
     A                                 22  2'F1=Filter  F3=Gå tilbake  F5=Forny-
     A                                        F9=Gjenta søk  F13=Status  F14=Gr-
     A                                      p.behandl.'
     A                                      COLOR(BLU)
     A                                 23  2'F15=Betingel.  F16=Påslag  F17=For-
     A                                      pakning  F18=Sortiment  F20=Overfør-
     A*                                      F23=Bytt'
     A                                        F23=Bytt'
     A                                      COLOR(BLU)
