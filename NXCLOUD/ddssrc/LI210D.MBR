     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
7.10 A*      170919  bsa  Nytt program basert på 132 posisjoners bilde
     A*
     A*----------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A            B1LDOR    R        H      REFFLD(LILDOR)
     A            B1FSNI        18A  H
     A            B1FNIV         6Y 0H
     A            B1VALG         1Y 0B 12  3
     A                                      VALUES(0 1)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            B1SSCC        18S 0O 12  5
     A            B1SCAN         1A  O 12 28
     A            B1KORD         1A  O 12 39
     A            B1STAT         2A  O 12131
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  33                                  COLOR(GRN)
     A  34                                  COLOR(WHT)
      *
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF05
     A                                      CA06
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW     W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Scanne kolli'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5 10'Scan et kolli. . . . . . . .'
     A            B2SSCC        18Y 0B  5 40EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 10'Antall kolli på pakkseddel .'
     A            B2PANT         8Y 0O  7 40EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 10'Pakkseddelnummer . . . . . .'
     A            B2PAID        17A  O  8 40
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PVER         6Y 0O  8 60EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 68'Meldingsnummer'
     A            B2MNUM         8Y 0O  8 84EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.04 A                                  8 93'Status'
7.04 A            B2STAT         2A  O  8100COLOR(YLW)
7.04 A                                      OVRDTA
7.04 A            B2AARR         4Y 0O  8103EDTCDE(Z)
     A                                      DSPATR(ND)
     A                                  9 10'Leverandør . . . . . . . . .'
     A            B2LDOR         6Y 0O  9 40EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LNAV        30A  O  9 50
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  5'1=Vis bestillinger'
     A                                      COLOR(BLU)
     A                                 11  5'SSC kode'
     A                                      COLOR(WHT)
     A                                 11 25'Scannet'
     A                                      COLOR(WHT)
     A                                 11 34'Kundeordre'
     A                                      COLOR(WHT)
7.04 A                                 11131'St'
     A                                      COLOR(WHT)
      *
     A          R B2CMD
     A*%%TS  SD  20041125  110702  GRO530      REL-V5R2M0  5722-WDS
     A                                 26  2'F3=Lagre for videre oppfølging'
     A                                      COLOR(BLU)
     A                                 26 34'F5=Forny'
     A                                      COLOR(BLU)
     A                                 26 44'F6=Godkjenn kolli'
     A                                      COLOR(BLU)
     A                                 26 63'F7=Ferdigstill varemottak'
     A                                      COLOR(BLU)
     A                                 26 89'F8=EDI pakksedler'
     A                                      COLOR(BLU)
     A                                 26108'F9=Tilbakefør pakkseddel'
     A                                      COLOR(BLU)
     A*                                26 78'F13=Statuskoder'
     A*                                     COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 27132' '
