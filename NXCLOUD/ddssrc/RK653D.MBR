     A*%%TS  SD  20040106  014156  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK653D
     A* Beskrivelse . . : Kunder, betalingsvarsel - paramter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150902 KOB  Nytt program
6.30 A*       150618 BSA  Sperre på nondisplay felt.
7.xx A*       060516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20040106  014156  KOB         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Betalingsvarsel - utskrift'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RK653A2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  8'Kjøredato. . . . . . . .:'
     A            A2PKDA    R        B  5 34REFFLD(RKPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                  7  8'Forfallsdato . . . . . .:'
     A            A2FFDA    R        B  7 34REFFLD(RKPKDA)
     A  32                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      DSPATR(MDT)
     A                                  9  8'Purregebyr . . . . . . .:'
     A            A25GEB    R        B  9 34REFFLD(RA5GEB)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                 11  8'Bankgiro skrives . . . .:'
     A            A25RPG    R        B 11 34REFFLD(RA5RPG)
     A  36                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES('G' 'S' 'R')
     A  36                                  ERRMSGID(RMA0590 *LIBL/QMFOKON 36)
7.xx A                                 11 36'G=Gebyr, S=Forfalt saldo+gebyr og-
     A                                       renter,'
7.xx A                                 12 37'R=Gebyr og renter'
     A                                 13  8'Rente % p.a. . . . . . .:'
     A  71                                  DSPATR(ND)
     A            A26PPR    R        B 13 34REFFLD(RA6PPR)
     A                                      EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  71                                  DSPATR(ND)
7.xx A  71                                  DSPATR(PR)
     A                                 14  8'Inkassotekst'
     A                                 14 21'ved'
     A                                 14 25'. . . .:'
     A            A25RTX    R        B 14 34REFFLD(RA5RTX)
     A                                      COLOR(YLW)
     A                                 14 37'purring'
     A                                 16  8'Ajourfør regnskap. . . .:'
     A            A25RPO    R        B 16 34REFFLD(RA5RPO)
     A  36                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A  36                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 36)
     A                                 16 37'(J=Ja)'
     A                                 18  8'Regnskapsperiode . . . .:'
     A            A2PRPE    R        B 18 34REFFLD(RKPPER)
     A  39                                  DSPATR(PC)
     A  39                                  ERRMSGID(RMH0040 *LIBL/QMFOKON 39)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            A2PRÅR    R        B 18 37REFFLD(RKPRÅR)
     A  40                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      COMP(GE 2000)
     A  40                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 40)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
