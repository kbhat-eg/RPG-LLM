     A*%%TS  SD  20051026  224008  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EK510D
     A* Beskrivelse . . : Kundebonus
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051021  082418  HKO530      REL-V5R3M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1RKAT    R        O  9  4REFFLD(EKRKAT)
7.xx A            B1KUND    R        O  9  8REFFLD(EKKUND)
7.xx A            B1KPRO    R        O  9 15REFFLD(EKKPRO)
7.xx A            B1OGRP    R        O  9 22REFFLD(EKOGRP)
7.xx A            B1HGRP    R        O  9 25REFFLD(EKHGRP)
7.xx A            B1UGRP    R        O  9 28REFFLD(EKUGRP)
7.xx A            B1LDOR    R        O  9 32REFFLD(EKLDOR)
7.xx A            B1MODN    R        O  9 39REFFLD(EKMODN)
7.xx A            B1VARE    R   -7   O  9 48REFFLD(EKVARE)
7.xx A            B1ENHE    R        O  9 57REFFLD(EKENHE)
7.xx A            B1LETY    R        O  9 61REFFLD(EKLETY)
7.xx A            B1BOTY    R        O  9 63REFFLD(EKBOTY)
7.xx A            B1FDAT         6Y 0O  9 66EDTCDE(Z)
7.xx A            B1TDAT         6  0O  9 73EDTCDE(Z)
7.xx A            B1MERK         1A  O  9 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051026  223046  HKO550      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kundebonus'
     A                                      COLOR(WHT)
7.xx A                                  2 38'EK510B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A                                      COLOR(BLU)
     A            B2BKUN    R        O  4 11REFFLD(EKBKUN)
     A                                      COLOR(BLU)
     A            B2BKNA        30A  O  4 18COLOR(BLU)
7.xx  *
7.xx A                                  5  4'Rkat'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  9'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 15'KProsj'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 22'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 25'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 28'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 32'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 39'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 48'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 57'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 61'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 63'BT'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis  7=Bonuselementer'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Rkat'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 15'KProsj'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 22'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 28'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 39'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 48'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 57'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'BT'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 67'Fdato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 73'Tdato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'M'
7.xx A                                      COLOR(WHT)
7.xx A**                                8  2'V Rab. Kunde Kunde  Og Hg Ugr Lev.-
7.xx A**                                       Modulnr. Varenr.  Enh L BT  Fra -
7.xx A**                                       Til   M'
7.xx A**                                    COLOR(WHT)
7.xx A**                                9  4'kat        prosj.                 -
7.xx A**                                                                 dato  -
7.xx A**                                     dato'
7.xx A**                                    COLOR(WHT)
7.xx A            B2RKAT    R        B  6  4REFFLD(EKRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2KUND    R        B  6  8REFFLD(EKKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KPRO    R        B  6 15REFFLD(EKKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OGRP    R        B  6 22REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2HGRP    R        B  6 25REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2UGRP    R        B  6 28REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  6 32REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2MODN    R        B  6 39REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2VARE    R   -7   B  6 48REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ENHE    R        B  6 57REFFLD(EKENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2LETY    R        B  6 61REFFLD(EKLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2BOTY    R        B  6 63REFFLD(EKBOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20051026  224008  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051026  223333  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF20
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise bonus-nivå'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rabattkategori . . . . . . .'
     A            C1RKAT    R        O  4 31REFFLD(EKRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1RTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kunde. . . . . . . . . . . .'
     A            C1KUND    R        O  5 31REFFLD(EKKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundeprosjekt. . . . . . . .'
     A            C1KPRO    R        O  6 31REFFLD(EKKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PNAV    R        O  6 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . . . . . . . .'
     A            C1OGRP    R        O  7 31REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGRP    R        O  7 34REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGRP    R        O  7 37REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT    R        O  7 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  8 41COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O  9 31REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTXT        35A  O  9 41COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenr . . . . . . . . . . .'
     A            C1VARE    R   -7   O 10 31REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Enhet. . . . . . . . . . . .'
     A            C1ENHE    R        O 11 31REFFLD(EKENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1ETXT    R        O 11 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 12 31REFFLD(EKLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O 12 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Bonustype. . . . . . . . . .'
     A            C1BOTY    R        O 13 31REFFLD(EKBOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BTXT    R        O 13 41REFFLD(VATBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Fra-dato . . . . . . . . . .'
     A            C1FDAT         6Y 0O 14 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 15  2'Til-dato . . . . . . . . . .'
     A            C1TDAT         6Y 0O 15 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17  2'Merknad. . . . . . . . . . .'
     A            C2MERK    R        O 17 31REFFLD(EKMERK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Beregnet bonusgrunnlag . . .'
     A            C2BGRU    R        O 19 31REFFLD(EKBGRU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Beregnet bonus . . . . . . .'
     A            C2SBON    R        O 20 31REFFLD(EKSBON)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Beregnet dato. . . . . . . .'
     A            C2BDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Beregnet tidspunkt . . . . .'
     A            C2BTIM         6Y 0O 22 31COLOR(YLW)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(FREUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
