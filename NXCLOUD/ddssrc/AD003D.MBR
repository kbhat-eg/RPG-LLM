     A*%%TS  SD  20061002  091545  SST         REL-V5R3M0  5722-WDS
     A*
7.xx A*          271117 Bsa Endret til å passe ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IADFRF IADFRFR)
     A                                      PRINT
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1SFL                     SFL
     A*%%TS  SD  19980123  110414  MWE         REL-V3R7M0  5716-PW1
     A            B1VALG    R        B  6  2REFFLD(ZZVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('1' ' ')
     A                                      CHKMSGID(ZM00010 *LIBL/QMFADGANG)
     A            B1DRUT    R        O  6  5REFFLD(IADRUT)
     A            B1DRTX    R        O  6 16REFFLD(IADRTX)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  19980123  110414  MWE         REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
7.xx A                                  1  5'Rutine '
7.xx A                                      COLOR(WHT)
7.xx A                                  3  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  4  1' 1=Hente inn Rutine      '
     A                                      COLOR(BLU)
7.xx A                                  5  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  5'Rutine  '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 16'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2DRUT    R        B  2  5REFFLD(IADRUT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B2DRTX    R        B  2 12REFFLD(IADRTX)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  19951103  132646  HK          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 15 70)
     A                                 14  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 14 14'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
