     A*%%TS  SD  20140526  130031  BOF630      REL-V7R1M0  5770-WDS
     A*
     A*%%EC
6.NU A* 260514 Utvidelse ihht nummerserieutvidelse
6.2i A* 6.20  080515 nho  Feilmelding dersom vare er utgått og det på lager ligg
     A*                   utgått dato frem i tid
6.33 A* 6.30  170216 BSA  Problemer med feilmeldinger i jacada. Linje 24 KAN
6.33 A*                   ikke tas i bruk til F-taster. Da blir det JIT gui
6.33 A*                   så fort det sendes ut feilmeldinger.
6.37 A* 7.00  100719 bsa  Disablet F23, siden den ikke fungerer
7.xx A* 7.00  030316 bsa  Endringer relatert til ny GUI
Q.01 A*       270319 bsa  Mulighet for større skjerm
Q.02 A*       270319 bsa  Quick wins - Lagrer ordre fra egen knapp i
Q.02 A*                   cloud, aktiverer F5.
7.10 A*       210420 bsa  Tilpasset bred skjerm
7.11 A* K00   270420 bsa  Gjort om til tabellarisk visning i subfile
7.19 A* K00   120121 ask  Lagt inn visning av aktuell valutakurs
8.01 A* 800   290421 ask  Lagt inn visning av mer valutainformasjon
8.01 A* 800               og styring med indikator om det skal vises
     A*
     A*----------------------------------------------------------------
     A*
7.10 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20010924  094005  HKO         REL-V4R4M0  5769-PW1
     A            B1LINE    R        H      REFFLD(LDLINE)
     A            B1VARE         8A  H
     A            B1NOBB    R        H      REFFLD(LDNOBB)
7.11 A*           B1TEK1    R        H      REFFLD(LDTEK1)
7.11 A*           B1ANTA         9S 3H
7.11 A*           B1ENHE    R        H      REFFLD(LDENH1)
7.11 A*           B1INPR         8S 2H
7.11 A*           B1RABA    R        H      REFFLD(LDRAB1)
7.11 A*           B1SUMN    R        H      REFFLD(LDSUMI)
7.11 A*           B1SUMK    R        H      REFFLD(LDSUMK)
7.10 A            B1VALG    R        B 12  2REFFLD(LÅVALG)
7.xx A                                      VALUES(0 2 4 5 9)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
7.xx A                                      COLOR(YLW)
7.10 A            B1LETY         1Y 0O 12  4EDTCDE(Z)
7.11 A            B1IDEN         8A  O 12  6
7.11 A            B1TEK1    R        O 12 16REFFLD(LDTEK1)
7.11 A*           B1TEK2        10   O 12 43
7.11 A            B1ANTA         9Y 3O 12 55EDTCDE(M)
7.11 A            B1ENHE    R        O 12 67REFFLD(LDENH1)
7.11 A            B1INPR         8Y 2O 12 71EDTCDE(M)
7.11 A            B1RABA    R        O 12 82REFFLD(LDRAB1)
7.11 A                                      EDTCDE(M)
7.11 A            B1SUMN    R        O 12 90REFFLD(LDSUMI)
7.11 A                                      EDTCDE(M)
7.11 A            B1SUMK    R        O 12104REFFLD(LDSUMK)
7.11 A                                      EDTCDE(M)
7.11 A**          B1TEXT        75A  O 12  6
7.11 A            B1VEKT         5Y 0O 12118EDTCDE(Z)
7.11 A            B1VOLU         6Y 3O 12124EDTCDE(M)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140526  130031  BOF630      REL-V7R1M0  5770-WDS
7.10 A                                      SFLSIZ(0013)
7.10 A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A                                      CF10
     A                                      CF11
6.30 A                                      CF14
6.36 A                                      CF20
     A                                      CF21
6.37 A**                                    CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            B2ANTA    R        H      REFFLD(LDANTA)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.10 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.10 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Registrering av varelinjer'
     A                                      COLOR(WHT)
7.xx A                                  2 34'LD100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.10 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.10 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.10 A                                      ------------------------------------
7.10 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Best.:'
     A                                      COLOR(WHT)
     A            B2OTYP    R        O  4 18REFFLD(LOOTYP)
     A                                      OVRDTA
     A            B2NAVN    R   -1   O  4 21REFFLD(LONAVN)
     A                                      OVRDTA
     A            B3NAVN    R        O  4 51REFFLD(LONAVN)
     A                                      OVRDTA
7.10 A                                  4 83'Tlf:'
7.10 A            B2TLFN    R        O  4 88REFFLD(RLTLFN *LIBL/RFRF)
7.10 A                                      OVRDTA
7.10 A            B2VALG         2  0B  4104EDTCDE(Z)
7.10 A                                      COLOR(YLW)
     A                                  5  2'Lev. :'
     A                                      COLOR(WHT)
     A            B2LDOR    R        O  5  9REFFLD(LOLDOR)
     A                                      OVRDTA
     A            B2ADR1    R   -1   O  5 21REFFLD(LOADR1)
     A                                      OVRDTA
     A            B3ADR1    R        O  5 51REFFLD(LOADR1)
     A                                      OVRDTA
7.10 A                                  5 83'Mob:'
7.10 A            B2MOBN    R        O  5 88REFFLD(RLMOBN *LIBL/RFRF)
7.10 A                                      OVRDTA
     A                                  6  2'Ordre:'
     A                                      COLOR(WHT)
     A  23                                  DSPATR(ND)
     A            B2ORNR    R        O  6  9REFFLD(LOORNR)
     A                                      OVRDTA
     A  23                                  DSPATR(ND)
     A            B2ORSU    R        O  6 18REFFLD(LOORSU)
     A                                      OVRDTA
     A  23                                  DSPATR(ND)
     A            B2STED    R   -1   O  6 21REFFLD(LOSTED)
     A                                      OVRDTA
     A            B3STED    R        O  6 51REFFLD(LOSTED)
     A                                      OVRDTA
7.10 A                                  6 83'Val'
8.01 A  24                                  DSPATR(ND)
7.10 A            B2VALU         3A  O  6 88OVRDTA
8.01 A  24                                  DSPATR(ND)
7.19 A            B2KURS        10Y 6O  6 92EDTCDE(1)
8.01 A  24                                  DSPATR(ND)
8.01 A                                  6106'Låst'
8.01 A  24                                  DSPATR(ND)
8.01 A            B2KURL        10Y 6O  6111EDTCDE(1)
7.19 A                                      OVRDTA
8.01 A  24                                  DSPATR(ND)
      *
7.10 A            B2OTXT    R        O  7  2REFFLD(VAOTXT *LIBL/FFRF)
     A                                      COLOR(TRQ)
     A                                      OVRDTA
7.10 A                                  7 40'Kampanje'
7.10 A            B2KAMP         5A  O  7 49OVRDTA
7.10 A                                  7 83'Vekt'
7.10 A                                      COLOR(WHT)
7.10 A            B2VEKT         6Y 0O  7 95EDTCDE(M)
7.10 A                                      OVRDTA
7.10 A                                  7104'M3'
7.10 A                                      COLOR(WHT)
7.10 A            B2VOLU         9Y 3O  7113EDTCDE(M)
7.10 A                                      OVRDTA
7.10 A  70                              8 40'MVA'
     A                                      COLOR(WHT)
7.10 A  70        B2MVAB        11Y 2O  8 45EDTCDE(M)
     A                                      OVRDTA
7.10 A  70                              8 60'Faktura'
     A                                      COLOR(WHT)
7.10 A  70        B2TOTF        11Y 2O  8 69EDTCDE(M)
     A                                      OVRDTA
7.10 A  70                              8 83'Diff'
     A                                      COLOR(WHT)
7.10 A  70        B2DIFF        11Y 2O  8 89EDTCDE(M)
     A                                      OVRDTA
7.10 A                                  8104'Netto'
     A                                      COLOR(WHT)
7.10 A            B2NETT        11Y 2O  8111EDTCDE(M)
     A                                      OVRDTA
7.10 A                                  8  2'LT'
7.xx A                                      COLOR(WHT)
7.10 A                                  8  7'Vare'
     A                                      COLOR(WHT)
7.10 A            B2LTYP    R        B  9  2REFFLD(LDLTYP)
     A  31                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
7.11 A                                      OVRDTA
     A                                  9  5'+'
     A            B2VARE        26A  B  9  7COLOR(YLW)
     A  33                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(LM00290 *LIBL/QMFLAGR 36)
6.2I A  40                                  ERRMSGID(LM00410 *LIBL/QMFLAGR 40)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.11 A                                  9104'Kost'
7.11 A                                      COLOR(WHT)
7.11 A            B2KOST        11Y 2O  9111EDTCDE(M)
8.01 A                                      OVRDTA
8.01 A                                 10104'NOKbel'
8.01 A  24                                  DSPATR(ND)
8.01 A            B2NETV        11Y 2O 10111EDTCDE(M)
8.01 A                                      OVRDTA
8.01 A  24                                  DSPATR(ND)
7.10 A                                 10  2'2=Endre  4=Slett-
7.10 A                                        5=Vis  9=Sett inn linje'
     A                                      COLOR(BLU)
7.10 A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.10 A                                 11  4'L'
     A                                      COLOR(WHT)
7.11 A N71                             11  6'Varenr'
7.11 A                                      COLOR(WHT)
7.11 A  71                             11  6'NOBBnr'
7.11 A                                      COLOR(WHT)
7.11 A*           B2OVER        75A  O 11  6COLOR(WHT)
7.11 A*                                     OVRDTA
7.11 A                                 11 16'Varetekst'
7.11 A                                      COLOR(WHT)
7.11 A                                 11 57'Antall'
7.11 A                                      COLOR(WHT)
7.11 A                                 11 67'Enh'
7.11 A                                      COLOR(WHT)
7.11 A                                 11 72'Innkj.pris'
7.11 A                                      COLOR(WHT)
7.11 A                                 11 83'Rabatt'
7.11 A                                      COLOR(WHT)
7.11 A                                 11 93'Sum netto'
7.11 A                                      COLOR(WHT)
7.11 A                                 11108'Sum kost'
7.11 A                                      COLOR(WHT)
7.11 A                                 11119'Vekt'
7.11 A                                      COLOR(WHT)
7.11 A                                 11127'M3'
7.11 A                                      COLOR(WHT)
     A            B2ONUM    R        O  4  9REFFLD(LONUMM)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20021120  085159  HKO         REL-V5R1M0  5722-WDS
7.10 A                                 25  2'F1=Vis tot.  F2=Tekstl.  F3=Avslut-
7.10 A                                      t  F4=Varesøk  F5=Hurtiglagre  F6=K-
7.10 A                                      ampanje  F9=Lagerbeh.  F10=Faste op-
7.10 A                                      pl.  F11=Topp/bunntekst'
     A                                      COLOR(BLU)
7.10 A                                 26  2'F14=Leverd.info.  F20=Bytt til NOB-
7.10 A                                      B-nr  F21=Vare/pris'
     A                                      COLOR(BLU)
6.37 A**                               23  2'F5=Hurtiglagre  F23=Kostpris  F20=-
     A*
     A          R F1WIN
     A*%%TS  SD  20020623  211603  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Utkjøring/utplukk '
     A                                      DSPATR(RI)
     A                                  4  2'Velg behandling'
     A            F1VALG    R        B  4 18REFFLD(LÅVALG)
     A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0050 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 22'0=Oppdatering'
     A                                  5 22'1=Utskrift'
     A                                  6 22'2=Utplukk'
6.13 A                                  7 22'3=Send best.'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R T1WIN
     A*%%TS  SD  20021119  175255  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 42)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Sett inn linje mellom eksisterend-
     A                                      e '
     A                                      DSPATR(RI)
     A                                  4  2'Opprett etter'
     A            T1VALG    R        B  4 16REFFLD(LÅVALG)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 25'1=ja,0=nei'
     A                                  6  2'Linjetype . .'
     A            T1LTYP    R        B  6 16REFFLD(LDLTYP)
     A  31                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Vare. . . . .'
     A            T1VARE        26A  B  7 16COLOR(YLW)
     A  33                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.27 A*
6.27 A          R X1WIN
6.27 A*%%TS  SD  20020623  211603  HKO         REL-V5R1M0  5722-WDS
6.27 A                                      WINDOW(*DFT 9 35)
6.27 A                                      CA03
6.27 A                                      CA12
6.27 A                                      PUTOVR
6.27 A                                  2  2'    V A R S E L    '
6.27 A                                      DSPATR(RI)
6.27 A  37N38N39                        4  2'Ordren finnes ikke lenger.'
6.27 A  37N38N39                        5  2'Ingen synkronisering kan foretas!'
6.27 A  38N37N39                        4  2'Ordren er dellevert.'
6.27 A  38N37N39                        5  2'Ingen synkronisering kan foretas!'
6.2B A  39N37N38                        4  2'Ordren er intern.'
6.2B A  39N37N38                        5  2'Ingen synkronisering kan foretas!'
6.27 A                                  8  2'F3=Gå tilbake'
6.27 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
