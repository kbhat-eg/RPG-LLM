     A*%%TS  SD  20080321  205043  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK100D
     A* Beskrivelse . . : Kunder, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
PDF  A*       080610 BSA  Foreløpig fjernes mulighet for oppslag mot arkiv.
6.20 A*       051113 NHO  Lagt inn feilmelding dersom leverandøren prøves slett-
     A*                   et og det finnes varer i VVARPF
     A*
7.xx A*     BSA  240216    Endringer relatert til ny GUI
7.01 A*       171214 NHO  Kunde har ekstra feilmelding
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20020730  235810  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1LEVR    R        O  9  4REFFLD(RLLEVR)
     A            B1NAVN    R        O  9 11REFFLD(RLNAVN)
     A            B1STED        27   O  9 42
     A            B1TLFN    R        O  9 70REFFLD(RLTLFN)
     A            B1ALFA    R        H      REFFLD(RLALFA)
     A            B1PONR    R        H      REFFLD(RLPONR)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080321  205043  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CA09
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandører'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RL100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk .'
     A            B2STEK        35A  B  4 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Leverandørnavn       '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 42'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A            B2LEVR    R        B  6  4REFFLD(RLLEVR)
     A  38                                  ERRMSGID(RML0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RML0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RML0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  6 11REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PONR    R        B  6 42REFFLD(RLPONR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
PDF  A                                       6=Utskrift  8=Vis poster'
PDF  A**                                     6=Utskrift  8=Vis poster  9=Vis ar-
PDF  A**                                    kiv'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Leverandørnavn     '
     A                                      COLOR(WHT)
7.xx A N80                              8 42'Postnr/poststed'
     A                                      COLOR(WHT)
7.xx A  80                              8 42'Gateadresse    '
     A                                      COLOR(WHT)
7.xx A N80                              8 73'Telefon'
     A                                      COLOR(WHT)
7.xx A  80                              8 73'Telefax'
     A                                      COLOR(WHT)
7.xx A  81                              8 70'Foretaksnr'
     A                                      COLOR(WHT)
7.xx A  82                              8 70'Bankktonr '
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  20050827  114801  KOB540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 22  2'F3=Gå tilbake  F5=Aktive/Alle  F6=-
     A                                      Opprett  F8=Poster  F9=Gjenta søk'
     A                                      COLOR(BLU)
     A                                 23  2'F11=Endre liste'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020815  232022  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandørnummer . . . . . .'
     A            C1LEVR    R        B  5 31REFFLD(RLLEVR)
     A  31                                  ERRMSGID(RML0030 *LIBL/QMFOKON 31)
7.01 A  38                                  ERRMSGID(RML0005 *LIBL/QMFOKON 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020730  235810  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
6.20 A*                                 2  2'Leverandør  finnes fra før'
6.20 A N39                              2  2'Leverandør  finnes fra før'
6.20 A  39                              2  2'Leverandør kan ikke slettes'
6.20 A  39                              3  2'Finnes i vareregisteret'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020730  235810  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 58)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slett leverandør '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør. . .'
     A            D1LEVR    R        O  4 18REFFLD(RLLEVR)
     A  32                                  ERRMSGID(RML0220 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RML0221 *LIBL/QMFOKON 33)
     A                                  5  2'Leverandørnavn.'
     A            RLNAVN    R        O  5 18REFFLD(RLNAVN)
     A                                  6  2'Adresse . . . .'
     A            RLGATE    R        O  6 18REFFLD(RLGATE)
     A                                  7  2'Adresse 2 . . .'
     A            RLADR2    R        O  7 18REFFLD(RLADR2)
     A                                  8  2'Postadresse . .'
     A            RLSTED    R        O  8 18REFFLD(RLSTED)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R K1WIN
     A*%%TS  SD  20020815  234932  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 58)
     A                                      CA03
     A                                      CA12
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopier leverandør '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør. . .'
     A            K1LEVR    R        B  4 18REFFLD(RLLEVR)
     A  31                                  ERRMSGID(RML0030 *LIBL/QMFOKON 31)
     A  37                                  ERRMSGID(RML0005 *LIBL/QMFOKON 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4 26'Alfa.:'
     A            K1ALFA    R        B  4 33REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Leverandørnavn.'
     A            K1NAVN    R        B  5 18REFFLD(RLNAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      DSPATR(PC)
     A                                      OVRDTA
     A                                  6  2'Adresse . . . .'
     A            K1GATE    R        B  6 18REFFLD(RLGATE)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7  2'Adresse 2 . . .'
     A            K1ADR2    R        B  7 18REFFLD(RLADR2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Postnummer. . .'
     A            K1PONR    R        B  8 18REFFLD(RLPONR)
     A  32                                  ERRMSGID(RM01000 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Postadresse . .'
     A            K1STED    R        B  9 18REFFLD(RLSTED)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Telefon . . . .'
     A            K1TLFN    R        B 10 18REFFLD(RLTLFN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 31'Fax.:'
     A            K1TLFX    R        B 10 37REFFLD(RLTLFX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Betalingsbet. .'
     A            K1BETB    R        B 11 18REFFLD(RLBETB)
     A  33                                  ERRMSGID(RM01030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Lever.kategori.'
     A            K1KATG    R        B 12 18REFFLD(RLKATG)
     A  35                                  ERRMSGID(RM01110 *LIBL/QMFOKON 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Betalingsmåte .'
     A            K1BETM    R        B 13 18REFFLD(RLBETM)
     A  34                                  ERRMSGID(RM01020 *LIBL/QMFOKON 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
