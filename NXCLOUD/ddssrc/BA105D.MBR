     A*%%TS  SD  20141014  143831  ASK630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BA105D
     A* Beskrivelse . . : vedlikehold av korttyper
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       141014 ASK  Nytt program
7.01 A*       131117 bsa  Lagt inn reskontronummer for dobbelføring av
7.01 A*                   postering. Lagt til bilagsnummerserie for
7.01 A*                   bongbetalinger til reskontro. Viser også bilagskode.
7.xx A* 7.xx  230119  bsa Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20141014  143831  ASK630      REL-V7R1M0  5770-WDS
7.xx A            B1VALG    R        B  7  2REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1KTYP         3A  O  7  6
7.xx A            B1KTTX        20A  O  7 10
7.xx A            B1HKON         6  0O  7 32
     A                                      EDTCDE(Z)
7.xx A            B1SPES         4  0O  7 39
     A                                      EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140923  203726  ASK630      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0016)
7.xx A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betalingskort'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A                                  6 32'Konto'
     A                                      COLOR(WHT)
7.xx A                                  6 39'Spes'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  195916  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av korttyper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Korttype . . . . . . . . . .'
     A            C1KTYP         3A  B  5 31
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  005206  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Korttype finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20141014  110943  ASK630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av korttyper '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Korttype . . . . . . .'
     A            C1KTYP         3A  O  5 25COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . .'
     A            C2KTTX        20A  B  7 25COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
7.xx A                                  9  2'Hovedbokskonto . . . +'
     A            C2HKON         6Y 0B  9 25COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 10  2'Spesifikasjon. . . . .'
     A            C2SPES         4Y 0B 10 25COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
7.xx A                                 11  2'Bilagskode . . . . . +'
7.01 A            C2BILK         2Y 0B 11 25COLOR(YLW)
7.01 A  33                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 33)
7.01 A                                      OVRDTA
7.01 A  30                                  DSPATR(PR)
7.01 A                                      EDTCDE(Z)
7.01 A                                      CHECK(RB)
7.xx A                                 12  2'Resk.nr Santander. . +'
7.01 A            C2RESK         6Y 0B 12 25COLOR(YLW)
7.01 A  31                                  ERRMSGID(BMO2150 *LIBL/QMFSHOP 31)
7.01 A                                      OVRDTA
7.01 A  30                                  DSPATR(PR)
7.01 A                                      EDTCDE(Z)
7.01 A                                      CHECK(RB)
7.xx A                                 13  2'Bilagsnr kortbet.. . +'
7.01 A            C2BINU         6A  B 13 25COLOR(YLW)
6.10 A  32                                  ERRMSGID(AMN0020 *LIBL/QMFAGPL 32)
7.01 A                                      OVRDTA
7.01 A  30                                  DSPATR(PR)
7.01 A                                      CHECK(RB)
     A                                 20  2'Opprettet dato . . . .'
     A            C2ODAT         6Y 0O 20 25COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 21  2'Sist endret dato . . .'
     A            C2EDAT         6Y 0O 21 25COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 21 40'Endret av. . . . . . .'
     A            C2EUSR        10   O 21 63COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  005206  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette korttype '
     A                                      DSPATR(RI)
     A                                  4  2'Korttype . . . . .'
     A            D1KTYP         3A  O  4 21
     A                                  6  2'Beskrivelse. . . .'
     A            D1KTTX        20A  O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
