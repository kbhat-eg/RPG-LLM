     A*%%TS  SD  20171110  144250  G0XEGASSKO  REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA910D
     A* Beskrivelse . . : Regioner og regionkoblinger vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       051219 ASK  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20170920  152510  G0XEGASSKO  REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1REGI         2Y 0O  8  5
     A            B1LAGE         2Y 0O  8  9EDTCDE(Z)
     A            B1RTEK        30A  O  8 13
     A            B1LTEK        20A  O  8 44
     A            B1EBIZ         1Y 0O  8 71EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20170920  152510  G0XEGASSKO  REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av regioner og kobling-
     A                                      er'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Legg lager til region  -
     A                                      4=slett'
     A                                      COLOR(BLU)
     A                                  6  2'V  Rg  Lg  Region                 -
     A                                              Lager                Skjul -
     A                                      i ehandel'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20150612  095234  ASP_G01     REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett region'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20150615  141421  ASP_G01     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av regioner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Region . .'
     A            C1REGI         2Y 0B  6 15COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                  7  2'Beskrivelse'
     A            C1RTEK        30A  B  7 15COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20150612  122611  ASP_G01     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Region finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20150612  122611  ASP_G01     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kobling finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20171110  144250  G0XEGASSKO  REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Angi kjedekode for lager'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'Bruk XL eller BT'
     A          R C2BLD
     A*%%TS  SD  20171110  144250  G0XEGASSKO  REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold lagerkobling til regio-
     A                                      n'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Region . . . . . . . . . . .'
     A            C2REGI         2Y 0O  6 31COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Regionsnavn  . . . . . . . .'
     A            C2RTEK        30A  O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lager  . . . . . . . . . . .'
     A            C2LAGE         2Y 0B  9 31DSPATR(UL)
     A                                      OVRDTA
     A  40                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10  2'Lagernavn  . . . . . . . . .'
     A            C2LTEK        30A  O 10 31OVRDTA
     A                                      COLOR(YLW)
     A                                 12  2'Vise lager i ehandel . . . .'
     A            C2EBIZ         1Y 0B 12 31COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 12 35'(0=Ja, 1=Nei)'
     A                                 13  2'Kjedekode for lager  . . . .'
     A            C2KJED         2   B 13 31COLOR(YLW)
     A                                 13 35'(XL,BT)'
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20150614  181109  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette region '
     A                                      DSPATR(RI)
     A                                  4  2'Region . .'
     A            D1REGI         2Y 0O  4 14COLOR(YLW)
     A            D1RTEK        30A  O  4 17COLOR(YLW)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  7  4'Alle lagerkoblinger til denne regi-
     A                                      onen slettes'
     A                                      COLOR(RED)
     A          R D2WIN
     A*%%TS  SD  20150612  122611  ASP_G01     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kobling '
     A                                      DSPATR(RI)
     A                                  4  2'Region . .'
     A            D2REGI         2Y 0O  4 14COLOR(YLW)
     A            D2RTEK        30   O  4 17COLOR(YLW)
     A                                  5  2'Lager  . .'
     A            D2LAGE         2Y 0O  5 14COLOR(YLW)
     A            D2LTEK        20   O  5 17COLOR(YLW)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
