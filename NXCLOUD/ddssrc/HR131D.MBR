     A*%%TS  SD  20071219  081059  BOF560      REL-V5R4M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      MSGLOC(16)
     A          R B1SFL                     SFL
     A*%%TS  SD  20000926  132550  HKO         REL-V4R4M0  5769-PW1
     A            B1VARE    R        H      REFFLD(HRVARE)
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1TEK1         6A  O  8  4
     A            B1ENHE    R        O  8 11REFFLD(HRENHE)
     A            B1ANTA         5Y 1O  8 15EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040323  214143  ASK         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            B2SCA1        19A  B  4  2COLOR(YLW)
     A  31                                  ERRMSGID(HMR0120 *LIBL/QMFLAGR 31)
     A  22                                  DSPATR(PC)
     A*                                     OVRDTA
     A            B2SCA2         8A  B  5  2COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'2=Endre  4=Slett'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20000927  084512  HKO         REL-V4R4M0  5769-PW1
     A                                 15  2'F4=Søk  F5=Forny'
     A                                      COLOR(WHT)
     A          R C2BLD
     A*%%TS  SD  20040331  212926  ASK         REL-V5R2M0  5722-WDS
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CA05
     A                                      PUTOVR
     A            C1ENHE    R        H      REFFLD(HRENHE)
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            C1VARE    R        O  3  8REFFLD(HRVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1        19A  O  4  2COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK2        16A  O  5  2COLOR(YLW)
     A                                      OVRDTA
     A            C2LNAV        19   O  7  2COLOR(YLW)
     A                                      OVRDTA
     A            C2ENH1    R        O  9  2REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH2    R        O  9  6REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  81                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH3    R        O  9 10REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A  82                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 11  2'Antall:'
     A            C2ANTA    R   -1   B 11 10REFFLD(HRANTA)
     A  31                                  ERRMSGID(HMR0130 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 15  2'F1=Enhet  F2=Kalk.'
     A                                      COLOR(WHT)
6.31 A            C2INFO        18   O 14  2COLOR(RED)
     A                                 13  2'Skriv etikett'
     A            C2ETIK         1Y 0B 13 16COLOR(YLW)
     A          R D1BLD
     A*%%TS  SD  20040323  214143  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Bekrefte sletting'
     A                                  4  2'av registrering'
     A                                  6  2'Vare:'
     A            D1VARE    R        O  6  8REFFLD(HRVARE)
     A                                      COLOR(YLW)
     A            D1TEK1        19A  O  7  2COLOR(YLW)
     A            D1TEK2        16A  O  8  2COLOR(YLW)
     A                                 10  2'Enhet:'
     A            D1ENHE    R        O 10 10REFFLD(HRENHE)
     A                                      COLOR(YLW)
     A                                 11  2'Antall:'
     A            D1ANTA    R   -1   O 11 10REFFLD(HRANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A          R E1BLD
     A*%%TS  SD  20040604  155323  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Sluttbehandling'
     A                                  4  2'av varemottak'
     A                                  6  3'Valg:'
     A            E1VALG         1Y 0B  6  9COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  8  3'0=Lagre'
     A                                  9  3'1=Oppdater'
     A                                 15  3'F3=Tilbake'
     A                                      COLOR(WHT)
     A          R F1BLD
     A*%%TS  SD  20040331  222432  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Varenummer ikke'
     A                                  4  2'funnet!'
     A                                 12  3'F4=Søk'
     A                                      COLOR(WHT)
     A          R G1BLD
     A*%%TS  SD  20040331  212926  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Vare ikke funnet'
     A                                  4  2'på bestilling'
     A                                      COLOR(YLW)
     A                                  6  2'Legge til:'
     A            G1TILL         1Y 0B  6 13COLOR(YLW)
     A          R H1BLD
     A*%%TS  SD  20071219  081059  BOF560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Varenr. finnes '
     A                                  4  2'ikke i EVR, det'
     A                                  5  2'er Skaffevare. '
     A                                  6  2'Godkjennes ?'
     A            H1OK           1Y 0B  6 16COLOR(YLW)
     A          R I1BLD
     A*%%TS  SD  20040331  212926  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                  1  2' VAREMOTTAK '
     A                                      DSPATR(RI)
     A                                  3  2'Vare ikke funnet'
     A                                  4  2'på bestilling.  '
     A                                  5  2'Skaffevare kan  '
     A                                  6  2'ikke legges til.'
     A          R J1BLD
     A*%%TS  SD  20080103  144410  BOF560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' LAGERTELLING '
     A                                      DSPATR(RI)
     A                                  5  2'Trykk <Enter> for å'
     A                                  6  2'bekrefte antall    '
     A                                  7  2'større enn 1000    '
     A                                  8  2'<F3> returnerer til'
     A                                  9  2'registreringsbildet'
     A          R M1MSG
     A*%%TS  SD  20080103  144410  BOF560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  5  2'Varen du har valgt '
     A                                  6  2'er en skaffevare.  '
     A                                  7  2'Tast <enter> for å '
     A                                  8  2'godkjenne dette.'
     A                                  9  2'F3=Tilbake'
