     A*%%TS  SD  20041018  231032  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK510D
     A* Beskrivelse . . : Kundeposteringer, spørring bilag/beløp
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050802 KOB  Nytt program
7.xx A*       020516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19960228  123916  KB          REL-V3R1M0  5763-PW1
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(RNBILN)
6.NU A            B1BIDA         6Y 0O  9 13
6.NU A            B1BILK    R        O  9 20REFFLD(RNBILK)
6.NU A            B1TEXT        14A  O  9 23
     A            B1BELØ        11Y 2O  9 38EDTCDE(J)
     A            B1KUND    R        O  9 54REFFLD(RKKUND)
     A            B1NAVN    R  -10   O  9 61REFFLD(RKNAVN)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041018  231032  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på kundeposteringer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  5 61'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 77REFFLD(RNRAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  5  4'Bil.nr'
     A                                      COLOR(WHT)
7.xx A                                  5 14'Dato'
     A                                      COLOR(WHT)
7.xx A                                  5 44'Beløp'
     A                                      COLOR(WHT)
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
7.xx A                                  8 14'Dato'
     A                                      COLOR(WHT)
7.xx A                                  8 20'Bk'
     A                                      COLOR(WHT)
7.xx A                                  8 23'Tekst'
     A                                      COLOR(WHT)
7.xx A                                  8 44'Beløp'
     A                                      COLOR(WHT)
7.xx A                                  8 55'Kunde'
     A                                      COLOR(WHT)
7.xx A                                  8 61'Navn'
     A                                      COLOR(WHT)
7.xx A            B2BILN    R        B  6  4REFFLD(RNBILN)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
7.xx A            B2BDAT         6Y 0B  6 13CHECK(RZ)
7.xx A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2BELØ    R        B  6 42REFFLD(RNBELØ)
7.xx A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  19960228  111004  KB          REL-V3R1M0  5763-PW1
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20030326  154402  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 75)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      OVRDTA
     A                                  2 32'Kundeposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(RNBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(RNTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        O  6 19REFFLD(RNREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        O  7 19REFFLD(RNREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Generalnota-nr .:'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        O  8 19REFFLD(RNANTP)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 24'Dato:'
     A            C2PDAT         6Y 0O  8 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        O  9 19REFFLD(RNRENK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 24'Dato:'
     A            C2RDAT         6Y 0O  9 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(RNREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Inkassokode. . .:'
     A            C2INKO    R        O 10 19REFFLD(RNINKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 24'Dato:'
     A            C2IDAT         6Y 0O 10 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            C2VALK    R        O 10 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            C2AVDE    R        O 11 19REFFLD(RNAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2GTXT    R   18   O 11 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valutabeløp. . .:'
     A            C2VALB    R        O 11 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12  1'Selger . . . . .:'
     A            C2SLGR    R        O 12 19REFFLD(RNSLGR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2ITXT    R   18   O 12 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            C2VALR    R        O 12 61REFFLD(RNVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
