     A*%%TS  SD  20140530  125435  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO502D
     A* Beskrivelse . . : Varemottak, søke bestilling på vare
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230402 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  125435  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  6  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1SUFF    R        O  6 13REFFLD(LOSUFF)
     A            B1OTYP    R        O  6 16REFFLD(LOOTYP)
     A            B1LENA    R  -10   O  6 19REFFLD(LOLENA)
     A            B1DATO         6Y 0O  6 40EDTCDE(Y)
     A            B1NAVN    R   -6   O  6 49REFFLD(LONAVN)
     A            B1NUMM    R        O  6  4REFFLD(LONUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  083942  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Søking på bestilling via lev. '
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg  5=Vis'
     A                                      COLOR(BLU)
     A                                  5  2'V Bestnr         Leverandør       -
     A                                          Best.dat Vareadresse'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990114  171537  ASK         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 18 73)
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
