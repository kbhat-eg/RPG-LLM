     A*%%TS  SD  20060905  123157  BOF550      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO314D
     A* Beskrivelse . . : Kasseoppgjør rapport detaljer, vis
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050906 bø   Nytt program
7.xx A* 7.xx  260516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20060905  122625  BOF550      REL-V5R3M0  5722-WDS
7.xx A            B1VALG         1Y 0B  9  2COLOR(YLW)
     A                                      VALUES(0 5 7)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A                                      EDTCDE(Z)
7.xx A            B1BTRA    R        O  9 20REFFLD(BOTLST/BLBTRA *LIBL/BOTLST)
7.xx A            B1BONG    R        O  9  5REFFLD(BOTLST/BLBONG *LIBL/BOTLST)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20060905  123157  BOF550      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis rapport detaljer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Telling :'
     A            B2TELL    R        O  4 12REFFLD(BOTLST/BLTELL *LIBL/BOTLST)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  5  7'Bongnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis            '
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'Bongnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Behold.Trans'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V    Bongnr.      Behold.Trans    -
7.xx A**                                                                '
7.xx A**                                    COLOR(WHT)
7.xx A            B2BONG    R        B  6  5REFFLD(BOTLST/BLBONG *LIBL/BOTLST)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20060905  120459  BOF550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny          -
     A                                         '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20060901  132950  BOF550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis rapport detaljer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bongnummer . . . . . . . . .'
     A            C1BONG    R        O  5 31REFFLD(BOTLST/BLBONG *LIBL/BOTLST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beholdnings trans  . . . . .'
     A            C2BTRA    R        O  7 31REFFLD(BOTLST/BLBTRA *LIBL/BOTLST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2OTIM         6Y 0O 18 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ETIM         6Y 0O 19 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(BOTLST/BLEUSR *LIBL/BOTLST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
