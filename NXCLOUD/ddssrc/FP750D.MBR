     A*%%TS  SD  20070223  130709  BOF550      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FP750D
     A* Beskrivelse . . : Innlsing av priser fra fil
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A* 110515 BOF : Utvidet med nytt filformat
6.31 A* 130519 BOF : Utvidet med nytt filformat 6
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20070223  130709  BOF550      REL-V5R4M0  5722-WDS
7.xx A*                                      WINDOW(*DFT 23 54)
     A                                      CA03
     A                                      CA12
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Innlesing av spesialpriser fra fi-
     A                                      l '
     A                                      DSPATR(RI)
7.xx A*                                  2 44'FP750B1'
7.xx A*                                      COLOR(YLW)
     A                                  4  2'Format. . . . . . .'
     A            B1FORM         1Y 0B  4 22COLOR(YLW)
6.31 A                                      VALUES(1 2 3 4 5 6 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                  4 27'1=Nobb prisformat'
     A                                  5 27'2=Byggdata/5001-format'
     A                                  6 27'3=NIB prisformat'
     A                                  7 27'4=Proff partner'
     A                                  8 27'5=BM csv format'
6.31 A  50                              9 27'6=BM nobb hirarki'
7.xx A                                 11  2'Rabattkategori. . +'
7.xx A                                 10  2'Prisgruppe  . . . +'
7.xx A            B1PRGR    R        B 10 22REFFLD(FPPRGR)
     A  41                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
7.xx A            B1RKAT    R        B 11 22REFFLD(FPRKAT)
     A  31                                  ERRMSGID(FMP0200 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMP0210 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 12  2'Kunde . . . . . . +'
7.xx A            B1KUND    R        B 12 22REFFLD(FPKUND)
     A  34                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 13  2'Kundeprosjekt . . +'
7.xx A            B1KPRO    R        B 13 22REFFLD(FPKPRO)
     A  35                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 14  2'Filnavn . . . . . .'
7.xx A            B1FILE        12A  B 14 22COLOR(YLW)
     A  36                                  ERRMSGID(FM00600 *LIBL/QMFOFAK 36)
7.xx A                                 15  2'Mappenavn . . . . .'
7.xx A            B1FLDR        20A  B 15 22COLOR(YLW)
7.xx A                                 17  2'Fjern eksisterende.'
7.xx A            B1SLET    R        B 17 22REFFLD(FÅJANE)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
7.xx A                                 17 25'1=Ja,0=Nei'
7.xx A                                 18  2'Lev.type/påslag . +'
7.xx A            B1LET1    R        B 18 22REFFLD(FPLETY)
     A  37                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
7.xx A                                 18 24'/'
7.xx A            B1PAS1         5Y 2B 18 26COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
7.xx A                                 18 34'%'
7.xx A                                 19  2'Lev.type/påslag . +'
7.xx A            B1LET2    R        B 19 22REFFLD(FPLETY)
     A  38                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
7.xx A                                 19 24'/'
7.xx A            B1PAS2         5Y 2B 19 26COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
7.xx A                                 19 34'%'
7.xx A                                 20  2'Lev.type/påslag . +'
7.xx A            B1LET3    R        B 20 22REFFLD(FPLETY)
     A  39                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
7.xx A                                 20 24'/'
7.xx A            B1PAS3         5Y 2B 20 26COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
7.xx A                                 20 34'%'
7.xx A                                 21  2'Lev.type/påslag . +'
7.xx A            B1LET4    R        B 21 22REFFLD(FPLETY)
     A  40                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
7.xx A                                 21 24'/'
7.xx A            B1PAS4         5Y 2B 21 26COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
7.xx A                                 21 34'%'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R B1MSG
     A*%%TS  SD  20010904  091133  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 12 36)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Valgt mappenavn er ugyldig, filene'
     A                                  3  2'ble ikke funnet i mappen, eller du'
     A                                  4  2'mangler tilgang for behandling av'
     A                                  5  2'mapper.'
     A                                  7  2'Sjekk at filene finnes i mappen.'
     A                                  8  2'Dersom du mangler tilgang må du'
     A                                  9  2'kontakte systemansvarlig.'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
