     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RB101D
     A* Beskrivelse . . : Momskoder, Nexstep/ ny
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*K00    031120 sst  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RB00PF)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A            B1VALG         1S 0B  8  2
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1GKOD    R        O  8  9REFFLD(R00KOD)
     A            B1NKOD         6   O  8 24
     A            B1VTXT    R        O  8 36REFFLD(R00TXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av momskoder  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  9'Nexstep'
     A                                      COLOR(WHT)
     A                                  4 36'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2GKOD    R        B  5  9REFFLD(R00KOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2VTXT    R        B  5 36REFFLD(R00TXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  9'Nexstep'
     A                                      COLOR(WHT)
     A                                  7 24'Ny mvak.'
     A                                      COLOR(WHT)
     A                                  7 36'Beskrivelse      '
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av momskoder  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Nexstep momskode . . . . . .'
     A            C1GKOD    R        B  5 31REFFLD(R00KOD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A  31                              2  2'Momskode   finnes fra før'
     A  32                              2  2'Momskode   finnes ikke'
     A  33                              2  2'Ny Momskode må angis'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20201103  102848  SST700      REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av momskoder  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  6  2'Nexstep momskode . . . . . .'
     A            C1GKOD    R        O  6 31REFFLD(R00KOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Ny momskode  . . . . . . . .'
     A            C2NKOD         6   B 10 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  34                                  DSPATR(PR)
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2VTXT    R        O  8 31REFFLD(R00TXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(R00USR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  233620  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Momskode'
     A                                      DSPATR(RI)
     A                                  4  2'Nexstep Mva.kode .'
     A            D1GKOD    R        O  4 21REFFLD(R00KOD)
     A                                  6  2'Beskrivelse. . . .'
     A            R00TXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
