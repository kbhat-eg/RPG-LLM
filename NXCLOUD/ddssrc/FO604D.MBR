     A*%%TS  SD  20050705  124206  AMD530      REL-V5R2M0  5722-WDS
     A*
9.01 A*  9.01   091117  bsa  Pga låsing av bilder i ny GUI, legger vi ut
9.01 A*                      bildene i fullsize.
Q.01 A*  7.xx   030419  bsa  Tillater bredere skjerm
     A*
8.01 A*K0000    170723  bsa  Åpner for fakturering av både positive og negative
8.01 A*                      fakturaer.
8.02 A*  8.01   120123  ask  Lagt inn kontroll av tilgang på switch register  -
8.03 A*K00000   180325  bsa  Fjernet mulighet valg av 5.
     A*
     A*
     A*
     A*-------------------------------------------------------------------------
     A*%%EC
Q.01 A                                      DSPSIZ(24 80 *DS3 -
Q.01 A                                             27 132 *WIDE)
Q.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R F2BLD
     A*%%TS  SD  20050705  124206  AMD530      REL-V5R2M0  5722-WDS
     A                                      CF03
     A                                      CF04
     A                                      CF18
     A                                      KEEP
7.xx A**                                    OVERLAY
7.xx A**                                    PUTOVR
7.xx A**                                    WINDOW(*DFT 9 35)
7.xx A                                  7 22'Periode og fakturadato'
     A                                      COLOR(WHT)
7.xx A                                  9 22'Periode. . . .'
7.xx A            F2PERI    R   -2   B  9 40REFFLD(FK1PER)
7.xx A**                                    OVRDTA
     A  31                                  ERRMSGID(FM01110 *LIBL/QMFOFAK 31)
     A  33                                  ERRMSGID(FM01120 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FM01180 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FM01190 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FM01230 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
7.xx A                                 10 22'Faktura-dato .'
7.xx A            F2DATO         6Y 0B 10 37COLOR(YLW)
7.xx A**                                    OVRDTA
     A  32                                  ERRMSGID(FM01130 *LIBL/QMFOFAK 32)
     A                                      EDTCDE(Y)
7.xx A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                 23 18'F4=Jobbkø'
     A                                      COLOR(BLU)
7.xx A                                 23 30'F18=Overstyre'
     A                                      COLOR(BLU)
     A*
     A          R F1BLD
     A*%%TS  SD  20020312  203232  HKO         REL-V5R1M0  5722-WDS
7.xx A*                                     WINDOW(05 05 16 65)
     A                                      CF03
     A                                      CF04
     A                                      KEEP
7.xx A*                                      OVERLAY
7.xx A*                                      PUTOVR
7.xx A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  1 46'Ordre/Fakturering'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 72TIME
7.xx A                                      COLOR(WHT)
7.xx A                                  2  2'Utkjøring av ordre-papirer'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 38'FO604F1'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 54USER
7.xx A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 72DATE
7.xx A                                      EDTCDE(Y)
7.xx A                                      COLOR(WHT)
7.xx A                                  3  2'-----------------------------------
7.xx A                                      ------------------------------------
7.xx A                                      ---------'
7.xx A                                      COLOR(WHT)
7.xx A*                                  3  2' Utkjøring av ordre-papirer '
7.xx A*                                      DSPATR(RI)
7.xx A*                                  3 38'FO604F1'
7.xx A*                                      COLOR(WHT)
     A                                  4  2'Velg hva skal skrives ut'
     A                                      COLOR(WHT)
     A                                  6  2'0='
     A            F1OT01    R        O  6  5REFFLD(FAAOT1)
7.xx A*                                      OVRDTA
     A                                  6  8'Tilbud'
     A                                  7  2'1='
     A            F1OT02    R        O  7  5REFFLD(FAAOT2)
7.xx A*                                      OVRDTA
     A                                  7  8'Ordrebekreftelse/Plukkliste'
     A                                  8  2'2='
     A            F1OT03    R        O  8  5REFFLD(FAAOT3)
7.xx A*                                      OVRDTA
     A                                  8  8'Pakkseddel'
     A                                  9  2'3='
     A            F1OT04    R        O  9  5REFFLD(FAAOT4)
7.xx A*                                      OVRDTA
     A                                  9  8'Faktura'
     A                                  9 36'Alternativ .'
     A            F1VALG    R        B  9 49REFFLD(FÅSVAR)
7.xx A*                                      OVRDTA
8.03 A*                                     VALUES(0 1 2 3 4 5)
8.03 A                                      VALUES(0 1 2 3 4)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
7.xx A                                  9 56'(kode)'
     A                                 10  2'4='
     A            F1OT05    R        O 10  5REFFLD(FAAOT5)
7.xx A*                                      OVRDTA
     A                                 10  8'Kreditnota'
     A                                 10 31'eller ordretype .'
     A            F1OTYP    R        B 10 49REFFLD(FAAOTY)
     A                                      DLTCHK
7.xx A*                                      OVRDTA
     A  31                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
8.03 A*                                11  2'5='
8.03 A*                                11  8'Faktura/Kreditnota'
8.01 A                                 13  2'Skriv ordre bare registrert ved de-
     A                                      nne skjerm .'
8.01 A            F1VLG2    R        B 13 49REFFLD(FÅSVAR)
     A                                      DLTCHK
7.xx A*                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
8.01 A                                 13 53'1=Ja,0=Nei'
8.01 A                                 14  2'Skriv ordre bare registrert av den-
     A                                      ne bruker. .'
8.01 A            F1VLG3    R        B 14 49REFFLD(FÅSVAR)
     A                                      DLTCHK
7.xx A*                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
8.01 A                                 14 53'1=Ja,0=Nei'
7.xx A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                 23 18'F4=Jobbkø'
     A                                      COLOR(BLU)
8.02 A          R F1MSG1
8.02 A                                      WINDOW(*DFT 8 50)
8.02 A                                      CF03
8.02 A                                      CF12
8.02 A                                      PUTOVR
8.02 A                                  3  2'Bruker har ikke tilgang til switc+
8.02                                         h register'
8.02 A                                      COLOR(RED)
8.02 A                                  4  2'Kontakt EG support.           '
8.02 A                                      COLOR(RED)
8.02 A                                  7  2'F3=Gå tilbake'
8.02 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
