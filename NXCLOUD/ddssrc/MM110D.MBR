     A*%%TS  SD  20050512  145525  BOF530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MM100D
     A* Beskrivelse . . : Matrise - bestillingsmengde /Sikkerhetstid
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250804 bø   Nytt program
7.xx A* 7.xx  210316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050425  103916  BOF530      REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.xx A            B1VSOR    R        O  9  4REFFLD(MMVSOR)
7.xx A            B1OGRP    R        O  9 15REFFLD(MMOGRP)
7.xx A            B1HGRP    R        O  9 18REFFLD(MMHGRP)
7.xx A            B1UGRP    R        O  9 21REFFLD(MMUGRP)
7.xx A            B1LDOR    R        O  9 25REFFLD(MMLDOR)
7.xx A            B1VARE    R   -7   O  9 32REFFLD(MMVARE)
7.xx A            B1CRDO    R        O  9 41REFFLD(MMCRDO)
     A                                      EDTCDE(Z)
7.xx A            B1DBAX    R        O  9 44REFFLD(MMDBAX)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBAY    R        O  9 48REFFLD(MMDBAY)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBAZ    R        O  9 52REFFLD(MMDBAZ)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBBX    R        O  9 56REFFLD(MMDBBX)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBBY    R        O  9 60REFFLD(MMDBBY)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBBZ    R        O  9 64REFFLD(MMDBBZ)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBCX    R        O  9 68REFFLD(MMDBCX)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBCY    R        O  9 72REFFLD(MMDBCY)
     A                                      EDTWRD(' 0 ')
7.xx A            B1DBCZ    R        O  9 76REFFLD(MMDBCZ)
     A                                      EDTWRD(' 0 ')
7.xx A            B1AKTI         1A  O  9 80
     A            B1LAGE    R        H      REFFLD(MMLAGE)
     A            B1LAGT    R        H      REFFLD(MMLAGT)
     A            B1LAGR    R        H      REFFLD(MMLAGR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050426  090059  BOF530      REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CF10
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innkjøpsparameter   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 18'Varegruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 33'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 42'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis '
     A                                      COLOR(BLU)
7.xx A                                  7  2'7=Beregn Innkjøpspar.  8=Vis Innkj-
     A                                      øpspar.  9=Oppd. lager'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 15'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 18'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 21'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 41'C'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'AX'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'AY'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'AZ'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 57'BX'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'BY'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 65'BZ'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 69'CX'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 73'CY'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 77'CZ'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'P'
7.xx A**                                8  2'V Varesort.  Varegruppe Lev.  Vare-
7.xx A**                                    nr.  C   AX  AY  AZ  BX  BY  BZ  CX-
7.xx A**                                      CY  CZ P'
     A                                      COLOR(WHT)
7.xx A                                  5 15'+'
7.xx A            B2OGRP    R        B  5 17REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 20'+'
7.xx A            B2HGRP    R        B  5 22REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 25'+'
7.xx A            B2UGRP    R        B  5 27REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'+'
7.xx A            B2VSOR    R        B  5  4REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 31'+'
7.xx A            B2LDOR    R        B  5 33REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 40'+'
7.xx A            B2VARE    R   -7   B  5 42REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett  F8=Resultat  F10=Avvik'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050429  103940  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av innkjøpsparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varesortiment  . . . . . . +'
     A            C1VSOR    R        B  4 31REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(MMA0040 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 45
     A                                      OVRDTA
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 45
     A                                      OVRDTA
7.xx A                                  6  2'Hovedgruppe  . . . . . . . +'
     A            C1HGRP    R        B  6 31REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 45
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe  . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 45
     A                                      OVRDTA
7.xx A                                  8  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  8 31REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 45
     A                                      OVRDTA
7.xx A                                  9  2'Varenr.  . . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 45
     A                                      OVRDTA
     A                                 10  2'Cross Docking  . . . . . . .'
     A            C1CRDO    R        B 10 31REFFLD(MMCRDO)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 10 33'1=ja'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                5 35'F4'
7.xx A**                                6 35'F4'
7.xx A**                                7 35'F4'
7.xx A**                                8 38'F4'
7.xx A**                                9 40'F4'
7.xx A**                                4 42'F4'
     A          R C1MSG
     A*%%TS  SD  20040825  140505  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Matrise       finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050512  145525  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av innkjøpsparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varesortiment  . . . . . . .'
     A            C1VSOR    R        O  4 31REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 43
     A                                      OVRDTA
     A                                  5  2'Overgruppe   . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 43
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe  . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 43
     A                                      OVRDTA
     A                                  7  2'Undergruppe  . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 43
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 43
     A                                      OVRDTA
     A                                  9  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 43
     A                                      OVRDTA
     A                                 10  2'Cross Docking  . . . . . . .'
     A            C1CRDO    R        O 10 31REFFLD(MMAKTI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                 12  2'Lager som oppdateres . . . .'
     A            C2LAGT    R        B 12 31REFFLD(MMLAGT)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
7.xx A                                 11  2'Valg av lager  . . . . . . +'
7.xx A**                               11 34'F4'
     A                                 11 37'blank alle'
     A            C2LAGR        30A  O 11 48COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Passiv v/automatisk kjøring.'
     A            C2AKTI    R        B 13 31REFFLD(MMAKTI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                 13 34'(1/0)         '
     A                                 15  2'Antall'
     A                                 16  2'bestillinger'
     A                                 17  2'pr. år'
     A                                 15 20' X '
     A                                      DSPATR(RI)
     A                                 15 26' Y '
     A                                      DSPATR(RI)
     A                                 15 32' Z '
     A                                      DSPATR(RI)
     A                                 16 15' A '
     A                                      DSPATR(RI)
     A            C2DBAX    R        B 16 20REFFLD(MMDBAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBAY    R        B 16 26REFFLD(MMDBAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBAZ    R        B 16 32REFFLD(MMDBAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 17 15' B '
     A                                      DSPATR(RI)
     A            C2DBBX    R        B 17 20REFFLD(MMDBAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBBY    R        B 17 26REFFLD(MMDBAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBBZ    R        B 17 32REFFLD(MMDBAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 18 15' C '
     A                                      DSPATR(RI)
     A            C2DBCX    R        B 18 20REFFLD(MMDBAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBCY    R        B 18 26REFFLD(MMDBAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DBCZ    R        B 18 32REFFLD(MMDBAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 15 44'Antall dager'
     A                                 16 44'minimumslager'
     A                                 17 44'varer'
     A                                 15 64' X '
     A                                      DSPATR(RI)
     A                                 15 70' Y '
     A                                      DSPATR(RI)
     A                                 15 76' Z '
     A                                      DSPATR(RI)
     A                                 16 59' A '
     A                                      DSPATR(RI)
     A            C2DSAX    R        B 16 64REFFLD(MMDSAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSAY    R        B 16 70REFFLD(MMDSAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSAZ    R        B 16 76REFFLD(MMDSAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 17 59' B '
     A                                      DSPATR(RI)
     A            C2DSBX    R        B 17 64REFFLD(MMDSAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSBY    R        B 17 70REFFLD(MMDSAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSBZ    R        B 17 76REFFLD(MMDSAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 18 59' C '
     A                                      DSPATR(RI)
     A            C2DSCX    R        B 18 64REFFLD(MMDSAX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSCY    R        B 18 70REFFLD(MMDSAY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A            C2DSCZ    R        B 18 76REFFLD(MMDSAZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 20 41'Sist kjørt dato .'
     A            C2KDAT         6Y 0O 20 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Opprettet dato  . . . . . . '
     A            C2ODAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Sist endret dato  . . . . . '
     A            C2EDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Endret av . . . . . . . . . '
     A            C2EUSR    R        O 22 31REFFLD(MMUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring '
     A                                      COLOR(BLU)
     A            C2LAGE    R        B 11 31REFFLD(MMLAGE)
     A  31                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 31)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2ANTP    R        H      REFFLD(MMANTP)
     A            C2ANTU    R        H      REFFLD(MMANTU)
     A            C2BRKU    R        H      REFFLD(MMBRKU)
     A            C2ANTG    R        H      REFFLD(MMANTG)
     A            C2BRKG    R        H      REFFLD(MMBRKG)
     A          R D1WIN
     A*%%TS  SD  20041202  142747  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Matrise'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            D1VSOR    R        O  4 21REFFLD(MMVSOR)
     A                                  5  2'Overgruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(MMOGRP)
     A                                  6  2'Hovedgruppe  . . .'
     A            D1HGRP    R        O  6 21REFFLD(MMHGRP)
     A                                  7  2'Undergruppe  . . .'
     A            D1UGRP    R        O  7 21REFFLD(MMUGRP)
     A                                  8  2'Leverandør . . . .'
     A            D1LDOR    R        O  8 21REFFLD(MMLDOR)
     A                                  9  2'Varenr.  . . . . .'
     A            D1VARE    R   -7   O  9 21REFFLD(MMVARE)
     A                                 10  2'Cross docking  . .'
     A            D1CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20050202  121933  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 20 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Varesortiment  . +'
     A            K1VSOR    R        B  3 21REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4  2'Overgruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe  . . +'
     A            K1HGRP    R        B  5 21REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  6  2'Undergruppe  . . +'
     A            K1UGRP    R        B  6 21REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Leverandør . . . +'
     A            K1LDOR    R        B  7 21REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'Varenr.  . . . . +'
     A            K1VARE    R   -7   B  8 21REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Cross docking  . .'
     A            K1CRDO    R        B  9 21REFFLD(MMCRDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 12  2'Varesortiment  . .'
     A            MMVSOR    R        O 12 21OVRDTA
     A                                 13  2'Overgruppe . . . .'
     A            MMOGRP    R        O 13 21OVRDTA
     A                                 14  2'Hovedgruppe  . . .'
     A            MMHGRP    R        O 14 21OVRDTA
     A                                 15  2'Undergruppe  . . .'
     A            MMUGRP    R        O 15 21OVRDTA
     A                                 16  2'Leverandør . . . .'
     A            K2LDOR    R        O 16 21REFFLD(MMLDOR)
     A                                 17  2'Varenr.  . . . . .'
     A            K2VARE    R   -7   O 17 21REFFLD(MMVARE)
     A                                      OVRDTA
     A                                 18  2'Cross docking  . .'
     A            K2CRDO    R        O 18 21REFFLD(MMCRDO)
     A                                      OVRDTA
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                5 24'F4'
7.xx A**                                4 24'F4'
7.xx A**                                6 25'F4'
7.xx A**                                8 30'F4'
7.xx A**                                7 28'F4'
     A          R L1WIN
     A*%%TS  SD  20050418  104212  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Beregn Innkjøpsparameter'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            L1VSOR    R        O  4 21REFFLD(MMVSOR)
     A            L1VSNA        25   O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            L1OGRP    R        O  5 21REFFLD(MMOGRP)
     A            L1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            L1HGRP    R        O  6 21REFFLD(MMHGRP)
     A            L1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            L1UGRP    R        O  7 21REFFLD(MMUGRP)
     A            L1UGNA        25A  O  7 32
     A                                  8  2'Leverandør . . . .'
     A            L1LDOR    R        O  8 21REFFLD(MMLDOR)
     A            L1LENA        25A  O  8 32
     A                                  9  2'Varenummer   . . .'
     A            L1VARE    R   -7   O  9 21REFFLD(MMVARE)
     A            L1VANA        25A  O  9 32
     A                                 10  2'Cross docking  . .'
     A            L1CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 11  2'Lager  . . . . . .'
     A            L1LAGE    R        O 11 21REFFLD(MMLAGE)
     A                                      OVRDTA
     A            L1LAGR        30A  O 12 21OVRDTA
7.xx A**                               10 24'F4'
     A                                 13  2'Lager som oppdateres . . . .'
     A            L1LAGT    R        O 13 31REFFLD(MMLAGT)
     A                                      OVRDTA
     A                                 14  2'Startdato for simulering . .'
     A            L1DATO    R        B 14 31REFFLD(LÅDATR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      EDTWRD('  .  .  ')
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R M1WIN
     A*%%TS  SD  20050421  133511  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Simulerte innkjøpstall oppdaterer-
     A                                       lager'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            M1VSOR    R        O  4 21REFFLD(MMVSOR)
     A            M1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            M1OGRP    R        O  5 21REFFLD(MMOGRP)
     A            M1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            M1HGRP    R        O  6 21REFFLD(MMHGRP)
     A            M1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            M1UGRP    R        O  7 21REFFLD(MMUGRP)
     A            M1UGNA        25A  O  7 32
     A                                  8  2'Leverandør . . . .'
     A            M1LDOR    R        O  8 21REFFLD(MMLDOR)
     A            M1LENA        25A  O  8 32
     A                                  9  2'Varenummer . . . .'
     A            M1VARE    R   -7   O  9 21REFFLD(MMVARE)
     A            M1VANA        25A  O  9 32
     A                                 10  2'Cross docking  . .'
     A            M1CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 11  2'Lager  . . . . . .'
     A            M1LAGE    R        O 11 21REFFLD(MMLAGE)
     A            M1LANA        25A  O 11 32
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
