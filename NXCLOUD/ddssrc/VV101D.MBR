     A*%%TS  SD  20140808  144554  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
      *
      * 080814 bof : Lagt inn nytt felt erstattes av, fjernet vedl. nobb.lev
7.xx A* 7.xx  020216  bsa  Justeringer basert på ny GUI og regler
7.06 A* R6.30 270616 BSA  Lagt inn F tast for VA info
     A*
8.01 A*PRG2   150622 bsa  Utvidet prisgruppe far 1 til 2 posisjoner
     A*
     A*---------------------------------------------------------------
      *
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081109  083043  BOF570      REL-V5R4M0  5722-WDS
     A            B1PRGR_GMLR        H      REFFLD(VPPRGR)
     A            B1ENHE_GMLR        H      REFFLD(VPENHE)
     A            B1LETY_GMLR        H      REFFLD(VPLETY)
     A            B1PDAT_GML     6Y 0H
     A            B1SAPR_GMLR        H      REFFLD(VPSAPR)
     A            B1KOPR_GMLR        H      REFFLD(VPKOPR)
     A            B1INPR_GMLR        H      REFFLD(VPINPR)
     A            B1BUPR_GMLR        H      REFFLD(VPBUPR)
8.01 A            B1PRGR    R        B 15  2REFFLD(VPPRGR)
     A                                      COLOR(YLW)
     A            B1ENHE    R        B 15  5REFFLD(VPENHE)
     A                                      COLOR(YLW)
     A            B1LETY    R        B 15  9REFFLD(VPLETY)
     A                                      COLOR(YLW)
     A            B1PDAT         6Y 0B 15 11COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A            B1SAPR    R        B 15 20REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1KOPR    R   -1   B 15 31REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A            B1INPR    R   -1   B 15 41REFFLD(VPINPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A            B1BUPR    R   -1   B 15 51REFFLD(VPBUPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A            B1DEKN         4Y 2O 15 61COLOR(YLW)
     A                                      EDTCDE(M)
     A            B1OMRE    R        O 15 68REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140808  144554  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF02
     A                                      CA03
     A                                      CF04
7.06 A                                      CF10
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
6.36 A                                      CF21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  60                                  SFLMSGID(VMP0100 *LIBL/QMFOFAK 60)
     A  61                                  SFLMSGID(VMA1020 *LIBL/QMFOFAK 61)
     A  62                                  SFLMSGID(FRA3020 *LIBL/QMFOFAK 62)
     A  63                                  SFLMSGID(VMY0020 *LIBL/QMFOFAK 63)
     A  67                                  SFLMSGID(FM00050 *LIBL/QMFOFAK 67)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vare'
     A                                      COLOR(WHT)
     A                                  2 38'VV101B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer. . .'
     A            B2VARE    R   -7   O  4 18REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 56'Varetype. . . +'
     A            B2VTYP    R        B  4 72REFFLD(VVVTYP)
     A  40                                  ERRMSGID(VMA7010 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(VMA7020 *LIBL/QMFOFAK 41)
     A  42                                  ERRMSGID(VMA7040 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80                              4 77' N '
     A                                      COLOR(YLW)
     A                                      DSPATR(RI)
     A                                  5  2'Varetekst . . .'
     A            B2TEK1    R        B  5 18REFFLD(VVTEK1)
     A  31                                  ERRMSGID(VMV0100 *LIBL/QMFOFAK 31)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 56'Priskode. . . .'
     A            B2KPRI    R        B  5 72REFFLD(VVKPRI)
     A  43                                  ERRMSGID(VMV0120 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 74'(k)'
     A                                  6  2'Varetekst 2 . .'
     A            B2TEK2    R        B  6 18REFFLD(VVTEK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 56'Modulnummer . .'
     A            B2NMOD    R        O  6 72REFFLD(VVNMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varegruppe. . +'
     A            B2OGRP    R        B  7 18REFFLD(VVOGRP)
     A  32                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 21'+'
7.xx A            B2HGRP    R        B  7 23REFFLD(VVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 26'+'
7.xx A            B2UGRP    R        B  7 28REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2UTXT        21A  O  7 32COLOR(YLW)
     A                                      OVRDTA
     A                                  7 56'Nobbnummer. . .'
     A            B2NOBB    R        B  7 72REFFLD(VVNOBB)
     A  37                                  ERRMSGID(VMV0170 *LIBL/QMFOFAK 37)
     A  38                                  ERRMSGID(VMV0180 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  8  2'Leverandør. . +'
     A            B2LDOR    R        B  8 18REFFLD(VVLDOR)
     A  35                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LNAV        21A  O  8 32COLOR(YLW)
     A                                      OVRDTA
     A                                  8 56'Nobb prisgiver.'
     A            B2NLEV    R        O  8 72REFFLD(VVNLEV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lev. varenummer'
     A            B2LVAR    R        B  9 18REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 56'Erstattes av  .'
     A            B2ENOB    R        O  9 72REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 56'Nobb utgår-dato'
     A            B2NUDA         6Y 0O 10 72COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                 10  2'Sammenlign.enh+'
     A            B2ENPS    R        B 10 18REFFLD(VVENPS)
     A  51                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 51)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 56'Utgår dato. . .'
     A            B2UDAT         6Y 0B 11 72COLOR(YLW)
     A  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11  2'Stat/lagerenhet'
     A            B2ENHS    R        B 11 18REFFLD(VVENHS)
     A  45                                  ERRMSGID(VMV0300 *LIBL/QMFOFAK 45)
     A  47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ENHL    R        B 11 22REFFLD(VVENHL)
     A  48                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 48)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Basisenhet. . +'
     A            B2ENHE    R        B 12 18REFFLD(VVENHE)
     A  46                                  ERRMSGID(VMV0310 *LIBL/QMFOFAK 46)
     A  49                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 49)
     A  55                                  ERRMSGID(VMV0320 *LIBL/QMFOFAK 55)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A  44                             12 56'Utgår Lager . .'
7.01 A  44        B2UTGL    R        B 12 72REFFLD(VVVTYP)
7.01 A  40                                  ERRMSGID(VMA7010 *LIBL/QMFOFAK 40)
7.01 A  41                                  ERRMSGID(VMA7020 *LIBL/QMFOFAK 41)
7.01 A  42                                  ERRMSGID(VMA7040 *LIBL/QMFOFAK 42)
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
7.04 A                                 13  2'#=Ikke bruk'
7.01 A            B2ODAT         6Y 0O 13 50COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 13 59'/'
7.01 A            B2EDAT         6Y 0O 13 61COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 13 70'/'
7.01 A            B2EUSR    R   -1   O 13 72REFFLD(VVEUSR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 14  2'Pg'
7.xx A                                 14  5'Enh'
7.xx A                                 14  9'L'
7.xx A                                 14 12'Dato   '
7.xx A                                 14 20'Salgspris '
7.xx A                                 14 31'Kostpris'
7.xx A                                 14 40'Innkj.pris'
7.xx A                                 14 51'Butikkpris'
7.xx A                                 14 62'DG  '
7.xx A                                 14 68'Omr.fakt.vol.'
7.xx A**                               14  2'Pg Enh L  Dato    Salgspris  Kostp-
7.xx A**                                    ris Innkj.pris Butikkpris DG    Omr-
7.xx A**                                    .fakt vol.'
     A          R B2CMD
     A*%%TS  SD  20000225  092535  HKO         REL-V4R4M0  5769-PW1
7.06 A                                 22  2'F3=Tilbake  F2=Notat  F4=Spørr -
7.06 A                                       F10=VA info  F13=Bild-2  F14=Enh  -
7.06 A                                      F15=Pris '
     A                                      COLOR(BLU)
7.06 A                                 23  2'16=Søk  17=GTIN  F18=Tilb.pris  F1-
7.06 A                                      9=Prisjust.  F20=Endr.bas.pr.  F21=-
7.06 A                                      Vare-lev.'
     A                                      COLOR(BLU)
     A          R B2MSG
     A*%%TS  SD  20020113  204104  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 44)
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                  2  2'Beholdningstall finnes på ett elle-
     A                                      r flere'
     A                                  3  2'lagre, eller varetype= L.'
     A                                  5  2'Endring av varetype fra L til'
     A            B2VTYP    R        O  5 32REFFLD(VVVTYP)
     A                                  5 34',må derfor'
     A                                  6  2'endre til S på alle lager først!'
     A                                  8  2'F3=Gå tilbake  F13=Vis lager  '
     A                                      COLOR(BLU)
     A          R B3MSG
     A*%%TS  SD  20120112  103143  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varetype kan enten skiftes fra L  '
     A                                  3  2'til S eller fra T til D.       '
     A                                  4  2'(eller omvendt)                '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
