     A*%%TS  SD  20080429  145222  KOB560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH514D
     A* Beskrivelse . . : Hovedboksumposter, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
7.xx A*       030516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040112  215140  KOB         REL-V5R2M0  5722-WDS
     A            B1RAAR    R        H      REFFLD(RZRAAR)
     A            B1BIDA    R        H      REFFLD(RZBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BDAT         6Y 0O  9  4EDTWRD('  .  .  ')
     A            B1TELL    R        O  9 13REFFLD(RZTELL)
     A            B1BELO    R        O  9 20REFFLD(RZBELØ)
     A            B1BILG    R        O  9 36REFFLD(RZBILG)
6.NU A            B1KONT    R        O  9 45REFFLD(RZKONT)
6.NU A            B1SPES    R        O  9 51REFFLD(RZSPES)
6.NU A            B1AVDE    R        O  9 56REFFLD(RZAVDE)
6.NU A            B1PROS    R        O  9 61REFFLD(RZPROS)
6.NU A            B1AKTI    R        O  9 68REFFLD(RZAKTI)
6.NU A            B1RESN    R        O  9 75REFFLD(RZRESN)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080429  145222  KOB560      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.21 A            B2FIRM         3  0O  1 34DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på hovedboksumposter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Konto .:'
     A                                      COLOR(WHT)
     A            B2KONT    R        O  4 11REFFLD(RZKONT)
     A                                      EDTCDE(Z)
     A            B2SPES    R        O  4 17REFFLD(RZSPES)
     A                                      EDTCDE(Z)
     A            B2AVDE    R        O  4 22REFFLD(RZAVDE)
     A                                      EDTCDE(Z)
     A            B2TEX1    R        O  4 27REFFLD(RHTEXT)
     A                                  5  2'Bilag .:'
     A                                      COLOR(WHT)
     A            B2BILN    R        O  5 11REFFLD(RZBILN)
6.NU A            B2TEXT    R        O  5 20REFFLD(RBTEXT)
6.NU A                                  5 57'Beløp .:'
     A                                      COLOR(WHT)
6.NU A            B2BELØ    R        O  5 66REFFLD(RZBELØ)
     A                                  7  2'5=Vis  7=Vis bilag  8=Vis faktura -
     A                                       9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  6'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Post'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 36'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 57'Avd.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 62'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 69'Akt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 75'Resk.'
7.xx A**                                8  2'V   Dato    Post       Beløp      -
7.xx A**                                    Bil.nr   Konto Bærer Avd. Prosj. Ak-
7.xx A**                                    t   Resk.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20040215  221003  KOB         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - Vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20061231  125613  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 19 61)
     A                                      CA02
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 20'Vis hovedbokspost'
     A                                      COLOR(WHT)
     A                                  3  2'Bilagsnr. . . .:'
     A            RZBILN    R        O  3 19REFFLD(RZBILN)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  4  2'Konto . . . . .:'
     A            RZKONT    R        O  4 19REFFLD(RZKONT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RHTEXT    R        O  4 25REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  5  2'Kostnadsbærer .:'
     A            RZSPES    R        O  5 19REFFLD(RZSPES)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            REBTXT    R        O  5 24REFFLD(REBTXT)
     A                                      OVRDTA
     A                                  6  2'Avdeling. . . .:'
     A            RZAVDE    R        O  6 19REFFLD(RZAVDE)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RAGTXT    R        O  6 24REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                  7  2'Fakturanummer .:'
     A            RZBILG    R        O  7 19REFFLD(RZBILG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 41'Linje . . . . .:'
     A            RZBILL    R        O  7 58REFFLD(RZBILL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Bilagdato . . .:'
     A            C2BDAT         6Y 0O  8 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8 41'Regnskapsår . .:'
     A            RZRAAR    R        O  8 58REFFLD(RZRAAR)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  9  2'Beløp . . . . .:'
     A            RZBELØ    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9 41'MVA-kode. . . .:'
     A            RZMVAK    R        O  9 58DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Grunnlag mva. .:'
     A            RZMVAG    R        O 10 19REFFLD(RZMVAG)
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 41'%-andel gr.lag.:'
     A            C2AMVA    R    3 00O 10 58REFFLD(RZAMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2'Beregnet mva. .:'
     A            RZMVAB    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11 41'MVA-% . . . . .:'
     A            C2PMVA    R    2 00O 11 58REFFLD(RZPMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'Mengde. . . . .:'
     A            RZMNGD    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12 41'Mengdekode. . .:'
     A            RZMNGK    R        O 12 58DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Prosjekt-nr . .:'
     A            RZPROS    R        O 13 19DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Aktivitet . . .:'
     A            RZAKTI    R        O 14 19DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Reskontronr.. .:'
     A            RZRESN    R        O 15 19REFFLD(RZRESN)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2RNAV    R        O 15 26REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                 18  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 18 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 15 56)
