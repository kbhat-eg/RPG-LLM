     A*%%TS  SD  20080226  154830  MHA560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BK100D
     A* Beskrivelse . . : Vedlikehold av kasser
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  260516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BKASST BKASST)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080226  154830  MHA560      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(BÅVALG *LIBL/BFRF)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A            B1KASS    R        O  8  6REFFLD(BKKASS)
     A                                      ALIAS(KASSE1)
     A            B1BESK    R        O  8 12REFFLD(BKBESK)
     A                                      ALIAS(BEKSRIVELSE1)
     A            B1PLAS        28A  O  8 49
     A            B1KODE         1A  O  8 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051223  142236  ASP_550     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kasser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 12'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Beskrivelse             '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Plassering'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 75'Brukt'
7.xx A**                                6  2'V  Kasse  Beskrivelse             -
7.xx A**                                                 Plassering            -
7.xx A**                                        Brukt'
     A                                      COLOR(WHT)
7.xx A            B2KASS    R        B  5  6REFFLD(BKKASS)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      ALIAS(KASSE2)
7.xx A            B2BESK    R        B  5 12REFFLD(BKBESK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      ALIAS(BESKRIVELSE2)
     A          R B2CMD
     A*%%TS  SD  20020226  080848  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040602  104702  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kasse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kasse. . . . . . . . . . . .'
     A            C1KASS    R        B  5 31REFFLD(BKKASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      ALIAS(KASSE3)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040602  104702  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kasse finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080226  153108  MHA560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA07
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kasse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kasse . . . . . . . . .'
     A            C1KASS    R        O  4 26REFFLD(BKKASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      ALIAS(KASSE4)
     A                                  4 63'Bong . .'
     A            C2BTEL    R        O  4 72REFFLD(BKBTEL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse . . . . . .'
     A            C2BESK    R        B  5 26REFFLD(BKBESK)
     A  31                                  ERRMSGID(BMK0100 *LIBL/QMFSHOP 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      ALIAS(BESKRIVELSE4)
     A                                  5 63'Gavekort'
     A            C2GTEL    R        O  5 74REFFLD(BKGTEL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Plassering. . . . . . .'
     A            C2PLAS    R        B  6 26REFFLD(BKPLAS)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Gruppe. . . . . . . . .'
     A            C2GRUP    R        B  7 26REFFLD(BKGRUP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Avdeling. . . . . . . .'
     A            C2AVDE    R        B  8 26REFFLD(BKAVDE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  9  2'Kostnadsbærer . . . . .'
     A            C2BAER    R        B  9 26REFFLD(BKBAER)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 10  2'Lager . . . . . . . . .'
     A            C2LAGE    R        B 10 26REFFLD(BKLAGE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 11  2'Leveringstype . . . . .'
     A            C2LETY    R        B 11 26REFFLD(BKLETY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 12  2'Parametersetting:'
     A                                      COLOR(WHT)
     A                                 13  2'Faktura . . . . . . . .'
     A            C2KFAK    R        B 13 26REFFLD(BKKFAK)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 13 29'1=Ja,0=Nei'
     A                                  9 43'Krav om kundens ref.. .'
     A            C2KKRF    R        B  9 67REFFLD(BKKKRF)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  9 70'1=Ja,0=Nei'
     A                                 14  2'Pakkseddel. . . . . . .'
     A            C2KPAK    R        B 14 26REFFLD(BKKPAK)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 14 29'1=Ja,0=Nei'
     A                                 10 43'Beholde selger. . . . .'
     A            C2KBSE    R        B 10 67REFFLD(BKKBSE)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 10 70'1=Ja,0=Nei'
     A                                 15  2'Hurtigordre . . . . . .'
     A            C2KHUO    R        B 15 26REFFLD(BKKHUO)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 15 29'1=Ja,0=Nei'
     A                                 11 43'A4-bong . . . . . . . .'
     A            C2KBA4    R        B 11 67REFFLD(BKKBA4)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 11 70'1=Ja,0=Nei'
     A                                 16  2'Betaling av faktura . .'
     A            C2KBFA    R        B 16 26REFFLD(BKKBFA)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 16 29'1=Ja,0=Nei'
     A                                 12 43'Fortrykte blanketter. .'
     A            C2KFTB    R        B 12 67REFFLD(BKKFTB)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 12 70'1=Ja,0=Nei'
     A                                 17  2'Bekrefte mottatt beløp.'
     A            C2KMOT    R        B 17 26REFFLD(BKKMOT)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 17 29'1=Ja,0=Nei'
     A                                 18  2'Bekrefte spesialavtale.'
     A            C2KAVT    R        B 18 26REFFLD(BKKAVT)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 18 29'1=Ja,0=Nei'
     A                                 18 43'Antall pakksedler . . .'
     A            C2ANTP    R        B 18 67REFFLD(BKANTP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 19  2'Vise tastatur . . . . .'
     A            C2KTAS    R        B 19 26REFFLD(BKKTAS)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 19 29'1=Ja,0=Nei'
     A                                 19 43'Timeout . . . . . . . .'
     A            C2TIMO         3Y 0B 19 67COLOR(YLW)
     A                                      EDTWRD('0 :  ')
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 73't:mm'
     A                                 20  2'Vise kostpris . . . . .'
     A            C2KKOP    R        B 20 26REFFLD(BKKKOP)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 20 29'1=Ja,0=Nei'
     A                                 20 43'Utkøer:'
     A                                      COLOR(WHT)
     A                                 21  2'Vise selgere. . . . . .'
     A            C2KSEL    R        B 21 26REFFLD(BKKSEL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 21 29'1=Ja,0=Nei'
     A                                 21 43'Fakturautskrift . . . .'
     A            C2UFAK        10A  B 21 67COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22  2'Vise varedetaljer . . .'
     A            C2KVAD    R        B 22 26REFFLD(BKKVAD)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 22 29'1=Ja,0=Nei'
     A                                 22 43'Hurtigordre . . . . . .'
     A            C2UHUO        10A  B 22 67COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F7=Frigi kasse'
     A                                      COLOR(BLU)
     A            C2MACA    R        O  7 63REFFLD(BKMACA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 43'Klient Mac adr. . .'
     A            C2ODAT         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C2EDAT         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C2EUSR    R        O 23 71REFFLD(BKEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 43'Hent holdte ordrer. . .'
     A            C2HHOR    R        B 13 67REFFLD(BKHHOR)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 14 43'Bruk lok.db der mulig .'
     A            C2BLDB    R        B 14 67REFFLD(BKBLDB)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 15 43'Overfør i bakgrunn. . .'
     A            C2OBKG    R        B 15 67REFFLD(BKOBKG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 16 43'Debug på. . . . . . . .'
     A            C2DEBG    R        B 16 67REFFLD(BKDEBG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 17 43'Eager online sjekking .'
     A            C2EAGR    R        B 17 67REFFLD(BKEAGR)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 13 70'1=Ja,0=Nei'
     A                                  8 43'Ekstra hjelp på . . . .'
     A            C2EKSH    R        B  8 67REFFLD(BKEKSH)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  8 70'1=Ja,0=Nei'
     A                                 14 70'1=Ja,0=Nei'
     A                                 15 70'1=Ja,0=Nei'
     A                                 16 70'1=Ja,0=Nei'
     A                                 17 70'1=Ja,0=Nei'
     A          R C3BLD
     A*%%TS  SD  20160704  131625  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA07
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kasse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kasse . . . . . . . . .'
     A            C1KASS    R        O  4 26REFFLD(BKKASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      ALIAS(KASSE4)
     A                                  4 63'Bong . .'
     A            C1BTEL    R        O  4 72REFFLD(BKBTEL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Parametersetting:'
     A                                      COLOR(WHT)
     A                                  7  2'Antall A4 bonger  . . .'
     A            C3ANTB    R        B  7 36REFFLD(BKANTB)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                  7 38'1=Ja,0=Nei'
     A                                  8  2'Dager før replikeringsvarsel'
     A            C3REPV    R        B  8 35REFFLD(BKREPV)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  2'Dager før replikeringskrav'
     A            C3REPK    R        B  9 34REFFLD(BKREPK)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A            C3ODAT         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C3EDAT         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C3EUSR    R        O 23 71REFFLD(BKEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040602  104702  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kasse '
     A                                      DSPATR(RI)
     A                                  4  2'Kasse. . . . . . .'
     A            D1KASS    R        O  4 21REFFLD(BKKASS)
     A                                      ALIAS(KASSE5)
     A                                  6  2'Beskrivelse. . . .'
     A            BKBESK    R        O  6 21ALIAS(BESKRIVELSE5)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040602  104702  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kasse. . . . . . .'
     A            K1KASS    R        B  4 21REFFLD(BKKASS)
     A  31                                  ERRMSGID(BMK0010 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BMK0030 *LIBL/QMFSHOP 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      ALIAS(KASSE6)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kasse. . . . . . .'
     A            BKKASS    R        O  8 21OVRDTA
     A                                      ALIAS(KASSE7)
     A                                  9  2'Beskrivelse. . . .'
     A            BKBESK    R        O  9 21OVRDTA
     A                                      ALIAS(BESKRIVELSE7)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
