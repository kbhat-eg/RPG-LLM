     A*%%TS  SD  20051124  141539  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO505D
     A* Beskrivelse . . : Vise ordre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  310316  bsa  Justeringer basert på ny GUI og regler
     A*
7.fb A*       040419 bsa  Farger ifbm fors.betaling/depositum
     A*
8.01 A*K0000  110122 bsa  Omrokkerer og legger ut varekunde
8.03 A*K0000  110625 bsa  Farger skaffevarer som blå
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03 A                                      DSPSIZ(27 132 *WIDE)
7.03 A*                                      DSPSIZ(24 80 *DS3 -
7.03 A*                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19990601  234500  AMD         REL-V4R2M0  5769-PW1
     A            B1NUMM    R        H      REFFLD(FONUMM)
     A            B1SUFF    R        H      REFFLD(FOSUFF)
7.03 A*            B1TEK1    R        H      REFFLD(FDTEK1)
7.03 A            B1KTXT    R        H      REFFLD(FOKTXT)
     A            B1LINE    R        H      REFFLD(FDLINE)
     A            B1SUMN    R        H      REFFLD(FDSUMS)
     A            B1SUMK    R        H      REFFLD(FDSUMK)
     A            B1SUMB    R        H      REFFLD(FDSUMK)
     A            B1GRAD         5Y 2H
7.03 A            B1VALG    R        B 14  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.03 A*            B1TEXT        70A  O 14  6
7.03 A            B1LETY    R        O 14  4REFFLD(FDLETY)
7.03 A            B1VARE         8A  O 14  6
8.03 A  41                                  COLOR(BLU)
7.03 A            B1TEK1    R        O 14 15REFFLD(FDTEK1)
7.03 A            B1TEK2    R        O 14 51REFFLD(FDTEK2)
7.03 A            B1ANTA    R        O 14 87REFFLD(FDANTA)
8.03 A  41                                  COLOR(BLU)
7.03 A            B1ENHE    R        O 14101REFFLD(FDENH1)
8.03 A  41                                  COLOR(BLU)
7.03 A            B1RABA        12A  O 14105
8.03 A  41                                  COLOR(BLU)
7.03 A            B1SALG    R        O 14118REFFLD(FDSAPR)
8.03 A  41                                  COLOR(BLU)
7.03 A                                      EDTWRD('     0 ,  -')
7.03 A*            B1SALG        12A  O 14118
7.03 A*            B1VARE         8A  H
7.03 A*            B1TEX2        75A  H
7.03 A*            B1ANTA        12A  H
7.03 A*            B1ENHE    R        H      REFFLD(FDENH1)
7.03 A            B1SAPR    R        H      REFFLD(FDSAPR)
7.03 A*            B1RABA        12A  H
7.03 A            B1KPRI         3A  H
7.xx A            B1NETO        12A  H
7.xx A            B1KOST        12A  H
7.xx A            B1LBEH        12A  H
7.xx A            B1DEKG         6A  H
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051124  141539  HKO550      REL-V5R3M0  5722-WDS
7.03 A                                      SFLSIZ(0012)
7.03 A                                      SFLPAG(0011)
7.03 A                                      ROLLUP(10)
7.xx A*                                     ROLLDOWN(11)
7.02 A*                                     CF01
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF14
     A                                      CA21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.xx A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
7.03 A                                  1118'Kl. :'
7.03 A                                      COLOR(WHT)
7.03 A                                  1124TIME
7.03 A                                      COLOR(WHT)
7.03 A                                  2  2'Vise Ordre  '
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO505B2'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
7.xx A                                  2 54USER
     A                                      COLOR(WHT)
7.03 A                                  2118'Dato:'
7.03 A                                      COLOR(WHT)
7.03 A                                  2124DATE
7.03 A                                      EDTCDE(Y)
7.03 A                                      COLOR(WHT)
7.03 A                                  3  1'-----------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ---------------------------'
7.03 A                                      COLOR(WHT)
8.01 A                                  4  2'Vare-   '
8.01 A            B3LKUN    R        O  4 11REFFLD(FOKUND)
8.01 A N45N46                               COLOR(YLW)
8.01 A  45                                  COLOR(RED)
8.01 A  46                                  COLOR(BLU)
8.01 A                                      OVRDTA
8.01 A            B3KUNA    R   -7   O  4 18REFFLD(FONAVN)
8.01 A N45N46                               COLOR(YLW)
8.01 A  45                                  COLOR(RED)
8.01 A  46                                  COLOR(BLU)
8.01 A                                      OVRDTA
8.01 A                                  4 44'Faktura-'
8.01 A            B2KUND    R        O  4 53REFFLD(FOKUND)
7.fb A N45N46                               COLOR(YLW)
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
     A                                      OVRDTA
8.01 A            B2KNAV    R   -7   O  4 60REFFLD(FONAVN)
7.fb A N45N46                               COLOR(YLW)
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
     A                                      OVRDTA
8.01 A                                  4 86'Ordrenr. . .'
8.01 A            B2NUMM    R        O  4 99REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2SUFF    R        O  4108REFFLD(FOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  5  2'kunde   '
8.01 A            B3LGAT    R        O  5 11REFFLD(FOADR1)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A                                  5 44'kunde   '
8.01 A            B2KGAT    R        O  5 53REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  5 86'Ordredato. .'
8.01 A            B2BDAT         6Y 0O  5 99EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B3LSTE    R        O  6 11REFFLD(FOSTED)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A            B2KSTE    R        O  6 53REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6 86'Oppf.dato. .'
8.01 A            B2DDAT         6Y 0O  6 99EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B3MOTX         4A  O  7 11COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A            B3MOBI    R        O  7 16REFFLD(FOMOBI)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A            B2MOTX         4A  O  7 53COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2MOBI    R        O  7 58REFFLD(FOMOBI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  7 86'Utgår dato .'
8.01 A            B2TDAT         6Y 0O  7 99EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kunde-  '
     A            B2NAVN    R        O  8 11REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  8 86'Lev. dato. .'
8.01 A            B2LDAT         6Y 0O  8 99EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'prosjekt'
     A            B2ADR1    R        O  9 11REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  9 86'Vår ref. . .'
8.01 A            B2VREF        25A  O  9 99COLOR(YLW)
     A                                      OVRDTA
     A            B2STED    R        O 10 11REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                 10 86'Kundens ref.'
8.01 A            B2DREF        25A  O 10 99COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Info.. .'
     A            B2INF1    R        O 11 11REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 44'Ordretotal .'
     A                                      COLOR(WHT)
     A            B2SALG    R        O 11 57REFFLD(FOTOTS)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                 12  2'5=Vis'
     A                                      COLOR(BLU)
     A            B2INF2    R        O 12 11REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.03 A*                                13  2'V L Varenr.  Varetekst            -
7.03 A*                                                 Antall Enh  Rab       B-
7.03 A*                                     eløp  '
7.03 A*                                     COLOR(WHT)
7.03 A                                 13  2'V'
7.03 A                                      COLOR(WHT)
7.03 A                                 13  4'L'
7.03 A                                      COLOR(WHT)
7.03 A                                 13  6'Varenr.'
7.03 A                                      COLOR(WHT)
7.03 A                                 13 15'Varetekst'
7.03 A                                      COLOR(WHT)
7.03 A                                 13 51'Varetekst'
7.03 A                                      COLOR(WHT)
7.03 A                                 13 87'Antall'
7.03 A                                      COLOR(WHT)
7.03 A                                 13101'Enh'
7.03 A                                      COLOR(WHT)
7.03 A                                 13105'Rab'
7.03 A                                      COLOR(WHT)
7.03 A                                 13118'Beløp '
7.03 A                                      COLOR(WHT)
7.03 A*
     A          R B2CMD
     A*%%TS  SD  20051107  140224  HKO550      REL-V5R3M0  5722-WDS
7.02 A*                                23  2'F3=Gå tilbake F1=DG-linje F9=Lager-
7.02 A*                                     beh  F14=Kundeinfo. F21=Spørring va-
7.02 A*                                     re/pris '
7.03 A                                 26  2'F3=Gå tilbake  F9=Lagerbeholdning -
7.02 A                                       F14=Kundeinfo.    F21=Spørring på -
7.02 A                                      vare/pris '
     A                                      COLOR(BLU)
