     A*%%TS  SD  20040827  125318  AMD         REL-V5R2M0  5722-WDS
     A*%%EC
7.xx A*          290116 bsa  Justeringer basert på ny GUI og regler
Q.01 A*         080219  bsa  Quick wins tilpasninger - breder skjerm
     A*-----------------------------------------------------------------
Q.01 A                                      DSPSIZ(24 80 *DS3 -
Q.01 A                                             27 132 *WIDE)
     A                                      PRINT
     A          R A1WIN
     A*%%TS  SD  20040827  125318  AMD         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(S1WIN)
     A                                      TEXT('Vinduversjon')
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF10
     A                                      CA12
     A                                      KEEP
7.xx A                                  1 36'AP600A1'
     A                                  2  2'Valg ved utskrift'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  3  2'Rutinenavn . . . . . . '
     A            A1RUTI    R        O  3 26REFFLD(AFRUTI AFRF)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Utkø/skriver . . . . + '
     A            A1OUTQ    R     A  B  4 26REFFLD(AFOUTQ AFRF)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(AM00100 *LIBL/QMFAGPL 33)
7.xx A                                  5  2'Papirtype. . . . . . . '
     A            A1FORM    R     A  B  5 26REFFLD(AFFORM AFRF)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Tegn pr. tomme (CPI) . '
     A            A1ACPI    R        B  6 26REFFLD(AFACPI AFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  7  2'Antall kopier. . . . . '
     A            A1AKOP    R        B  7 26REFFLD(AFAKOP AFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  8  2'Holde i kø . . . . . . '
     A            A1HLDQ    R        B  8 26REFFLD(AFHLDQ AFRF)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AMF0110 *LIBL/QMFAGPL)
7.xx A                                  8 30'1=Ja,0=Nei'
     A                                  9  2'Lagre utskrift . . . .:'
     A            A1SAVE    R        B  9 26REFFLD(AFSAVE AFRF)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AMF0120 *LIBL/QMFAGPL)
7.xx A                                  9 30'1=Ja,0=Nei'
     A                                 10  2'Prioritet. . . . . . .:'
     A            A1PRTY    R        B 10 26REFFLD(AFPRTY *LIBL/AFRF)
     A                                      TEXT('Prioritet')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '1' '2' '3' '4' '5' '6' -
     A                                      '7' '8' '9')
     A                                      CHKMSGID(AMF0130 *LIBL/QMFAGPL)
     A                                 10 30'(0 til 9)'
     A                                 11  2'Linjer pr. side. . . .:'
     A            A1ALIN    R        B 11 26REFFLD(AFALIN AFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  ERRMSGID(AM00080 *LIBL/QMFAGPL 30)
     A  31                                  ERRMSGID(AM00070 *LIBL/QMFAGPL 31)
     A                                 11 30'(5 til 96)'
     A                                 12  2'Posisjoner pr. linje .:'
     A            A1APOS    R        B 12 26REFFLD(AFAPOS AFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  ERRMSGID(AM00080 *LIBL/QMFAGPL 30)
     A  31                                  ERRMSGID(AM00070 *LIBL/QMFAGPL 31)
7.xx A                                 22  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                 22 26'F5=Forny'
     A                                      COLOR(BLU)
7.xx A                                 23  2'F4=Spørre'
     A                                      COLOR(BLU)
7.xx A                                 23 26'F10=Kjøre i batch'
     A                                      COLOR(BLU)
PDF  A          R A2WIN
PDF  A*%%TS  SD  20040827  125318  AMD         REL-V5R2M0  5722-WDS
PDF  A*                                     WINDOW(S1WIN)
PDF  A                                      TEXT('Vinduversjon')
PDF  A                                      CA03
PDF  A                                      CA04
PDF  A                                      CA05
PDF  A**                                    CF10
PDF  A                                      CA12
PDF  A                                      KEEP
7.xx A                                  1 36'AP600A2'
PDF  A                                  2  2'Valg ved utskrift'
PDF  A                                      COLOR(WHT)
PDF  A                                      DSPATR(UL)
7.xx A                                  3  2'Rutinenavn . . . . . . '
PDF  A            A2RUTI    R        O  3 26REFFLD(AFRUTI AFRF)
PDF  A                                      DSPATR(UL)
7.xx A                                  4  2'Utkø/skriver . . . . .+'
PDF  A            A2OUTQ    R     A  B  4 26REFFLD(AFOUTQ AFRF)
PDF  A                                      COLOR(YLW)
PDF  A  33                                  ERRMSGID(AM00100 *LIBL/QMFAGPL 33)
7.xx A                                  5  2'Arkiveres. . . . . . . '
6.20 A            A2ARKI         1N 0B  5 26
6.20 A                                      VALUES(0 1)
6.20 A                                      COLOR(YLW)
6.29 A  34                                  DSPATR(PR)
7.xx A                                  5 30'1=Ja,0=Nei'
7.xx A**34                              5 41'(Til info)'
7.xx A                                  6  2'Skjermvisning. . . . . '
PDF  A            A2SKJE         1N 0B  6 26COLOR(YLW)
PDF  A                                      VALUES(0 1)
7.xx A                                  6 30'1=Ja,0=Nei'
7.xx A                                  7  2'Liggend utskrift . . . '
PDF  A            A2LAND         1N 0B  7 26COLOR(YLW)
PDF  A                                      VALUES(0 1)
7.xx A                                  7 30'1=Ja,0=Nei'
7.xx A                                  8  2'Antall kopier. . . . . '
PDF  A            A2AKOP    R        B  8 26REFFLD(AFAKOP AFRF)
PDF  A                                      COLOR(YLW)
PDF  A                                      CHECK(RB)
7.xx A                                  9  2'Sendes på mail . . . . '
PDF  A            A2MAIL         1N 0B  9 26COLOR(YLW)
PDF  A                                      VALUES(0 1)
7.xx A                                  9 30'1=Ja,0=Nei'
PDF   *
PDF   *
7.xx A N35                             10  2'Excel eksport. . . . . '
6.30 A N35        A2EXLP         1N 0B 10 26COLOR(YLW)
6.30 A                                      VALUES(0 1)
7.xx A N35                             10 30'1=Ja,0=Nei'
7.xx A                                 22  2'F3=Gå tilbake'
PDF  A                                      COLOR(BLU)
7.xx A                                 22 26'F5=Forny'
PDF  A                                      COLOR(BLU)
7.xx A                                 23  2'F4=Spørre'
PDF  A                                      COLOR(BLU)
PDF  A**                               15 26'F10=Kjøre i batch'
PDF  A**                                    COLOR(BLU)
     A*          R S1WIN
     A**%%TS  SD  19950112  083340  RH          REL-V2R3M0  5738-PW1
     A*                                      TEXT('Ramme')
     A*                                      KEEP
     A*                                      WINDOW(&W_ALIN &W_APOS 16 44)
     A*            W_ALIN         2S 0P
     A*            W_APOS         3S 0P
     A          R ASUME
     A*%%TS  SD  19941222  135431  RH          REL-V2R3M0  5738-PW1
     A                                      KEEP
     A                                      ASSUME
     A                                 24 79' '
