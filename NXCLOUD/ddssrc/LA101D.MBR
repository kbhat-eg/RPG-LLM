     A*%%TS  SD  20090908  093113  BOF610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LA101D
     A* Beskrivelse . . : Statuskoder, generelle
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       260802 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20090908  093113  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Generelle systemverdier '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Identifikasjon for:'
     A                                      COLOR(WHT)
     A                                  6  2'Bestillingsregister. . . . . . .'
     A            A1ABRK    R        B  6 35REFFLD(LAABRK)
     A                                      COLOR(YLW)
     A                                  6 46'Slette-liste. . . . . . . .'
     A            A1ADEL    R        B  6 74REFFLD(LAADEL)
     A                                      COLOR(YLW)
     A                                  7  2'Bestillingsforslag . . . . . . .'
     A            A1AFOR    R        B  7 35REFFLD(LAAFOR)
     A                                      COLOR(YLW)
     A                                  7 46'Felles-lister . . . . . . .'
     A            A1ALSM    R        B  7 74REFFLD(LAALSM)
     A                                      COLOR(YLW)
     A                                  8  2'Batch-nummer . . . . . . . . . .'
     A            A1ABAT    R        B  8 35REFFLD(LAABAT)
     A                                      COLOR(YLW)
     A                                  8 46'Telle-lister. . . . . . . .'
     A            A1ATEL    R        B  8 74REFFLD(LAALSM)
     A                                      COLOR(YLW)
     A                                  9  2'Lager-buntnummer . . . . . . . .'
     A            A1ABUN    R        B  9 35REFFLD(LAABUN)
     A                                      COLOR(YLW)
     A                                  9 46'Telle-bunter. . . . . . . .'
     A            A1ATBU    R        B  9 74REFFLD(LAALSM)
     A                                      COLOR(YLW)
     A                                 10  2'Bilagsnummer . . . . . . . . . .'
     A            A1ABIN    R        B 10 35REFFLD(LAABUN)
     A                                      COLOR(YLW)
     A                                 13  2'Kontohenvisningskode . . . . . .'
     A            A1AKHV    R        B 13 35REFFLD(LAAKHV)
     A                                      COLOR(YLW)
     A                                 14  2'Standard ordretype . . . . . . .'
     A            A1AOTY    R        B 14 35REFFLD(LAAOTY)
     A                                      COLOR(YLW)
     A                                 15  2'Standard linjetype, varelinjer .'
     A            A1ALTY    R        B 15 35REFFLD(LAALTY)
     A                                      COLOR(YLW)
     A                                 16  2'Standard linjetype, tekstlinjer.'
     A            A1ATTY    R        B 16 35REFFLD(LAATTY)
     A                                      COLOR(YLW)
     A                                 18  2'Standard leverandørnummer. . . .'
     A            A1ALEV    R        B 18 35REFFLD(LAALEV)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 19  2'Leverandør for oppretting av nye'
     A            A1ALNR    R        B 19 35REFFLD(LAALNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 20  2'Øreavrunding varenummer. . . . .'
     A            A1AVNR    R        B 20 35REFFLD(LAAVNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 21  2'Øreavrunding bilagskode. . . . .'
     A            A1ABIL    R        B 21 35REFFLD(LAABIL)
     A                                      COLOR(YLW)
     A                                 22  2'Fakturaleverandør bilagskode . .'
     A            A1ABIF    R        B 22 35REFFLD(LAABIF)
     A                                      COLOR(YLW)
     A                                 23  2'Kjede Sortiement . . . . . . . .'
     A            A1AKSO    R        B 23 35REFFLD(LAAKSO)
     A                                      COLOR(YLW)
     A                                 24  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
