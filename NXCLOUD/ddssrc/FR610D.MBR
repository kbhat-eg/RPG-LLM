     A*%%TS  SD  20040513  143122  HKO         REL-V5R2M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FP600D
     A* Beskrivelse . . : Utskrift av spesialpriser
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010416  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20040513  143122  HKO         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(*DFT 20 62)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kundebrev m/rabatter og spesialpr-
     A                                      iser '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgruppe  . . +'
     A            B1PRGR    R        B  4 20REFFLD(FRPRGR)
     A  40                                  ERRMSGID(FRA3010 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A            B1PTXT        30A  O  4 28COLOR(YLW)
7.xx A                                  5  2'Rabattkategori. +'
     A            B1RKAT    R        B  5 20REFFLD(FRRKAT)
     A  31                                  ERRMSGID(FMR0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMR0060 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1RTXT        30A  O  5 28COLOR(YLW)
7.xx A                                  6  2'Kunde . . . . . +'
     A            B1KUND    R        B  6 20REFFLD(FRKUND)
     A  34                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1KNAV        30A  O  6 28COLOR(YLW)
7.xx A                                  7  2'Kundeprosjekt . +'
     A            B1KPRO    R        B  7 20REFFLD(FRKPRO)
     A  35                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1PNAV    R        O  7 28REFFLD(FLNAVN)
     A                                      COLOR(YLW)
7.xx A                                  9  2'Sortiment . . . +'
     A            B1VSOR    R        B  9 20REFFLD(VCVSOR)
     A  37                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                 10  2'Rabatt-/prisdato.'
     A            B1RDAT         6Y 0B 10 20COLOR(YLW)
     A  38                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 38)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                 12  2'Kommentar:'
     A            B1TXT1        60A  B 13  2COLOR(YLW)
     A                                      CHECK(LC)
     A            B1TXT2        60A  B 14  2COLOR(YLW)
     A                                      CHECK(LC)
     A            B1TXT3        60A  B 15  2COLOR(YLW)
     A                                      CHECK(LC)
     A                                 17  2'Ta med kundeprosjekt uten egne pri-
     A                                      ser/rabatter?'
     A            B1VALG    R        B 17 50REFFLD(FÅVALG)
     A  39                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 17 52'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
