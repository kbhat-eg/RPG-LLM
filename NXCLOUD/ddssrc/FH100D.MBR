     A*%%TS  SD  20060626  155339  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
7.xx A*     BSA  240216    Endringer relatert til ny GUI
7.01 A*k00  030620 BOF NXS-1195  Utvidet med parm inkl skaff/tilb i FKDIPF
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19971125  195358  HKO         REL-V3R7M0  5716-PW1
     A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVEDL)
     A            B1KUND    R        O  9  5REFFLD(FHKUND)
     A            B1KNAV        11   O  9 12
     A            B1KPRO    R        O  9 25REFFLD(FHKPRO)
     A            B1PBOK    R        O  9 35REFFLD(FHPBOK)
     A            B1PFIL    R        O  9 41REFFLD(FHPFIL)
     A            B1VSOR    R        O  9 45REFFLD(FHVSOR)
     A            B1RFMT    R        O  9 57REFFLD(FHRFMT)
     A            B1ENDR    R        O  9 69REFFLD(FHENDR)
     A            B1DATO         6Y 0O  9 73EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020308  135431  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 25'Kundeprosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kunde '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Navn       '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 25'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 34'Pris'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'Fil'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 45'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 57'Pris rec.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 68'Endr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 73' Siste '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 34'bok '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 57'format     '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 73' fors. '
7.xx A                                      COLOR(WHT)
7.xx A**                                7 25'prosj.   bok                    fo-
7.xx A**                                    rmat           fors.'
7.xx A**                                    COLOR(WHT)
7.xx A**                                6  2'V  Kunde  Navn         Kunde    Pr-
7.xx A**                                    is  Fil  Varesort.   Pris rec.  End-
7.xx A**                                    r  Siste'
7.xx A**                                    COLOR(WHT)
7.xx A**                                7 25'prosj.   bok                    fo-
7.xx A**                                    rmat           fors.'
7.xx A**                                    COLOR(WHT)
7.xx A                                  5  3'+'
7.xx A            B2KUND    R        B  5  5REFFLD(FHKUND)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 23'+'
7.xx A            B2KPRO    R        B  5 25REFFLD(FHKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  115428  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020308  135541  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Kunde. . . . . . . . . . . +'
     A            C1KUND    R        B  5 31REFFLD(FHKUND)
     A  31                                  ERRMSGID(FMH0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Kundeprosjekt. . . . . . . +'
     A            C1KPRO    R        B  6 31REFFLD(FHKPRO)
     A  33                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV    R        O  6 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020308  135541  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Distribusjonsliste finnes'
     A                                  3  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20060626  155339  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av distribusjonsliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kunde. . . . . . . . . . . .'
     A            C1KUND    R        O  5 31REFFLD(FHKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 42COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundeprosjekt. . . . . . . .'
     A            C1KPRO    R        O  6 31REFFLD(FHKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  6 42REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Prisbok. . . . . . . . . . .'
     A            C2PBOK    R        B  8 31REFFLD(FHPBOK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 42'1=Ja,0=Nei'
     A                                  9  2'Prisfil. . . . . . . . . . .'
     A            C2PFIL    R        B  9 31REFFLD(FHPFIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 42'1=Ja,0=Nei'
7.xx A                                 11  2'Varesortiment. . . . . . . +'
     A            C2VSOR    R        B 11 31REFFLD(FHVSOR)
     A  31                                  ERRMSGID(FMH0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2VBES    R        O 11 42REFFLD(VCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Varesortiment ekst spørring+'
     A            C2VSOS    R        B 12 31REFFLD(FHVSOS)
     A  33                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2VBE2    R        O 12 42REFFLD(VCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 14  2'Leveringstype. . . . . . . +'
     A            C2LETY    R        B 14 31REFFLD(FHLETY)
     A  34                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2LBES    R        O 14 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Prisdistribusjon via fil:'
     A                                      DSPATR(UL)
     A                                 17  2'Pris-recordformat. . . . . .'
     A            C2RFMT    R        B 17 31REFFLD(FHRFMT)
     A  35                                  ERRMSGID(FMH0110 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FMC0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2RBES    R        O 17 42REFFLD(FCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Kun endringer. . . . . . . .'
     A            C2ENDR    R        B 18 31REFFLD(FHENDR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 42'1=Ja,0=Nei'
     A                                 19  2'Prisdato siste forsendelse .'
     A            C2DATO         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(FHEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
7.01 A                                 20  2'Skaffevarer  . . . . . . . .'
7.01 A            C2SVAL    R        B 20 31REFFLD(FHSVAL)
7.01 A                                      COLOR(YLW)
7.01 A  30                                  DSPATR(PR)
7.01 A                                      OVRDTA
7.01 A                                 21  2'Tilbudspriser. . . . . . . .'
7.01 A            C2TVAL    R        B 21 31REFFLD(FHTVAL)
7.01 A                                      COLOR(YLW)
7.01 A  30                                  DSPATR(PR)
7.01 A                                      OVRDTA
     A                                 20 42'1=Ja,0=Nei'
     A                                 21 42'1=Ja,0=Nei'
     A          R D1WIN
     A*%%TS  SD  20020308  135541  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette distibusjonsliste '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . .'
     A            D1KUND    R        O  4 21REFFLD(FHKUND)
     A                                  5  2'Kundeprosjekt. . .'
     A            D1KPRO    R        O  5 21REFFLD(FHKPRO)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020308  135541  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 45)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . .'
     A            K1KUND    R        B  4 21REFFLD(FHKUND)
     A  31                                  ERRMSGID(FMH0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 32)
     A  34                                  ERRMSGID(FMH0030 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Kundeprosjekt. . .'
     A            K1KPRO    R        B  5 21REFFLD(FHKPRO)
     A  33                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Kunde. . . . . . .'
     A            FHKUND    R        O  9 21OVRDTA
     A                                 10  2'Kundeprosjekt. . .'
     A            FHKPRO    R        O 10 21OVRDTA
     A                                 13  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
