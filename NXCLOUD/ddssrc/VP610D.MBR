     A*%%TS  SD  20090528  120623  ASP_X50     REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VP610D
     A* Beskrivelse . . : Utskrift av prisbok til kunder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20090528  120623  ASP_X50     REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av prisbok til kunder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Rabattkategori . . . . . . +'
     A            B1RKAT         3Y 0B  4 31COLOR(YLW)
     A  31                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMP0300 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1RTXT        30A  O  4 42COLOR(YLW)
7.xx A                                  5  2'Kunde. . . . . . . . . . . +'
     A            B1KUND    R        B  5 31REFFLD(FHKUND)
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1KNAV        30A  O  5 42COLOR(YLW)
7.xx A                                  6  2'Kundeprosjekt. . . . . . . +'
     A            B1KPRO    R        B  6 31REFFLD(FHKPRO)
     A  35                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1PNAV        30A  O  6 42COLOR(YLW)
7.xx A                                  7  2'Leveringstype. . . . . . . +'
     A            B1LETY    R        B  7 31REFFLD(FHLETY)
     A  44                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LBES    R        O  7 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Lager. . . . . . . . . . . +'
     A            B1LAGE    R        B  8 31REFFLD(VLLAGE)
     A  45                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LANA    R        O  8 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                  9  2'Prisdato . . . . . . . . . .'
     A            B1DATO         6Y 0B  9 31COLOR(YLW)
     A  36                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                 10  2'Overstyring av vareutplukk:'
     A                                      DSPATR(UL)
7.xx A                                 11  2'Varesortiment. . . . . . . +'
     A            B1VSOR    R        B 11 31REFFLD(VCVSOR)
     A  37                                  ERRMSGID(VMP0350 *LIBL/QMFOFAK 37)
     A  38                                  ERRMSGID(VMP0310 *LIBL/QMFOFAK 38)
     A  39                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A            B1SBES    R        O 11 42REFFLD(VCBESK)
     A                                      COLOR(YLW)
7.xx A                                 12  2'Overgruppe . . . . . . . . +'
     A            B1OGRP    R        B 12 31REFFLD(VDOGRP)
     A  40                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A            B1OTXT    R        O 12 42REFFLD(VGOTXT)
     A                                      COLOR(YLW)
7.xx A                                 13  2'Hovedgruppe. . . . . . . . +'
     A            B1HGRP    R        B 13 31REFFLD(VDHGRP)
     A  41                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A            B1HTXT    R        O 13 42REFFLD(VGHTXT)
     A                                      COLOR(YLW)
7.xx A                                 14  2'Undergruppe. . . . . . . . +'
     A            B1UGRP    R        B 14 31REFFLD(VDUGRP)
     A  42                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A            B1UTXT    R        O 14 42REFFLD(VGUTXT)
     A                                      COLOR(YLW)
7.xx A                                 15  2'Leverandør . . . . . . . . +'
     A            B1LDOR    R        B 15 31REFFLD(VDLDOR)
     A  43                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LNAV        30A  O 15 42COLOR(YLW)
     A                                 17  2'Overstyringer i prisbok:'
     A                                      DSPATR(UL)
7.xx A                                 18  2'Skaffevarer. . . . . . . . .'
     A            B1SVAL    R        B 18 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 18 42'1=Ja,0=Nei'
7.xx A                                 19  2'Tilbudspriser. . . . . . . .'
     A            B1TVAL    R        B 19 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 19 42'1=Ja,0=Nei'
7.xx A                                 20  2'Varetekst-2. . . . . . . . .'
     A            B12VAL    R        B 20 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 20 42'1=Ja,0=Nei'
7.xx A                                 21  2'Utelate rabatter . . . . . .'
     A            B1RVAL    R        B 21 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 21 42'1=Ja,0=Nei'
7.xx A                                 22  2'Utelate leverandør . . . . .'
     A            B1LVAL    R        B 22 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 22 42'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
