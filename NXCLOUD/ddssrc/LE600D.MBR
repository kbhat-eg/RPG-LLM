     A*%%TS  SD  20110513  114529  ASK620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LE600D
     A* Beskrivelse . . : Utskrift av telleliste - parameterinntasting
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.21 A* 6.20  070115 BSA  Høyrejustering av felt ved bruk av Field Exit
7.xx A* 7.xx  190516 bsa  Justeringer basert på ny GUI og regler
     A*
7.01 A*K000   200820 bsa  Mangler cloud koder
     A*
8.01 A* 800  160124  ask  Lagt inn endringer i forhåndsutfylling bilde D1BILD
8.02 A* 800  050624  bsa  Feil ved sorteringsrekkefølge 6
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1BLD
     A*%%TS  SD  20110513  114529  ASK620      REL-V7R1M0  5770-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av telleliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  6  2'Bare varer som finnes i lager. .'
     A            B1LIST    R        B  6 35REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      CHKMSGID(LME0100 *LIBL/QMFLAGR)
     A                                      VALUES(0 1)
     A  32                                  ERRMSGID(LME0150 *LIBL/QMFLAGR 32)
     A                                  6 45'1=Ja,0=Nei'
     A                                  7  2'Legg til varer i hoved-liste . .'
     A            B1HOVD    R        B  7 35REFFLD(LÅVALG)
     A  31                                  ERRMSGID(LME0090 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHKMSGID(LME0100 *LIBL/QMFLAGR)
     A                                      VALUES(0 1)
     A                                  7 45'1=Ja,0=Nei'
     A                                  9  2'Syklisk telling'
     A            B1STEL    R        O  9 35REFFLD(LCSTEL)
     A                                      COLOR(GRN)
7.xx A                                      EDTCDE(1)
     A                                  9 45'1=Ja,0=Nei'
7.xx A                                 11  2'Varetype (Blank gir alle). . . +'
     A            B1VTYP         1A  B 11 35COLOR(YLW)
     A  33                                  ERRMSGID(LME0150 *LIBL/QMFLAGR 33)
     A                                 12  2'Skrive antall på telleliste. . .'
     A            B1VAL1         1Y 0B 12 35CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHKMSGID(LME0100 *LIBL/QMFLAGR)
     A                                      VALUES(0 1)
     A                                 12 45'1=Ja,0=Nei'
     A                                 13  2'Bare varer med negativ beholdn..'
     A            B1VAL2         1Y 0B 13 35CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHKMSGID(LME0100 *LIBL/QMFLAGR)
     A                                      VALUES(0 1)
     A                                 13 45'1=Ja,0=Nei'
     A                                 14  2'Utelukke etter siste telledato .'
     A            B1OPDA         6Y 0B 14 35COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 16  2'Sorteringsrekkefølge . . . . . .'
     A            B1VAL3         1Y 0B 16 35EDTCDE(Z)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      CHKMSGID(LME0110 *LIBL/QMFLAGR)
8.02 A                                      VALUES(1 2 3 4 5 6)
     A                                 17 45'2=Lager/vgr/varetekst'
     A                                 18 45'3=Lager/varetekst'
     A                                 19 45'4=Lokasjon/varetekst'
     A                                 20 45'5=Lager/vgr/varetekst/lokasjon'
     A                                 21 45'6=Lager/lev/vgr/varetekst/lokasjon'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 16 45'1=Lager/vgr/varenr'
     A*
     A          R D1BLD
     A*%%TS  SD  20020924  120158  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av telleliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Seleksjonskriterier:'
     A                                      COLOR(WHT)
     A                                  6 33'Fra'
     A                                  6 45'Til'
7.01 A                                  7  2'Varenummer . . . . . . . . +'
     A            D1FVAR         8Y 0B  7 31CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  31                                  ERRMSGID(LME0010 *LIBL/QMFLAGR 31)
7.01 A                                  7 41'+'
     A            D1TVAR         8Y 0B  7 43CHECK(RZ)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.01 A                                  8  2'Overgruppe . . . . . . . . +'
     A            D1FOGR    R        B  8 31REFFLD(FFRFR/VVOGRP *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LME0015 *LIBL/QMFLAGR 32)
7.01 A                                  8 41'+'
     A            D1TOGR    R        B  8 43REFFLD(FFRFR/VVOGRP *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
7.01 A                                  9  2'Hovedgruppe. . . . . . . . +'
     A            D1FHGR    R        B  9 31REFFLD(FFRFR/VVHGRP *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(LME0020 *LIBL/QMFLAGR 32)
7.01 A                                  9 41'+'
     A            D1THGR    R        B  9 43REFFLD(FFRFR/VVHGRP *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
7.01 A                                 10  2'Undergruppe. . . . . . . . +'
     A            D1FUGR    R        B 10 31REFFLD(FFRFR/VVUGRP *LIBL/FFRF)
     A  34                                  ERRMSGID(LME0030 *LIBL/QMFLAGR 33)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
7.01 A                                 10 41'+'
     A            D1TUGR    R        B 10 43REFFLD(FFRFR/VVUGRP *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                 11  2'Lagerplass . . . . . . . . .'
     A            D1FPLA    R        B 11 31REFFLD(LEPLAS)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(LME0040 *LIBL/QMFLAGR 35)
     A            D1TPLA    R        B 11 43REFFLD(LEPLAS)
     A                                      COLOR(YLW)
7.01 A                                 12  2'Lagernummer. . . . . . . . +'
     A            D1FLAG         2A  B 12 31COLOR(YLW)
6.21 A                                      CHECK(RB)
8.01 A  39                                  DSPATR(PC)
     A  36                                  ERRMSGID(LME0050 *LIBL/QMFLAGR 36)
     A  37                                  ERRMSGID(LME0055 *LIBL/QMFLAGR 37)
7.01 A                                 12 41'+'
     A            D1TLAG         2A  B 12 43COLOR(YLW)
6.21 A                                      CHECK(RB)
     A  38                                  ERRMSGID(LME0160 *LIBL/QMFLAGR 38)
8.01 A                                 11 52'(Bare ved valg på lagerplass)'
7.01 A                                 14  2'Produktansvarlig . . . . . +'
     A            D1PANS         4A  B 14 31COLOR(YLW)
7.01 A                                 15  2'Leverandør . . . . . . . . +'
     A            D1LEVR         6Y 0B 15 31COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
