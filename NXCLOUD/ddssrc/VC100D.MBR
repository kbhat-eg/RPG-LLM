     A*%%TS  SD  20111103  123148  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A* 7.xx  240216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101104  093320  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 7 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VSOR    R        O  8  5REFFLD(VCVSOR)
     A            B1LAGE    R        O  8 17REFFLD(VCLAGE)
     A            B1BESK    R        O  8 21REFFLD(VCBESK)
     A            B1TYPE    R        O  8 57REFFLD(VCTYPE)
     A            B1TBES        22A  O  8 59
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101104  093320  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF10
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VC100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Sortement'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 21'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Behandle varesortiment'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Sortement'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Lag.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 21'Beskrivelse           '
7.xx A                                      COLOR(WHT)
7.01 A                                  7 57'T'
7.xx A                                      COLOR(WHT)
7.01 A                                  7 59'Type'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V  Sortement  Lag. Beskrivelse    -
7.xx A**                                                        Type'
7.xx A**                                    COLOR(WHT)
7.xx A            B2VSOR    R        B  5  5REFFLD(VCVSOR)
     A  31                                  ERRMSGID(VMC0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  5 21REFFLD(VCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20101126  143930  ASP_X50     REL-V6R1M0  5761-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A                                 23 41'F10=Utskrift'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  092620  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VC100C1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Sortiment. . . . . . . . . .'
     A            C1VSOR    R        B  5 31REFFLD(VCVSOR)
     A  31                                  ERRMSGID(VMC0010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Lager  . . . . . . . . . . .'
     A            C1LAGE    R        B  6 31REFFLD(VCLAGE)
     A  32                                  ERRMSGID(MFA0160 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        30   O  6 35
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19971023  111118  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Sortiment finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20111103  123148  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VC100C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Sortiment. . . . . . . . . .'
     A            C1VSOR    R        O  5 31REFFLD(VCVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Lager  . . . . . . . . . . .'
     A            C1LAGE    R        O  6 31REFFLD(VCLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        30A  O  6 35OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2BESK    R        B  8 31REFFLD(VCBESK)
     A  31                                  ERRMSGID(VMC0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Type . . . . . . . . . . . .'
     A            C2TYPE    R        B 10 31REFFLD(VCTYPE)
7.01 A                                      VALUES(' ' 'F' 'V' 'H' 'P' 'C')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 35'Blank=Standard'
     A                                 11 35'V=Vareliste'
     A                                 12 35'H=Handleliste Nettbutikk'
     A                                 13 35'P=Periode Varer'
     A                                 14 35'C=Sentrallager vare '
7.01 A  40                             15 35'F=Prisfokus varer '
7.01 A                                 16  2'Periode - fra uke  . . . . .'
7.01 A            C2FUKE    R      00B 16 31REFFLD(VCFUKE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  34                                  ERRMSGID(FM01210 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
7.01 A            C2FDAT         6  0O 16 35EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 17  2'Periode - til uke  . . . . .'
7.01 A            C2TUKE    R      00B 17 31REFFLD(VCFUKE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  35                                  ERRMSGID(FM01210 *LIBL/QMFOFAK 35)
     A                                      OVRDTA
7.01 A            C2TDAT         6  0O 17 35EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 19  2'Opprettet dato . . . . . . .'
7.01 A            C2ODAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 20  2'Sist endret dato . . . . . .'
7.01 A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.01 A                                 21  2'Endret av. . . . . . . . . .'
7.01 A            C2EUSR    R        O 21 31REFFLD(VCEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040421  102009  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette sortiment '
     A                                      DSPATR(RI)
     A                                  4  2'Sortiment. . . . .'
     A            D1VSOR    R        O  4 21REFFLD(VCVSOR)
     A                                  5  2'Lager  . . . . . .'
     A            D1LAGE    R        O  5 21REFFLD(VCLAGE)
     A                                  6  2'Beskrivelse. . . .'
     A            VCBESK    R        O  6 21
     A                                  7  2'Type . . . . . . .'
     A            VCTYPE    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040421  102009  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Sortiment. . . . .'
     A            K1VSOR    R        B  4 21REFFLD(VCVSOR)
     A  31                                  ERRMSGID(VMC0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMC0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.24 A**                                    DSPATR(MDT)
     A                                  5  2'Lager  . . . . . .'
     A            K1LAGE    R        B  5 21REFFLD(VCLAGE)
     A  33                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Sortiment. . . . .'
     A            VCVSOR    R        O  9 21OVRDTA
6.24 A                                 10  2'Lager  . . . . . .'
6.24 A            VCLAGE    R        O 10 21OVRDTA
     A                                 11  2'Beskrivelse. . . .'
     A            VCBESK    R        O 11 21OVRDTA
     A                                 12  2'Type . . . . . . .'
6.24 A            VCTYPE    R        O 12 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
