     A*%%TS  SD  20051027  155527  HKO550      REL-V5R3M0  5722-WDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * K000  021120 bsa  Viser parameter uansett om switchen er satt eller
7.04  * K000              ikke.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20051027  155527  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise dekningsbidrag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  3' Salg. . . . . . . . . . . . .'
     A            A1TOTS    R        O  5 34REFFLD(FOTOTS)
     A                                  6  3'-Rabatt. . . . . . . . . . . .'
     A            A1TOTR    R        O  6 34REFFLD(FOTOTR)
     A                                  7  3'=Netto salgsum . . . . . . . .'
     A            A1NETT    R        O  7 34REFFLD(FOTOTS)
     A                                  9  3'-Kostnader . . . . . . . . . .'
     A            A1TOTK    R        O  9 34REFFLD(FOTOTK)
     A                                 10  3'-Ved oppnådd maksimal bonus. .'
     A            A1TOTB    R        O 10 34REFFLD(FOTOTK)
     A                                 12  4'Dekning . . . . . . . . . . .'
     A            A1DEKN    R        O 12 34REFFLD(FOTOTS)
     A                                 13  4'Dekningsgrad  . . . . . . . .'
     A            A1GRAD    R        O 13 40REFFLD(FOPASL)
     A                                 13 48'%'
     A                                 15  4'Rabattkategori. . . . . . . .'
     A            A1RKAT    R        B 15 34REFFLD(FORKAT)
     A  31                                  ERRMSGID(FMA0130 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 16  4'Ordrerabatt . . . . . . . . .'
     A            A1RABP    R        B 16 34REFFLD(FORABP)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 17  4'Ønsket dekn.grad/rabatt . . .'
     A            A1PASL    R        B 17 34REFFLD(FOPASL)
     A                                      EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 18  4'Hvordan vise rabatt/pris. . .'
     A            A1PASK    R        B 18 34REFFLD(FOPASK)
     A                                      COLOR(YLW)
7.05 A                                 18 37'(k)'
7.05 A                                 18 45'P: Som nettopris, R: Rabatt'
     A                                 19  4'Hent priser fra tilbudsnummer'
     A            A1PNUM    R        B 19 34REFFLD(FOPORD)
     A  32                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 19 45'Blank=Rabatt på std.pris'
7.04 A**41                             20  4'Bruk NOBB frakt - kostpris=innpris'
7.04 A                                 20  4'Bruk NOBB frakt - kostpris=innpris'
7.04 A**41        A1SVA2    R        B 20 41REFFLD(FÅVALA)
7.04 A            A1SVA2    R        B 20 41REFFLD(FÅVALA)
7.01 A                                      VALUES('0' '1')
7.01 A                                      COLOR(YLW)
7.04 A**41                             20 45'1=Ja,0=Nei'
7.04 A                                 20 45'1=Ja,0=Nei'
     A                                 21  4'Skal ny behandling utføres. .'
     A            A1SVAR    R        B 21 41REFFLD(FÅVALA)
     A                                      VALUES('0' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 21 45'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
