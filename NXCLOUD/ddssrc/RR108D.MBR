     A*%%TS  SD  20040216  102850  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RR108D
     A* Beskrivelse . . : Resultatrapport - vedlikehold fra-til begrep
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160902 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020916  221126  KOB         REL-V5R1M0  5722-WDS
     A            B18RAP    R        H      REFFLD(RR8RAP)
     A            B18LIN    R        H      REFFLD(RR8LIN)
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
     A            B18LØP    R        O 10  5REFFLD(RR8LØP)
     A            B18FKT    R        O 10  9REFFLD(RR8FKT)
     A            B18FSP    R        O 10 15REFFLD(RR8FSP)
     A            B18FAV    R        O 10 20REFFLD(RR8FAV)
     A            B18TKT    R        O 10 27REFFLD(RR8TKT)
     A            B18TSP    R        O 10 33REFFLD(RR8TSP)
     A            B18TAV    R        O 10 38REFFLD(RR8TAV)
     A            B18KLF    R        O 10 44REFFLD(RR8KLF)
     A            B18LIF    R        O 10 47REFFLD(RR8LIF)
     A            B18MOT    R        O 10 54REFFLD(RR8MOT)
     A            B18PRO    R        O 10 58REFFLD(RR8PRO)
     A                                      EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040216  102850  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rapport:'
     A            B20RAP    R        O  4 11REFFLD(RR0RAP)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            B20TXT    R        O  4 14REFFLD(RR0TXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  2'Linje. :'
     A            B25LIN    R        O  5 11REFFLD(RR5LIN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            B25TXT    R        O  5 17REFFLD(RR5TXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  8  2'V   Nr <- Konto fra ->   <- Konto -
     A                                      til ->  K   Fra  Snu %-vis'
     A                                      COLOR(WHT)
     A            B28LØP    R        B  9  5REFFLD(RR8LØP)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  9'Konto Bærer Avd   Konto Bærer Avd'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020916  214700  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Løpenummer . . . . . . . . .'
     A            C18LØP    R        B  5 31REFFLD(RR8LØP)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020916  150943  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  1'Linjenummer finnes fra før'
     A                                      COLOR(BLU)
     A                                  6  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020917  164517  KOB         REL-V5R1M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rapport . . . .:'
     A            C20RAP    R        O  4 19REFFLD(RR0RAP)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C20TXT    R        O  4 22REFFLD(RR0TXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  2'Linjenr . . . .:'
     A            C25LIN    R        O  5 19REFFLD(RR5LIN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C25TXT    R        O  5 25REFFLD(RR5TXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'Fra konto . . .:'
     A            C28FKT    R        B  7 19REFFLD(RR8FKT)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31 80                               ERRMSGID(RM02005 *LIBL/QMFOKON 31)
     A  31 81                               ERRMSGID(RM02000 *LIBL/QMFOKON 31)
     A  31 82                               ERRMSGID(RM02020 *LIBL/QMFOKON 31)
     A  31 83                               ERRMSGID(RM02030 *LIBL/QMFOKON 31)
     A  31 84                               ERRMSGID(RM02001 *LIBL/QMFOKON 31)
     A  31 85                               ERRMSGID(RM02100 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                  7 33'Til konto . . .:'
     A            C28TKT    R        B  7 50REFFLD(RR8TKT)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32 80                               ERRMSGID(RM02005 *LIBL/QMFOKON 32)
     A  32 81                               ERRMSGID(RM02000 *LIBL/QMFOKON 32)
     A  32 82                               ERRMSGID(RM02020 *LIBL/QMFOKON 32)
     A  32 83                               ERRMSGID(RM02030 *LIBL/QMFOKON 32)
     A  32 84                               ERRMSGID(RM02002 *LIBL/QMFOKON 32)
     A  32 85                               ERRMSGID(RM02100 *LIBL/QMFOKON 32)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                  8  2'Fra bærer . . .:'
     A            C28FSP    R        B  8 19REFFLD(RR8FSP)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                  8 33'Til bærer . . .:'
     A            C28TSP    R        B  8 50REFFLD(RR8TSP)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                  9  2'Fra avdeling. .:'
     A            C28FAV    R        B  9 19REFFLD(RR8FAV)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  35                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                  9 33'Til avdeling. .:'
     A            C28TAV    R        B  9 50REFFLD(RR8TAV)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 11  2'Fra linje-nr. .:'
     A            C28LIF    R        B 11 19REFFLD(RR8LIF)
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33 80                               ERRMSGID(RM02070 *LIBL/QMFOKON 33)
     A  33 81                               ERRMSGID(RM02080 *LIBL/QMFOKON 33)
     A  33 82                               ERRMSGID(RM02090 *LIBL/QMFOKON 33)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 13  2'Snu fortegn . .:'
     A            C28MOT    R        B 13 19REFFLD(RR8MOT)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 14  2'%andel. . . . .:'
     A            C28PRO    R        B 14 19REFFLD(RR8PRO)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RÅEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020916  193002  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette løpenummer '
     A                                      DSPATR(RI)
     A                                  4  2'Løpenummer. . .:'
     A            D18LØP    R        O  4 19REFFLD(RR8LØP)
     A  32                                  ERRMSGID(RMH0250 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMH0260 *LIBL/QMFOKON 33)
     A                                  5  2'Fra avdeling. .:'
     A            RR8FAV    R        O  5 19DSPATR(UL)
     A                                  6  2'Fra konto . . .:'
     A            RR8FKT    R        O  6 19DSPATR(UL)
     A                                  7  2'Fra bærer . . .:'
     A            RR8FSP    R        O  7 19DSPATR(UL)
     A                                  8  2'Til avdeling. .:'
     A            RR8TAV    R        O  8 19DSPATR(UL)
     A                                  9  2'Til konto . . .:'
     A            RR8TKT    R        O  9 19DSPATR(UL)
     A                                 10  2'Til bærer . . .:'
     A            RR8TSP    R        O 10 19DSPATR(UL)
     A                                 11  2'Fra linje-nr. .:'
     A            RR8LIF    R        O 11 19DSPATR(UL)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020916  193002  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Løpenummer .'
     A            K18LØP    R        B  4 15REFFLD(RR8LØP)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Løpenummer .'
     A            RR8LØP    R        O  8 15
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
