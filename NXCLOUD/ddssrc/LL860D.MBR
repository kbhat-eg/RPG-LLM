     A*%%TS  SD  20100913  084549  ASP_MGR     REL-V6R1M0  5761-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.00  040117 bsa  Endringer pga ny GUI
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20100913  084549  ASP_MGR     REL-V6R1M0  5761-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      CA12
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Inng.fakturainfo til regneark  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Leverandør . . . . . . . . +'
     A            A1LDOR         6D 0B  5 31CHECK(RZ)
     A  30                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 30)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            A1LNAV        30A  O  5 38
     A                                  6  2'Fra dato . . . . . . . . . .'
     A            A1FDAT         6D 0B  6 31CHECK(RZ)
     A  31                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  7  2'Til dato . . . . . . . . . .'
     A            A1TDAT         6D 0B  7 31CHECK(RZ)
     A  44                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                 11  2'PC-mappe/folder/katalog. . .'
     A            A1MAPP        12A  B 11 31CHECK(LC)
     A  39                                  ERRMSGID(LM00011 *LIBL/QMFLAGR 39)
     A                                      COLOR(YLW)
7.xx A                                 11 44'Filen lagres i denne katalogen'
     A                                 12  2'Filnavn. . . . . . . . . . .'
     A            A1FILN        12A  B 12 31CHECK(LC)
     A  40                                  ERRMSGID(LM00011 *LIBL/QMFLAGR 40)
     A                                      COLOR(YLW)
7.xx A                                 12 44'Filen lagres med dette navnet'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 32'F10=Batch'
     A                                      COLOR(BLU)
     A                                  6 38'DDMMÅÅ'
     A                                  7 38'DDMMÅÅ'
