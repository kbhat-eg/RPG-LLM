     A*%%TS  SD  20070306  134011  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF031D
     A* Beskrivelse . . : Fordelingsparametre, vedlikehold - underrec.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       071102 KOB  Nytt program
7.xx A* 7.xx  100516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021107  183532  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B 12  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        H      REFFLD(RFKONT)
     A            B1SPES    R        H      REFFLD(RFSPES)
     A            B1AVDE    R        H      REFFLD(RFAVDE)
     A            B1LINJ    R        O 12  5REFFLD(RFLINJ)
     A            B1PROS    R        O 12 10REFFLD(RFPROS)
     A                                      EDTCDE(K)
     A            B1KTO1    R        O 12 19REFFLD(RFKONT)
     A            B1SPS1    R        O 12 25REFFLD(RFSPES)
     A            B1AVD1    R        O 12 30REFFLD(RFAVDE)
     A            B1KTO2    R        O 12 42REFFLD(RFKONT)
     A            B1SPS2    R        O 12 48REFFLD(RFSPES)
     A            B1AVD2    R        O 12 53REFFLD(RFAVDE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20021107  183532  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Fra: Konto  Bærer  Avd  Kode  Pros-
     A                                      ent   Kontobeskrivelse'
7.xx A**                                    COLOR(WHT)
7.xx A            B2KONT    R        O  6  7REFFLD(RFKONT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2SPES    R        O  6 14REFFLD(RFSPES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2AVDE    R        O  6 20REFFLD(RFAVDE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2KODE    R        O  6 27REFFLD(RFKODE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PROS    R        O  6 33REFFLD(RFPROS)
     A                                      OVRDTA
7.XX A**                                    OVRATR
     A                                      COLOR(YLW)
7.xx A            B2TEXT    R        O  6 42REFFLD(RHTEXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  4'Lin'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                 10 11'3=Kopiere'
     A                                      COLOR(BLU)
7.xx A                                 10 22'4=Slette'
     A                                      COLOR(BLU)
7.xx A                                 10 32'5=Vise '
     A                                      COLOR(BLU)
7.xx A                                  9 19'   Medkonto    '
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
7.xx A                                  9 42'   Motkonto    '
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
7.xx A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  4'Lin'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  9'Prosent'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 19'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 25'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 31'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 42'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 48'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 54'Avd'
7.xx A**                               10  2'V Lin  Prosent   Konto Bærer Avd  -
7.xx A**                                          Konto Bærer Avd'
     A                                      COLOR(WHT)
7.xx A            B2LINJ    R        B  8  5REFFLD(RFLINJ)
     A                                      DLTCHK
     A  31                                  ERRMSGID(RMF0110 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMF0120 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMF0130 *LIBL/QMFOKON 33)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20021107  100205  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjenr. . . . . . .'
     A            C1LINJ    R        B  5 23REFFLD(RFLINJ)
     A  31                                  ERRMSGID(RMF0010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20021107  150655  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Linjenummer finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070306  134011  KOB550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5 17'     Medkonto      '
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
7.xx A                                  5 43'     Motkonto      '
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
7.xx A                                  6  2'Lin Prosent    Konto   Bærer   Avd-
7.xx A                                             Konto   Bærer   Avd'
     A                                      COLOR(WHT)
     A            C2LINJ    R        O  7  2REFFLD(RFLINJ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2PROT    R        B  7  7REFFLD(RFPROT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  7 15'+'
7.xx A            C2KTO1    R        B  7 17REFFLD(RFKONT)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  32                                  ERRMSGID(RMF0050 *LIBL/QMFOKON 32)
     A  34                                  ERRMSGID(RMF0010 *LIBL/QMFOKON 34)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  7 23'+'
7.xx A            C2SPS1    R        B  7 25REFFLD(RFSPES)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  7 30'+'
7.xx A            C2AVD1    R        B  7 32REFFLD(RFAVDE)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  7 41'+'
7.xx A            C2KTO2    R        B  7 43REFFLD(RFKONT)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(RMF0050 *LIBL/QMFOKON 33)
     A  35                                  ERRMSGID(RMF0140 *LIBL/QMFOKON 35)
     A  36                                  ERRMSGID(RMF0150 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  7 49'+'
7.xx A            C2SPS2    R        B  7 51REFFLD(RFSPES)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  7 56'+'
7.xx A            C2AVD2    R        B  7 58REFFLD(RFAVDE)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            C2TXT1        25A  O  9  7OVRDTA
     A                                      TEXT('Konto-tekst ........')
     A            C2TXT2        25A  O  9 37OVRDTA
     A                                      TEXT('Konto-tekst ........')
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RFEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20021107  145919  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette fordelingsparameter '
     A                                      DSPATR(RI)
     A                                  4  2'Linje. . . . . . .'
     A            D1LINJ    R        O  4 21REFFLD(RFLINJ)
     A                                  6  2'Prosent. . . . . .'
     A            RFPROT    R        O  6 21
     A                                  7  2'Konto. . . . . . .'
     A            RFKONT    R        B  7 21
     A                                  8  2'Kostnadsbærer. . .'
     A            RFSPES    R        B  8 21
     A                                  9  2'Avdeling . . . . .'
     A            RFAVDE    R        B  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20021107  145919  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Linje. . . . . . .'
     A            K1LINJ    R        B  4 21REFFLD(RFLINJ)
     A  31                                  ERRMSGID(RMF0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Linje. . . . . . .'
     A            RFLINJ    R        O  8 21OVRDTA
     A                                  9  2'Prosent. . . . . .'
     A            RFPROT    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10  2'Konto. . . . . . .'
     A            RFKONT    R        B 10 21
     A                                 11  2'Kostnadsbærer. . .'
     A            RFSPES    R        B 11 21
     A                                 12  2'Avdeling . . . . .'
     A            RFAVDE    R        B 12 21
