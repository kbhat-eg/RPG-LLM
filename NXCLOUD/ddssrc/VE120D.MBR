     A*%%TS  SD  20170315  125512  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : NEXSTEP
     A* Format. . . . . : VE120D
     A* Beskrivelse . . : Vedlikehold av utstillingsvarer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120419 bsa  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20170315  125512  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1TLAG    R        O  8  5REFFLD(FOLAGE)
     A            B1TVAR    R        O  8  8REFFLD(VVVARE)
     A            B1TEK1    R        O  8 24REFFLD(VVTEK1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20170315  125512  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Nexstep vare'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ustillingsvarer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  5'Lg'
     A                                      COLOR(WHT)
     A                                  4  8'Varenr'
     A                                      COLOR(WHT)
     A                                  6  2'2=Endre  4=slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  5'Lg'
     A                                      COLOR(WHT)
     A                                  7  8'Varenr'
     A                                      COLOR(WHT)
     A                                  7 24'Varetekst                    '
     A                                      COLOR(WHT)
     A            B2TLAG    R        B  5  5REFFLD(FOLAGE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2TVAR    R        B  5  8REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20160901  130747  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny   F6=Oppre-
     A                                      tt'
     A                                      COLOR(BLU)
     A                                 23 40' '
     A          R C1BLD
     A*%%TS  SD  20161122  151732  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Nexstep vare'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av utstillingsvarer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager. . . . . +'
     A            C1TLAG         2Y 0B  5 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Varenummer . . +'
     A            C1TVAR         8A  B  6 22COLOR(YLW)
     A                                      OVRDTA
      *
     A                                 23  2'F3=Gå tilbake  F4=Spørre        '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20170315  125512  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Nexstep tilbud'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av slettekode'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager . . . . . .'
     A            C1TLAG    R        O  5 20REFFLD(FOLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        30A  O  5 23COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Varenummer. . . .'
     A            C1TVAR         8A  O  6 20COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT        30A  O  6 30COLOR(YLW)
     A                                      OVRDTA
      *
     A                                  8  2'Enhet . . . . . +'
     A            C2TENH         3A  B  8 20COLOR(YLW)
     A  31                                  ERRMSGID(FME0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(KMD0100 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FME0020 *LIBL/QMFOFAK 33)
     A                                      OVRDTA
     A                                  9  2'GTIN nummer . . .'
     A            C2TGTI        15A  O  9 20COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Plassering. . . .'
     A            C2TPLS        35A  B 10 20COLOR(YLW)
     A                                      OVRDTA
      *
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2TODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2TEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2TEUS    R        O 20 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20161122  151732  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Erstatningsvare finnes fra-
     A                                       før !'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20161122  151732  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette utstillingsvare '
     A                                      DSPATR(RI)
     A                                  4  2'Lager . . . .'
     A            D1TLAG         2Y 0O  4 17
     A                                  5  2'Varenummer. .'
     A            D1TVAR         8A  O  5 17
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
