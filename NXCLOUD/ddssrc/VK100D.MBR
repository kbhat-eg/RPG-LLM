     A*%%TS  SD  20030206  130502  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  230216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030206  130046  HKO         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1OGRP    R        O 10  4REFFLD(VKOGRP)
7.xx A            B1HGRP    R        O 10  8REFFLD(VKHGRP)
7.xx A            B1UGRP    R        O 10 12REFFLD(VKUGRP)
7.xx A            B1UTXT        20A  O 10 16
7.xx A            B1LDOR    R        O 10 37REFFLD(VKLDOR)
7.xx A            B1LNAV         8A  O 10 44
7.xx A            B1AHGR    R        O 10 53REFFLD(VKAHGR)
7.xx A            B1AUGR    R        O 10 57REFFLD(VKAUGR)
7.xx A            B1AUTX        20A  O 10 61
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030206  130133  HKO         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF04
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varegruppeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4 53'Alt'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 58'Alt'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  4'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  9'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 15'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 37'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 53'hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 58'hgr'
     A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A**                                5 53'Alt Alt'
7.xx A                                  8 53'Alt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 58'Alt'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  8'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 12'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 37'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 53'hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 57'hgr'
     A                                      COLOR(WHT)
7.xx A**                                6  2'V Ogr Hgr Ugr                     -
7.xx A**                                     Lev.            hgr ugr'
7.xx A                                  6  2'+'
7.xx A            B2OGRP    R        B  6  4REFFLD(VKOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  7'+'
7.xx A            B2HGRP    R        B  6  9REFFLD(VKHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 13'+'
7.xx A            B2UGRP    R        B  6 15REFFLD(VKUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 35'+'
7.xx A            B2LDOR    R        B  6 37REFFLD(VKLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 51'+'
7.xx A            B2AHGR    R        B  6 53REFFLD(VKAHGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 56'+'
7.xx A            B2AUGR    R        B  6 58REFFLD(VKAUGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20030204  085104  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varegruppeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(VKOGRP)
     A  31                                  ERRMSGID(VMK0010 *LIBL/QMFOFAK 31)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 39COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  6 31REFFLD(VKHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 39COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(VKUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 39COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  8 31REFFLD(VKLDOR)
     A  38                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        35A  O  8 39COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020821  091008  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varegruppeknytning finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varegruppeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(VKOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 39COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(VKHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 39COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(VKUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 39COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(VKLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        35A  O  8 39COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Alternativ varegruppe. . . +'
     A            C2AHGR    R        B 10 31REFFLD(VKAHGR)
     A  31                                  ERRMSGID(VMK0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMF0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10 34'+'
7.xx A            C2AUGR    R        B 10 36REFFLD(VKAUGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2AUTX        35A  O 10 40COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VKEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette knytning '
     A                                      DSPATR(RI)
     A                                  4  2'Overgruppe . . . .'
     A            D1OGRP    R        O  4 21REFFLD(VKOGRP)
     A                                  5  2'Hovedgruppe. . . .'
     A            D1HGRP    R        O  5 21REFFLD(VKHGRP)
     A                                  6  2'Undergruppe. . . .'
     A            D1UGRP    R        O  6 21REFFLD(VKUGRP)
     A                                  7  2'Leverandør . . . .'
     A            D1LDOR    R        O  7 21REFFLD(VKLDOR)
     A                                  9  2'Alt. hovedgruppe .'
     A            VKAHGR    R        O  9 21
     A                                 10  2'Alt. undergruppe .'
     A            VKAUGR    R        O 10 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20030206  130502  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Overgruppe  . . +'
     A            K1OGRP    R        B  4 20REFFLD(VKOGRP)
     A  31                                  ERRMSGID(VMK0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMK0030 *LIBL/QMFOFAK 32)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe . . +'
     A            K1HGRP    R        B  5 20REFFLD(VKHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Undergruppe . . +'
     A            K1UGRP    R        B  6 20REFFLD(VKUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Leverandør. . . +'
     A            K1LDOR    R        B  7 20REFFLD(VKLDOR)
     A  38                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  2'Overgruppe. . . .'
     A            VKOGRP    R        O 11 20OVRDTA
     A                                 12  2'Hovegruppee . . .'
     A            VKHGRP    R        O 12 20OVRDTA
     A                                 13  2'Undergruppe . . .'
     A            VKUGRP    R        O 13 20OVRDTA
     A                                 14  2'Leverandør. . . .'
     A            VKLDOR    R        O 14 20OVRDTA
     A                                 15  2'Alt. varegruppe .'
     A            VKAHGR    R        O 15 20OVRDTA
     A            VKAUGR    R        O 15 23OVRDTA
     A                                 17  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
