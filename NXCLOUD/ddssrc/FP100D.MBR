     A*%%TS  SD  20081113  123513  BOF570      REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A*     BSA  240216    Endringer relatert til ny GUI
8.01 A*     bof  100622    Utvidet prisgr. fra 1 til 2 posisjoner
8.02 A*PGRP bsa  080124    Felter overlapper pgs prisgrp utvidelse
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081113  123513  BOF570      REL-V5R4M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1PRGR    R        O  9  4REFFLD(FPPRGR)
8.01 A            B1RKAT    R        O  9  7REFFLD(FPRKAT)
8.01 A            B1KKAT    R        O  9 11REFFLD(FPKKAT)
8.01 A            B1KUND    R        O  9 16REFFLD(FPKUND)
8.01 A            B1KPRO    R        O  9 23REFFLD(FPKPRO)
8.01 A            B1VARE    R   -7   O  9 30REFFLD(FPVARE)
8.01 A            B1TEK1         2A  O  9 39
7.xx A            B1ENHE    R        O  9 42REFFLD(FPENHE)
7.xx A            B1LETY    R        O  9 46REFFLD(FPLETY)
7.xx A            B1NUMM    R        O  9 48REFFLD(FPNUMM)
7.xx A            B1SAPR         8Y 2O  9 57EDTCDE(4)
7.xx A            B1RAB1         4Y 1O  9 67EDTCDE(4)
7.xx A            B1KOPR         7Y 2O  9 73EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081113  123513  BOF570      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av spesialpriser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FP100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Kunde .'
     A                                      COLOR(BLU)
7.xx A            B2KUNF    R        O  4 10REFFLD(FPKUND)
     A                                      COLOR(BLU)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Avtalenotat'
     A                                      COLOR(BLU)
7.xx A                                  4 20'Prosj..'
     A                                      COLOR(BLU)
7.xx A            B2KPRF    R        O  4 28REFFLD(FPKPRO)
     A                                      COLOR(BLU)
7.xx A                                  5  4'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  8'Rka'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Kka'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 21'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 30'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 39'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 50'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 56'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 58'Ordre'
     A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'P'
7.xx A                                      COLOR(WHT)
8.01 A                                  8  7'Rka'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 11'Kka'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 16'Kunde'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 23'Prosj.'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 30'Vare   '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 42'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 46'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 48'Ordre  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 57'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 67'Rab-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 73'Kostpris'
7.xx A**                                8  2'V P Rka Kka  Kun    Prosj. Vare   -
7.xx A**                                          Enh L Ordre    Salgspris Rab--
7.xx A**                                    1 Kostpris'
     A                                      COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2PRGR    R        B  6  4REFFLD(FPPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.02 A                                  6  7'+'
8.02 A            B2RKAT    R        B  6  9REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  6 13'+'
8.02 A            B2KKAT    R        B  6 15REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  6 20'+'
8.02 A            B2KUND    R        B  6 22REFFLD(FPKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  6 29'+'
8.02 A            B2KPRO    R        B  6 31REFFLD(FPKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  6 38'+'
8.02 A            B2VARE    R   -7   B  6 40REFFLD(FPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                  6 49'+'
8.02 A            B2ENHE    R        B  6 51REFFLD(FPENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                  6 55'+'
8.02 A            B2LETY    R        B  6 57REFFLD(FPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A            B2NUMM    R        B  6 59REFFLD(FPNUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20031116  154631  HKO         REL-V5R2M0  5722-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake   F4=Spør-
     A                                      ring   F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030519  140121  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av spesialpriser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgruppe . . . . . . . . +'
     A            C1PRGR    R        B  4 31REFFLD(FPPRGR)
     A  49                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Rabattkategori . . . . . . +'
     A            C1RKAT    R        B  5 31REFFLD(FPRKAT)
     A  31                                  ERRMSGID(FMP0050 *LIBL/QMFOFAK 31)
     A  34                                  ERRMSGID(FMP0060 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1RTXT        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundekategori. . . . . . . +'
     A            C1KKAT    R        B  6 31REFFLD(FPKKAT)
     A  41                                  ERRMSGID(FRA1120 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1KTXT        30A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kunde. . . . . . . . . . . +'
     A            C1KUND    R        B  7 31REFFLD(FPKUND)
     A  36                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1KNAV        30A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Prosjektnummer . . . . . . +'
     A            C1KPRO    R        B  8 31REFFLD(FPKPRO)
     A  37                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1PNAV    R        O  8 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(FPVARE)
     A  38                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O  9 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Enhet. . . . . . . . . . . +'
     A            C1ENHE    R        B 10 31REFFLD(FPENHE)
     A  39                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1ETXT    R        O 10 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Leveringstype. . . . . . . +'
     A            C1LETY    R        B 11 31REFFLD(FPLETY)
     A  40                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O 11 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020308  152403  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Spesialpris finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030519  140121  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av spesialpris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgruppe . . . . . . . . .'
     A            C1PRGR    R        O  4 31REFFLD(FPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Rabattkategori . . . . . . .'
     A            C1RKAT    R        O  5 31REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1RTXT        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundekategori. . . . . . . .'
     A            C1KKAT    R        O  6 31REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KTXT        30A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kunde. . . . . . . . . . . .'
     A            C1KUND    R        O  7 31REFFLD(FPKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KNAV        30A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kundeprosjekt. . . . . . . .'
     A            C1KPRO    R        O  8 31REFFLD(FPKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PNAV    R        O  8 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(FPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O  9 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Enhet. . . . . . . . . . . .'
     A            C1ENHE    R        O 10 31REFFLD(FPENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1ETXT    R        O 10 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 11 31REFFLD(FPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O 11 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Ordrenummer. . . . . . . . .'
     A            C2NUMM    R        O 13 31REFFLD(FPNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Priskode . . . . . . . . . .'
     A            C2PKOD         1A  O 14 31COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Dekningsgrad . . . . . . . .'
     A            C2DEKN         5Y 2O 15 31COLOR(YLW)
     A                                      EDTCDE(K)
     A                                      OVRDTA
     A                                 17  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        B 17 31REFFLD(FPSAPR)
     A  31                                  ERRMSGID(FMP0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Rabatt-1 . . . . . . . . . .'
     A            C2RAB1    R        B 18 31REFFLD(FPRAB1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Rabatt-2 . . . . . . . . . .'
     A            C2RAB2    R        B 19 31REFFLD(FPRAB2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Kostpris . . . . . . . . . .'
     A            C2KOPR    R        B 20 31REFFLD(FPKOPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22  2'Periode. . . . . . . . . . .'
     A            C2FDAT         6Y 0B 22 31COLOR(YLW)
     A  35                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 35)
     A  37                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 37)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 40'-'
     A            C2TDAT         6Y 0B 22 42COLOR(YLW)
     A  36                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(FPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20030519  140121  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 19 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette spesialpris '
     A                                      DSPATR(RI)
     A                                  3  2'Prisgruppe . . . .'
     A            D1PRGR    R        O  3 21REFFLD(FPPRGR)
     A                                  4  2'Rabattkategori . .'
     A            D1RKAT    R        O  4 21REFFLD(FPRKAT)
     A                                  5  2'Kundekategori. . .'
     A            D1KKAT    R        O  5 21REFFLD(FPKKAT)
     A                                  6  2'Kunde. . . . . . .'
     A            D1KUND    R        O  6 21REFFLD(FPKUND)
     A                                  7  2'Kundeprosjekt. . .'
     A            D1KPRO    R        O  7 21REFFLD(FPKPRO)
     A                                  8  2'Varenummer . . . .'
     A            D1VARE    R        O  8 21REFFLD(FPVARE)
     A                                  9  2'Enhet. . . . . . .'
     A            D1ENHE    R        O  9 21REFFLD(FPENHE)
     A                                 10  2'Leveringstype. . .'
     A            D1LETY    R        O 10 21REFFLD(FPLETY)
     A                                 12  2'Salgspris. . . . .'
     A            FPSAPR    R        O 12 21
     A                                 13  2'Rabatt-1 . . . . .'
     A            FPRAB1    R        O 13 21
     A                                 14  2'Rabatt-2 . . . . .'
     A            FPRAB2    R        O 14 21
     A                                 15  2'Kostpris . . . . .'
     A            FPKOPR    R        O 15 21
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20081113  123513  BOF570      REL-V5R4M0  5722-WDS
6.20 A                                      WINDOW(2 2 21 45)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  3  2'Prisgruppe . . . +'
     A            K1PRGR    R        B  3 21REFFLD(FPPRGR)
     A  49                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4  2'Rabattkategori . +'
     A            K1RKAT    R        B  4 21REFFLD(FPRKAT)
     A  31                                  ERRMSGID(FMP0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMP0030 *LIBL/QMFOFAK 32)
     A  34                                  ERRMSGID(FMP0060 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Kundekategori. . +'
     A            K1KKAT    R        B  5 21REFFLD(FPKKAT)
     A  41                                  ERRMSGID(FRA1120 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Kunde. . . . . . +'
     A            K1KUND    R        B  6 21REFFLD(FPKUND)
     A  36                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Kundeprosjekt. . +'
     A            K1KPRO    R        B  7 21REFFLD(FPKPRO)
     A  37                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2'Varenummer . . . +'
     A            K1VARE    R   -7   B  8 21REFFLD(FPVARE)
     A  38                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Enhet. . . . . . +'
     A            K1ENHE    R        B  9 21REFFLD(FPENHE)
     A  39                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Leveringstype. . +'
     A            K1LETY    R        B 10 21REFFLD(FPLETY)
     A  40                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 12  2'Prisgruppe'
     A            FPPRGR    R        O 12 21OVRDTA
     A                                 13  2'Rabattkategori . .'
     A            FPRKAT    R        O 13 21OVRDTA
     A                                 14  2'Kundekategori. . .'
     A            FPKKAT    R        O 14 21OVRDTA
     A                                 15  2'Kunde. . . . . . .'
     A            FPKUND    R        O 15 21OVRDTA
     A                                 16  2'Kundeprosjekt. . .'
     A            FPKPRO    R        O 16 21OVRDTA
     A                                 17  2'Varenummer . . . .'
     A            FPVARE    R        O 17 21OVRDTA
     A                                 18  2'Enhet. . . . . . .'
     A            FPENHE    R        O 18 21OVRDTA
     A                                 19  2'Leveringstype. . .'
     A            FPLETY    R        O 19 21OVRDTA
     A                                 20  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20031116  140826  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 13 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            F1RKAT    R        H      REFFLD(FRRKAT)
     A            F1KKAT    R        H      REFFLD(FRKKAT)
     A                                  2  2' Filter '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . +'
     A            F1KUND    R        B  4 21REFFLD(FPKUND)
     A  31                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            F1KNAV        30A  O  4 28COLOR(YLW)
     A                                  5  2'Kundeprosjekt. . +'
     A            F1KPRO    R        B  5 21REFFLD(FPKPRO)
     A  32                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            F1PNAV    R        O  5 28REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                 12  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
