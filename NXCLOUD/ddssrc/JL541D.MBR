     A*%%TS  SD  20111030  074119  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A* 280716 bsa Endringer relatert til ny GUI
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080528  140130  BOF560      REL-V5R4M0  5722-WDS
     A            B1ENUM    R        H      REFFLD(JLENUM)
7.xx A            B1VALG         2Y 0B 11  2COLOR(YLW)
     A                                      VALUES(6 7 10 11 12 13 14 15 -
     A                                             16 17 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A                                      EDTCDE(Z)
7.xx A            B1NAVN        20A  O 11  5
7.xx A            B1LDOR    R        O 11 26REFFLD(JLLDOR)
7.xx A            B1HLEV         1A  O 11 33
7.xx A            B1ENDR         6Y 0O 11 35EDTCDE(Z)
7.xx A            B1EVRE         6Y 0O 11 49EDTCDE(Z)
7.xx A            B1SLET         5Y 0O 11 56EDTCDE(Z)
7.xx A            B1PDAE         6Y 0O 11 62EDTWRD('  .  .  ')
7.xx A            B1PDAN         6Y 0O 11 71EDTWRD('  .  .  ')
7.xx A            B1KEVR         1Y 0O 11 80EDTCDE(Z)
7.xx A            B1OPPR         6Y 0O 11 42EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20111030  074119  BOF620      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0012)
7.xx A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA10
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Historikk'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 26'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'6=Prisoppdatering  7=Oppretting av-
     A                                       nye varer  10=Varer med nye/endred-
     A                                      e priser'
     A                                      COLOR(BLU)
7.xx A                                  7  2'11=Nye varer  12=Slettede varer  1-
     A                                      3=EVR-varer m.gml priser  14=Frigit-
     A                                      te varer'
     A                                      COLOR(BLU)
7.xx A                                  8  2'15=Bytte enhet  16=Varer i EVR ute-
     A                                      n sortiment  17=Underleverandører  -
     A                                              '
     A                                      COLOR(BLU)
7.xx A                                  9 35'Nye   '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 43'Nye  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 49'Nye   '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 56'Utg. '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 62'Eldste  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 71'Nyeste  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 80'E'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  5'Navn                '
7.xx A                                      COLOR(WHT)
7.xx A                                 10 26'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 35'priser'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 43'varer'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 49'EVR   '
7.xx A                                      COLOR(WHT)
7.xx A                                 10 56'varer'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 62'prisdato'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 71'prisdato'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 80'V'
7.xx A                                      COLOR(WHT)
7.xx A**                                7  2'V  Navn                 Lev.nr H N-
7.xx A**                                    ye    Nye    Nye   Utg.   Eldste   -
7.xx A**                                    Nyeste   E'
7.xx A**                                    COLOR(WHT)
7.xx A            B2NAVN        20A  B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  5 26REFFLD(JLLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A**                                8 35'priser'
7.xx A**                                    COLOR(WHT)
7.xx A**                                8 42'varer  EVR   varer  prisdato prisd-
7.xx A**                                    ato V'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031204  130947  HKO         REL-V5R2M0  5722-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake  F5=Forny-
     A                                          F10=Varer med sortiment'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20080827  101643  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Utg. Varer.   . . . .:'
     A            H1SLET    R        B  4 28REFFLD(JÅVALG)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 33'1=Ja'
     A                                  6  2'Nye,ikke overf.priser:'
     A            H1EVAR    R        B  6 28REFFLD(JÅVALG)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A                                      COLOR(YLW)
     A                                  6 33'1=Ja'
7.xx A                                  8  2'Sortimentskode  . . .+'
     A            H1SORT    R        B  8 28REFFLD(JVSORT)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
