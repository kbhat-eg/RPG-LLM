     A*%%TS  SD  20070112  131136  KOB         REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  090316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040313  235457  KOB         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VARE         8Y 0O  8  5
     A            B1LAGE    R        O  8 15REFFLD(FFRFR/VMLAGE *LIBL/FFRF)
     A            B1TEK1    R        O  8 19REFFLD(VVTEK1)
     A            B1ETYP    R        O  8 55REFFLD(VMETYP)
     A            B1ETPR    R        O  8 59REFFLD(VMETPR)
     A            B1FEN1    R        O  8 63REFFLD(VMFEN1)
     A            B1FEN2    R        O  8 67REFFLD(VMFEN2)
     A            B1SDAT         6Y 0O  8 71EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041027  121023  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av etikettinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6 55'E'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 59'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 63'H'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 67'H'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 70'Dato siste'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Varetekst                '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 55'T'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 59'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'2'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'etikett   '
7.xx A                                      COLOR(WHT)
7.xx A            B2VARE         8Y 0B  5  5COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A**                                7 55'T   P   1   2  etikett'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020311  081024  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20031116  121659  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av etikettinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Varenummer . . . . . . . . +'
     A            C1VARE         8Y 0B  5 31COLOR(YLW)
     A  33                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 33)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 43REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Lager. . . . . . . . . . . +'
6.11 A            C1LAGE    R        B  6 31REFFLD(FFRFR/VMLAGE *LIBL/FFRF)
6.11 A  34                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 34)
6.11 A                                      COLOR(YLW)
6.11 A                                      OVRDTA
7.xx A            C1JTXT        20A  O  6 43
6.11 A                                      COLOR(YLW)
6.11 A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020311  093852  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Etikettinformasjon finnes på'
     A                                  3  2'vare fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070112  131136  KOB         REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av etikettinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
6.11 A                                  4  2'Varenummer . . . . . . . . .'
6.11 A            C1VARE         8Y 0O  4 31COLOR(YLW)
     A                                      OVRDTA
6.11 A            C1TEK1    R        O  4 43REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.11 A                                  5  2'Lager. . . . . . . . . . . .'
6.11 A            C1LAGE    R        O  5 31REFFLD(FFRFR/VMLAGE *LIBL/FFRF)
6.11 A                                      COLOR(YLW)
6.11 A                                      OVRDTA
7.xx A            C1JTXT        20A  O  5 43
6.11 A                                      COLOR(YLW)
6.11 A                                      OVRDTA
     A                                  6  2'Etikett-type . . . . . . . .'
     A            C2ETYP    R        B  6 31REFFLD(VMETYP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                      OVRDTA
     A                                  6 43'2=Hylle 75x28 mm'
     A                                  7 43'3=Hylle 100x28 mm'
     A                                  8 43'4=Reol 202x60 mm'
     A                                  9 43'5=Hylle 76x39 mm'
     A                                 10 43'6=Hylle 48x38 mm'
     A                                 11 43'7=Hylle 100x38 mm'
     A                                 12 43'8=Reol 210x60 mm'
     A                                 13 43'9=Hylle 76x39 mm A4 ark'
     A                                 14  2'Vareetikett. . . . . . . . .'
     A            C2ETPR    R        B 14 31REFFLD(VMETPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                      OVRDTA
     A                                 14 43'0=Ikke etikett'
     A                                 15 43'1=Uten pris'
     A                                 16 43'2=Pris'
     A                                 17  2'Pris 1 hentes fra enhet. . .'
     A            C2FEN1    R        B 17 31REFFLD(VMFEN1)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 17 43'1=Enhet-1, 2=Enhet-2, 3=Enhet-3'
     A                                 18  2'Pris 2 hentes fra enhet. . .'
     A            C2FEN2    R        B 18 31REFFLD(VMFEN2)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(1)
     A                                 18 43'1=Enhet-1, 2=Enhet-2, 3=Enhet-3'
     A                                 20  2'Dato for siste etikett . . .'
     A            C2SDAT         6Y 0B 20 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.11 A                                 20 43'Siste pris . . .'
6.11 A            C2SAPR    R        O 20 65REFFLD(VMSAPR)
6.11 A                                      COLOR(YLW)
6.11 A                                      EDTCDE(M)
6.11 A                                      OVRDTA
     A                                 21  2'Dato for siste tilb.etikett.'
     A            C2TDAT         6Y 0B 21 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.11 A                                 21 43'Siste GTIN nr. .'
6.11 A            C2GTIN    R        O 21 61REFFLD(VMGTIN)
6.11 A                                      COLOR(YLW)
6.11 A                                      EDTCDE(Z)
6.11 A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040313  235457  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette etikettinformasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer . . . .'
     A            D1VARE         8Y 0O  4 21
     A                                  5  2'Lager. . . . . . .'
     A            D1LAGE    R        O  5 21REFFLD(FÅLAGE)
     A                                  6  2'Varetekst-1. . . .'
     A            D1TEK1    R        O  6 21REFFLD(VVTEK1)
     A                                  7  2'Etikett type . . .'
     A            VMETYP    R        O  7 21
     A                                  8  2'Etikett/Pris . . .'
     A            VMETPR    R        O  8 21
     A                                  9  2'Pris 1 hentes fra.'
     A            VMFEN1    R        O  9 21
     A                                 10  2'Pris 2 hentes fra.'
     A            VMFEN2    R        O 10 21
     A                                 11  2'Dato siste etikett'
     A            D1SDAT         6Y 0O 11 21EDTWRD('  .  .  ')
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020311  090058  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varenummer . . . +'
     A            K1VARE         8Y 0B  4 21COLOR(YLW)
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0030 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 33)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Varenummer . . . .'
     A            VMVARE    R        O  8 21OVRDTA
     A                                  9  2'Varetekst-1. . . .'
     A            K1TEK1    R        O  9 21REFFLD(VVTEK1)
     A                                      OVRDTA
     A                                 10  2'Etikett-type . . .'
     A            VMETYP    R        O 10 21OVRDTA
     A                                 11  2'Etikett/Pris . . .'
     A            VMETPR    R        O 11 21OVRDTA
     A                                 12  2'Pris 1 hentes fra.'
     A            VMFEN1    R        O 12 21OVRDTA
     A                                 13  2'Pris 2 hentes fra.'
     A            VMFEN2    R        O 13 21OVRDTA
     A                                 15  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
