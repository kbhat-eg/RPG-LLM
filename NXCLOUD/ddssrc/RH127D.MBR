     A*%%TS  SD  20020328  172914  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH127D
     A* Beskrivelse . . : Omberegning av budsjett, parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20020328  172914  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Omberegning av budsjett'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  7 10'Omberegn budsjett for . .:'
     A            A2PAAR    R        B  7 37REFFLD(RHPFÅR)
     A  34                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 34)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9 10'Budsjett-type . . . . . .:'
     A            A2PTYP    R        B  9 37REFFLD(RHPTYP)
     A                                      COLOR(YLW)
     A                                      VALUES('B' 'A')
     A                                  9 41'(B=Budsjett, A=Alt.budsjett)'
     A                                 11 10'Utvalg:'
     A                                 11 18'Konto fra . . . .:'
     A            A2PKTF    R        B 11 37REFFLD(RHPKTF)
     A  32                                  ERRMSGID(RMH0040 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 11 43'til'
6.NU A**                               11 43'til'
     A            A2PKTT    R        B 11 47REFFLD(RHPKTT)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 12 18'Kostnadsbærer fra:'
     A            A2PSPF    R        B 12 37REFFLD(RHPBØF)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(RMH0030 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 12 43'til'
     A            A2PSPT    R        B 12 47REFFLD(RHPBØT)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 13 18'Avdeling fra. . .:'
     A            A2PAVF    R        B 13 37REFFLD(RHPAVF)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSGID(RMH0030 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 13 43'til'
     A            A2PAVT    R        B 13 47REFFLD(RHPAVT)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 15 10'Budsjettnøkkel. . . . . .:'
     A            A2PKOD    R        B 15 37REFFLD(RFRFR/RAMKOD *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                 15 41'(00 = Alle)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
