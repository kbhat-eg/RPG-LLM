     A*%%TS  SD  20080919  092446  KOB560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL101D
     A* Beskrivelse . . : Leverandør, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       300702 KOB  Nytt program
6.20 A* 6.20  240412 BSA  Avdelingstekst er utvidet. Avkortes i skjermbildet
7.xx A*       240216 bsa  Endringer relatert til ny GUI
7.xx A*       010220 hko  Lagt inn vedlikehold av Nobb deltakernr
7.01 A*       250220 bsa  tar i bruk feltet RLISOK til "Fast forretningssted"
7.02 A*       070121 ask  Lagt inn kobling til "Låst valutakurs" med F9
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20070109  160545  KOB         REL-V5R4M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandørregister'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RL101C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN        11A  O  4 69DSPATR(RI)
     A                                  5  2'Lev.nr.. :'
     A            C2LEVR    R        O  5 13REFFLD(RLLEVR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PC)
     A                                      OVRDTA
     A  94        C2NOTB         8A  O  5 44DSPATR(RI)
     A                                  6  2'Navn.  . :'
     A            C2NAVN    R        B  6 13REFFLD(RLNAVN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  45 81                               ERRMSGID(RML0230 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  6 44'Alfa . . . :'
     A            C2ALFA    R        B  6 57REFFLD(RLALFA)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7  2'Adresse. :'
     A            C2GATE    R        B  7 13REFFLD(RLGATE)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A**65 81                               ERRMSGID(RML0750 *LIBL/QMFOKON 81)
     A  27 81                               ERRMSG('Ugyldige data i feltet' 81)
     A  65 81                               ERRMSG('Gateadresse mangler   ' 81)
     A                                  8  2'Adresse2 :'
     A            C2ADR2    R        B  8 13REFFLD(RLADR2)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A                                  8 44'Postnr.. . +'
     A            C2PONR    R        B  8 57REFFLD(RLPONR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32 81                               ERRMSGID(RM01000 *LIBL/QMFOKON 81)
     A  66 81                               ERRMSGID(RML0760 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  2'Postadr. :'
     A            C2STED    R        B  9 13REFFLD(RLSTED)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  9 44'Land . . . :'
     A            C2LAND    R        B  9 57REFFLD(RLLAND)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41 81                               ERRMSGID(RM01120 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2LTXT    R        O  9 60REFFLD(RALTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 10 44'Sperret. . J'
     A            C2SPRK    R        B 10 57REFFLD(RLSPRK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
6.31 A  47                                  DSPATR(PC)
6.31 A  47 81                               ERRMSGID(RM02150 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 10 60'Passiv.J'
     A            C2PASS    R        B 10 69REFFLD(RLPASS)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
6.31 A  50                                  DSPATR(PC)
6.31 A  50 81                               ERRMSGID(RM02160 *LIBL/QMFOKON 81)
     A                                      VALUES(' ' 'J')
     A                                 10  2'Telefon  :'
     A            C2TLFN    R        B 10 13REFFLD(RLTLFN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A**68 81                               ERRMSGID(RML0770 *LIBL/QMFOKON 81)
     A  68 81                               ERRMSG('Telefon/Mobil mangler ' 81)
     A  24 81                               ERRMSG('Ugyldige data i feltet' 81)
     A                                 10 26'Mobil'
     A            C2MOBN    R        B 10 32REFFLD(RLMOBN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 44'Vårt kundenr'
     A            C2KUNR    R        B 11 57REFFLD(RLKUNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Bankgiro :'
     A            C2BGIR    R        B 11 13REFFLD(RLBGIR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53 81                               ERRMSGID(RML0340 *LIBL/QMFOKON 81)
     A  72 81                               ERRMSG('Bankkonto skal registreres -
     A                                      på denne kategori' 81)
     A  23 81                               ERRMSG('Kontrollsiffer er feil' 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 26'Fax'
     A            C2TLFX    R        B 11 32REFFLD(RLTLFX)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A  69 81                               ERRMSGID(RML0780 *LIBL/QMFOKON 81)
     A  25 81                               ERRMSG('Ugyldige data i feltet' 81)
     A                                 12 44'Avgiftsfri :'
7.xx A                                 12 60'0/1/2'
     A            C2MKOD    R        B 12 57REFFLD(RLMKOD)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12  2'Foretaksnr'
     A            C2FTNR    R        B 12 13REFFLD(RLFTNR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  49 81                               ERRMSG('Foretaksnummer skal registr-
     A                                      eres på denne kateg.' 81)
     A  21 81                               ERRMSG('Kontrollsiffer er feil' 81)
7.xx A                                 13 44'Saksbehandl+'
     A            C2SAKS    R        B 13 57REFFLD(RLSAKS)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46 81                               ERRMSGID(RM01040 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2ZTXT    R   18   O 13 62REFFLD(RAZTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
7.xx A                                 13  2'Betal.bet+'
     A            C2BETB    R        B 13 13REFFLD(RLBETB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01030 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRATR
     A            C2CTXT    R        O 13 16REFFLD(RACTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 14 44'Distrikt . +'
     A            C2DIST    R        B 14 57REFFLD(RLDIST)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RM01080 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2HTXT    R   18   O 14 62REFFLD(RAHTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 14  2'Valuta . +'
     A            C2VALK    R        B 14 13REFFLD(RLVALK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42 81                               ERRMSGID(RM01160 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2PMNA    R        O 14 17REFFLD(RAPMNA)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 15 44'Lager. . . +'
     A            C2LAGR    R        B 15 57REFFLD(RLLAGR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39 81                               ERRMSGID(RM01100 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2JTXT    R        O 15 60REFFLD(RAJTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 15  2'Avdeling +'
     A            C2AVDE    R        B 15 13REFFLD(RLAVDE)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A  36 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
6.20 A            C2GTXT    R   25   O 15 18REFFLD(RAGTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16 44'Ordrerabatt:'
     A            C2ORAB    R        B 16 57REFFLD(RLORAB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTCDE(1)
7.xx A                                 16  2'Rabattkat+'
     A            C2RABK    R        B 16 13REFFLD(RLRABK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01060 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2FTXT    R        O 16 17REFFLD(RAFTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 17 44'Fors. måte.+'
     A            C2FORM    R        B 17 57REFFLD(RLFORM)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43 81                               ERRMSGID(RM01170 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2QTXT    R        O 17 60REFFLD(RAQTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 17  2'Bet. måte+'
     A            C2BETM    R        B 17 13REFFLD(RLBETM)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01020 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2BTXT    R        O 17 15REFFLD(RABTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18 44'Frie koder :'
     A            C2FRIE    R        B 18 57REFFLD(RLFRIE)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 18  2'Lev.kat..+'
     A            C2KATG    R        B 18 13REFFLD(RLKATG)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40 81                               ERRMSGID(RM01140 *LIBL/QMFOKON 81)
     A  67                                  ERRMSGID(RML0790 *LIBL/QMFOKON 67)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2NTXT    R   25   O 18 18REFFLD(RANTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 19  2'Lev.bet. +'
     A            C2LBET    R        B 19 13REFFLD(RLLBET)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44 81                               ERRMSGID(RM01180 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2RTX1    R   20   O 19 16REFFLD(RARTX1)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 19 44'NOBB delt.nr'
7.xx A            C2NOBB    R        B 19 57REFFLD(RLNOBB)
7.xx A N30                                  COLOR(YLW)
7.xx A                                      DSPATR(UL)
7.xx A                                      OVRDTA
     A                                 20  2'E-mail . :'
     A            C2EMAL    R        B 20 13REFFLD(RLEMAL)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A  70 81                               ERRMSGID(RML0740 *LIBL/QMFOKON 81)
     A                                 21  2'Sist endret dato:'
     A            C2EDAT         6Y 0O 21 20
     A N30                                  COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                 21 44'Endret av. :'
     A            C2ESIG    R        O 21 57REFFLD(RLESIG)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20050602  163321  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 22  2'F2=Notatark  F3=Gå tilbake  F4=Spø-
     A                                      rring  F5=Poster  F6=Flere koder'
     A                                      COLOR(BLU)
7.02 A                                 23  2'F7=Fact./EDI  F8=Utenl.bank  F9=Va-
     A                                      luta  F11=Besøkadresse  F10=Kontakt-
     A                                      pers.'
     A                                      COLOR(BLU)
     A          R E2WIN
     A*%%TS  SD  20050525  143728  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      PUTOVR
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Besøkadresse '
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr.. . . .'
     A            E2LEVR    R        O  4 18REFFLD(RLLEVR)
     A                                  5  2'Navn. . . . . .'
     A            E2NAVN    R        O  5 18REFFLD(RLNAVN)
     A                                  6  2'Adresse . . . .'
     A            E2BGAT    R        B  6 18REFFLD(RLBGAT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  7  2'Adresse 2 . . .'
     A            E2BADR    R        B  7 18REFFLD(RLBADR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
7.xx A                                  8  2'Poststed. . . +'
     A            E2BPNR    R        B  8 18REFFLD(RLBPNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            E2BSTE    R   25   O  8 23REFFLD(RLSTED)
     A                                      TEXT('Poststed')
     A                                 10  2'Web . .:'
     A            E2WEBA    R        B 10 11REFFLD(RLWEBA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R F2WIN
     A*%%TS  SD  20080919  092446  KOB560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 62)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Koder AS Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  4 33'Purrekode. . . . . .:'
     A            F2PURR    R        B  4 55REFFLD(RLPURR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  4 57'0/1'
     A                                  5  1'Ordrebekreftelse . .:'
     A            F2ORSK    R        B  5 23REFFLD(RLORSK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5 26'0/1'
     A                                  5 33'Prioritetskode . . .:'
     A            F2PRIO    R        B  5 55REFFLD(RLPRIO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5 57'0/1'
     A                                  6  1'Pakkseddel . . . . .:'
     A            F2PAKK    R        B  6 23REFFLD(RLPAKK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  6 26'0/1'
     A                                  6 33'Restordre. . . . . .:'
     A            F2REST    R        B  6 55REFFLD(RLREST)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  6 57'0/1'
     A                                  7  1'Spesialavtale. . . .:'
     A            F2SPES    R        B  7 23REFFLD(RLSPES)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  7 26'0/1'
     A                                  7 33'Bonuskode. . . . . .:'
     A            F2BONU    R        B  7 55REFFLD(RLBONU)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  7 57'0/1'
     A                                  8  1'Avtalespesifikasjon.:'
     A            F2AVTS    R        B  8 23REFFLD(RLAVTS)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  9  1'Kostpriskontering. .:'
     A            F2KPRI    R        B  9 23REFFLD(RLKPRI)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  9 26'0/1'
7.01 A                                  9 33'Ikke fast foret.sted:'
7.01 A            F2ISOK    R        B  9 55REFFLD(RLISOK)
7.01 A  50                                  ERRMSG('Gyldig kode er 0/1' 50)
7.01 A N30                                  COLOR(YLW)
7.01 A                                      DSPATR(UL)
7.01 A  30                                  DSPATR(PR)
7.01 A                                      OVRDTA
7.01 A                                      OVRATR
7.01 A                                  9 57'0/1'
     A                                 10  1'Påslagsprosent . . .:'
     A            F2PPRO    R        B 10 23REFFLD(RLPPRO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTCDE(1)
     A                                 10 33'Type hylle-etikett .:'
     A            F2HYLE    R        B 10 55REFFLD(RLHYLE)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 10 57'(kode)'
     A                                 11  1'Remmiteringskode . .J'
     A            F2REMK    R        B 11 23REFFLD(RLREMK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A N30                                  COLOR(YLW)
     A*                                     ERRMSGID(RML0150 *LIBL/QMFOKON 81)
     A                                 11 33'Skrive i div. felt .:'
     A            F2LVKO    R        B 11 55REFFLD(RLLVKO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 11 57'(kode)'
     A                                 12  1'Frie statistikk-kode:'
     A            F2STKO    R        B 12 23REFFLD(RLSTKO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 13  1'Fakturaleverandør. .+'
     A            F2FLEV    R        B 13 23REFFLD(RLFLEV)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  33                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            F2NAVN    R   -4   O 13 30REFFLD(RLNAVN)
     A                                      OVRDTA
     A                                 14  1'Opprettet av . . . .:'
     A            F2NSIG    R        O 14 23REFFLD(RLNSIG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'Opprettet dato . . .:'
     A            F2NDAT         6Y 0O 15 23EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R G2WIN
     A*%%TS  SD  19961018  075309  KOB         REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  4  2'EDI-Kode ...........:'
7.xx A                                  4 26' /A/E'
     A            G2EDIK    R        B  4 24REFFLD(RLEDIK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  5  2'EAN-Leverandør......:'
     A            G2EANL    R        B  5 24REFFLD(RLEANL)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6  2'Bestillings-mottaker:'
     A            G2EANB    R        B  6 24REFFLD(RLEANB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7  2'Factoring L.nr. ....:'
     A            G2FACT    R        B  7 24REFFLD(RLFACT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            G2NAVN    R        O  7 31REFFLD(RLNAVN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      TEXT('Leverandørnavn......')
     A                                  8  2'Factoring-nummer ...:'
     A            G2FACN    R        B  8 24REFFLD(RLFACN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20031106  125828  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Utenlandsk bank'
     A                                      COLOR(WHT)
7.xx A                                  4  1'Bank  SWIFT/BIC-nr..:'
     A            H2HSWA    R        B  4 23REFFLD(RVHSWA)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  5  1'Bankadresse-1 ......:'
     A            H2HBA1    R        B  5 23REFFLD(RVHBA1)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  1'Bankadresse-2 ......:'
     A            H2HBA2    R        B  6 23REFFLD(RVHBA2)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  1'Bankadresse-3 ......:'
     A            H2HBA3    R        B  7 23REFFLD(RVHBA3)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  1'Bankadresse-4 ......:'
     A            H2HBA4    R        B  8 23REFFLD(RVHBA4)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                  9  1'Kontonr   IBAN-nr...:'
     A            H2HBKO    R        B  9 23REFFLD(RVHBKO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  1'Omkostninger i utl. :'
     A            H2HBKU    R        B 10 23REFFLD(RVHBKU)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 26'(O=Dekkes av oss, B=av mottaker)'
     A                                 11  1'Omkostninger i No. .:'
     A            H2HBKI    R        B 11 23REFFLD(RVHBKI)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 26'(O=Dekkes av oss, B=av mottaker)'
     A                                 12  1'Belaste valuta(J/N) :'
     A            H2HBVK    R        B 12 23REFFLD(RVHBVK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R T2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme ean-leverandørnr. er'
     A                                  3  2'registrert på leverandørnr.'
     A            T2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            T2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R V2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme foretaksnummer er '
     A                                  3  2'registrert på leverandørnr.'
     A            V2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            V2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R W2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme alfakode og postnr. er'
     A                                  3  2'registrert på leverandørnr.'
     A            V2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            V2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R X2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme bankgironr. er    '
     A                                  3  2'registrert på leverandørnr.'
     A            X2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            X2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Y2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme telefonnummer er'
     A                                  3  2'registrert på leverandørnr.'
     A            Y2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Y2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Z2MSG
     A*%%TS  SD  20050704  110725  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme e-mail adresse er'
     A                                  3  2'registrert på leverandørnr.'
     A            Z2LEVR    R        O  5  1REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Z2NAVN    R   -7   O  5  8REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
