     A*%%TS  SD  20041102  153115  GRO530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK818D
     A* Beskrivelse . . : Kunder, autogiro - vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       021104 GRO  Nytt program
6.30 A*       290216 bsa  Justert pga jacada
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A***                                   WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A***                                    (*CHAR '        '))
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1WSF1                    SFL
     A*%%TS  SD  20041102  153115  GRO530      REL-V5R2M0  5722-WDS
     A            B1ALFA    R        H      REFFLD(RKALFA)
     A            B1PONR    R        H      REFFLD(RKPONR)
     A            B1VALG    R        B  8  2REFFLD(RWVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '5' '8' ' ')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O  8  4REFFLD(RKKUND)
     A            B1NAVN    R        O  8 11REFFLD(RKNAVN)
6.30 A            B1STED        26A  O  8 42
6.30 A            B1TLFN    R        O  8 69REFFLD(RKTLFN)
     A*
     A*  Bilde B2 - subfile kontroll 1
     A*
     A          R B2WCT1                    SFLCTL(B1WSF1)
     A*%%TS  SD  20041102  153115  GRO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CF11
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
6.30 A**                                    EDTCDE(Y)
     A                                      COLOR(WHT)
6.NU A**                                1 72DATE
6.NU A**                                    EDTCDE(Y)
6.NU A**                                    COLOR(WHT)
     A                                  2  2'Vedlikehold av autogiro'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Velg post og trykk enter'
     A                                      COLOR(BLU)
     A                                  5  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  5 11'5=Se på'
     A                                      COLOR(BLU)
     A                                  5 20'8=Poster'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Kunde'
     A                                      COLOR(WHT)
     A                                  6 11'Kundenavn'
     A                                      COLOR(WHT)
     A                                  6 42'Poststed'
     A                                      COLOR(WHT)
     A                                  6 73'Telefon'
     A                                      COLOR(WHT)
     A            B2KUND    R        B  7  4REFFLD(RKKUND)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  31 81                               ERRMSGID(RMK0010 *LIBL/QMFOKON 81)
     A  32 81                               ERRMSGID(RMK0030 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A            B2ALFA    R        B  7 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PONR    R        B  7 42REFFLD(RKPONR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A*
     A*  Bilde B1 - subfile 2
     A*
     A          R B1WSF2                    SFL
     A*%%TS  SD  19940531  162134  AMD         REL-V2R3M0  5738-PW1
     A            B1ALFA    R        H      REFFLD(RKALFA)
     A            B1PONR    R        H      REFFLD(RKPONR)
     A            B1VALG    R        B  8  2REFFLD(RWVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '5' '8' ' ')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O  8  4REFFLD(RKKUND)
     A            B1NAVN    R        O  8 11REFFLD(RKNAVN)
6.30 A            B1GATE        26   O  8 42
6.30 A            B1TLFX    R        O  8 69REFFLD(RKTLFX)
     A*
     A*  Bilde B2 - subfile kontroll 2
     A*
     A          R B2WCT2                    SFLCTL(B1WSF2)
     A*%%TS  SD  19970311  153313  KOB         REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CF11
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
6.30 A**                                    EDTCDE(Y)
     A                                      COLOR(WHT)
6.NU A**                                1 72DATE
6.NU A**                                    EDTCDE(Y)
6.NU A**                                    COLOR(WHT)
     A                                  2  2'Vedlikehold av autogiro'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Velg post og trykk enter'
     A                                      COLOR(BLU)
     A                                  5  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  5 11'5=Se på'
     A                                      COLOR(BLU)
     A                                  5 20'8=Poster'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Kunde'
     A                                      COLOR(WHT)
     A                                  6 11'Kundenavn'
     A                                      COLOR(WHT)
     A                                  6 42'Gateadresse'
     A                                      COLOR(WHT)
     A                                  6 73'Telefax'
     A                                      COLOR(WHT)
     A            B2KUND    R        B  7  4REFFLD(RKKUND)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  31 81                               ERRMSGID(RMK0010 *LIBL/QMFOKON 81)
     A  32 81                               ERRMSGID(RMK0030 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A            B2ALFA    R        B  7 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PONR    R        B  7 42REFFLD(RKPONR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  19970311  153313  KOB         REL-V3R1M0  5763-PW1
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 31'F8=Poster'
     A                                      COLOR(BLU)
     A                                 23 44'F11=Poststed/adresse'
     A                                      COLOR(BLU)
     A*
     A*
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20041013  135358  GRO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 15 50)
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  3  2'Kunde . . . . .:'
     A            C2KUND    R        O  3 19REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  2'Navn. . . . . .:'
     A            C2NAVN    R        O  4 19REFFLD(RKNAVN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A**                                    OVRATR
     A                                  5  2'Land. . . . . .:'
     A            C2LAND    R        B  5 19REFFLD(RKLAND)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2LTXT    R        O  5 22REFFLD(RALTXT)
     A                                      OVRDTA
     A**                                    OVRATR
     A                                  6  2'Bankgiro. . . .:'
     A            C2BGIR    R        B  6 19REFFLD(RKBGIR)
     A                                      OVRDTA
     A**                                    OVRATR
     A                                  8  2'Avdeling. . . .:'
     A            C2AVDE    R        B  8 19REFFLD(RKAVDE)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  2'Kundekategori .:'
     A            C2KATG    R        B  9 19REFFLD(RKKATG)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9 28'Selger. . . . .:'
     A            C2SLGR    R        B  9 45REFFLD(RKSLGR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  2'Betalingsbet. .:'
     A            C2BETB    R        B 10 19REFFLD(RKBETB)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 28'Betalingsmåte .:'
     A            C2BETM    R        B 10 45REFFLD(RKBETM)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A  36 81                               ERRMSGID(RM00010 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11  2'Rentekode . . .:'
     A            C2RENK    R        B 11 19REFFLD(RKRENK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 28'Purrekode . . .:'
     A            C2PURR    R        B 11 45REFFLD(RKPURR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 14 30'F5=Full registrering'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
