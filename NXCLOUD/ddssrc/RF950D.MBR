     A*%%TS  SD  20021005  214543  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF950D
     A* Beskrivelse . . : Sletting av poster på journal
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20021005  214543  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Fjerning av alle poster på en jour-
     A                                      nal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  7  9'Regnskapsår . . . .:'
     A            A2PRÅR    R        B  7 30REFFLD(RHPRÅR)
     A  34                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 34)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7 38'Journalnr.'
     A            A2PJNR    R        B  7 50REFFLD(RHPJNR)
     A  35                                  ERRMSGID(RMH0290 *LIBL/QMFOKON 35)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 10  9'Rutinen fjerner alle poster i hove-
     A                                      dbok, prosjekt, kunde- og'
     A                                      COLOR(RED)
     A                                 11  9'leverandør-reskontro innenfor oppg-
     A                                      itt regnskapsår og journalnummer.'
     A                                      COLOR(RED)
     A                                 13  9'Etter at rutinen er kjørt må saldi-
     A                                       og kjøp i år/kjøp i fjor oppdatere-
     A                                      s'
     A                                      COLOR(RED)
     A                                 14  9'på nytt. Kontakt ASP før rutinen k-
     A                                      jøres'
     A                                      COLOR(RED)
     A                                 14 57' '
     A                                 23 10'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
