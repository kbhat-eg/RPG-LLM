     A*%%TS  SD  20090122  014733  KOB560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA016D
     A* Beskrivelse . . : Printer-styring, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       270605 AMD  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050627  143612  AMD530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1PRID    R        O  8  4REFFLD(AEPRID)
     A            B1BESK    R        O  8 15REFFLD(AEBESK)
     A            B1TILK    R        O  8 50REFFLD(AETILK)
     A            B1STRT    R        O  8 55REFFLD(AESTRT)
     A            B1QMSG    R        O  8 60REFFLD(AEQMSG)
     A            B1OCRK    R        O  8 65REFFLD(AEOCRK)
     A            B1FORM    R        O  8 70REFFLD(AEFORM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050630  083924  AMD530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av utkøer/enheter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Utkø/enhet'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 15'Beskrivelse'
7.xx A                                      COLOR(WHT)
     A                                  6 48'Til- Start Meld Ocr-  Formular'
     A                                      COLOR(WHT)
7.xx A            B2PRID    R        B  5  4REFFLD(AEPRID)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AME0020 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  5 15REFFLD(AEBESK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7 48'Kobl'
     A                                      COLOR(WHT)
     A                                  7 53'autom'
     A                                      COLOR(WHT)
     A                                  7 59'alt.'
     A                                      COLOR(WHT)
     A                                  7 64'Kode'
     A                                      COLOR(WHT)
     A                                  7 70'type'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050629  105212  AMD530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A                                 23 42'F9=Restarte alle skrivere'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050629  094906  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av utkøer/enheter '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Utkø/enhet . . . . . . . . .'
     A            C1PRID    R        B  5 31REFFLD(AEPRID)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20050627  132228  AMD530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Utkø/enhet finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050629  105212  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av utkøer/enheter '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Utkø/enhet . . . . . . . . .'
     A            C1PRID    R        O  4 31REFFLD(AEPRID)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse. . . . . . . . .'
     A            C2BESK    R        B  5 31REFFLD(AEBESK)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2'Plassering . . . . . . . . .'
     A            C2PLAS    R        B  6 31REFFLD(AEPLAS)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Tilkobling . . . . . . . . .'
     A            C2TILK    R        B  8 31REFFLD(AETILK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 34'0=Txinax'
     A                                  8 45'1=PC/Axis nettboks'
     A                                  9 45'2=NC/Std. nettboks'
     A                                 10  2'Starte automatisk ?. . . . .'
     A            C2STRT    R        B 10 31REFFLD(AESTRT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 34'0=Nei'
     A                                 10 45'1=Ja'
     A                                 11  2'Meldings-alternativ. . . . .'
     A            C2QMSG    R        B 11 31REFFLD(AEQMSG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 34'0=*NOMSG'
     A                                 11 45'1=*INQMSG'
     A                                 13  2'Formulartype . . . . . . . .'
     A            C2FORM    R        B 13 31REFFLD(AFFORM)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 45'Blank/*ALL eller formular'
     A                                 15  2'Ocr-skrift . . . . . . . . .'
     A            C2OCRK    R        B 15 31REFFLD(AFOCRK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 34'0=Uten'
     A                                 15 45'1=Med Ocr'
     A                                 15 58'2=Axis boks(¤)'
     A                                 16 34'3=5224'
     A                                 16 45'4=Andre'
     A                                 18  2'Styrestreng 1. . . . . . . .'
     A            C2HEX1    R        B 18 31REFFLD(AFHEX1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 58'<-Benyttes ved'
     A                                 19  2'Styrestreng 2. . . . . . . .'
     A            C2HEX2    R        B 19 31REFFLD(AFHEX2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 58'<-kode 2 og 4'
     A***                              18  2'Opprettet dato . . . . . . .'
     A***         C2ODAT         6Y 0O 18 31COLOR(YLW)
     A***                                   EDTWRD('  .  .  ')
     A***                                   OVRDTA
     A                                 21  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 21 31EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 45'Endret av. .'
     A            C2EUSR    R        O 21 58REFFLD(AEEUSR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F9=Tilleggsinformasjon'
     A                                      COLOR(BLU)
     A                                 23 45'NB! Ved kode 4 må blanke'
     A                                      COLOR(RED)
     A                                 24 49'fylles med nuller'
     A                                      COLOR(RED)
     A          R C3WIN
     A*%%TS  SD  20090122  014733  KOB560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Tilleggsinformasjon   '
     A                                      DSPATR(RI)
     A                                  4  2'Sett inn kode for type skriver:'
     A                                  5 22'0 = Meto 8100, twinax'
     A                                      COLOR(WHT)
     A                                  6 22'1 = Meto 8100, standard'
     A                                      COLOR(WHT)
     A                                  7 22'2 = Meto Bandit      '
     A                                      COLOR(WHT)
     A                                  8 22'4 = Meto MI-4 (Heat->Styrestreng 1-
     A                                      )'
     A                                      COLOR(WHT)
     A                                  9 30'(Printspeed->Styrestreng 2)'
     A                                      COLOR(WHT)
     A                                 10 22'5 = Zebra QL320'
     A                                      COLOR(WHT)
     A                                 11 22'6 = Datamax EX-2'
     A                                      COLOR(WHT)
     A                                 13  2'Type skriver . . .'
     A            C3TYPE    R        B 13 22REFFLD(AETYPE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      VALUES(0 1 2 4 5 6)
     A                                 14  2'F3=Gå tilbake              '
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050629  094906  AMD530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette utkø / enhet '
     A                                      DSPATR(RI)
     A                                  4  2'Utkø/enhet . . . .'
     A            D1PRID    R        O  4 21REFFLD(AEPRID)
     A                                  6  2'Beskrivelse. . . .'
     A            AEBESK    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Utkø/enhet . . . .'
     A            K1PRID    R        B  4 21REFFLD(AEPRID)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AME0030 *LIBL/QMFAGPL 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Utkø/enhet . . . .'
     A            AEPRID    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            AEBESK    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
