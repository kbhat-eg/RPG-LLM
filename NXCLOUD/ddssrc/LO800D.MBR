     A*%%TS  SD  20040525  194538  ASK         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO800D
     A* Beskrivelse . . : Varemottak, valg av bestilling
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120302 ASK  Nytt program
6.NU A*       131014 NHO  Endring i forb. med nummerutvidelse
7.xx A* 7.xx  090316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.xx  270919 bsa  Flagger at bestilling er mottatt elektronisk.
7.01 A*                   Kan ikke varemottas manuelt. F tast med overgang
7.01 A*                   til elektroniske pakksedler.
7.02 A*       250814 BKV  Ta med lager i oppslag
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1BLD
     A*%%TS  SD  20040525  194538  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
7.01 A                                      CA07
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Varemottak - valg av bestilling '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  8  2'Varenummer . . . . . . . . +'
     A            B1VARE    R        B  8 31REFFLD(LDVARE)
     A  32                                  ERRMSGID(LSO0140 *LIBL/QMFLAGR 32)
     A                                      CHECK(ER)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
7.02 A            B1VTEK    R   -1   O  8 47REFFLD(LDTEK1)
     A                                  6  2'Bestilling . . . . . . . . .'
     A                                      COLOR(GRN)
6.nu A            B1NUMM         8Y 0B  6 31EDTCDE(Z)
     A  31                                  ERRMSGID(LMO0020 *LIBL/QMFLAGR 31)
     A  34                                  ERRMSGID(LM00320 *LIBL/QMFLAGR 34)
7.01 A  35                                  ERRMSG('Elektronisk pakkseddel er -
7.01 A                                      mottatt på bestilling. Kan ikke var-
7.01 A                                      emottas manuelt' 35)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
6.nu A                                  6 41'/'
6.nu A            B1SUFF         2Y 0B  6 43EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Leverandør . . . . . . . . +'
     A            B1LDOR    R        B  7 31REFFLD(LOLDOR)
     A  33                                  ERRMSGID(LSO0080 *LIBL/QMFLAGR 33)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
7.02 A            B1LTEK        30A  O  7 47
7.02 A                                  9  2'Lager  . . . . . . . . . . .'
7.02 A            B1LAGE    R        B  9 31REFFLD(LOLAGE)
7.02 A  35                                  ERRMSGID(LMO0680 *LIBL/QMFLAGR 35)
7.02 A  36                                  ERRMSGID(LMO0690 *LIBL/QMFLAGR 36)
7.02 A                                      DSPATR(UL)
7.02 A                                      CHECK(RB)
7.02 A                                      EDTCDE(Z)
7.02 A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
7.01 A**                               23 31'F7=Elektroniske pakksedler'
7.01 A**                                    COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
