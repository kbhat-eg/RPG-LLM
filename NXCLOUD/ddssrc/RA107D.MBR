     A*%%TS  SD  20040217  123227  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA107D
     A* Beskrivelse . . : Avdelinger, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060302 KOB  Nytt program
6.21 A* 6.20  250511 bsa  Legger ut web og e-mail adresse
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1GKOD    R        O  8  5REFFLD(RAGKOD)
6.20 A            B1GTXT    R   20   O  8 11REFFLD(RAGTXT)
6.20 A            B1GADR    R   29   O  8 32REFFLD(RAGADR)
     A            B1GPNR    R        O  8 62REFFLD(RAGPNR)
     A            B1GSTE    R   13   O  8 67REFFLD(RAGSTE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  195916  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdelinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse         '
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 32'Adresse'
     A                                      COLOR(WHT)
7.xx A                                  7 62'Post'
     A                                      COLOR(WHT)
7.xx A                                  7 67'Sted     '
7.xx A                                      COLOR(WHT)
7.xx A            B2GKOD    R        B  5  5REFFLD(RAGKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2GTXT    R   20   B  5 11REFFLD(RAGTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  195916  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdelinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Avdeling . . . . . . . . . .'
     A            C1GKOD    R        B  5 31REFFLD(RAGKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  005206  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Avdeling finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040217  123227  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdelinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
6.21 A                                  5  2'Avdeling . . . . . . .'
6.21 A            C1GKOD    R        O  5 25REFFLD(RAGKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                  7  2'Beskrivelse. . . . . .'
6.21 A            C2GTXT    R        B  7 25REFFLD(RAGTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
6.21 A                                  9  2'Mellomadresse. . . . .'
6.21 A            C2GAD2    R        B  9 25REFFLD(RAGAD2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
6.21 A                                 10  2'Adresse. . . . . . . .'
6.21 A            C2GADR    R        B 10 25REFFLD(RAGADR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
7.xx A                                 11  2'Poststed . . . . . . +'
6.21 A            C2GPNR    R        B 11 25REFFLD(RAGPNR)
     A  32                                  ERRMSGID(RM01000 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
6.21 A            C2GSTE    R        O 11 31REFFLD(RAGSTE)
     A                                      OVRDTA
6.21 A                                 12  2'Telefonnummer. . . . .'
6.21 A            C2GTLF    R        B 12 25REFFLD(RAGTLF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                 13  2'Telefaxnummer. . . . .'
6.21 A            C2GFAX    R        B 13 25REFFLD(RAGFAX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                 14  2'Bankgironummer . . . .'
6.21 A            C2GBGI    R        B 14 25REFFLD(RAGBGI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                 15  2'Sperrekode . . . . . .'
6.21 A            C2GSPR    R        B 15 25REFFLD(RAGSPR)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
7.xx A                                 16  2'Leveringstype. . . . +'
6.21 A            C2GLTY    R        B 16 25REFFLD(RAGLTY)
6.21 A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 16 28'(kode)'
7.xx A                                      COLOR(WHT)
6.21 A                                 18  2'E-mail adresse . . . .'
6.21 A            C2GMAI    R        B 18 25REFFLD(RAGMAI)
6.21 A  30                                  DSPATR(PR)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A                                      CHECK(LC)
6.21 A                                 19  2'WEB adresse. . . . . .'
6.21 A            C2GWEB    R        B 19 25REFFLD(RAGWEB)
6.21 A  30                                  DSPATR(PR)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A                                      CHECK(LC)
6.21 A                                 20  2'Opprettet dato . . . .'
6.21 A            C2ODAT         6Y 0O 20 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 21  2'Sist endret dato . . .'
6.21 A            C2EDAT         6Y 0O 21 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 21 40'Endret av. . . . . . .'
6.21 A            C2EUSR    R        O 21 64REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  005206  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette avdeling '
     A                                      DSPATR(RI)
     A                                  4  2'Avdeling . . . . .'
     A            D1GKOD    R        O  4 21REFFLD(RAGKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAGTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020307  005206  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Avdeling . . . . .'
     A            K1GKOD    R        B  4 21REFFLD(RAGKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Avdeling . . . . .'
     A            RAGKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAGTXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
