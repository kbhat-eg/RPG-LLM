     A*%%TS  SD  20020922  233631  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : HO100D
     A* Beskrivelse . . : Vedlikehold av ordre fra håndterminal
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20010323  122408  HKO         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      VALUES(0 4 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1REFN    R        O  8  4REFFLD(HOREFN)
6.NU A            B1KUND    R        O  8 13REFFLD(HOKUND)
6.NU A            B1KNAV        15A  O  8 20
     A            B1PNAV         6A  O  8 36
     A            B1NAVN        10A  O  8 43
     A            B1DREF        10A  O  8 54
     A            B1SELG    R        O  8 65REFFLD(HOSELG)
     A            B1DATO         4Y 0O  8 70EDTWRD('  .  ')
     A            B1TIME         4Y 0O  8 76EDTWRD('  :  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020922  233631  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordre fra håndtermi-
     A                                      nal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Refnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 15'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'4=Slett  5=Se på detaljer/varelinj-
     A                                      er'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Refnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 13'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Kundenavn      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 36'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 43'Lev.adr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 54'Kunde.ref'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 65'Selg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 77'Kl.'
7.xx A**                                6  2'V Refnr.   Kunde  Kundenavn       -
7.xx A**                                    Prosj. Lev.adr.   Kunde.ref. Selg D-
7.xx A**                                    ato   Kl.'
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  5  2REFFLD(LÅVALG)
7.xx A                                      COLOR(YLW)
7.xx A                                      VALUES(0 4 5)
7.xx A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.xx A                                      OVRDTA
7.xx A            B2REFN    R        B  5  4REFFLD(HOREFN)
     A  31                                  ERRMSGID(HMO0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(HMO0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 13'+'
7.xx A            B2KUND    R        B  5 15REFFLD(HOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20000605  151759  ASK         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 32'F5=Forny'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20010323  090104  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 17 55)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette referanse '
     A                                      DSPATR(RI)
     A                                  4  2'Referanse . . .'
     A            D1REFN    R        O  4 18REFFLD(HOREFN)
     A                                  6  2'Kunde . . . . .'
     A            HOKUND    R        O  6 18
     A            D1KNAV        30A  O  6 25
     A                                  7  2'Kundeprosjekt .'
     A            HOKPRO    R        O  7 18
     A            D1PNAV        30A  O  7 25
     A                                  8  2'Lev.adr/navn. .'
     A            HONAVN    R        O  8 18
     A                                  9  2'Kundens ref.. .'
     A            HODREF    R        O  9 18
     A                                 10  2'Rekv.nummer . .'
     A            HOREKV    R        O 10 18
     A                                 11  2'Selger. . . . .'
     A            HOSELG    R        O 11 18
     A                                 13  2'Dato. . . . . .'
     A            D1DATO         6Y 0O 13 18EDTWRD('  .  .  ')
     A                                 14  2'Kl. . . . . . .'
     A            D1TIME         6Y 0O 14 18EDTWRD('  :  :  ')
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
