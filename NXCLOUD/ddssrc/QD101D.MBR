     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : QD101D
     A* Beskrivelse . . : Finne bestillinger
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       010419 bsa  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  6  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM    R        O  6  4REFFLD(SONUMM)
     A            B1SUFF    R        O  6 13REFFLD(SOSUFF)
     A            B1DATO         6Y 0O  6 17EDTCDE(Y)
     A            B1LEVN        30A  O  6 27
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge bestilling '
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V Bestnr         Dato    Lev-
     A                                       erandør '
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050620  134449  ASK530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 61)
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
