     A*%%TS  SD  20190814  132945  G0TEGSVSTR  REL-V7R3M0  5770-WDS
     A*
8.02 A*K000 bsa  120824   Valg for opprette tilbud med nye ordrenummer.
     A*
     A*
     A*
     A*----------------------------------------------------------------*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20190807  103555  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A            B1NUMM    R        H      REFFLD(FONUMM)
     A            B1SUFF    R        H      REFFLD(FOSUFF)
     A            B1KTXT    R        H      REFFLD(FOKTXT)
     A            B1LINE    R        H      REFFLD(FDLINE)
     A            B1VALG    R        B 14  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 1 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LETY    R        O 14  4REFFLD(FDLETY)
7.01 A            B1TEXT        73A  O 14  6
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20190814  132945  G0TEGSVSTR  REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA10
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 42'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 64'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 70TIME
     A                                      COLOR(WHT)
     A                                  2  2'Slettede ordre '
     A                                      COLOR(WHT)
     A                                  2 42'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 52USER
     A                                      COLOR(WHT)
     A                                  2 64'Dato:'
     A                                      COLOR(WHT)
     A                                  2 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A            B2KUND    R        O  4 11REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV    R   -7   O  4 18REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 42'Ordrenr. . .'
     A            B2NUMM    R        O  4 55REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2SUFF    R        O  4 64REFFLD(FOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KGAT    R        O  5 11REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 42'Ordredato. .'
     A            B2BDAT         6Y 0O  5 55EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KSTE    R        O  6 11REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 42'Slettet dato'
     A            B2DDAT         6Y 0O  6 55EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2DTIM         6Y 0O  6 65EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2MOTX         4A  O  7 11COLOR(YLW)
     A                                      OVRDTA
     A            B2MOBI    R        O  7 16REFFLD(FOMOBI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 42'Lev. dato. .'
     A            B2LDAT         6Y 0O  7 55EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kunde-  '
     A            B2NAVN    R        O  8 11REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 42'Vår ref. . .'
     A            B2VREF        25A  O  8 55COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'prosjekt'
     A            B2ADR1    R        O  9 11REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 42'Kundens ref.'
     A            B2DREF        25A  O  9 55COLOR(YLW)
     A                                      OVRDTA
     A            B2STED    R        O 10 11REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 42'Ordretotal .'
     A                                      COLOR(WHT)
     A            B2SALG    R        O 10 55REFFLD(FOTOTS)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                 11  2'Info.. .'
     A            B2INF1        30A  O 11 11COLOR(YLW)
     A                                      OVRDTA
     A            B2INF2        30A  O 11 42COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'5=Vis'
     A                                      COLOR(BLU)
     A                                 13  2'V L Varenr.  Varetekst            -
     A                                                  Antall Enh  Rab       B-
     A                                      eløp Suff'
     A                                      COLOR(WHT)
     A                                 12 77'Ny'
     A                                      COLOR(WHT)
     A                                 12 11'1=Hent linje til tilbud'
     A                                      COLOR(BLU)
     A*
     A          R B2CMD
     A*%%TS  SD  20190806  141511  G01EGSVSTR  REL-V7R3M0  5770-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            B2FUNK        30   O 23 19
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20190807  121314  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Hente hele ordre til nytt tilbud'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Skal tidligere hentede linjer til'
8.02 A                                  5  2'nye tilbud tas med på nytt ?'
     A            H1SVAR         1A  B  5 31CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 33'(J=Ja)'
8.02 A                                  6  2'Nytt tilbudsnummer . . . . ?'
8.02 A            H1TILB         1A  B  6 31CHECK(RB)
8.02 A                                      OVRDTA
8.02 A                                      COLOR(YLW)
8.02 A                                  6 33'(J=Ja)'
     A                                  8  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 80' '
