     A*%%TS  SD  20051124  150445  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
7.xx A*          290116 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051124  143133  HKO550      REL-V5R3M0  5722-WDS
     A            B1KUND         6S 0H
     A            B1KUNA    R        H      REFFLD(FOKUNA)
     A            B1SALG    R        H      REFFLD(FOTOTS)
7.xx A            B1VALG         1Y 0B 10  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 2 5 6 7 8)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A            B1NUMM    R        O 10  4REFFLD(FONUMM)
7.xx A            B1SUFF    R        O 10 13REFFLD(FFRFR/FOSUFF *LIBL/FFRF)
7.xx A            B1OTYP    R        O 10 16REFFLD(FOOTYP)
7.xx A            B1KNAV        16A  O 10 19
7.xx A            B1DATO         6Y 0O 10 36EDTWRD('  .  .  ')
     A  39                                  COLOR(RED)
7.xx A            B1NAVN        17A  O 10 45
7.xx A            B1INFO        12A  O 10 63
7.xx A            B1ITYP    R        O 10 76REFFLD(FOOTYP)
     A  43                                  COLOR(BLU)
     A  44                                  COLOR(RED)
     A  41                                  COLOR(WHT)
     A  42                                  COLOR(YLW)
7.xx A            B1KODE         2A  O 10 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051124  143133  HKO550      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CF22
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppfølging av tilbud'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO580B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Bruker. . :'
7.xx A                                      COLOR(BLU)
7.xx A            H1USER    R        O  4 14REFFLD(FOUSER)
7.xx A                                      COLOR(BLU)
7.xx A                                  4 30'Kunde . . :'
7.xx A                                      COLOR(BLU)
7.xx A            H1KUND    R        O  4 42REFFLD(FOKUND)
7.xx A                                      COLOR(BLU)
7.xx A                                  5  4'Tilbud'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 16'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 19'Kundealfa     '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 37'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 45'Vareadresse'
7.xx A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  6  4REFFLD(FONUMM)
7.xx A                                      DLTCHK
7.xx A  31                                  ERRMSGID(FMO0010 *LIBL/QMFOFAK 31)
7.xx A  32                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 32)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A            B2SUFF    R        B  6 13REFFLD(FOSUFF)
7.xx A                                      OVRDTA
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A            B2OTYP    R        B  6 16REFFLD(FOOTYP)
7.xx A                                      DLTCHK
7.xx A                                      OVRDTA
7.xx A                                      COLOR(YLW)
7.xx A            B2KUNA        16A  B  6 19COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A            B2DATO         6Y 0B  6 36COLOR(YLW)
7.xx A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
7.xx A                                      OVRDTA
7.xx A                                      CHECK(RB)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A            B2NAVN        17A  B  6 45COLOR(YLW)
7.xx A                                      OVRDTA
      *
7.xx A                                  7  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  7 16'5=Vis tilbud'
     A                                      COLOR(BLU)
7.xx A                                  7 32'6=Skrive ut'
     A                                      COLOR(BLU)
7.xx A                                  7 45'8=Hvem behandler tilbud'
     A                                      COLOR(BLU)
7.xx A                                  8  2'7=Behandle tilbud'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Tilbud'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 13'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 16'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 19'Kundealfa     '
     A                                      COLOR(WHT)
7.xx A            B2HEDD         9A  O  9 35COLOR(WHT)
     A                                  9 45'Vareadresse'
     A                                      COLOR(WHT)
7.xx A            B2HED2        12A  O  9 63COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051013  142554  AMD540      REL-V5R3M0  5722-WDS
     A                                 23  2'F1=Filter '
     A                                      COLOR(BLU)
     A                                 23 16'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 47'F22=Inkl.mva'
     A                                      COLOR(BLU)
     A                                 23 64'F23=Bytt info'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20051124  150445  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 10 60)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Ajourføring av :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Oppfølgingsdato.'
     A            C2DDAT         6Y 0B  4 19COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Utgår-dato . . .'
     A            C2TDAT         6Y 0B  5 19COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Informasjon. . .'
     A            C2INF1    R        B  6 19REFFLD(FOINF1)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            C2INF2    R        B  7 19REFFLD(FOINF2)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20051124  144639  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 14 45)
     A                                      CA03
     A                                      CF04
     A                                      CF09
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Filter '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg bruker. . . . . .'
     A            H1USER    R        B  4 25REFFLD(FOUSER)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5  2'Velg kundenr . . . . +'
     A            H1KUND    R        B  5 25REFFLD(FOKUND)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  6  2'Velg selger. . . . . +'
     A            H1SELG    R        B  6 25REFFLD(FOSELG)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Velg avdeling. . . . +'
7.xx A            H1AVDE    R        B  7 25REFFLD(FOAVDE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'Velg lager . . . . . .'
     A            H1LAGE    R        B  8 25REFFLD(FOLAGE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  2'Rekvisisjon. . . . . .'
     A            H1REKV    R        B  9 25REFFLD(FOREKV)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  2'Utelate utgåtte tilbud'
     A            H1UTGT         1Y 0B 11 25CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 28'1=Ja,0=Nei'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 13 32'F9=Lagre'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
