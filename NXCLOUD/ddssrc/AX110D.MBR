     A*%%TS  SD  20140716  085517  BOF620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AX110D
     A* Beskrivelse . . : XML NOBB overføring, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       140111 bof  Nytt program
     A*       140313 bof  Utvidet med en del felt
     A*       150714 BOF  UTVIDET MED EN DEL FELT
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140715  125541  BOF620      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFAGPL)
     A            B1MKUN    R        O  8  5REFFLD(AXMKUN)
     A            B1MAKT    R        O  8 22REFFLD(AXMAKT)
     A                                      EDTCDE(Z)
     A            B1MVER         3A  O  8 24
     A            B1MBAN    R  -48   O  8 28REFFLD(AXMBAN)
     A            B1MSDA    R        O  8 12REFFLD(AHTTID)
     A                                      EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140715  125738  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av XML nobb'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Kunde  Sist dato A Ver.    Bane'
     A                                      COLOR(WHT)
     A            B2MKUN    R        B  7  5REFFLD(AXMKUN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20110114  090223  ASP_S61     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av XML nobb'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kundenr (Asp)  . . . . . . .'
     A            C1MKUN    R        B  5 31REFFLD(AXMKUN)
     A  31                                  ERRMSGID(AM00120 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'XML overføring finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140716  085517  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av XML nobb'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kundenr (Asp)  . . . . . . .'
     A            C1MKUN    R        O  5 31REFFLD(AXMKUN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Sist overført dato . . . . .'
     A            C2MSDA    R        B  7 31REFFLD(AHTTID)
     A  31                                  ERRMSGID(AM00110 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AM00110 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Bane (50 første posisjoner).'
     A            C2MBA1        50A  B  9 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(AMX0100 *LIBL/QMFAGPL 33)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 10  2'Bane (50 neste posisjoner) .'
     A            C2MBA2        50A  B 10 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 11  2'Aktiv på ny løsning  . . . .'
     A            C2MAKT    R        B 11 31REFFLD(AXMAKT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'Versjon  . . . . . . . . . .'
     A            C2MVER    R        B 12 31REFFLD(AXMVER)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12 35'0=Ingen  1=2.6  2=8.0       '
     A                                 13  2'Host-info :'
     A                                      COLOR(WHT)
     A                                 13 15'Filgruppe  '
     A                                 13 28'Host       '
     A            C2MFGR    R        B 14 15REFFLD(AXMFGR)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(AM00150 *LIBL/QMFAGPL 34)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2MFHO        50A  B 14 28COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  35                                  ERRMSGID(AM00150 *LIBL/QMFAGPL 35)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 15  2'Directory.:'
     A            C2MDI1        63A  B 15 15COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  36                                  ERRMSGID(AM00150 *LIBL/QMFAGPL 36)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2MDI2        63A  B 16 15COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2MFL1        63A  B 17 15COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2MFL2        63A  B 18 15COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 19  2'Bruker....:'
     A            C2MUSR    R        B 19 15REFFLD(AXMUSR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  37                                  ERRMSGID(AM00150 *LIBL/QMFAGPL 37)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 20  2'Passord...:'
     A            C2MPSV    R        B 20 15REFFLD(AXMPSV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  38                                  ERRMSGID(AM00150 *LIBL/QMFAGPL 38)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 21 50'Oprettet dato . . '
     A            C2MODA         6Y 0O 21 70COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 50'Sis endret dato . '
     A            C2MEDA         6Y 0O 22 70COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 50'Endre av. . . . . '
     A            C2MEUS    R        O 23 70REFFLD(AXMEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=G tilbake   F44=Spøring '
     A                                      COLOR(BLU)
     A                                 17  2'Mappe.....:'
     A          R D1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette XML nobb'
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr (Asp). . .'
     A            D1MKUN    R        O  4 21REFFLD(AXMKUN)
     A                                  6  2'Sist Overført dato'
     A            D1MSDA    R        O  6 21REFFLD(AHTTID)
     A                                      EDTWRD('  .  .  ')
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20110114  090223  ASP_S61     REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr (Asp)  . .'
     A            K1MKUN    R        B  4 21REFFLD(AXMKUN)
     A  31                                  ERRMSGID(AM00120 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AM00130 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kundenr (Asp)  . .'
     A            AXMKUN    R        O  8 21OVRDTA
     A                                  9  2'Sist overført dato'
     A            K2MSDA    R        O  9 21REFFLD(AHTTID)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
