     A*%%TS  SD  20110810  143439  BOF620      REL-V7R1M0  5770-WDS
6.20 A* 180411 NHO Nye felt inngår, varegr, lev.nr og prosentsatser
7.xx A* 7.00  040216  bsa  Justeringer basert på ny GUI og regler
7.01 A* 010317 BOF Utvidet med fra til innkjøpsdato
     A*
8.01 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
8.02 A*     050822 bof Forsøk på å gjøre så salgspris 1 vises ved ny
     A*
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081126  143844  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.20 A            B1OGRP    R        O  8  4REFFLD(VTOGRP)
6.20 A            B1HGRP    R        O  8  7REFFLD(VTHGRP)
6.20 A            B1UGRP    R        O  8 10REFFLD(VTUGRP)
6.20 A            B1LDOR    R        O  8 14REFFLD(VTLDOR)
6.20 A            B1PRGR    R        O  8 30REFFLD(VTPRGR)
8.01 A            B1TEK1        10A  O  8 33
6.20 A            B1LETY    R        O  8 44REFFLD(VTLETY)
6.20 A            B1FDAT         6Y 0O  8 46EDTWRD('  .  .  ')
6.20 A            B1FTIM         4Y 0O  8 55EDTWRD('  :  ')
6.20 A            B1TDAT         6Y 0O  8 61EDTWRD('  .  .  ')
6.20 A            B1TTIM         4Y 0O  8 70EDTWRD('  :  ')
6.20 A            B1KAMP    R        O  8 76REFFLD(VTKAMP)
6.20 A            B1ENH1         3   H
6.20 A            B1TIP1         9  2H
6.20 A            B1VARE    R   -7   O  8 21REFFLD(VTVARE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081129  085633  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
6.20 A                                      CA11
6.20 A                                      CA08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspriser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VT100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  6'Varegr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 21'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 30'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 40'PG'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 45'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 48'--- Tilbudsperiode ---'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 72'Kamp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2OGRP    R        B  5  4REFFLD(VTOGRP)
6.20 A  22                                  DSPATR(PC)
6.20 A                                      OVRDTA
6.20 A                                      COLOR(YLW)
7.xx A                                  5  7'+'
7.xx A            B2HGRP    R        B  5  9REFFLD(VTHGRP)
6.20 A                                      OVRDTA
6.20 A                                      COLOR(YLW)
7.xx A                                  5 12'+'
7.xx A            B2UGRP    R        B  5 14REFFLD(VTUGRP)
6.20 A                                      OVRDTA
6.20 A                                      COLOR(YLW)
7.xx A                                  5 19'+'
7.xx A            B2LDOR    R        B  5 21REFFLD(VTLDOR)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  5 28'+'
7.xx A            B2VARE    R   -7   B  5 30REFFLD(VTVARE)
6.20 A  31                                  ERRMSGID(VMT0010 *LIBL/QMFOFAK 31)
6.20 A  32                                  ERRMSGID(VMT0020 *LIBL/QMFOFAK 32)
6.20 A                                      COLOR(YLW)
6.20 A  22                                  DSPATR(PC)
6.20 A                                      OVRDTA
7.xx A                                  5 39'+'
7.xx A            B2PRGR    R        B  5 41REFFLD(VTPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  5 44'+'
8.01 A            B2LETY    R        B  5 46REFFLD(VTLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2FDAT         6Y 0B  5 49COLOR(YLW)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2TDAT         6Y 0B  5 62COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KAMP    R        B  5 72REFFLD(VTKAMP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  7'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 10'Ugr'
6.20 A                                      COLOR(WHT)
7.xx A                                  7 14'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 21'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 29'Pg'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 33'Varetekst1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 44'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 46'Fradato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 55'Tid '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 61'Tildato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Tid '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 76'Kamp.'
7.xx A**                                7 21'Varenr. Pg Varetekst-1 L  ---- -
7.xx A**                                     Tilbudsperiode  -----  Kamp.'
6.20 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F4=Spørring   F5=Fo-
6.20 A                                      rny  F6=Opprett'
     A                                      COLOR(BLU)
6.20 A                                 23 53'F8=Endre dato/tid  F11=Slett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Varegruppe . . . . . . . +'
7.xx A            C1OGRP    R        B  5 29REFFLD(VTOGRP)
6.20 A  51                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 51)
6.20 A  56                                  ERRMSGID(VMG1031 *LIBL/QMFOFAK 56)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  5 32'+'
7.xx A            C1HGRP    R        B  5 34REFFLD(VTHGRP)
6.20 A  52                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 52)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  5 37'+'
7.xx A            C1UGRP    R        B  5 39REFFLD(VTUGRP)
6.20 A  53                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 53)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A            C1GTXT    R        O  5 43REFFLD(VGUTXT)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  6  2'Leverandør . . . . . . . +'
7.xx A            C1LDOR    R        B  6 29REFFLD(VTLDOR)
6.20 A  54                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 54)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A            C1LNAV        30A  O  6 43COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  8  2'Varenummer . . . . . . . +'
     A            C1VARE    R   -7   B  8 29REFFLD(VTVARE)
6.20 A**31                                  ERRMSGID(VMT0050 *LIBL/QMFOFAK 31)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
6.20 A  57                                  ERRMSGID(VMG1032 *LIBL/QMFOFAK 57)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1TEK1    R        O  8 43REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Prisgruppe . . . . . . . +'
7.xx A            C1PRGR    R        B  9 29REFFLD(VTPRGR)
     A  43                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1PTXT    R        O  9 43REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Leveringstype. . . . . . +'
7.xx A            C1LETY    R        B 10 29REFFLD(VTLETY)
     A  36                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C1LTXT    R        O 10 43REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Fra dato/tidspunkt . . . .'
7.xx A            C1FDAT         6Y 0B 12 29COLOR(YLW)
6.20 A**37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
6.20 A  37                                  ERRMSGID(VMT0060 *LIBL/QMFOFAK 37)
     A  41                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                 12 38'-'
7.xx A            C1FTIM         4Y 0B 12 40COLOR(YLW)
     A  38                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 38)
     A  42                                  ERRMSGID(FM00065 *LIBL/QMFOFAK 42)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                 13  2'Til dato/tidspunkt . . . .'
7.xx A            C1TDAT         6Y 0B 13 29COLOR(YLW)
     A  39                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 39)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                 13 38'-'
7.xx A            C1TTIM         4Y 0B 13 40COLOR(YLW)
     A  40                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 40)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990516  192110  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Tilbudspris finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20110810  143439  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilbudspris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vargruppe. . . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(VTOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGRP    R        O  5 34REFFLD(VTHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGRP    R        O  5 37REFFLD(VTUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT    R        O  5 42REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  6 31REFFLD(VTLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  6 42COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  8 31REFFLD(VTVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  8 42REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Prisgruppe   . . . . . . . .'
     A            C1PRGR    R        O  9 31REFFLD(VTPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT    R        O  9 42REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 10 31REFFLD(VTLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O 10 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Fra dato/tidspunkt . . . . .'
     A            C1FDAT         6Y 0O 12 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12 40'-'
     A            C1FTIM         4Y 0O 12 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 13  2'Til dato/tidspunkt . . . . .'
     A            C1TDAT         6Y 0O 13 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 13 40'-'
     A            C1TTIM         4Y 0O 13 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 15  2'Kampanje . . . . . . . . . .'
     A            C2KAMP    R        B 15 31REFFLD(VTKAMP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PRO1    R        B 15 65REFFLD(VTPRO1)
     A  45                                  ERRMSGID(VMT0021 *LIBL/QMFOFAK 45)
     A  46                                  ERRMSGID(VMT0022 *LIBL/QMFOFAK 46)
     A  47                                  ERRMSGID(VMT0023 *LIBL/QMFOFAK 47)
     A  48                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 15 49'Prosentsats. . '
7.01 A                                 16  2'Innkjøpsdato fra / til . . .'
7.01 A            C2VIDF         6Y 0B 16 31COLOR(YLW)
7.01 A  39                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 39)
7.01 A  41                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 41)
7.01 A                                      EDTWRD('  .  .  ')
7.01 A                                      CHECK(RB)
7.01 A                                      OVRDTA
7.01 A                                 16 40'-'
7.01 A            C2VIDT         6Y 0B 16 42COLOR(YLW)
7.01 A  40                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 40)
7.01 A                                      EDTWRD('  .  .  ')
7.01 A                                      CHECK(RB)
7.01 A                                      OVRDTA
7.xx A                                 17 30'Kostpris'
7.xx A  80                             17 69'Omr.faktor'
7.xx A                                 17  4'Enh'
7.xx A                                 17  9'Ink.'
7.xx A                                 18  9'Mva'
7.xx A                                 18 18'Tilbudspris'
7.xx A                                 18 30'tilb. per.'
7.xx A  80                             18 55'Salgspris'
7.xx A  80                             18 69'volum'
7.xx A                                 19  2'+'
7.xx A            C2ENH1    R        B 19  4REFFLD(VTENH1)
     A  32                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2VA01    R        B 19  9REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 19 11'1/0'
7.xx A                                 19 15'.'
7.xx A            C2TIP1    R        B 19 18REFFLD(VTTIP1)
     A  31                                  ERRMSGID(VMT0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2KOP1    R        B 19 30REFFLD(VTKOP1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2INP1    R        B 19 42REFFLD(VTKOP1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 19 53'.'
8.02 A  80        C2SAP1    R        O 19 55REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                 19 67' '
8.02 A                                 20  2'+'
7.xx A            C2ENH2    R        B 20  4REFFLD(VTENH2)
     A  34                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2VA02    R        B 20  9REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 20 11'1/0'
7.xx A                                 20 15'.'
7.xx A            C2TIP2    R        B 20 18REFFLD(VTTIP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2KOP2    R        B 20 30REFFLD(VTKOP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2INP2    R        B 20 42REFFLD(VTKOP2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 20 53'.'
8.02 A  80        C2SAP2    R        O 20 55REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A  80        C2OMR2    R        O 20 67REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 21  2'+'
7.xx A            C2ENH3    R        B 21  4REFFLD(VTENH3)
     A  36                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2VA03    R        B 21  9REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 21 11'1/0'
7.xx A                                 21 15'.'
7.xx A            C2TIP3    R        B 21 18REFFLD(VTTIP3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A            C2INP3    R        B 21 42REFFLD(VTKOP3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 21 53'.'
8.02 A  80        C2SAP3    R        O 21 55REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A  80        C2OMR3    R        O 21 67REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VTEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C2KOP3    R        B 21 30REFFLD(VTKOP3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 17 42'Innkjøpspris'
7.xx A                                 18 42'tilb.per'
     A          R D1WIN
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
6.20 A*                                     WINDOW(*DFT 15 50)
6.20 A                                      WINDOW(*DFT 18 50)
     A                                  2  2' Slette tilbudspris '
     A                                      DSPATR(RI)
6.20 A                                  4  2'Varegruppe . . . .'
6.20 A            D1OGRP    R        O  4 21REFFLD(VTOGRP)
6.20 A            D1HGRP    R        O  4 24REFFLD(VTHGRP)
6.20 A            D1UGRP    R        O  4 27REFFLD(VTUGRP)
6.20 A                                  5  2'Leverandør . . . .'
6.20 A            D1LDOR    R        O  5 21REFFLD(VTLDOR)
     A                                  7  2'Varenummer . . . .'
     A            D1VARE    R        O  7 21REFFLD(VTVARE)
     A                                  8  2'Prisgruppe . . . .'
     A            D1PRGR    R        O  8 21REFFLD(VTPRGR)
     A                                  9  2'Leveringstype. . .'
     A            D1LETY    R        O  9 21REFFLD(VTLETY)
     A                                 10  2'Fra dato/tidspunkt'
     A            D1FDAT         6Y 0O 10 21EDTWRD('  .  .  ')
     A                                 10 30'-'
     A            D1FTIM         4Y 0O 10 32EDTWRD('  :  ')
     A                                 11  2'Til dato/tidspunkt'
     A            D1TDAT         6Y 0O 11 21EDTWRD('  .  .  ')
     A                                 11 30'-'
     A            D1TTIM         4Y 0O 11 32EDTWRD('  :  ')
     A                                 13  2'Kampanje . . . . .'
     A            VTKAMP    R        O 13 21
     A                                 14  2'Enhet. . . . . . .'
     A            VTENH1    R        O 14 21
     A                                 15  2'Salgspris. . . . .'
     A            VTTIP1    R        O 15 21
     A                                 16  2'Kostpris . . . . .'
     A            VTKOP1    R        O 16 21
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
6.20 A*                                     WINDOW(*DFT 17 50)
6.20 A                                      WINDOW(*DFT 20 50)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  1' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  2  2'Varegruppe . . . +'
6.20 A            K1OGRP    R        B  2 21REFFLD(VTOGRP)
6.20 A  51                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 51)
6.20 A  32                                  ERRMSGID(VMT0030 *LIBL/QMFOFAK 32)
6.20 A                                      COLOR(YLW)
6.20 A                                      CHECK(RB)
6.20 A                                      OVRDTA
7.xx A                                  2 24'+'
7.xx A            K1HGRP    R        B  2 26REFFLD(VTHGRP)
6.20 A  52                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 52)
6.20 A                                      COLOR(YLW)
6.20 A                                      CHECK(RB)
6.20 A                                      OVRDTA
7.xx A                                  2 29'+'
7.xx A            K1UGRP    R        B  2 31REFFLD(VTUGRP)
6.20 A  53                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 53)
6.20 A                                      COLOR(YLW)
6.20 A                                      CHECK(RB)
6.20 A                                      OVRDTA
7.xx A                                  3  2'Leverandør . . . +'
6.20 A            K1LDOR    R        B  3 21REFFLD(VTLDOR)
6.20 A  54                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 54)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A                                  4  2'Varenummer . . . +'
     A            K1VARE    R   -7   B  4 21REFFLD(VTVARE)
     A  31                                  ERRMSGID(VMT0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMT0030 *LIBL/QMFOFAK 32)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Prisgruppe . . . +'
     A            K1PRGR    R        B  5 21REFFLD(VTPRGR)
     A  43                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Leveringstype. . +'
     A            K1LETY    R        B  6 21REFFLD(VTLETY)
     A  36                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Fra dato/tidspunkt'
     A            K1FDAT         6Y 0B  7 21COLOR(YLW)
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A  41                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7 30'-'
     A            K1FTIM         4Y 0B  7 32COLOR(YLW)
     A  38                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 38)
     A  42                                  ERRMSGID(FM00065 *LIBL/QMFOFAK 42)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2'Til dato/tidspunkt'
     A            K1TDAT         6Y 0B  8 21COLOR(YLW)
     A  39                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 39)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8 30'-'
     A            K1TTIM         4Y 0B  8 32COLOR(YLW)
     A  40                                  ERRMSGID(FM00060 *LIBL/QMFOFAK 40)
     A                                      EDTWRD('  :  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
6.20 A                                 11  2'Varegruppe . . . .'
6.20 A            VTOGRP    R        O 11 21OVRDTA
6.20 A            VTHGRP    R        O 11 24OVRDTA
6.20 A            VTUGRP    R        O 11 27OVRDTA
6.20 A                                 12  2'Leverandør . . . .'
6.20 A            VTLDOR    R        O 12 21OVRDTA
     A                                 13  2'Varenummer . . . .'
     A            VTVARE    R        O 13 21OVRDTA
     A                                 14  2'Prisgruppe . . . .'
     A            VTPRGR    R        O 14 21OVRDTA
     A                                 15  2'Leveringstype. . .'
     A            VTLETY    R        O 15 21OVRDTA
     A                                 16  2'Fra dato/tidspunkt'
     A            K2FDAT         6Y 0O 16 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 16 30'-'
     A            K2FTIM         4Y 0O 16 32EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 17  2'Til dato/tidspunkt'
     A            K2TDAT         6Y 0O 17 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17 30'-'
     A            K2TTIM         4Y 0O 17 32EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 19  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
6.23 A          R C2MSG
6.23 A*%%TS  SD  20051021  083406  HKO530      REL-V5R3M0  5722-WDS
6.23 A                                      WINDOW(*DFT 7 30)
6.23 A                                      CA03
6.23 A                                      CA12
6.23 A                                  2  2'NB Ingen automatisk prisopp-'
6.23 A                                  3  2'datering av alle enheter !!'
6.23 A                                  6  2'ENTER=Fortsett'
6.23 A                                      COLOR(BLU)
