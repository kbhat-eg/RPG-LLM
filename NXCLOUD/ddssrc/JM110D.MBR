     A*%%TS  SD  20140219  080413  BOFBMP      REL-V7R1M0  5770-WDS
     A*%%EC
     A**--------------------------------------------------------
     A** 190214 BOF : Utvidet med innkjøpsdato fra / til
6.34 A* R6.30 201014 BSA  Innfører F tast for visning av NOBB info. Erstatter
6.34 A*                   koding i jacada, samt gir tilsvarenede tilgang
6.34 A*                   i grønt.
6.36 A*       310817 NHO  Prisgigende leverandør ikke åpnet for endring
7.xx A*       171116 bsa  Endringer relatert til ny GUI
7.04 A*       090421 bof  Utvidet med DG  og kostpris fra vA
7.05 A*       210702      MGR  ekstra
     A**--------------------------------------------------------
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050919  221316  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG         2Y 0B  9  2COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 2 3 4 5 7 8 9 10)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1VLDO    R        O  9  5REFFLD(JMVLDO)
     A            B1LNAV         6   O  9 12
     A            B1VVAR    R   -7   O  9 19REFFLD(JMVVAR)
     A            B1TEK1    R   20   O  9 28REFFLD(JVTEK1)
     A            B1ENHE    R        O  9 49REFFLD(JWENHE)
     A            B1VKPR    R        O  9 53REFFLD(JMVKPR)
     A            B1PRIS    R        O  9 64REFFLD(JMVKAA)
     A            B1LAST         1Y 0O  9 77EDTCDE(Z)
     A            B1STAT         1Y 0O  9 80EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050919  221316  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjepriser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JM110B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Leverandør'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 19'Varenr'
7.xx A                                      COLOR(WHT)
7.01 A                                  4 49'Kampanje:'
     A                                      COLOR(BLU)
7.01 A            B2KAMP    R        O  4 59REFFLD(JMKAMP)
     A                                      COLOR(YLW)
7.01 A                                  4 66'Prisgruppe:'
7.01 A                                      COLOR(BLU)
7.01 A            B2PRGR    R        O  4 78REFFLD(JMPRGR)
7.01 A                                      COLOR(YLW)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'7=Vedlikehold av vare  8=Betingels-
     A                                      er  9=Priser  10=Enheter'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 12'Navn '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 19'Varenr '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 28'Varetekst-1     '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 55'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 65'Kamp.pris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 76'Lå'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'S'
7.xx A                                      COLOR(WHT)
7.xx A**                                7  2'V  Leverandør    Varenr   Vareteks-
7.xx A**                                    t-1          Enh   Kostpris  Kamp.p-
7.xx A**                                    ris Låst S'
7.xx A**                                    COLOR(WHT)
7.xx A                                  5  3'+'
7.xx A            B2VLDO    R        B  5  5REFFLD(JMVLDO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 17'+'
7.xx A            B2VVAR    R   -7   B  5 19REFFLD(JMVVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020819  132047  HKO         REL-V5R1M0  5722-WDS
     A                                 22  2'F3=Gå tilbake  F4=Spørring  F5=For-
     A                                      ny  F6=Opprett   F13=Status  F14=På-
     A                                      slag kost.'
     A                                      COLOR(BLU)
     A                                 23  2'F16=Grp. leverandør   F17=Grp. var-
     A                                      er  F18=Utpl. kampanje       F20=Ov-
     A                                      erfør'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19990104  163033  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjepriser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Vare . . . . . . . . . . . +'
     A            C1VVAR    R   -7   B  5 31REFFLD(JMVVAR)
     A  31                                  ERRMSGID(JMM1010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19970927  170405  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Vare finnes allerede i   '
     A                                  3  2'kampanjen'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140219  080413  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
6.34 A                                      CF18
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjepriser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare . . . . . . . . . . . .'
     A            C1VVAR    R   -7   O  5 31REFFLD(JMVVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Nobb-nummer. . . . . . . . .'
     A            C2NOBB    R        O  7 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Prisgivende leverandør . . .'
6.36 A***         C2VLDO    R        O  8 31REFFLD(JMVLDO)
6.36 A            C2VLDO    R        B  8 31REFFLD(JMVLDO)
     A                                      COLOR(YLW)
6.36 A  40                                  ERRMSGID(JML0300 *LIBL/QMFVADM 40)
     A                                      OVRDTA
     A            C2LNAV    R        O  8 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O 10 31REFFLD(JWENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.33 A                                 11  2'Låst lokalt  . . . . . . . .'
6.33 A            C2VLAS    R        B 11 31REFFLD(JMVLAS)
6.33 A                                      COLOR(YLW)
6.33 A                                      OVRDTA
7.xx A                                 11 33'1/0'
7.xx A                                 11 37'Sett kryss for å låse lokalt'
6.33 A                                      COLOR(RED)
     A                                 12  2'Innkjøpspris . . . . . . KAI'
     A            C2VKAI    R        B 12 31REFFLD(JMVKAI)
     A  31                                  ERRMSGID(JMM1100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 46'Innkjøpsdato fra '
     A            C2VIDF         6Y 0B 12 64
     A  38                                  ERRMSGID(JM00250 *LIBL/QMFVADM 38)
     A  41                                  ERRMSGID(JMM0150 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 13  2'Kostpris . . . . . . . . . .'
     A            C2VKPR    R        B 13 31REFFLD(JMVKAI)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 46'Innkjøpsdato til '
     A            C2VIDT         6Y 0B 13 64
     A  39                                  ERRMSGID(JM00250 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.04 A            C2KOPR    R        O 14 32REFFLD(JMVKAI)
7.04 A                                      COLOR(YLW)
7.04 A                                      OVRDTA
     A                                 15  2'Egen/anbefalt kamp.pris. KAA'
7.05 A            C2VKAA    R        B 15 32REFFLD(JMVKAA)
     A  32                                  ERRMSGID(JMM1110 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JMM1120 *LIBL/QMFVADM 33)
     A  36                                  ERRMSGID(JMC0110 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.04 A                                 15 46'DG  '
7.04 A            C2DG           5Y 2B 15 52
7.04 A                                      COLOR(YLW)
7.04 A                                      CHECK(RB)
7.04 A  30                                  DSPATR(PR)
7.04 A                                      EDTCDE(M)
7.04 A                                      OVRDTA
     A                                 17  2'Låst kampanjepris. . . . KAU'
     A            C2VKAU    R        O 17 31REFFLD(JMVKAU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.05 A                                 18  2'Kampanjepris ink. mva  . . .'
7.05 A            C2KPIM    R        B 18 31REFFLD(JMVKAA)
7.05 A  32                                  ERRMSGID(JMM1110 *LIBL/QMFVADM 32)
7.05 A  33                                  ERRMSGID(JMM1120 *LIBL/QMFVADM 33)
7.05 A  36                                  ERRMSGID(JMC0110 *LIBL/QMFVADM 36)
7.05 A                                      COLOR(YLW)
7.05 A                                      CHECK(RB)
7.05 A  30                                  DSPATR(PR)
7.05 A                                      OVRDTA
7.05
6.33 A                                 19  2'Opprettet dato . . . . . . .'
6.33 A            C2VODA         6Y 0O 19 31COLOR(YLW)
6.33 A                                      EDTWRD('  .  .  ')
6.33 A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2VEDA         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2VEUS    R        O 21 31REFFLD(JMVEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.34 A                                 23  2'F3=Gå tilbake  F18=NOBB info'
     A                                      COLOR(BLU)
     A                                 14  2'Kostpris i VA. . . . . . . .'
7.04 A                                  9  2'Reskontro leverandør   . . .'
7.04 A***         C2VLDO    R        O  8 31REFFLD(JMVLDO)
7.04 A            C2LEVR    R        O  9 31REFFLD(JMVLDO)
7.04 A                                      COLOR(YLW)
7.04 A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20020822  121125  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette vare fra kampanje '
     A                                      DSPATR(RI)
     A                                  4  2'Vare . . . . . . .'
     A            D1VVAR    R   -7   O  4 21REFFLD(JMVVAR)
     A                                  6  2'KAI. . . . . . . .'
     A            JMVKAI    R        O  6 21
     A                                  7  2'KAA. . . . . . . .'
     A            JMVKAA    R        O  7 21
     A                                  8  2'KAU. . . . . . . .'
     A            JMVKAU    R        O  8 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020822  121125  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 45)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Vare . . . . . . .'
     A            K1VVAR    R   -7   B  4 21REFFLD(JMVVAR)
     A  31                                  ERRMSGID(JMM1010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMM1030 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Vare . . . . . . .'
     A            JMVVAR    R        O  8 21OVRDTA
     A                                  9  2'KAI. . . . . . . .'
     A            JMVKAI    R        O  9 21OVRDTA
     A                                 10  2'KAA. . . . . . . .'
     A            JMVKAA    R        O 10 21OVRDTA
     A                                 11  2'KAU. . . . . . . .'
     A            JMVKAU    R        O 11 21OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
