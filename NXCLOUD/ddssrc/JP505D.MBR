     A*%%TS  SD  20160808  132234  NHO630      REL-V7R1M0  5770-WDS
     A*
8.01 A*PRG2 140622 bof Utvidet prisgruppe fra 1 til 2 posisjoner
6.31 A*  080816 NHO  Viser om betingelse er aktiv
6.31 A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20150604  105709  BOF630      REL-V7R1M0  5770-WDS
     A            B1RSTS    R        H      REFFLD(JPRSTS)
     A            B1LINE    R        H      REFFLD(JPLINE)
     A            B1FAKT         8S 5H
     A            B1NTPR         9S 2H
     A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 5 7 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1PRGR    R        O  9  4REFFLD(JPPRGR)
8.01 A            B1OGRP    R        O  9  7REFFLD(JPOGRP)
8.01 A            B1HGRP    R        O  9 10REFFLD(JPHGRP)
8.01 A            B1UGRP    R        O  9 13REFFLD(JPUGRP)
8.01 A            B1MODN    R        O  9 17REFFLD(JPMODN)
8.01 A            B1VARE    R   -7   O  9 26REFFLD(JPVARE)
8.01 A            B1VTYP    R        O  9 35REFFLD(JPVTYP)
8.01 A            B1LETY    R        O  9 37REFFLD(JPLETY)
6.31 A*           B1BETI    R  -10   O  9 38REFFLD(JPBETI)
8.01 A            B1BETI    R  -12   O  9 39REFFLD(JPBETI)
6.31 A*           B1FANT        11A  O  9 64
8.01 A            B1FANT        11A  O  9 63
8.01 A            B1AKTI         1A  O  9 75
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20150604  105709  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge betingelse '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 23REFFLD(JPLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2LNAV    R        O  4 30REFFLD(JLNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'1=Velg  5=Vis betingelse  7=Vis ra-
     A                                      battelement'
     A                                      COLOR(BLU)
     A                                  7  2'V Pg Varegrp. Modulnr  Varenr   V -
     A                                      L Betingel'
     A                                      COLOR(WHT)
6.31 A*N80                              7 65'Nettopris'
6.31 A N80                              7 62'Nettopr.'
     A N80                                  COLOR(WHT)
6.31 A* 80                              7 67'Faktor'
6.31 A  80                              7 64'Faktor'
     A  80                                  COLOR(WHT)
6.31 A                                  7 71'Aktiv'
6.31 A                                      COLOR(WHT)
     A            B2PRGR    R        B  8  4REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.01 A            B2OGRP    R        B  8  7REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2HGRP    R        B  8 10REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2UGRP    R        B  8 13REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2MODN    R        B  8 17REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2VARE    R   -7   B  8 26REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2VTYP    R        B  8 35REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2LETY    R        B  8 37REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.31 A*           B2BETI    R  -10   B  8 38REFFLD(JPBETI)
8.01 A            B2BETI    R  -12   B  8 39REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990902  202428  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 19 75)
     A                                 18  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A  80                             18 58'F23=Vis nettopris'
     A  80                                  COLOR(BLU)
     A N80                             18 58'F23=Vis faktor'
     A N80                                  COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20150604  105709  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A            C2RSTS    R        H      REFFLD(JPRSTS)
     A            C2LINE    R        H      REFFLD(JPLINE)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise betingelse'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . . . . . . . . .'
     A            C2LDOR    R        O  4 31REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A            C2LNAV    R        O  4 42REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  5 31REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A            C2PTXT        35A  O  5 42COLOR(YLW)
     A                                  6  2'Overgruppe . . . . . . . . .'
     A            C2OGRP    R        O  6 31REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A            C2OTXT        35A  O  6 42COLOR(YLW)
     A                                  7  2'Hovedgruppe. . . . . . . . .'
     A            C2HGRP    R        O  7 31REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A            C2HTXT        35A  O  7 42COLOR(YLW)
     A                                  8  2'Undergruppe. . . . . . . . .'
     A            C2UGRP    R        O  8 31REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A            C2UTXT        35A  O  8 42COLOR(YLW)
     A                                  9  2'Modulnummer. . . . . . . . .'
     A            C2MODN    R        O  9 31REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A            C2MTE1    R        O  9 42REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                 10  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O 10 31REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A            C2TEK1    R        O 10 42REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                 11  2'Varetype . . . . . . . . . .'
     A            C2VTYP    R        O 11 31REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A            C2TTXT        30A  O 11 42COLOR(YLW)
     A                                 12  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O 12 31REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A            C2LBES        35A  O 12 42COLOR(YLW)
     A                                 14  2'Nobb-nummer. . . . . . . . .'
     A            C2NOBB    R        O 14 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                 16  2'Betingelse . . . . . . . . .'
     A            C2BETI    R        O 16 31REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                 18  2'Fra-dato . . . . . . . . . .'
     A            C2FDAT         6Y 0O 18 31COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      EDTWRD('  .  .  ')
     A                                 19  2'Til-dato . . . . . . . . . .'
     A            C2TDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 20  2'Endringsdato avtaleeier. . .'
     A            C2DATO         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(JPEUSR)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake  F13=Vise rab.elem. -
     A                                       F18=NOBB info'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 78' '
