     A*%%TS  SD  20131003  130524  SST620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP200D
     A* Beskrivelse . . : Vedlikehold av prosjekt-rapport
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       090516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20130925  114852  SST620      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 6 7)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A            B1RAP     R        O  8  4REFFLD(RPHRAP)
     A            B1LIN     R        O  8  9REFFLD(RPHLI1)
     A            B1ULIN    R        O  8 14REFFLD(RPHLI2)
     A            B1BESK        61A  O  8 19
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20130926  150852  SST620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Rapp'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Linj'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Ulin'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 19'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A            B2RAP     R        B  5  4REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  5 19REFFLD(RPHTXT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A  22                                  ERRMSGID(RMS0011 *LIBL/QMFOKON 22)
     A                                      OVRDTA
     A                                  2  2'Vedlikehold av Prosjekt-rapport'
     A                                      COLOR(WHT)
7.xx A**                                5 66'          Skr '
7.xx A**                                    COLOR(WHT)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Rapp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  9'Linj'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Ulin'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 38'Kont'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 43'Bær'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 48'Kont'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'Bær'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 58'Lin1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Lin2'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 68'F'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Pros'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 76'PKL'
7.xx A**                                7  2'V Rapp Linj Ulin Beskrivelse      -
7.xx A**                                      Kont Bær  Kont Bær  Lin1 Lin2 F P-
7.xx A**                                    ros  PKL '
     A                                      COLOR(WHT)
7.xx A            FLD001    R        B  5  9REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            FLD002    R        B  5 14REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett'
7.xx A                                      COLOR(BLU)
7.xx A**                                5 38'--Fra---  --Til--- '
7.xx A                                  6 31'5=Vise   6=Detaljer              '
7.xx A                                      COLOR(BLU)
     A          R B2CMD
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20130925  141640  SST620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Prosjekt-rapport . . . . . .'
     A            C1RAP     R        B  5 32REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'  Rapport-linje. . . . . . .'
     A            C1LIN     R        B  6 32REFFLD(RPHLIN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'  Rapport-under_linje. . . .'
     A            C1ULIN    R        B  7 32REFFLD(RPHULI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  2  2'Opprette Prosjekt-rapport'
     A                                      COLOR(WHT)
     A          R C1MSG
     A*%%TS  SD  20130925  092924  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Rapport  finnes fra før.   '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20131003  125651  SST620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prosjekt-rapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  5  2'Prosjekt-rapport . . . . . .'
     A            C1RAP     R        B  5 31REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                  6  2'  Rapport-linje. . . . . . .'
     A            C1LIN     R        B  6 31REFFLD(RPHLIN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'  Rapport-under_linje. . . .'
     A            C1ULIN    R        B  7 31REFFLD(RPHULI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2BESK    R        B  8 31REFFLD(RPHTXT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  23                                  ERRMSGID(RMP0400 *LIBL/QMFOKON 23)
     A                                      OVRDTA
     A                                  9  2'Fra konto - Bærer. . . . . .'
     A            C2FKTO    R        B  9 31REFFLD(RPHKTF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A            C2FBER    R        B  9 39REFFLD(RPHLI1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 10  2'Til konto - Bærer. . . . . .'
     A            C2TKTO    R        B 10 31REFFLD(RPHKTT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A            C2TBER    R        B 10 39REFFLD(RPHLI1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 11  2'Linje 1  . . . . . . . . . .'
     A            C2LIN1    R        B 11 31REFFLD(RPHLI2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 12  2'Linje 2  . . . . . . . . . .'
     A            C2LIN2    R        B 12 31REFFLD(RPHLI2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 12 37'Fra-til:'
     A            C2FRTI    R        B 12 46REFFLD(RPHFUN)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 12 49'(J)'
     A                                 13  2'Funksjon . . . . . . . . . .'
     A            C2FUNK    R        B 13 31REFFLD(RPHFUN)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 13 34'(+,-,%, )'
     A                                 14  2'Prosent sats . . . . . . . .'
     A            C2PROS    R        B 14 31REFFLD(RPHPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 15  2'Skrive posteringer . . . . .'
     A            C2SPOS    R        B 15 31REFFLD(RPHLKT)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 15 34'(J)                '
     A                                 16  2'Skrive konto . . . . . . . .'
     A            C2SKTO    R        B 16 31REFFLD(RPHLKT)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 16 34'(J)                     '
     A                                 17  2'Skrive linje . . . . . . . .'
     A            C2SLIN    R        B 17 31REFFLD(RPHLKT)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 17 34'(J)'
     A                                 19  2'Endret dato. . . . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RPHEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 44'(l1-l2, l1 i % av l2)'
     A                                 13 66'(K=KostBudsj.)'
     A                                 14 66'(S=SalgBudsj.)'
     A          R D1WIN
     A*%%TS  SD  20131003  130524  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette rapport    '
     A                                      DSPATR(RI)
     A                                  4  2'Prosjekt-rapport .'
     A            D1RAP     R        O  4 23REFFLD(RPHRAP)
     A                                  5  2'Prosjekt-linje . .'
     A            D1LIN     R        O  5 23REFFLD(RPHLIN)
     A                                  6  2'Prosjekt-under-linje'
     A            D1ULIN    R        O  6 23REFFLD(RPHULI)
     A                                  7  2'Beskrivelse. . . .'
     A            RPHTXT    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5 29'NB!  Hele rapporten slettes'
     A  41                                  DSPATR(ND)
     A                                      COLOR(RED)
     A          R K1WIN
     A*%%TS  SD  20130925  092924  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  6  2'Prosjekt-rapport .'
     A            K1RAP     R        B  6 24REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Prosjekt-linje . .'
     A            K1LIN     R        B  7 24REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Prosjekt-under-linje'
     A            K1ULIN    R        B  8 24REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Prosjekt-rapport .'
     A            RPHRAP    R        O 10 24OVRDTA
     A                                 11  2'Beskrivelse. . . .'
     A            RPHTXT    R        O 11 24OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  4  2'FILGRUPPE  . . . .'
     A                                  5  2'FIRMA  . . . . . .'
     A            K1FILG         3A  B  4 24
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            K1FIRM    R        B  5 24REFFLD(RPHFIR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R CCMSG
     A*%%TS  SD  20130218  084604  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Feilmelding:               '
     A            CCMSG1        30   O  4  1
     A            CCMSG2        30   O  5  1
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20130927  124951  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 12 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_AFLD        10A  H
     A            W_ARCD        10A  H
     A                                  2  2'Valg for utskrift.'
     A                                  4  2'Prosjekt(fra). . .'
     A            C3FRAP    R        B  4 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 29'Ev.til'
     A            C3TILP    R        B  4 36REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Denne Periode(fra)'
     A            C3FPER    R        B  5 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3TPER    R        B  5 36REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 29'Ev.til'
     A                                  5 43'(ååååmm)'
     A                                  6  2'Avdeling . . . . .'
     A            C3AVDE    R        B  6 21REFFLD(RP1AVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3AVDX        20   B  6 29
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Prosjektleder. . .'
     A            C3PROL    R        B  7 21REFFLD(RP1PRL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3PROX        20   B  7 29
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Rapport. . . . . .'
     A            C3RAPP    R        B  8 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
