     A*%%TS  SD  20140305  115447  MHA620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LH102D
     A* Beskrivelse . . : Bestillingsforslag, detaljlinjer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120302 ASK  Nytt program
6.30 A*
6.31 A*       130916 NHO  Skal vise full varetekst når en tar '5' foran
     A*                   på vedlikehold av varelinje
6.NU A*       260514 bof  sjekket mht. nummerutvidelse
7.xx A* 7.xx  170316 bsa  Justeringer basert på ny GUI og regler
7.03 A* K00   270420 bsa  Utvidet til bredere skjerm.
7.04 A* K00   100221 ask  Håndterer desimaler på vektvisning endringsbilde (B3)
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.03 A                                      DSPSIZ(27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090914  123107  BOF610      REL-V5R4M0  5722-WDS
6.31 A            XXTEK1    R        H      REFFLD(LFTEK1)
     A            B1LINE    R        H      REFFLD(LFLINE)
     A            B1LTYP    R        H      REFFLD(LFLTYP)
     A            B1UDAT         6Y 0H
7.03 A*           B1BMEN         8Y 2H
7.03 A*           B1BMIN         8Y 2H
7.03 A            B1MEKS         8Y 2H
7.03 A            B1MIXS         8Y 2H
     A            B1LBEL    R        H      REFFLD(LFLBEL)
7.04 A            B1VEKD    R        H      REFFLD(LFVEKT)
      *
7.03 A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 4 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
7.03 A            B1VARE    R   -7   O  9  4REFFLD(LFVARE)
7.xx A            B1MANU         1A  O  9 13
7.xx A            B1STAT    R        O  9 15REFFLD(LFSTAT)
7.xx A            B1SORT         1A  O  9 17
7.03 A            B1ANTA         8Y 2O  9 19EDTWRD('      ,  ')
7.03 A            B1ENH1    R        O  9 29REFFLD(LFENH1)
7.03 A            B1PKLA         1A  O  9 33
7.03 A            B1BMEN         8Y 2O  9 35EDTWRD('      ,  ')
7.03 A            B1MINI         8Y 2O  9 45EDTWRD('      ,  ')
7.03 A            B1MAKS         8Y 2O  9 56EDTWRD('      ,  ')
7.03 A            B1BMIN         8Y 2O  9 66EDTWRD('      ,  ')
7.03 A            B1BPUN         8Y 2O  9 76EDTWRD('      ,  ')
7.03 A            B1ENH2    R        O  9 86REFFLD(LFENH1)
7.03 A            B1TEK1    R   -9   O  9 90REFFLD(LFTEK1)
7.03 A            B1VEKT         5Y 0O  9118EDTCDE(Z)
7.03 A            B1VOLU         6Y 3O  9124EDTCDE(M)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101216  143521  BOF610      REL-V5R4M0  5722-WDS
7.03 A                                      SFLSIZ(0017)
7.03 A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A                                      CF10
7.03 A*                                     CF11
     A                                      CF13
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.03 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.03 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forslags-linjer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LH102B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.03 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.03 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Lev.:'
     A            B2LDOR    R        O  4  8REFFLD(LHLDOR)
     A            B2LNAV        20A  O  4 15
7.03 A            B2ONUM    R        O  4 37REFFLD(LONUMM)
7.03 A                                  4 47'Ordreverdi:'
7.03 A            B2TBEL    R        O  4 59REFFLD(LHTBEL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
7.03 A                                  4 74'Forslagnr.'
7.03 A            B2NUMM    R        O  4 85REFFLD(LHNUMM)
7.xx A                                  5  4'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  7 11'4=Slett'
     A                                      COLOR(BLU)
7.xx A                                  7 20'5=Godkjenn status'
     A                                      COLOR(BLU)
7.xx A                                  7 39'6=Innkj.plan.'
     A                                      COLOR(BLU)
7.xx A                                  7 54'7=Vareinfo'
     A                                      COLOR(BLU)
7.xx A                                  7 68'8=Lagervedl.'
     A                                      COLOR(BLU)
7.03 A                                  7 83'9=Lagerspørr.'
     A                                      COLOR(BLU)
7.03 A                                  8  2'V'
     A                                      COLOR(WHT)
7.03 A                                  8  4'Varenr.'
     A                                      COLOR(WHT)
7.03 A                                  8 13'M'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 15'S'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 17'S'
     A                                      COLOR(WHT)
7.03 A                                  8 21'Antall'
     A                                      COLOR(WHT)
7.03 A                                  8 28'Enh'
     A                                      COLOR(WHT)
7.03 A                                  8 32'Pk'
     A                                      COLOR(WHT)
7.03 A                                  8 35'Best.men.'
     A                                      COLOR(WHT)
7.03 A                                  8 46'Minimum  '
     A                                      COLOR(WHT)
7.03 A*N51                              8 55'Varetekst    '
7.03 A*                                     COLOR(WHT)
7.03 A                                  8 56'Maksimum '
     A                                      COLOR(WHT)
7.03 A*N50N51                           8 45'Anbr.men.'
7.03 A                                  8 66'Anbr.men.'
     A                                      COLOR(WHT)
7.03 A* 51                              8 45'Innkj.men'
7.03 A                                  8 76'Best.pkt.'
     A                                      COLOR(WHT)
7.03 A                                  8 86'Enh'
7.03 A                                      COLOR(WHT)
7.03 A* 51                              8 55'Enh Varetekst '
7.03 A                                  8 90'Varetekst '
     A                                      COLOR(WHT)
7.03 A                                  8119'Vekt'
7.03 A                                      COLOR(WHT)
7.03 A                                  8127'M3'
7.03 A                                      COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2VARE    R   -7   B  6  4REFFLD(LFVARE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20040614  141159  BOF         REL-V5R2M0  5722-WDS
7.03 A                                 26  2'F1=Bla-alt'
     A                                      COLOR(BLU)
7.03 A                                 26 14'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.03 A                                 26 29'F4=Spørring'
     A                                      COLOR(BLU)
7.03 A                                 26 42'F6=Opprett'
     A                                      COLOR(BLU)
7.03 A                                 26 54'F9=Feilstatus/Alle'
     A                                      COLOR(BLU)
7.03 A                                 26 74'F10=Godkjenn status'
     A                                      COLOR(BLU)
7.03 A*                                26 95'F11=Skift'
7.03 A*                                     COLOR(BLU)
7.xx A*                                23 66'F13=Status'
7.xx A*                                     COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20050408  104751  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Endre linje på bestillingsforslag-
     A                                       '
     A                                      DSPATR(RI)
     A                                  4  2'Varenr    :'
     A            B3VARE    R   -7   O  4 14REFFLD(LFVARE)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B3TEK1    R        O  5 14REFFLD(LFTEK1)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B3TEK2    R        O  6 14REFFLD(LFTEK2)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                  8  2'Antall . .:'
     A            B3ANTA         8Y 2B  8 14EDTWRD('      ,  ')
     A  33                                  ERRMSGID(LM00410 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A N50                              8 28'Best.mengde. .:'
     A  50                              8 28'Minimumslager :'
     A N50        B3BMEN         8  2O  8 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A  50        B3MINI         8  2O  8 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A                                  9  2'Enhet. . .:'
     A            B3ENH1    R        B  9 14REFFLD(LFENH1)
     A  31                                  ERRMSGID(LMH0040 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A N50                              9 28'Anbrekksmengde:'
     A  50                              9 28'Maksimumslager:'
     A N50        B3BMIN         8  2O  9 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A  50        B3MAKS         8  2O  9 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A                                 10 28'Status . . . .:'
     A            B3STAT    R        O 10 44REFFLD(LFSTAT)
     A                                      OVRDTA
     A                                 11 28'Vekt . . . . .:'
     A            B3VEKT    R        O 11 44REFFLD(LFVEKT)
     A                                      EDTCDE(L)
     A                                      OVRDTA
6.2C A                                 12 28'Innkjøpspris .:'
     A            B3LBEL    R        O 12 44REFFLD(LFLBEL)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 10  2'Utgår dato:'
     A            B3UDAT         6Y 0O 10 14COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.30 A            B3INFO        20   O 13  5
     A                                 14  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20020924  121535  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette linje i bestillingsforslag-
     A                                       '
     A                                      DSPATR(RI)
     A            B4VARE    R   -7   O  4  2REFFLD(LFVARE)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B4TEK1    R        O  5  2REFFLD(LFTEK1)
     A                                      OVRDTA
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B6WIN
     A*%%TS  SD  20040524  141830  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 54)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Registrering av ny vare på bestil-
     A                                      lingsforslag '
     A                                      DSPATR(RI)
     A                                  4  2'Varenr    :'
     A            B6VARE    R   -7   B  4 14REFFLD(LFVARE)
     A  34                                  ERRMSGID(LM00410 *LIBL/QMFLAGR 34)
     A  35                                  ERRMSGID(LM00290 *LIBL/QMFLAGR 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B6TEK1    R        O  5 14REFFLD(LFTEK1)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B6TEK2    R        O  6 14REFFLD(LFTEK2)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                  8  2'Antall . .:'
     A            B6ANTA         8Y 2B  8 14EDTWRD('      ,  ')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  33                                  DSPATR(PC)
     A                                  8 28'Best.mengde. .:'
     A            B6BMEN         8Y 2O  8 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                  9  2'Enhet. . .:'
     A            B6ENH1    R        B  9 14REFFLD(LFENH1)
     A  31                                  ERRMSGID(LMH0040 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9 28'Anbrekksmengde:'
     A            B6BMIN         8Y 2O  9 44EDTWRD('      ,  ')
     A                                      OVRDTA
     A                                 10  2'Utgår dato:'
     A            B6UDAT         6Y 0O 10 14COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20040614  135513  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Endre status til 0 '
     A                                      DSPATR(RI)
     A            B7VARE    R   -7   O  4  2REFFLD(LFVARE)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B7TEK1    R        O  5  2REFFLD(LFTEK1)
     A                                      OVRDTA
     A            B7STAT    R        O  6 11REFFLD(LFSTAT)
     A                                      OVRDTA
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  6  2'Status :'
     A          R B10WIN
     A*%%TS  SD  20040614  142436  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Endre alle med status :'
     A                                      DSPATR(RI)
     A            B10STAT   R        B  4 14REFFLD(LFSTAT)
     A                                      OVRDTA
     A  31                                  ERRMSGID(LMH0080 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  6  2' Til status 0'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20140305  115447  MHA620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg utelate  .:'
     A            H1UTEL         1Y 0B  4 19OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A*                                 4 21'1=ja, 0=Nei'
7.xx A*                                 4 35'0 gir alle'
7.xx A                                  4 21'0=alle,1=ja'
     A                                  5  2'Velg statuskode:'
     A            H1STAT    R        B  5 19REFFLD(LHSTAT)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5 21'(kode)'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20100920  090036  TEW610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                  2  2'Forklaring på statuskoder'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'0 '
     A                                  4  5'Varen har 0 eller minus i beholdni-
     A                                      ng.'
     A                                  5  2'1 '
     A                                  5  5'Beholdning er lik eller under min.-
     A                                      pkt.'
     A                                  6  2'2 '
     A                                  6  5'Varen er under bestillingspunkt'
     A                                  7  2'3 '
     A                                  7  5'Tatt med for å øke ordreverdien'
     A                                  8  2'4 '
     A                                  8  5'Bestilt volum samsvarer ikke med b-
     A                                      est.mengde.'
     A                                  9  2'5 '
     A                                  9  5'Bestilt volum samsvarer ikke med a-
     A                                      nbrekk.'
     A                                 10  2'6 '
     A                                 10  5'Varen ligger allerede på en annen -
     A                                      bestilling.'
     A                                 11  2'7 '
     A                                 11  5'Varen er en cross docking vare.   -
     A                                                 '
     A                                 12  2'8 '
     A                                 12  5'Varens lev.nr er ikke best.forslag-
     A                                      ets.       '
     A                                 14  2'9 '
     A                                 14  5'Varen ligger ikke Kjede Sortiment '
     A                                 16  5'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13  2'M'
     A                                 13  5'Bestillingsmengde ut fra  manuelle-
     A                                       verdier   '
6.30 A  55                             15  2'E'
6.30 A  55                             15  5'Varen er en erstatningsvare'
     A          R B11WIN
     A*%%TS  SD  20101215  105510  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Vise tekstlinjer '
     A                                      DSPATR(RI)
     A            B11TE1    R        O  5 14REFFLD(LFTEK1)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B11TE2    R        O  6 14REFFLD(LFTEK2)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 14  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
