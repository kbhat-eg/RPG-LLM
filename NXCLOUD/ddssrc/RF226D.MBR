     A*%%TS  SD  20050720  123451  KOB540      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF226D
     A* Beskrivelse . . : Kobling avdeling og kasse id - vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       201114 bsa  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1AAVD    R        O  8  5REFFLD(RBAAVD)
     A            B1AKAS    R        O  8 10REFFLD(RBAKAS)
     A            B1ABES    R        O  8 14REFFLD(RBABES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  200627  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdeling/kasse id'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Avde Kid Beskrivelse'
     A                                      COLOR(WHT)
     A            B2AAVD    R        B  7  5REFFLD(RBAAVD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  200627  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdeling/kasse id'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Avdeling . . . . . . . . . .'
     A            C1AAVD    R        B  5 31REFFLD(RBAAVD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Spørre'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  005748  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kobling finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  125738  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av avdeling/kasse id'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Avdeling . . . . . . . . . .'
     A            C1AAVD    R        O  5 31REFFLD(RBAAVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kasse id . . . . . . . . . .'
     A            C2AKAS    R        B  7 31REFFLD(RBAKAS)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2ABES    R        B  8 31REFFLD(RBABES)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2AEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2AEUS    R        O 20 31REFFLD(RBAEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  005748  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette koblingg '
     A                                      DSPATR(RI)
     A                                  4  2'Avdeling . . . . .'
     A            D1AAVD    R        O  4 21REFFLD(RBAAVD)
     A            RBABES    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020307  005748  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Avdeling . . . . .'
     A            K1AAVD    R        B  4 21REFFLD(RBAAVD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  7  2'Avdeling . . . . .'
     A            RBAAVD    R        O  7 21
     A                                  8  2'Beskrivelse. . . .'
     A            RBABES    R        O  8 21
     A                                 14  2'F3=Gå tilbake  F4=Spørre'
     A                                      COLOR(BLU)
