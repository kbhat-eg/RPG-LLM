     A*%%TS  SD  20191128  142326  HKO700      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VP680D
     A* Beskrivelse . . : Generering av prisfil til kunder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916 bsa  Justeringer basert på ny GUI og regler
     A* 7.xx  281119 hko  Lagt inn valg rundt Logiq/EDI varekatalog
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20090511  100940  ASP_X50     REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Generering av prisfil til kunder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Rabattkategori . . . . . . +'
     A            B1RKAT         3Y 0B  4 31COLOR(YLW)
     A  31                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMP0300 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1RTXT        30A  O  4 42COLOR(YLW)
7.xx A                                  5  2'Kunde. . . . . . . . . . . +'
     A            B1KUND    R        B  5 31REFFLD(FHKUND)
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(VMP0320 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1KNAV        30A  O  5 42COLOR(YLW)
7.xx A                                  6  2'Kundeprosjekt. . . . . . . +'
     A            B1KPRO    R        B  6 31REFFLD(FHKPRO)
     A  36                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1PNAV        30A  O  6 42COLOR(YLW)
7.xx A                                  7  2'Pris-recordformat. . . . . +'
     A            B1RFMT    R        B  7 31REFFLD(FHRFMT)
     A  37                                  ERRMSGID(FMC0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A            B1FBES    R        O  7 42REFFLD(FCBESK)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Leveringstype. . . . . . . +'
     A            B1LETY    R        B  8 31REFFLD(FHLETY)
     A  48                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 48)
     A                                      COLOR(YLW)
     A            B1LBES    R        O  8 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                  9  2'Prisdato . . . . . . . . . .'
     A            B1DATO         6Y 0B  9 31COLOR(YLW)
     A  38                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 38)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
7.xx A                                 10  2'Lager  . . . . . . . . . . +'
     A            B1LAGE    R        B 10 31REFFLD(VLLAGE)
     A  49                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A            B1LTXT    R        O 10 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                 11  2'Overstyring av vareutplukk:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
7.xx A                                 12  2'Varesortiment. . . . . . . +'
     A            B1VSOR    R        B 12 31REFFLD(VCVSOR)
     A  39                                  ERRMSGID(VMP0360 *LIBL/QMFOFAK 39)
     A  41                                  ERRMSGID(VMP0310 *LIBL/QMFOFAK 41)
     A  42                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A            B1SBES    R        O 12 42REFFLD(VCBESK)
     A                                      COLOR(YLW)
7.xx A                                 13  2'Overgruppe . . . . . . . . +'
     A            B1OGRP    R        B 13 31REFFLD(VDOGRP)
     A  43                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A            B1OTXT    R        O 13 42REFFLD(VGOTXT)
     A                                      COLOR(YLW)
7.xx A                                 14  2'Hovedgruppe. . . . . . . . +'
     A            B1HGRP    R        B 14 31REFFLD(VDHGRP)
     A  44                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A            B1HTXT    R        O 14 42REFFLD(VGHTXT)
     A                                      COLOR(YLW)
7.xx A                                 15  2'Undergruppe. . . . . . . . +'
     A            B1UGRP    R        B 15 31REFFLD(VDUGRP)
     A  45                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A            B1UTXT    R        O 15 42REFFLD(VGUTXT)
     A                                      COLOR(YLW)
7.xx A                                 16  2'Leverandør . . . . . . . . +'
     A            B1LDOR    R        B 16 31REFFLD(VDLDOR)
     A  46                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LNAV        30A  O 16 42COLOR(YLW)
     A                                 17  2'Overstyringer i prisfil:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
7.xx A                                 18  2'Pris-recordformat. . . . . +'
     A            B1RFM2    R        B 18 31REFFLD(FHRFMT)
     A  40                                  ERRMSGID(VMP0370 *LIBL/QMFOFAK 40)
     A  47                                  ERRMSGID(FMC0020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A            B1FBE2    R        O 18 42REFFLD(FCBESK)
     A                                      COLOR(YLW)
7.xx A                                 19  2'Skaffevarer. . . . . . . . .'
     A            B1SVAL    R        B 19 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 19 42'1=Ja,0=Nei'
     A                                 19 53'Kun nobbnr . .'
     A            B1NVAL    R        B 19 68REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 19 70'1=Ja,0=Nei'
7.xx A                                 20  2'Tilbudspriser. . . . . . . .'
     A            B1TVAL    R        B 20 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 20 42'1=Ja,0=Nei'
7.xx A                                 21  2'Utelate varetekst-2. . . . .'
     A            B12VAL    R        B 21 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 21 42'1=Ja,0=Nei'
7.xx A                                 22  2'Utelate bruttopris . . . . .'
     A            B1BVAL    R        B 22 31REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 22 42'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R B2BLD
     A*%%TS  SD  20000426  120032  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 11 47)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Valg av mappe for lagring av pris-
     A                                      filer '
     A                                      DSPATR(RI)
     A                                  4  2'Mappenavn PC . . . . . . . .'
     A            B2FLDR        12A  B  4 31COLOR(YLW)
     A                                  6  2'Vær klar over at mappen tømmes før-
     A                                       generering'
     A                                  7  2'av nye prisfiler.'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B2MSG
     A*%%TS  SD  20000426  121157  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 10 41)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Valgt mappenavn er ugyldig, eller -
     A                                      mappe'
     A                                  3  2'er i bruk.'
     A                                  5  2'Velg en annen mappe, eller avslutt-
     A                                       all'
     A                                  6  2'aktivitet mot mappen både fra AS/4-
     A                                      00'
     A                                  7  2'og PC.'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B2WIN
     A*%%TS  SD  20191128  142326  HKO700      REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 13 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' EDI varekatalog '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde er satt opp med EDI'
     A                                  6  2'Skal prisfilen sendes som'
     A                                  7  2'EDI varekatalog . . . . . .'
     A            B2EDIV    R        B  7 31REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                  7 35'1=Ja,0=Nei'
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B2SBM
     A*%%TS  SD  20000426  105052  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Jobben er sendt som bakgrunnsjobb.'
     A                                  5  2'Det må ikke startes generering av -
     A                                      ny'
     A                                  6  2'prisfil før det gis melding om at'
     A                                  7  2'denne jobben er avsluttet.'
