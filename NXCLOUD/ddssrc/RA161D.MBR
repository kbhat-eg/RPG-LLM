     A*%%TS  SD  20020730  130313  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : NXCLOUD
     A* Format. . . . . : RL120D
     A* Beskrivelse . . : Låste valutakurser pr. leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       040121 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A            B1DATE         6Y 0O  8  4EDTWRD('  .  .  ')
     A            B1TIME         6Y 0O  8 13EDTWRD('  .  .  ')
     A            B1KURN        10Y 6O  8 23EDTCDE(1)
     A            B1KURG        10Y 6O  8 41EDTCDE(1)
     A            B1USER        10A  O  8 60
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  222058  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NXCLOUD'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Historisk valutaregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  3'Valuta'
     A                                      COLOR(WHT)
     A            B2VAKO         3A  O  4 13
     A                                      COLOR(WHT)
     A            B2VATX        20A  O  4 21
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7  4'Dato '
     A                                      COLOR(WHT)
     A                                  7 27'Ny kurs '
     A                                      COLOR(WHT)
     A                                  7 42'Gammel kurs '
     A                                      COLOR(WHT)
     A                                  7 60'Oppdatert av'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
