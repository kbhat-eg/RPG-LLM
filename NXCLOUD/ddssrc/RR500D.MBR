     A*%%TS  SD  20020921  220311  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RR500D
     A* Beskrivelse . . : Resultatrapport, spørring rapport
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160902 KOB  Nytt program
7.xx A*       040516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020921  220154  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 8)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
     A            B10RAP    R        O  9  5REFFLD(RR0RAP)
     A                                      EDTCDE(Z)
     A            B10TXT    R        O  9  9REFFLD(RR0TXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020921  220154  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Skriv resultatrapporter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  5'Rap'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  9'Rapportnavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'1=Standard   2=13. perioder   3=Fo-
     A                                      rventet resultat   8=Beregn/Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Rap'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Rapportnavn'
     A                                      COLOR(WHT)
7.xx A            B20RAP    R        B  6  5REFFLD(RR0RAP)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      OVRDTA
7.xx A            B20TXT    R        B  6  9REFFLD(RR0TXT)
7.xx A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
