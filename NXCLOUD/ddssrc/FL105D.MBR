     A*%%TS  SD  20200505  140902  ASP_IMT     REL-V7R3M0  5770-WDS
     A*%%EC
     A*
8.01 A*     bsa  090221    Legger ut verdi for Profile Manager
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C2BLD
     A*%%TS  SD  20200505  140902  ASP_IMT     REL-V7R3M0  5770-WDS
     A                                      CF03
     A                                      CF04
     A                                      CF06
     A                                      CF12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
7.xx A                                  2  2'Vedlikehold av kundepro-
     A                                      sjekt'
     A                                      COLOR(WHT)
     A                                  2 38'FL105C2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. . . . .'
     A            C2KUND    R        O  4 17REFFLD(FLKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV        30A  O  4 24COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prosjektnummer'
     A            C2KPRO    R        O  5 17REFFLD(FLKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Fra Dato . . .'
     A            C2FDAT         6Y 0B  6 17COLOR(YLW)
     A  31                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 31)
     A  33                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 33)
     A  49                                  ERRMSGID(FM00058 *LIBL/QMFOFAK 49)
     A  34                                  ERRMSGID(FML0110 *LIBL/QMFOFAK 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 27'til dato'
     A            C2TDAT         6Y 0B  6 37COLOR(YLW)
     A  32                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Selger . . . +'
     A            C2SELG    R        B  7 17REFFLD(FLSELG)
     A  35                                  ERRMSGID(FRA0910 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FRA0920 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2SNAV        20A  O  7 22COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Navn . . . . .'
     A            C2NAVN    R        B  8 17REFFLD(FLNAVN)
     A  37                                  ERRMSGID(FML0100 *LIBL/QMFOFAK 37)
     A  50                                  ERRMSGID(FML0400 *LIBL/QMFOFAK 50)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8 49'Ekst. prosjektnr'
     A            C2EPRO    R        B  8 66REFFLD(FLEPRO)
     A  51                                  ERRMSGID(FML0450 *LIBL/QMFOFAK 51)
     A  52                                  ERRMSGID(FML0460 *LIBL/QMFOFAK 52)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Adresse 1. . .'
     A            C2ADR1    R        B  9 17REFFLD(FLADR1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  60                                  ERRMSGID(FML0490 *LIBL/QMFOFAK 60)
     A                                  9 49'ByggDok  . . . .'
     A            C2BDOK    R        B  9 66REFFLD(FLBDOK)
     A  53                                  ERRMSGID(FML0470 *LIBL/QMFOFAK 53)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 68'1=Ja,0=Nei'
     A                                 10 49'Cobuilder. . . .'
     A            C2COBU    R        B 10 66REFFLD(FLCOBU)
     A  54                                  ERRMSGID(FML0480 *LIBL/QMFOFAK 54)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 68'1=Ja,0=Nei'
     A                                 10  2'Adresse 2. . .'
     A            C2ADR2    R        B 10 17REFFLD(FLADR2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 11  2'Postnummer . +'
     A            C2PONR    R        B 11 17REFFLD(FLPONR)
     A  38                                  ERRMSGID(FAP0020 *LIBL/QMFOFAK 38)
     A  61                                  ERRMSGID(FML0495 *LIBL/QMFOFAK 61)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2STED        25A  O 11 22COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Kontaktperson+'
     A            C2KPRS    R        B 12 17REFFLD(FLKPRS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12 49'Tlf. arbeid. . .'
     A            C2TLFA    R        B 12 66REFFLD(FLTLFA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Mobiltelefon .'
     A            C2TLFM    R        B 13 17REFFLD(FLTLFM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 49'Tlf. privat. . .'
     A            C2TLFP    R        B 13 66REFFLD(FLTLFP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'E-mail adresse'
     A            C2MAIL    R        B 14 17REFFLD(FLMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 15  2'Prosj/U.Prosj+'
     A            C2PROJ    R        B 15 17REFFLD(FLPROJ)
     A  45                                  ERRMSGID(FRP0020 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2UPRO    R        B 15 24REFFLD(FLPROJ)
     A  45                                  ERRMSGID(FRP0020 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2PTXT        30A  O 15 31COLOR(YLW)
     A                                      OVRDTA
     A                                 15 65'F.Kund +'
     A            C2KUNF    R        B 15 75REFFLD(FLOKUN)
     A  41                                  ERRMSGID(FML0160 *LIBL/QMFOFAK 41)
     A  42                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FKTX        19A  O 16 62COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Bonuskunde . +'
     A            C2BOKU    R        B 16 17REFFLD(FLBOKU)
     A  46                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2BOKN        30A  O 16 24COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Privatkjøp . .'
     A            C2PRIV    R        B 17 17REFFLD(FLPRIV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 20'1=Ja,0=Nei'
     A                                      COLOR(WHT)
7.05 A**                               17 49'Fakturatype . . '
7.05 A**          C2FATY    R        B 17 66REFFLD(FLFATY)
7.05 A**                                    COLOR(YLW)
7.05 A**30                                  DSPATR(PR)
7.05 A**47                                  ERRMSGID(FMA0280 *LIBL/QMFOFAK 47)
7.05 A**                                    OVRDTA
7.05 A**                               18 49'Prisdato. . . . '
7.05 A**          C2OPDA         6Y 0B 18 66COLOR(YLW)
7.05 A**44                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 44)
7.05 A**                                    CHECK(RB)
7.05 A**                                    EDTWRD('  .  .  ')
7.05 A**30                                  DSPATR(PR)
7.05 A**                                    OVRDTA
     A                                 18  2'Overstyringer for prisberegning:'
     A                                 19  2'Rabattkat.   +'
     A            C2ORKA    R        B 19 17REFFLD(FLORKA)
     A  39                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(FML0150 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ORTX        30A  O 19 24COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Kunde. . . . +'
     A            C2OKUN    R        B 20 17REFFLD(FLOKUN)
     A  41                                  ERRMSGID(FML0160 *LIBL/QMFOFAK 41)
     A  42                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2OKNA        30A  O 20 24COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Prosjektnr.  +'
     A            C2OKPR    R        B 21 17REFFLD(FLOKPR)
     A  43                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A            C2OPNA    R        O 21 24REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'Kjørerute  . +'
     A            C2KJOR    R        B 22 17REFFLD(FLKJOR)
     A  48                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 48)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 56'Rekkefølge . .'
     A            C2KJNR    R        B 22 71REFFLD(FLKJNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring  F6=Fl-
     A                                      ere koder'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C2EDAT         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C2EUSR    R        O 23 71REFFLD(FLEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KJNA    R        O 22 24REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.30 A          R F2WIN
     A*%%TS  SD  20080406  105115  KOB560      REL-V5R4M0  5722-WDS
8.01 A                                      WINDOW(*DFT 18 71)
     A                                      CF03
     A                                      CF04
     A                                      CF12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2 19'Koder Nexstep handel'
     A                                      COLOR(WHT)
7.05 A                                  6  2'Betalingsbetingelse +'
7.05 A            F2BETB    R        B  6 24REFFLD(FLBETB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(RM01030 *LIBL/QMFOKON 33)
     A                                      OVRDTA
7.05 A            F2CTXT        20A  O  6 27
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.05 A                                  7  2'Avgiftsfri . . . . .'
7.05 A            F2MKOD    R        B  7 24REFFLD(FLMKOD)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.05 A                                  4  2'Pris/Pakkseddel. . . '
7.05 A            F2PRPA    R        B  4 24REFFLD(FLPRPA)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.05 A                                  5 38'Pris på faktura. . . '
7.05 A            F2NETO    R        B  5 60REFFLD(FLNETO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A N30                                  COLOR(YLW)
7.05 A                                  3  2'Fakturagebyr . . . . '
7.05 A            F2FAGB    R        B  3 24REFFLD(FLFAGB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.05 A                                  3 38'Ekspedisjonsgebyr .  '
7.05 A            F2EKGB    R        B  3 60REFFLD(FLEKGB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.05 A                                  5  2'Fakturasekvens . . . '
7.05 A            F2SEKV    R        B  5 24REFFLD(FLSEKV)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.05 A                                  4 38'Samlefaktura . . . . '
7.05 A            F2SAMF    R        B  4 60REFFLD(FLSAMF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  4 62'0/1/2/3/4'
     A*                                     VALUES(0 1 2 3 4)
7.05 A*                                 4 62'(Samf ,)'
7.05 A                                  8  2'Overstyrt kunde. . . '
7.05 A            F2FKUN    R        B  8 24REFFLD(FLFKUN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.05 A                                  8 27'1=Ja,0=Nei'
7.xx A**                               11 27'1=Ja,0=Nei, hvis nei blir'
     A                                      COLOR(WHT)
7.xx A**                               12 27'endringer på kunde kopiert'
7.05 A                                  9 32'Hvis nei blir endringer på kunde'
     A                                      COLOR(WHT)
7.05 A                                 10 32'kopiert til kundeprosjekt'
7.xx A**                               13 27'til kundeprosjekt'
     A                                      COLOR(WHT)
7.05 A                                 11  2'Fakturatype . . . . .'
7.05 A            C2FATY    R        B 11 24REFFLD(FLFATY)
7.05 A                                      COLOR(YLW)
7.05 A  30                                  DSPATR(PR)
7.05 A  47                                  ERRMSGID(FMA0280 *LIBL/QMFOFAK 47)
7.05 A                                      OVRDTA
7.05 A                                 11 38'Prisdato. . . . '
7.05 A            C2OPDA         6Y 0B 11 55COLOR(YLW)
7.05 A  44                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 44)
7.05 A                                      CHECK(RB)
7.05 A                                      EDTWRD('  .  .  ')
7.05 A  30                                  DSPATR(PR)
7.05 A                                      OVRDTA
7.02 A                                 12  2'Konto utgiftsbilag .+'
7.02 A            F2KTOU         5Y 0B 12 24
7.02 A  30                                  DSPATR(PR)
7.02 A  55                                  ERRMSGID(RM01031 *LIBL/QMFOKON 55)
7.02 A                                      CHECK(RB)
7.02 A                                      OVRDTA
7.02 A                                      COLOR(YLW)
7.02 A            F2KTXT        30A  O 12 30OVRDTA
7.02 A                                 13  2'Postering svinn Deb +'
7.02 A            F2KTSD         5Y 0B 13 24COLOR(YLW)
7.02 A  30                                  DSPATR(PR)
7.02 A  56                                  ERRMSGID(RM01032 *LIBL/QMFOKON 56)
7.02 A  58                                  ERRMSGID(RM01034 *LIBL/QMFOKON 58)
7.02 A                                      CHECK(RB)
7.02 A                                      OVRDTA
7.02 A            F2TXTD        30A  O 13 30OVRDTA
7.02 A                                 14 18'Kre +'
7.02 A            F2KTSC         5Y 0B 14 24COLOR(YLW)
7.02 A  30                                  DSPATR(PR)
7.02 A  57                                  ERRMSGID(RM01033 *LIBL/QMFOKON 57)
7.02 A  59                                  ERRMSGID(RM01035 *LIBL/QMFOKON 59)
7.02 A                                      CHECK(RB)
7.02 A                                      OVRDTA
7.02 A            F2TXTC        30A  O 14 30OVRDTA
7.xx A                                 15  2'Opprettet av . . . . '
     A            F2OUSR    R        O 15 24REFFLD(FLOUSR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 15 38'Opprettet dato . . '
     A            F2ODAT         6Y 0O 15 58EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.05 A                                  3 26'1/0'
7.05 A                                  4 26'1/0'
7.05 A                                  7 26'1/0'
7.05 A                                  3 62'1/0'
7.05 A**                                4 62'1/0'
7.05 A                                  5 62'1/0'
      *
8.01 A                                 16  2'PM profil +'
8.01 A            F2PMTX        50   B 16 14DSPATR(UL)
8.01 A  30                                  DSPATR(PR)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A            F2NOIT         7  0O 16 65
8.01 A                                      EDTCDE(Z)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      OVRDTA
      *
8.01 A                                 17  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
      *
