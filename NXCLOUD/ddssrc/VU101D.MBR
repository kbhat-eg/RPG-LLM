     A*%%TS  SD  20020530  130450  AMD         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VU100D
     A* Beskrivelse . . : Struktur-vare, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       290502 AMD  Nytt program
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020530  100706  AMD         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A**          B1VARE    R        O  9  5REFFLD(VUVARE)
7.xx A            B1VARS    R        O  9  5REFFLD(VUVARE)
7.xx A            B1BESK    R        O  9 22REFFLD(VUBESK)
7.xx A            B1ANTA    R        O  9 58REFFLD(VUANTA)
7.xx A            B1ENHE    R        O  9 72REFFLD(VUENHE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020530  100706  AMD         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-varer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Hovedvare:'
     A            B2VARE    R        O  4 13REFFLD(VUVARE)
     A            B2BESX    R        O  4 30REFFLD(VUBESK)
7.xx A                                  5  5'Strukturvare'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 22'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Strukturvare   '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 22'Beskrivelse          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 72'Enhet'
7.xx A                                      COLOR(WHT)
     A*           B2VARE    R        B  7  5REFFLD(VUVARE)
     A*                                     COLOR(YLW)
     A* 22                                  DSPATR(PC)
     A*                                     CHECK(RB)
     A*                                     OVRDTA
7.xx A            B2VARS    R        B  6  5REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BESK    R        B  6 22REFFLD(VUBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020530  130450  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Hovedvare. . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Strukturvare . . . . . . . +'
     A            C1VARS    R        B  6 31REFFLD(VUVARE)
     A  33                                  ERRMSGID(VMU0130 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSG('Strukturvare kan ikke være -
     A                                      blank. Velg på nytt')
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020529  101558  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varenummer finnes fra før  '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020530  123022  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Hovedvare. . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Strukturvare . . . . . . . .'
     A            C1VARS    R        O  6 31REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varetekst. . . . . . . . . .'
     A            C2BESK    R        O  7 31REFFLD(VUBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Antall . . . . . . . . . . .'
     A            C2ANTA    R        B  9 31REFFLD(VUANTA)
     A  31                                  ERRMSGID(VMU0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMU0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10  2'Enhet. . . . . . . . . . . +'
     A            C2ENHE    R        B 10 31REFFLD(VUENHE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ENTX        25   O 10 35
     A  34                                  ERRMSG('Enheten er ikke gyldig for -
     A                                      varen. Velg på nytt')
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VUEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020529  135139  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette struktur-vare '
     A                                      DSPATR(RI)
     A                                  4  2'Hovedvare. . . . .'
     A            D1VARE    R        O  4 21REFFLD(VUVARE)
     A                                  5  2'Strukturvare . . .'
     A            D1VARS    R        O  5 21REFFLD(VUVARE)
     A                                  6  2'Varetekst. . . . .'
     A            VUBESK    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020529  135139  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Hovedvare. . . . .'
     A            K1VARE    R        B  4 21REFFLD(VUVARE)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMU0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Strukturvare . . .'
     A            K1VARS    R        B  5 21REFFLD(VUVARE)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMU0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Hovedvare. . . . .'
     A            VUVARE    R        O  9 21OVRDTA
     A                                 10  2'Strukturvare . . .'
     A            VUVARS    R        O 10 21OVRDTA
     A                                 11  2'Varetekst. . . . .'
     A            VUBESK    R        O 11 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
