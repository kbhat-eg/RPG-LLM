     A*%%TS  SD  20140530  130610  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LI500D
     A* Beskrivelse . . : Viser bestilling på pakkseddel
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       270819 bsa  Nytt program
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  130610  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1SUFF    R        O  8 13REFFLD(LOSUFF)
     A            B1OTYP    R        O  8 16REFFLD(LOOTYP)
     A            B1DATO         6Y 0O  8 19EDTCDE(Y)
     A            B1NAVN    R   -6   O  8 28REFFLD(LONAVN)
     A            B1TOTI    R        O  8 62REFFLD(LOTOTI)
     A            B1ORNR    R        O  8 53REFFLD(LOORNR)
     A            B1NUMM    R        O  8  4REFFLD(LONUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050909  132207  GRO540      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Vise bestillinger på pakkseddel'
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør:'
     A            B2LDOR         6Y 0O  4 14OVRDTA
     A            B2LNAV        20A  O  4 22OVRDTA
     A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V Bestnr         Best.dat Vareadre-
     A                                      sse              Ordre    Best.tot'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050912  152124  GRO540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 20 75)
     A*                                19  2'F1=Filter'
     A*                                     COLOR(BLU)
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 19 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
