     A*%%TS  SD  20020820  092025  HKO         REL-V5R1M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       020816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20020820  092025  HKO         REL-V5R1M0  5722-WDS
     A*                                     WINDOW(*DFT 18 71)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Oppdatering/overføring av vareinf-
     A                                      ormasjon til EVR - matchede varer '
     A                                      DSPATR(RI)
     A                                  4  2'Overføring. . . . . . . . . . .'
     A            B1OPPD         1Y 0B  4 34COLOR(YLW)
     A                                      VALUES(1 2)
     A                                      EDTCDE(4)
     A                                  4 45'1=Kontrolliste'
     A                                  5 45'2=Journalliste med'
     A                                  6 47'oppdatering'
7.xx A                                  8  2'Leverandør. . . . . . . . . . +'
     A            B1LDO2    R        B  8 34REFFLD(JOLDO2)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JML0020 *LIBL/QMFVADM 32)
     A  39                                  ERRMSGID(JML0200 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1NAV2    R  -10   O  8 48REFFLD(JLNAVN)
     A                                      COLOR(BLU)
7.xx A                                  9  2'Varegruppe. . . . . . . . . . +'
     A            B1OGRP    R        B  9 34REFFLD(JVOGRP)
     A  33                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
7.xx A                                  9 37'+'
7.xx A            B1HGRP    R        B  9 39REFFLD(JVHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
7.xx A                                  9 42'+'
7.xx A            B1UGRP    R        B  9 44REFFLD(JVUGRP)
     A  35                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 35)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            B1UTXT        25A  O  9 48COLOR(BLU)
     A                                 11  2'Overstyringer:'
     A                                      DSPATR(UL)
7.xx A                                 12  2'Prisgivende leverandør. . . . +'
     A            B1LDO3    R        B 12 34REFFLD(JOLDO2)
     A  38                                  ERRMSGID(JML0020 *LIBL/QMFVADM 38)
     A  40                                  ERRMSGID(JML0200 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1NAV3    R  -10   O 12 45REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                 13  2'Beholde egen varetype/priskode.'
     A            B1BEHP         1Y 0B 13 34COLOR(YLW)
     A  41                                  ERRMSGID(JM00010 *LIBL/QMFVADM 41)
     A                                      EDTCDE(4)
     A                                 13 45'1=ja,0=nei'
     A                                 14  2'Beholde egen varetekst. . . . .'
     A            B1BEHT         1Y 0B 14 34COLOR(YLW)
     A  42                                  ERRMSGID(JM00010 *LIBL/QMFVADM 42)
     A                                      EDTCDE(4)
     A                                 14 45'1=ja,0=nei'
     A                                 15  2'Beholde egen varegruppe . . . .'
     A            B1BEHG         1Y 0B 15 34COLOR(YLW)
     A  43                                  ERRMSGID(JM00010 *LIBL/QMFVADM 43)
     A                                      EDTCDE(4)
     A                                 15 45'1=ja,0=nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
