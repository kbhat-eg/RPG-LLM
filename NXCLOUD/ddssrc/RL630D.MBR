     A*%%TS  SD  20040321  232259  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL630D
     A* Beskrivelse . . : Leverandører, utskrift av saldoliste - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       031002 HFL  Nytt program
7.xx A*       090516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20040321  232259  KOB         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av saldoliste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6 11'Dato .:'
     A            A2PKDA    R        B  6 19REFFLD(RLPKDA)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A  37                                  ERRMSGID(RM02130 *LIBL/QMFOKON 37)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6 27'Regnskapsår .:'
     A            A2PRÅR    R        B  6 42REFFLD(RLPRÅR)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6 48'Periode .:'
     A            A2PPER    R        B  6 59REFFLD(RLPPER)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  7 11'Utvalg:'
     A                                      COLOR(WHT)
7.xx A                                  7 35'Fra -   Til'
7.xx A                                  8 13'Avdeling. . . . . .+'
     A            A2PAVF    R        B  8 34REFFLD(RLPAVF)
     A  51                                  DSPATR(PC)
     A  33                                  ERRMSGID(RMH0130 *LIBL/QMFOKON 33)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 39'-'
7.xx A                                  8 41'+'
7.xx A            A2PAVT    R        B  8 43REFFLD(RLPAVT)
     A  52                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                  9 13'Leverandørkategori.+'
     A            A2PKAF    R        B  9 34REFFLD(RKPKAF)
     A  53                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMK0400 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 39'-'
7.xx A                                  9 41'+'
7.xx A            A2PKAT    R        B  9 43REFFLD(RLPKAT)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 11 11'Skriv :'
     A                                      COLOR(WHT)
     A                                 12 13'Kontaktperson . . .:'
     A            A2PKPE    R        B 12 34REFFLD(RLPKPE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 41'(J=Ja)'
     A                                 13 13'Telefonnummer . . .:'
     A            A2PTLF    R        B 13 34REFFLD(RLPTLF)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 41'(J=Ja)'
     A                                 14 13'Kun konti m/saldo .:'
     A            A2PSAL    R        B 14 34REFFLD(RLPSAL)
     A N98                                  DFTVAL('J')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 41'(J=Ja)'
     A                                 15 13'Alle med kjøp i år.:'
     A            A2PKJO    R        B 15 34REFFLD(RLPSAL)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 41'(J=Ja)'
     A                                 16 11'Sortering:'
     A                                      COLOR(WHT)
     A                                 17 13'Kategori. . . . . .:'
     A            A2PKTG    R        B 17 34REFFLD(RLPKTG)
     A  36                                  DSPATR(PC)
     A  36                                  ERRMSGID(RMK0420 *LIBL/QMFOKON 36)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 17 41'(J=Ja, Blank)'
     A                                 18 13'Avdeling. . . . . .:'
     A            A2PAVD    R        B 18 34REFFLD(RLPAVD)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 18 41'(J=Ja, Blank)'
     A                                 19 13'Alfabetisk. . . . .:'
     A            A2PALF    R        B 19 34REFFLD(RLPALF)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 19 41'(J=Ja, Blank)'
     A                                 20 13'Synkende på saldo .:'
     A            A2PSSA    R        B 20 34REFFLD(RLPKPE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 20 41'(J=Ja, Blank)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
