     A*%%TS  SD  20201021  131837  ASP_C36     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RC100D
     A* Beskrivelse . . : Inkasso, vedlikehold faste data
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130302 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020922  233735  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A            B10FIR    R        O  9  5REFFLD(RC0FIR)
     A            B10TXT    R        O  9 10REFFLD(RC0TXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  215439  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av statuskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre     3=Kopiere    4=Slette -
     A                                         5=Vise'
     A                                      COLOR(BLU)
     A                                  7  2'V Firma Beskrivelse'
     A                                      COLOR(WHT)
     A            B2VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B20FIR         3D 0B  8  5
     A  31                                  ERRMSGID(AMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(AMA0020 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(AMA0030 *LIBL/QMFOKON 33)
     A                                      OVRDTA
     A                                      TEXT('Firma')
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20020913  145040  KOB         REL-V5R1M0  5722-WDS
     A                                 23  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 23'F6=Opprette'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020923  214341  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CLRL(*ALL)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av statuskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firmanummer. . . . . . . . .'
     A            C10FIR    R        B  5 31REFFLD(RC0FIR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Annuller'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Firmanummer finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20190926  122711  ASP_N50     REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av statuskoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firmanummer. . . . . . . . .'
     A            C10FIR    R        O  5 31REFFLD(RC0FIR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2NYEN    R        O  5 69REFFLD(RWNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C20TXT    R        B  7 31REFFLD(RC0TXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'Kundenummer hos byrå . . . .'
     A            C20CNK    R        B  9 31REFFLD(RC0CNK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Rentesats. . . . . . . . . .'
     A            C20RNT    R        B 10 31REFFLD(RC0RNT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                 11  2'Inkassovarsel nr.. . . . . .'
     A            C20INR    R        B 11 31REFFLD(RC0INR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 12  2'Bilagskode innbetaling . . .'
     A            C20BIK    R        B 12 31REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 13  2'Mellomregning - Konto. . . .'
     A            C20KTO    R        B 13 31REFFLD(RC0KTO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 14  2'Mellomregning - Avdeling . .'
     A            C20AVD    R        B 14 31REFFLD(RC0AVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 15  2'Mellomregning - Spes . . . .'
     A            C20SPS    R        B 15 31REFFLD(RC0SPS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 16  2'Poster renter. . . . . . . .'
     A            C20PRT    R        B 16 31REFFLD(RC0PRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RC0USR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17  2'Inkasso Byrå . . . . . . . .'
     A            C2INKF    R        B 17 31REFFLD(RC0CNK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      TEXT('InkassoByrå')
     A                                      CHECK(LC)
     A                                 17 48'Lindorf, Svea, Collectio'
     A                                 18  2'Utelate Bilagskode . . . . .'
     A            C2UBK1    R        B 18 31REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A            C2UBK2    R        B 18 34REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 02')
     A            C2UBK3    R        B 18 37REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 3')
     A            C2UBK4    R        B 18 40REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 4')
     A            C2UBK5    R        B 18 43REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 5')
     A                                 18 48'(Blir ikke sendt til inkasso)'
     A                                 19  2'Bilagskode kreditnotaer. . .'
     A            C2BKK1    R        B 19 31REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A            C2BKK2    R        B 19 34REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A            C2BKK3    R        B 19 37REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A            C2BKK4    R        B 19 40REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A            C2BKK5    R        B 19 43REFFLD(RC0BIK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                      TEXT('Utelate Bilagskode 1')
     A          R D1WIN
     A*%%TS  SD  20020923  214720  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A                                  2  2' Slette post '
     A                                      DSPATR(RI)
     A                                  4  2'Firmanummer . .'
     A            D10FIR    R        O  4 18REFFLD(RC0FIR)
     A                                      DSPATR(UL)
     A                                  6  2'Beskrivelse . .'
     A            RC0TXT    R        O  6 18REFFLD(RC0TXT)
     A                                      DSPATR(UL)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020913  145040  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  4  2'Firmanummer . .'
     A            K10FIR    R        O  4 18REFFLD(RC0FIR)
     A                                      DSPATR(UL)
     A                                  5  2'Beskrivelse . .'
     A            K10TXT    R        O  5 18REFFLD(RC0TXT)
     A                                      DSPATR(UL)
     A                                  9  2'Firmanummer . .'
     A            K20FIR    R        B  9 18REFFLD(RC0FIR)
     A  31                                  ERRMSGID(AMA0030 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19940114  130219  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 12 48)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
