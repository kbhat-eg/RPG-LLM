     A*%%TS  SD  20071011  234125  KOB550      REL-V5R4M0  5722-WDS
     A*
7.xx A*     BSA  180216  Endringer relatert til ny GUI
7.01 A*     BSA  180517  Lagt inn oppslag mot arkiv
7.03 A*K000 BSA  240820  Trøbbel med listing av info
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20071011  234125  KOB550      REL-V5R4M0  5722-WDS
     A            B1LINE    R        H      REFFLD(SFLINE)
     A            B1WSID    R        H      REFFLD(SFWSID)
     A            B1BONG    R        H      REFFLD(SFBONG)
7.03 A**          B1KPRO    R        H      REFFLD(SFKPRO)
7.xx A            B1VALG         1Y 0B 10  2COLOR(YLW)
7.01 A                                      VALUES(0 5 6 7 9)
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A                                      EDTCDE(Z)
7.xx A            B1VARE    R   -7   O 10  4REFFLD(SFVARE)
7.xx A            B1TEK1        12A  O 10 13
7.03 A            B1KPRO    R        O 10 26REFFLD(SFKPRO)
7.03 A            B1PNAV         8A  O 10 33
7.xx A            B1BIDA         6Y 0O 10 42EDTCDE(Z)
7.xx A            B1BINR    R        O 10 49REFFLD(SFBINR)
7.xx A            B1ORNR    R        O 10 58REFFLD(SFORNR)
7.xx A            B1OTYP    R        O 10 67REFFLD(SFOTYP)
7.xx A            B1ANTA    R   -2   O 10 70REFFLD(SFANTA)
     A                                      EDTCDE(M)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20071011  233354  KOB550      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
6.32 A  40                                  CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på salg til kunde'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SF501B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde .'
     A                                      COLOR(BLU)
     A            B2VKUN    R        O  4 10REFFLD(SFVKUN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2KNAV        30A  O  4 17COLOR(BLU)
     A                                      OVRDTA
     A                                  4 65'År . . .'
     A                                      COLOR(BLU)
     A            B2PAAR    R        O  4 74REFFLD(SFPAAR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5 65'Dato . .'
     A                                      COLOR(BLU)
     A            B2BID1         6Y 0O  5 74COLOR(BLU)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  8  2'5=Vis  6=Skriv fakturakopi  7=Vise-
7.01 A                                       faktura/bong  9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                  4 48'K.prosj.'
     A                                      COLOR(BLU)
7.xx A            B2LAD1    R        O  4 57REFFLD(SFKPRO)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  6  2'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 28'Varetekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'Kundeprosjekt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 28'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 47'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 64'Ordre'
     A                                      COLOR(WHT)
7.xx A                                  6  7'+'
7.xx A            B2VARE         8Y 0B  6 18COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2TEK1        12A  B  6 38COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 16'+'
7.xx A            B2KPRO    R        B  7 18REFFLD(SFKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BIDA         6Y 0B  7 38COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                      EDTWRD('  .  .  ')
7.xx A*                                     EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BINR    R        B  7 54REFFLD(SFBINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ORNR    R        B  7 71REFFLD(SFORNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 13'Varetekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 26'Nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 33'Kprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 42'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 49'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 58'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 67'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 70'Antall'
7.xx A                                      COLOR(WHT)
7.xx A*                                 9  2'V Vare     Varetekst    Kundeprosj-
     A*                                     ekt    Dato  Faktnr   Ordre    OT  -
     A*                                      Antall'
     A*                                     COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19980615  155607  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
6.32 A  40                             23 30'F9=Eksport til excel'
6.32 A                                      COLOR(BLU)
