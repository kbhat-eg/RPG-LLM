     A*%%TS  SD  19990525  013531  KAI         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : SF520D
     A* Beskrivelse . . : Vedlikehold av salgsstatistikk
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19990522  164312  KAI         REL-V4R2M0  5769-PW1
     A            B1LINE    R        H      REFFLD(SFLINE)
     A            B1WSID    R        H      REFFLD(SFWSID)
     A            B1VALG    R        B  9  2REFFLD(SÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('2' ' ')
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A            B1PAAR    R        H      REFFLD(SFPAAR)
     A            B1VARE    R   -7   O  9  4REFFLD(SFVARE)
6.nu A            B1TEK1    R   -4   O  9 13REFFLD(SFTEK1)
6.nu A            B1BIDA         6Y 0O  9 45EDTCDE(Z)
6.nu A            B1BINR    R        O  9 52REFFLD(SFBINR)
6.nu A            B1ORNR    R        O  9 61REFFLD(SFORNR)
     A            B1SUMK    R   -2   O  9 70REFFLD(SFSUMK)
     A                                      EDTCDE(M)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  19990525  013531  KAI         REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'AS Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av salgsstatistikk'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  4 65'År. :'
     A                                      COLOR(WHT)
     A            B2PAAR    R        B  4 71REFFLD(SFPAAR)
     A                                      COLOR(YLW)
7.xx A                                  5  4'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 52'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  3'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Vare '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'Varetekst   '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 52'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'Kostpris'
7.xx A**                                7  2'V Vare     Varetekst              -
7.xx A**                                             Dato   Faktnr   Ordre    K-
7.xx A**                                    ostpris'
     A                                      COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2VARE         8Y 0B  6  4COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BINR    R        B  6 52REFFLD(SFBINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  19980615  155607  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
