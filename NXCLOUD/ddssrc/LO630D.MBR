     A*%%TS  SD  20090204  102021  NHO560      REL-V5R4M0  5722-WDS
5.61 A* --------------------------------------------------------------
5.61 A* Ref.   Dato  Sign Endringsbeskrivelse
5.61 A* R5.61 040209 NHX  Lister avvik på totalliste dersom < > enn
5.61 A*                   inntastet beløp
5.61 A* --------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R B1BLD
     A*%%TS  SD  20000915  143032  ASK         REL-V4R4M0  5769-PW1
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av kontroll-liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 20'Seleksjonskriterier :'
     A                                  8  5'Ordre-type  . . . . . . .'
     A            B1OTYP    R        B  8 31REFFLD(LOOTYP)
     A  31                                  ERRMSGID(LSO0010 *LIBL/QMFLAGR 31)
6.10 A  32                                  ERRMSGID(LMA0270 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                 15 42'Fra'
     A                                 15 57'Til'
     A                                 16  5'Bestillingsnummer . . . .'
     A            B1FBES    R        B 16 39REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
     A            B1TBES    R        B 16 55REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
     A                                 17  5'Kundenummer . . . . . . .'
     A            B1FLEV    R        B 17 39REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1TLEV    R        B 17 55REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 18  5'Bestillingsdato . . . . .'
     A            B1FDAT         6Y 0B 18 39COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LSO0040 *LIBL/QMFLAGR 36)
     A                                      EDTWRD('      ')
     A            B1TDAT         6Y 0B 18 55COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LSO0040 *LIBL/QMFLAGR 36)
     A                                      EDTWRD('      ')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20090204  102021  NHO560      REL-V5R4M0  5722-WDS
     A                                      CF04
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av kontroll-liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  5'Utskriftvariant  . . . . . . . .'
     A            C1VAL1         1Y 0B  5 39VALUES(1 2 3 4 5)
     A                                      CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  5 49'1 = Kontroll-liste med totaler'
     A                                  6 49'5 = Kontroll oversikt'
     A                                  8  5'Sorteringsrekkefølge . . . . . .'
     A            C1VAL2         1Y 0B  8 39CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2)
     A                                  8 49'1=Registreringsrekkefølge'
     A                                  9 49'2=Vareadresse'
     A                                 11  5'Bestilling som skal skrives ut .'
     A            C1VAL3         1Y 0B 11 39VALUES(0 1 2)
     A                                      CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 11 49'0=Alle, 1=De som er plukket'
     A                                 13  5'Bare bestilling for denne bruker'
     A            C1VAL5         1Y 0B 13 39VALUES(0 1)
     A                                      CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 13 49'1=Ja, 0=Nei'
     A                                 15  5'Skriv bestilling der'
     A                                 16  5'Innkjøpspris og rabatt ikke std.'
     A            C1VAL7         1Y 0B 16 39VALUES(0 1)
     A                                      CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 16 49'1=Ja, 0=Nei'
5.61 A                                 18  5'Utskrift differanse > < enn kr..'
5.61 A            C1AVV1    R        B 18 39REFFLD(LAEAV1)
5.61 A                                      COLOR(YLW)
5.61 A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Jobbkø'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
