     A*%%TS  SD  20031126  085928  HKO         REL-V5R2M0  5722-WDS
     A*%%EC
6.NU A* 260514 sjeket vedr.utvidelse  nummerserieutvidelse
     A*
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021119  143057  HKO         REL-V5R1M0  5722-WDS
     A            B1VARE    R        H      REFFLD(LDVARE)
     A            B1NOBB    R        H      REFFLD(LDNOBB)
     A            B1LVAR    R        H      REFFLD(LDLVAR)
     A            B1ANTA    R   -4 -1B 10  2REFFLD(LDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A            B1NYPO        15A  O 10 11
     A            B1TEK1    R        O 10 27REFFLD(VVTEK1 *LIBL/FFRF)
     A  50                                  COLOR(RED)
     A            B1ENHE    R        O 10 63REFFLD(VPENHE *LIBL/FFRF)
     A            B1INPR    R        O 10 67REFFLD(VPINPR *LIBL/FFRF)
     A            B1VTYP    R        H      REFFLD(VVVTYP *LIBL/FFRF)
     A            B1SENT         1A  H
     A            B1SESS         1A  H
     A            B1STAT         3A  O 10 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20021120  085847  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      CA21
     A                                      CA22
     A                                      CA23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring/Registrering av varelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Lev. . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 11REFFLD(LOLDOR)
     A            B2LNAV        17A  O  4 18
     A                                  4 47'Kampanje'
     A                                      COLOR(BLU)
     A            B2KAMP    R        O  4 56REFFLD(LOKAMP)
     A                                  5  2'Best.. .'
     A                                      COLOR(BLU)
     A            LONUMM    R        O  5 11
     A                                  5 63'Tot:'
     A                                      COLOR(WHT)
     A            B2TOTI    R        O  5 68REFFLD(LOTOTI)
     A                                      EDTCDE(4)
     A                                  7 11'Varenr'
     A**                                7 11'Varenr   Nobbnr   Lev.varenr'
     A**                                      Varenr   Nobbnr   Lev.varenr'
     A                                      COLOR(WHT)
     A**          B2TEK1    R        B  8 45REFFLD(VVTEK1 *LIBL/FFRF)
     A**                                    COLOR(YLW)
     A**                                    CHECK(LC)
     A**                                    OVRDTA
     A            B2VARE         8Y 0B  8 11COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A**          B2NOBB    R        B  8 20REFFLD(VVNOBB *LIBL/FFRF)
     A**                                    COLOR(YLW)
     A**                                    CHECK(RB)
     A**                                    OVRDTA
     A**          B2LVAR    R        B  8 29REFFLD(VVLVAR *LIBL/FFRF)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A                                  9  3'Antall'
     A                                      COLOR(WHT)
     A  80                              9 11'Varenr'
     A                                      COLOR(WHT)
     A  81                              9 11'Nobbnr'
     A                                      COLOR(WHT)
     A  82                              9 11'Lev. varenr'
     A                                      COLOR(WHT)
     A                                  9 27'Varetekst'
     A                                      COLOR(WHT)
     A                                  9 63'Enh'
     A                                      COLOR(WHT)
     A N84                              9 67'Innkj.pris'
     A                                      COLOR(WHT)
     A  84                              9 68'inkl. mva'
     A                                      COLOR(WHT)
     A                                  9 78'CPT'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031126  085928  HKO         REL-V5R2M0  5722-WDS
     A                                 22  2'F2=Tekstlinje   F3=Gå tilbake   F4-
     A                                      =Spørring'
     A                                      COLOR(BLU)
     A N83                             23  2'F21=Alle varetyper'
     A                                      COLOR(BLU)
     A  83                             23  2'F21=Kun lagervarer'
     A                                      COLOR(BLU)
     A  84                             23 34'F22=Eksl.mva.'
     A                                      COLOR(BLU)
     A N84                             23 34'F22=Inkl.mva.'
     A                                      COLOR(BLU)
     A                                 23 50'F23=Bytt informasjon'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
