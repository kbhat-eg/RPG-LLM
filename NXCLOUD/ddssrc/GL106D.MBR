     A*%%TS  SD  20030120  111202  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL106D
     A* Beskrivelse . . : Overføring fra samlefil
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
6.20 A*       211212 NHO  Uividet felt antall poster
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A          R B1SFL                     SFL
     A*%%TS  SD  19980916  083430  RHO         REL-V4R2M0  5769-PW1
     A                                      TEXT('Subfile')
     A                                      KEEP
     A            B1ZRID         4A  H      TEXT('Header/Retur-ID.....')
     A            B1VALG    R        B 10  2REFFLD(AAVALG)
     A                                      COLOR(YLW)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      VALUES('1' '4' '5' ' ')
     A            B1ZFGR    R        O 10  5REFFLD(GDZFGR)
     A                                      TEXT('Filgruppe')
     A            B1ZFIR    R        O 10 13REFFLD(GDZFIR)
     A                                      TEXT('Firma')
     A            B1ZIUK    R        O 10 21REFFLD(GDZIUK)
     A                                      TEXT('Innenlands/Utenlands')
     A            B1ZTOT    R        O 10 39REFFLD(GDZTOT)
     A                                      TEXT('Totalsum')
     A                                      EDTCDE(K)
     A            B1SEKV    R        O 10 58REFFLD(GDZSKF)
     A                                      TEXT('Sekvensnummer fra')
     A                                      EDTCDE(Z)
     A                                 10 63'-'
     A            B1ZSKF    R        O 10 65REFFLD(GDZSKF)
     A                                      TEXT('Sekvensnummer til')
     A                                      EDTCDE(Z)
     A            B1ZUBF    R        O 10 74REFFLD(GDZUBF)
     A                                      TEXT('Forslagsnr')
     A            B1ZBAN    R        O 10 34REFFLD(GEFBAN)
     A                                      TEXT('Bankforbindelse')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  19980916  150246  RHO         REL-V4R2M0  5769-PW1
     A                                      TEXT('Hovedbilde')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oversikt over filer til remitterin-
     A                                      g'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'1=Velg for oppdatering'
     A                                      COLOR(BLU)
     A                                  6 27'4=Slette batch'
     A                                      COLOR(BLU)
     A                                  6 44'5=Vise batch'
     A                                      COLOR(BLU)
     A                                  6 59'Blank=Vise fra'
     A                                      COLOR(BLU)
     A                                  7  2'                                  -
     A                                                            Sekv.teller  -
     A                                            '
     A                                      COLOR(WHT)
     A                                  8  2'V  Filgr   Firma   Type        Ban-
     A                                      k         Totalsum    Fra  -  Til  -
     A                                       Forsl'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            B2VALG    R        B  9  2REFFLD(AAVALG)
     A                                      COLOR(YLW)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A  30                                  ERRMSGID(RML0390 *LIBL/QMFOKON 30)
     A                                      OVRDTA
     A                                      VALUES('4' '5' ' ')
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            B2ZFGR    R        B  9  5REFFLD(GDZFGR)
     A                                      DLTCHK
     A  31                                  ERRMSGID(RM00020 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RM00030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      TEXT('Bet.forslagslinjenummer')
     A          R B2CMD
     A*%%TS  SD  20030120  110124  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Kommando taster')
     A                                      KEEP
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R A1WIN
     A*%%TS  SD  19950809  085612  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Angrebilde v/sletting')
     A                                      CA03
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Sletting av batch '
     A                                      DSPATR(RI)
     A                                  4  2'Trykk Enter for å slette batch.'
     A                                      COLOR(WHT)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A2WIN
     A*%%TS  SD  19980916  121134  RHO         REL-V4R2M0  5769-PW1
     A                                      TEXT('Feilmelding -> batchene inneh-
     A                                      older ulike banker')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Det er valgt overføringer med ulik'
     A                                      COLOR(WHT)
     A                                  5  2'bankforbindelse.  Trykk'
     A                                      COLOR(WHT)
     A                                  5 26'ENTER,'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5 33'og'
     A                                      COLOR(WHT)
     A                                  6  2'foreta deretter'
     A                                      COLOR(WHT)
     A                                  6 18'nytt utplukk.'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2  2'FEIL I UTPLUKK'
     A                                      DSPATR(UL)
     A                                      DSPATR(RI)
     A          R B1WIN
     A*%%TS  SD  19950329  150954  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Antall slettede poster Start')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Sletting pågår...'
     A                                      COLOR(WHT)
     A                                  6  2'Antall slettede poster:'
     A                                      COLOR(WHT)
     A            A1SLPO         4  0O  6 26DSPATR(UL)
     A                                      TEXT('Antall slettede poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A          R B2WIN
     A*%%TS  SD  19950321  160409  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Sletting pågår...')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Sletting pågår...'
     A                                      COLOR(WHT)
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A          R C1WIN
     A*%%TS  SD  19950821  093635  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Trykk Enter for å starte over-
     A                                      føring...')
     A                                      CA03
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 8 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Trykk Enter for å starte overførin-
     A                                      g'
     A                                      COLOR(WHT)
     A                                  5  2'av poster til remitteringsfile.'
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  2  2' Overføring '
     A                                      DSPATR(RI)
     A          R C2WIN
     A*%%TS  SD  19950329  150954  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Antall overførte poster')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Antall poster lagt ut:'
     A                                      COLOR(WHT)
6.20 A**          A1OVPO         4Y 0O  4 25DSPATR(UL)
6.20 A            A1OVPO         6Y 0O  4 25DSPATR(UL)
     A                                      TEXT('Antall overførte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A          R C3WIN
     A*%%TS  SD  19981117  125344  RHO         REL-V4R2M0  5769-PW1
     A                                      TEXT('Antall overførte poster END')
     A                                      KEEP
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Remitteringsfile med'
     A                                      COLOR(WHT)
6.20 A**          A1OVPO         4Y 0O  4 23TEXT('Antall overførte poster')
6.20 A            A1OVPO         6Y 0O  4 23TEXT('Antall overførte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
6.20 A**                                4 28'poster,'
6.20 A                                  4 30'poster,'
     A                                      COLOR(WHT)
     A                                  5  2'er nå klar for overføring til    '
     A                                      COLOR(WHT)
     A                                  6  2'banken.                          '
     A                                      COLOR(WHT)
     A                                  8  2'Trykk Enter for å avslutte rutinen-
     A                                      .'
     A                                      COLOR(WHT)
     A          R C4WIN
     A*%%TS  SD  19950329  150954  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('DIRREM klargjøres')
     A                                      CA03
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Overførselsfil klargjøres...'
     A                                      COLOR(WHT)
     A          R C5WIN
     A*%%TS  SD  19950321  160409  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Leser samlefile..')
     A                                      CA03
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 7 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Samlefil leses...'
     A                                      COLOR(WHT)
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A          R C6WIN
     A*%%TS  SD  19981117  125344  RHO         REL-V4R2M0  5769-PW1
     A                                      TEXT('ICF-kommunikasjon starter opp-
     A                                      .')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 9 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Remitteringsfile med'
     A                                      COLOR(WHT)
6.20 A**          A1OVPO         4Y 0O  4 23TEXT('Antall overførte poster')
6.20 A            A1OVPO         6Y 0O  4 23TEXT('Antall overførte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
6.20 A**                                4 28'poster,'
6.20 A                                  4 30'poster,'
     A                                      COLOR(WHT)
     A                                  5  2'vil nå bli overført til banken.'
     A                                      COLOR(WHT)
     A                                  7  2'Oppkobling pågår...'
     A                                      COLOR(WHT)
     A          R C7WIN
     A*%%TS  SD  20000607  133836  RHO         REL-V4R4M0  5769-PW1
     A                                      TEXT('Viser Start og Slutt for Sekv-
     A                                      ensteller')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 8 29)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  4' SEKVENSTELLER: '
     A                                      DSPATR(RI)
     A                                  4  4'Start fortløpende:'
     A                                      COLOR(WHT)
     A            C7FOST         4Y 0B  4 23TEXT('Start sekvensteller')
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  4'Start daglig. . .:'
     A                                      COLOR(WHT)
     A            C7DAST         4Y 0B  5 23TEXT('Start sekvensteller')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  7  4'Trykk Enter...'
     A                                      COLOR(WHT)
     A          R ASUME
     A*%%TS  SD  19941223  114838  RH          REL-V2R3M0  5738-PW1
     A                                      KEEP
     A                                      ASSUME
     A                                 24 79' '
