     A*%%TS  SD  20060201  104719  ASK550      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LX500D
     A* Beskrivelse . . : Ordrespørring multifunksjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.23 A* 6.20  170112 BSA  Utvidet med F11=EDI mottaksrutine
7.01 A* 7.00  100221 ASK  Bare korreksjon i RPG program
     A*
8.02 A* 6.30  160517 BSA  Utvidet med valg for oppslag i arkiv.
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20060201  104719  ASK550      REL-V5R3M0  5722-WDS
     A            B1VALG         1Y 0B  9  2CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      VALUES(0 5)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
6.NU A            B1NUMM         8Y 0O  9  4EDTCDE(Z)
6.NU A            B1SUFF         2Y 0O  9 13EDTCDE(Z)
6.NU A            B1OTYP         2A  O  9 16
6.NU A            B1STAT        15A  O  9 19
6.NU A            B1REFN        12A  O  9 35
6.NU A            B1USER         8A  O  9 48
     A            B1DATO         6Y 0O  9 57EDTWRD('  .  .  ')
     A            B1TIME         6Y 0O  9 66EDTWRD('  .  .  ')
     A            B1BTYP         1A  H
     A            B1KUND         6Y 0H
     A            B1KASS        12A  H
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050616  145742  ASK530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
6.20 A                                      CF10
6.23 A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Total ordreoversikt'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordrenr:'
6.NU A            B2ONUM         8Y 0B  4 11COLOR(YLW)
8.02 A  31                                  ERRMSG('Ugyldig ordretype for -
8.02 A                                      oppslag')
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
6.NU A                                  4 20'Fakturanr:'
6.NU A            B2FAKT         8Y 0B  4 31COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
6.NU A                                  4 40'Bongnr:'
6.NU A            B2BONG        12A  B  4 48COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Best.nr:'
6.NU A            B2BEST         8Y 0B  5 11COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
6.NU A                                  5 20'Lev. onr :'
6.NU A            B2LONR        15A  B  5 31COLOR(YLW)
     A                                      OVRDTA
6.NU A                                  5 48'Lev. faknr:'
6.NU A            B2LFAK        15A  B  5 60COLOR(YLW)
     A                                      OVRDTA
6.NU A                                  8  2'  O/B.nr   Su Ot Status          R-
6.NU A                                      eferanse    Bruker      Dato    Tid-
     A                                           '
     A                                      COLOR(WHT)
8.02 A**                                7  4'5=Detaljer'
8.02 A                                  7  4'5=Detaljer  9=Arkiv'
     A                                      COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  20020924  130941  HKO         REL-V5R1M0  5722-WDS
6.20 A**6.25                           23  2'F3=Gå tilbake  F5=Forny  F10=Inng.+
6.25 A                                 23  2'F3=Gå tilbake'
6.25 A                                      COLOR(BLU)
6.25 A                                 23 17'F5=Forny'
6.25 A                                      COLOR(BLU)
6.25 A                                 23 27'F10=Inng. fakt.hist'
6.25 A                                      COLOR(BLU)
6.25 A                                 23 48'F11=EDI-mottak'
     A                                      COLOR(BLU)
