     A*%%TS  SD  20090914  140254  BOF610      REL-V5R4M0  5722-WDS
     A*
7.xx A*      211116 bsa Endringer relatert til ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20090914  140254  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
6.32 A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise vareinformasjon'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JV510C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . .'
     A            C2VARE    R        O  4 19REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80                              4 65' Slettet i NOBB '
     A  80                                  DSPATR(RI)
     A  80                                  COLOR(YLW)
     A                                  5  2'NOBB-nummer. . .'
     A            C2NOBB    R        O  5 19REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Modulnummer. . .'
     A            C2MODN    R        O  6 19REFFLD(JVMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2MTXT        35A  O  6 29COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . .'
     A            C2OGRP    R        O  7 19REFFLD(JVOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2HGRP    R        O  7 22REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UGRP    R        O  7 25REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UTXT        35A  O  7 29COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Egen varegruppe.'
     A            C2EOGR    R        O  8 19REFFLD(JVEOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2EHGR    R        O  8 22REFFLD(JVEHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2EUGR    R        O  8 25REFFLD(JVEUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2EUTX        35A  O  8 29COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varetekst 1. . .'
     A            C2TEK1    R        O  9 19REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varetekst 2. . .'
     A            C2TEK2    R        O 10 19REFFLD(JVTEK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Egen varetekst 1'
     A            C2ETE1    R        O 11 19REFFLD(JVETE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Egen varetekst 2'
     A            C2ETE2    R        O 12 19REFFLD(JVETE2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Skaffevare . . .'
     A            C2DSKO    R        O 13 19REFFLD(JVDSKO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 22'1=Ja,0=Nei'
     A                                 13 41'Egen priskode. .'
     A            C2DPKO    R        O 13 58REFFLD(JVDPKO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Bestillingsvare.'
     A            C2LEVE    R        O 14 19REFFLD(JVLEVE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14 22'1=Ja,0=Nei'
     A                                 15  2'Produsent. . . .'
     A            C2PROD    R        O 15 19REFFLD(JVPROD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PNAV        13A  O 15 26COLOR(YLW)
     A                                      OVRDTA
     A                                 15 41'Varenr. prod.. .'
     A            C2PVAR    R        O 15 58REFFLD(JVPVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Leverandør . . .'
     A            C2LDOR    R        O 16 19REFFLD(JVLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LNAV        13A  O 16 26COLOR(YLW)
     A                                      OVRDTA
     A                                 16 41'Varenr. lev. . .'
     A            C2LVAR    R        O 16 58REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Sortimentkode  .'
7.xx A            C2SORT    R   -9   O 17 19REFFLD(JVSORT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17 41'Miljømerke 1 . .'
7.xx A            C2MMK1    R        O 17 58REFFLD(JVMMK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Utstillingskode'
7.xx A            C2UTKO    R        O 18 19REFFLD(JVUTKO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18 41'Landkode 1 .. . '
7.xx A            C2MLK1    R        O 18 58REFFLD(JVMLK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19 41'Miljømerke 2. . '
7.xx A            C2MMK2    R        O 19 58REFFLD(JVMMK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20 41'Landkode 2 .. . '
7.xx A            C2MLK2    R        O 20 58REFFLD(JVMLK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'EAN(ikke nobb) .'
     A            C2EAN1    R        O 19 19REFFLD(JVEAN1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Andre strekkoder'
     A            C2STRE    R        O 20 19REFFLD(JVSTRE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Dok.indikator. .'
     A            C2DOKI    R        O 21 19REFFLD(JVDOKI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 56'Aktiv dato . . .'
     A            C2ADAT         6Y 0O  9 73COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 56'Utgår dato . . .'
     A            C2UDAT         6Y 0O 10 73COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 21 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 57'/'
     A            C2EDAT         6Y 0O 21 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 68'/'
     A            C2EUSR    R        O 21 70REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.32 A                                 22  2'F3=Gå tilbake    F13=NOBB info    -
     A                                      F14=Enheter         F15=Priser'
     A                                      COLOR(BLU)
     A                                 23  2'F16=Søketekster  F17=Betingelser  -
     A                                      F18=Kampanjepriser  F19=Produktinfo-
     A                                      rmasjon'
     A                                      COLOR(BLU)
