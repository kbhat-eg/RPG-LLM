     A*%%TS  SD  20071002  130934  TSV         REL-V5R4M0  5722-WDS
6.23 A*  6.20 291215 nho  Feilmelding vedr. lev.måte
6.25 A*  6.20 010616 bsa  Feilmelding hvis overføring pågår.
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20031124  152303  ASK         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 40)
     A                                      CA03
     A                                      CF10
     A                                      KEEP
     A                                  2  2'Sende bestilling til leverandør'
     A                                      COLOR(WHT)
     A            A1LDOR         6S 0O  4  2
6.22 A  43                                  ERRMSGID(LSO0520 *LIBL/QMFLAGR 43)
6.22 A**43                                  ERRMSG('Varer på best ikke NEB std')
6.22 A**44                                  ERRMSG('Leverandør mangler info')
6.22 A  44                                  ERRMSGID(LSO0530 *LIBL/QMFLAGR 44)
6.23 A  38                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 38)
6.25 A  45                                  ERRMSG('Overføring pågår. Vent litt-
6.25 A                                       og forsøk igjen')
     A            A1LTXT        30A  O  4  9
     A                                  6  2'Forsendelsesmåte:'
     A            A1TYPE    R        O  6 20REFFLD(LPTYPE)
     A                                  7  2'Format. . . . . :'
     A            A1FORM    R        O  7 20REFFLD(LPFORM)
     A                                  8  2'Mottaker. . . . :'
     A            A1MOTT    R        O  8 20REFFLD(LPMOTT)
     A  31                             11  2'Sist overført:'
     A  31        A1SIDA         6Y 0O 11 17EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A  31                             12  2'Overført av .:'
     A  31        A1SIUS        10A  O 12 17COLOR(WHT)
     A            A1STAT        35A  O 14  2COLOR(WHT)
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 15 17'F10=Omkjøring'
     A                                      COLOR(BLU)
     A          R A2BLD
     A*%%TS  SD  20050822  124732  ASK540      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 12 40)
     A                                      CA03
     A                                      CF10
     A                                      KEEP
     A                                  2  2'Tilleggsinformasjon for Jernia'
     A                                      COLOR(WHT)
     A                                  4  2'Ordretype:'
     A            A2OTYP         4A  B  4 13COLOR(YLW)
     A  31                                  ERRMSGID(LSO0470 *LIBL/QMFLAGR 31)
     A                                  4 21'(LAG=Lagerordre)'
     A                                  5 21'(HAS=Hasteordre)'
     A                                  6 21'(FOE=Forhåndsordre)'
     A                                  7  2'Adressekode . :'
     A            A2AKOD         2S 0B  7 18COLOR(YLW)
     A                                  8  2'Aktivitetskode:'
     A            A2AKTK         2A  B  8 18COLOR(YLW)
     A  32                                  ERRMSGID(LSO0500 *LIBL/QMFLAGR 32)
     A                                  9  2'Leveringsuke. :'
     A            A2LUKE         4S 0B  9 18COLOR(YLW)
     A  33                                  ERRMSGID(LSO0510 *LIBL/QMFLAGR 33)
     A                                 11  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A                                  9 24'(ÅÅUU)'
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
     A          R A3BLD
     A*%%TS  SD  20071002  130934  TSV         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 50)
     A                                      CA03
     A                                      CF10
     A                                      KEEP
     A                                  2  2'Tilleggsinformasjon for Norgros   -
     A                                         '
     A                                      COLOR(WHT)
     A                                  4  2'Kampanje.:'
     A            A3TEXT        35A  B  4 13COLOR(YLW)
     A                                  7  2'F3=Tilbake'
     A                                      COLOR(BLU)
