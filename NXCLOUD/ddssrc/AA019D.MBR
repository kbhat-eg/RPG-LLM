     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA019D
     A* Beskrivelse . . : Parametre - egenskaper for pdf skrivere
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160710 BSA  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1VPRT    R        O  8  5REFFLD(AFVPRT)
     A            B1PTXT        30A  O  8 17
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av skriver egenskaper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre            5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Skriver    Beskrivelse'
     A                                      COLOR(WHT)
     A            B2VPRT    R        B  7  5REFFLD(AFVPRT)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av skriver egenskaper '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skriver. . . . .'
     A            C1VPRT    R        B  5 20REFFLD(AFVPRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040913  082010  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Feil med skriver '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av skriver egenskaper '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skriver. . . . .'
     A            C2VPRT    R        O  5 20REFFLD(AFVPRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PTXT        30A  O  5 32
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Standadrskuff. .'
     A            C2VSTD    R        B  7 20REFFLD(AFVSTD)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Giro skuff . . .'
     A            C2VGIR    R        B  8 20REFFLD(AFVGIR)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040913  082010  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette skriver      '
     A                                      DSPATR(RI)
     A                                  4  2'Skriver. . . . . .'
     A            D1VPRT    R        O  4 21REFFLD(AFVPRT)
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Skriver. . . . . .'
     A            K1VPRT    R        O  4 21REFFLD(AFVPRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Skriver. . . . . .'
     A            K2VPRT    R        O 10 21REFFLD(AFVPRT)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
