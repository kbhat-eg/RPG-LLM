     A*%%TS  SD  20040219  231453  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL620D
     A* Beskrivelse . . : Leverandørregister, utskrift - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240702 KOB  Nytt program
7.xx A* 7.xx  180516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20040219  231453  KOB         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av leverandørregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  7 11'Utvalg:'
7.xx A                                  7 35'Fra    -    Til'
7.xx A                                  8 13'Leverandører. . . .+'
     A            A2PLNF    R        B  8 34REFFLD(RLPLNR)
     A  57                                  DSPATR(PC)
     A  32                                  ERRMSGID(RMH0140 *LIBL/QMFOKON 34)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Leveranørnummer fra')
     A                                      COLOR(YLW)
     A                                  8 42'-'
7.xx A                                  8 45'+'
7.xx A            A2PLNT    R        B  8 47REFFLD(RLPLNR)
     A  58                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Leverandørnummer til')
     A                                      COLOR(YLW)
7.xx A                                  9 13'Avdeling. . . . . .+'
     A            A2PAVF    R        B  9 34REFFLD(RLPAVF)
     A  51                                  DSPATR(PC)
     A  33                                  ERRMSGID(RMH0130 *LIBL/QMFOKON 33)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 42'-'
7.xx A                                  9 45'+'
7.xx A            A2PAVT    R        B  9 47REFFLD(RLPAVT)
     A  52                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                 10 13'Leverandørkategori.+'
     A            A2PKAF    R        B 10 34REFFLD(RLPKAF)
     A  53                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMH0350 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 10 42'-'
7.xx A                                 10 45'+'
7.xx A            A2PKAT    R        B 10 47REFFLD(RLPKAT)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
7.xx A                                 11 13'Distrikt. . . . . .+'
     A            A2PDIF    R        B 11 34REFFLD(RLDIST)
     A  55                                  DSPATR(PC)
     A  36                                  ERRMSGID(RMH0400 *LIBL/QMFOKON 36)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Distrikt fra. . . .')
     A                                      COLOR(YLW)
     A                                 11 42'-'
7.xx A                                 11 45'+'
7.xx A            A2PDIT    R        B 11 47REFFLD(RLDIST)
     A  56                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Distrikt til. . . .')
     A                                      COLOR(YLW)
     A                                 13 13'Eldre enn dato. . .:'
     A            A2PEDA    R        B 13 34REFFLD(RLPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Eldre enn dato.')
     A                                      COLOR(YLW)
     A                                 15 11'Type liste. . . . . .:'
     A            A2PTYP    R        B 15 34REFFLD(RLPLTY)
     A  37                                  DSPATR(PC)
     A  37                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Type liste. . . . .')
     A                                      COLOR(YLW)
     A                                 15 37'(E=Enkel, F=Full liste)'
     A                                 16 11'Tilleggsfelt:'
     A                                 17 13'Saldo . . . . . . .:'
     A            A2PSAL    R        B 17 34REFFLD(RLPSAL)
     A  38                                  DSPATR(PC)
     A  38 45                               ERRMSGID(RM00040 *LIBL/QMFOKON 45)
     A  38 46                               ERRMSGID(RMH0090 *LIBL/QMFOKON 46)
     A N98                                  DFTVAL('J')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Skriv saldo . . . .')
     A                                      COLOR(YLW)
     A                                 17 37'(J=Ja, Blank)'
     A                                 19 11'Alfabetisk. . . . . .:'
     A            A2PSRT    R        B 19 34REFFLD(RLPALF)
     A  41                                  DSPATR(PC)
     A  41                                  ERRMSGID(RMH0290 *LIBL/QMFOKON 41)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      TEXT('Sortering . . . . .')
     A                                      COLOR(YLW)
     A                                 19 37'(J=Ja, Blank)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
