     A*%%TS  SD  19990601  152621  HKO         REL-V4R2M0  5769-PW1
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VV610D
     A* Beskrivelse . . : Utskrift av vareregister m/ddekningskontroll
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010416  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990601  152621  HKO         REL-V4R2M0  5769-PW1
     A*                                     WINDOW(*DFT 15 67)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av vareregister med dekn-
     A                                      ingskontroll '
     A                                      DSPATR(RI)
7.xx A                                  5  2'Varegruppe . +'
7.xx A            B1OGRP    R        B  5 17REFFLD(VVOGRP)
     A  31                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
7.xx A                                  5 20'+'
7.xx A            B1HGRP    R        B  5 22REFFLD(VVHGRP)
     A  32                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
7.xx A                                  5 25'+'
7.xx A            B1UGRP    R        B  5 27REFFLD(VVUGRP)
     A  33                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1UTXT    R        O  5 32REFFLD(VGUTXT)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Dato . . . . .'
7.xx A            B1DATO         6Y 0B  7 17COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
