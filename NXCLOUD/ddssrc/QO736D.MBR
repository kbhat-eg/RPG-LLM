     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A*-------------------------------------------------------------------
7.xx A*     BSA  220116    Endringer relatert til ny GUI
Q.01 A*     bsa  140219    Quick wins tilpasninger - bredere skjerm
7.02 A*     bsa  020221    Legger opp mulighet for direkte utskrift
7.02 A*     bsa            av plukkliste. Styrt av switch.
     A*
     A*
     A*
     A*
     A*-------------------------------------------------------------------
     A*%%EC
Q.01 A                                      DSPSIZ(24 80 *DS3 -
Q.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R O1WIN
     A*%%TS  SD  20070328  105948  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 58)
     A                                      CF03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Oppdatering av ordre - klar til p-
     A                                      lukking '
     A                                      DSPATR(RI)
7.02 A                                  4  2'Leveringsdato .'
7.02 A            O1LDAT         6Y 0B  4 18COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  33                                  ERRMSGID(FMO0257 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FMO0250 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.02 A                                  6  2'Informasjon . .'
7.02 A            O1INF1    R        B  6 18REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.02 A            O1INF2    R        B  7 18REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.02 A  57                              9  2'Plukkliste. . .'
7.02 A  57        O1PLUK    R        B  9 18REFFLD(FÅVALG)
7.02 A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.02 A                                      VALUES(0 1)
7.02 A                                      COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A  57                              9 20'1=ja,0=nei'
      *
     A                                 13  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R A1MSG1
     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 41)
     A                                      CF03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kunden er sperret for kreditt,   '
     A                                  3  2' kan ikke plukkes !               '
     A                                  5  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A1MSG2
     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 41)
     A                                      CF03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kontantkunde kan ikke plukkes !  '
     A                                  5  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A1MSG3
     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 41)
     A                                      CF03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Ordretype kan ikke plukkes   !  '
     A                                  5  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A1MSG4
     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 41)
     A                                      CF03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Ordren er allerede sendt til pluk-
     A                                      king !'
     A                                  5  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
