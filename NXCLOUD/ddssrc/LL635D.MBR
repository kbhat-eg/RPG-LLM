     A*%%TS  SD  20020924  134411  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL635D
     A* Beskrivelse . . : Utskrift av historisk lagerbok - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20020924  134411  HKO         REL-V5R1M0  5722-WDS
     A                                      CA01
     A                                      CA03
     A                                      CA04
     A                                      KEEP
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av historisk lagerbok'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL635A1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5 35'Fra'
7.xx A                                  5 51'Til'
7.xx A                                  6  2'Varenr . . . . . . . . . . +'
     A            A1FVAR         8D 0B  6 31COLOR(YLW)
     A                                      CHECK(RZ)
     A  30                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 30)
     A                                      DSPATR(PC)
7.xx A                                  6 47'+'
7.xx A            A1TVAR         8D 0B  6 49COLOR(YLW)
     A                                      CHECK(RZ)
7.xx A                                  7  2'Lager. . . . . . . . . . . +'
     A            A1FLAG         2D 0B  7 31COLOR(YLW)
     A                                      CHECK(RZ)
     A  31                                  ERRMSGID(LMA0160 *LIBL/QMFLAGR 31)
     A  42                                  ERRMSGID(LME0050 *LIBL/QMFLAGR 35)
7.xx A                                  7 47'+'
7.xx A            A1TLAG         2D 0B  7 49COLOR(YLW)
     A                                      CHECK(RZ)
     A  43                                  ERRMSGID(LME0050 *LIBL/QMFLAGR 35)
7.xx A                                  8  2'Varegruppe . . . . . . . . +'
     A            A1FOGR    R     D  B  8 31REFFLD(FFRFR/VGOOGR *LIBL/FFRF)
     A                                      CHECK(RZ)
     A  32                                  ERRMSGID(LMA0160 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
7.xx A                                  8 34'+'
7.xx A            A1FHGR    R     D  B  8 36REFFLD(FFRFR/VGHHGR *LIBL/FFRF)
     A  33                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
7.xx A                                  8 39'+'
7.xx A            A1FUGR    R     D  B  8 41REFFLD(FFRFR/VGUUGR *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
7.xx A                                  8 47'+'
7.xx A            A1TOGR    R     D  B  8 49REFFLD(FFRFR/VGOOGR *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8 52'+'
7.xx A            A1THGR    R     D  B  8 54REFFLD(FFRFR/VGHHGR *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8 57'+'
7.xx A            A1TUGR    R     D  B  8 59REFFLD(FFRFR/VGUUGR *LIBL/FFRF)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                 10  2'Dato . . . . . . . . . . . .'
     A            A1FDAT         6Y 0B 10 31COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTWRD('  .  .  ')
     A  34                                  ERRMSGID(LMO0240 *LIBL/QMFLAGR 34)
7.xx A            A1TDAT         6Y 0B 10 49COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTWRD('  .  .  ')
     A  35                                  ERRMSGID(LMO0240 *LIBL/QMFLAGR 35)
     A                                 12  2'Oppgi systemkode . . . . . .'
     A            A1SYST         1X  B 12 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  35                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 35)
     A                                 12 35'Blank gir alle'
     A                                 13  2'Oppgi systemtype (ordretype)'
     A            A1TYPE         2X  B 13 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  36                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 36)
     A                                 13 35'Blank gir alle'
     A                                 14  2'Oppgi Inngang/Uttak. . . . .'
     A            A1INUT         1X  B 14 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  37                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 37)
     A                                 14 35'Blank gir begge'
     A                                 16  2'Sorteringsrekkefølge:'
     A                                      COLOR(WHT)
     A                                 17  2'Varenummer . . . . . . . . .'
     A            A1SRT1         1D 0B 17 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  38                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 38)
     A                                 17 35'0, 1, 2, 3'
     A                                 18  2'Lagernummer. . . . . . . . .'
     A            A1SRT2         1D 0B 18 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  39                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 39)
     A                                 18 35'0, 1, 2, 3'
     A                                 19  2'Dato . . . . . . . . . . . .'
     A            A1SRT3         1D 0B 19 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  40                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 40)
     A                                 19 35'0, 1, 2, 3'
     A                                 20  2'Ordretype. . . . . . . . . .'
     A            A1SRT4         1D 0B 20 31COLOR(YLW)
     A                                      DSPATR(CS)
     A  41                                  ERRMSGID(LM99999 *LIBL/QMFLAGR 41)
     A                                 20 35'0, 1, 2, 3'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
