     A*%%TS  SD  20070605  132557  ASP_N50     REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MM511D
     A* Beskrivelse . . : Innnkjøpsprameter detaljer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       020404 bø   Nytt program
7.xx A* 7.xx  210316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070605  132557  ASP_N50     REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   H      REFFLD(MOVARE)
     A            B1LAGE    R        H      REFFLD(MOLAGE)
     A            B1KABC    R        H      REFFLD(MOKABC)
     A            B1TEK1    R        H      REFFLD(MOTEXT)
     A            B1ENHL    R        H      REFFLD(LÅENHE)
     A            B1TEXT        36A  O  8  4
     A            B1BPUN         6Y 1O  8 42EDTCDE(M)
     A*           B1BPUNH        6Y 1O  8 48
     A*                                     EDTCDE(M)
     A*                                     COLOR(TRQ)
     A            B1BMEN         6Y 1O  8 55EDTCDE(M)
     A*           B1BMENH        6Y 1O  8 62
     A*                                     EDTCDE(M)
     A*                                     COLOR(TRQ)
     A            B1VSOR    R        H      REFFLD(MOVSOR)
     A            B1OGRP    R        H      REFFLD(MOOGRP)
     A            B1HGRP    R        H      REFFLD(MOHGRP)
     A            B1UGRP    R        H      REFFLD(MOUGRP)
     A            B1LDOR    R        H      REFFLD(MOLDOR)
     A            B1MINI         6Y 1O  8 67EDTCDE(M)
     A*           B1MINIH        6Y 1O  8 76EDTCDE(M)
     A*                                     COLOR(TRQ)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070605  132557  ASP_N50     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innkjøpsparameter simulert'
     A                                      COLOR(WHT)
7.xx A                                  2 38'MM512B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 41'Best.pkt'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 53'Best.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 66'Min.lager'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6 11'7=Pr. uke'
     A                                      COLOR(BLU)
7.xx A                                  6 25'8=Lagervedl.'
     A                                      COLOR(BLU)
7.xx A                                  6 41'9=Lagerspørr.'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 13'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'Tekst         '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'B.pkt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 55'B.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'M.lager'
7.xx A**                                7  2'V  Varenr. Lag ABC  Tekst         -
7.xx A**                                        Best.pkt.    Best.mengde  Min.l-
7.xx A**                                    ager      '
     A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2VARE    R   -7   B  5  4REFFLD(MOVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A**                                7 13'F4'
7.xx A            B2LAGE    R        B  5 16REFFLD(MOLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KABC    R        B  5 19REFFLD(MOKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2BPUN    R   -6   B  5 42REFFLD(MOBPUN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2MINI    R   -6   B  5 67REFFLD(MOMINI)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2BMEN    R   -6   B  5 55REFFLD(MOBMEN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F1=Bla-Alt  F3=Gå tilbake  F5=Fo-
     A                                      rny  F11=Skift'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050306  142304  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg ABC-kode .:'
     A            H1KABC    R        B  4 19REFFLD(MOKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 26'Blank gir alle'
     A                                  5 26'Eks. AX,A , X'
     A                                  6  2'Velg lager  . .:'
     A            H1LAGE    R        B  6 19REFFLD(MOLAGE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8 18'Over'
     A                                  9  2'Best.punkt  . .:'
     A            H1BPOV    R        B  9 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9 21'(1/0)'
     A            H1BPUN    R        B  9 29REFFLD(MOBPUN)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10  2'Best.mengde . .:'
     A            H1BMOV    R        B 10 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 21'(1/0)'
     A            H1BMEN    R        B 10 29REFFLD(MOBMEN)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
