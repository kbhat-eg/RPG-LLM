     A*%%TS  SD  20041018  133940  HKO         REL-V5R2M0  5722-WDS
     A*%%EC
6.30  *  6.30  150515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990611  194459  AMD         REL-V4R2M0  5769-PW1
     A            B1VARE    R        H      REFFLD(BDVARE)
     A            B1ANTA    R   -4 -1B  9  2REFFLD(BDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A            B1TEK1    R        O  9 11REFFLD(VVTEK1 *LIBL/FFRF)
     A            B1NYPO        15A  O  9 47
     A            B1ENHE    R        O  9 63REFFLD(VPENHE *LIBL/FFRF)
     A            B1SAPR    R        O  9 67REFFLD(VPSAPR *LIBL/FFRF)
     A            B1KPRI    R        O  9 78REFFLD(VVKPRI *LIBL/FFRF)
     A            B1VTYP    R        O  9 80REFFLD(VVVTYP *LIBL/FFRF)
     A            B1TEK2    R        O 10 11REFFLD(VVTEK2 *LIBL/FFRF)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041018  133940  HKO         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CA13
     A                                      CA14
     A                                      CA15
     A                                      CA21
     A                                      CA22
     A                                      CA23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLDROP(CA06)
     A                                      SFLCSRRRN(&WCURRN)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WCURRN         5S 0H
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            DSFNAK        20A  O  1  2COLOR(WHT)
     A                                  1 31'***  AS Butikk  ***'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 10USER
     A                                      COLOR(WHT)
     A                                  2 24'Spørring/Registrering av varelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Varegrp.'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  4 11REFFLD(VVOGRP *LIBL/FFRF)
     A  31                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2HGRP    R        B  4 14REFFLD(VVHGRP *LIBL/FFRF)
     A  32                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UGRP    R        B  4 17REFFLD(VVUGRP *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4 47'Kunde. .'
     A                                      COLOR(BLU)
     A            B2KUND    R        O  4 56REFFLD(BOKUND)
     A            B2KNAV        17A  O  4 63
     A                                  5  2'Frisøk .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  5 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 47'K-prosj.'
     A                                      COLOR(BLU)
     A            B2KPRO    R        O  5 56REFFLD(FOKPRO *LIBL/FFRF)
     A            B2PNAV        17A  O  5 63
     A                                  7 11'Varetekst'
     A                                      COLOR(WHT)
     A  80                              7 47'Varenr'
     A  80                                  COLOR(WHT)
     A                                  7 63'Tot:'
     A                                      COLOR(WHT)
     A            B2TOTS    R        O  7 68REFFLD(BOTOTS)
     A                                      EDTCDE(4)
     A                                  8  3'Antall'
     A                                      COLOR(WHT)
     A            B2TEK1    R        B  8 11REFFLD(BDTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80        B2VARE         8Y 0B  8 47COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                  8 63'Enh'
     A                                      COLOR(WHT)
     A N84                              8 68'Salgspris'
     A                                      COLOR(WHT)
     A  81                              7 47'Lev. varenr'
     A  81                                  COLOR(WHT)
     A  82                              7 47'Nobbnr'
     A  82                                  COLOR(WHT)
     A  81        B2LVAR    R   -5   B  8 47REFFLD(VVLVAR *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  82        B2NOBB    R        B  8 47REFFLD(VVNOBB *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  84                              8 68'eksl.mva.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031016  085435  HKO         REL-V5R2M0  5722-WDS
     A                                 22  2'F2=Tekstlinje'
     A                                      COLOR(BLU)
     A                                 22 17'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 22 32'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 22 45'F6=Txt2'
     A                                      COLOR(BLU)
     A                                 22 54'F9=Gjenta søk'
     A                                      COLOR(BLU)
     A                                 22 69'F13=Vis vare'
     A                                      COLOR(BLU)
     A                                 23  2'F14=Kundeinfo'
     A                                      COLOR(BLU)
     A                                 23 17'F15=Rab.matr.'
     A                                      COLOR(BLU)
     A  83                             23 32'F21=Eksl.skaffevarer'
     A                                      COLOR(BLU)
     A N83                             23 32'F21=Inkl.skaffevarer'
     A                                      COLOR(BLU)
     A  84                             23 54'F22=Inkl.mva.'
     A                                      COLOR(BLU)
     A N84                             23 54'F22=Eksl.mva.'
     A                                      COLOR(BLU)
     A                                 23 69'F23=Ny pos.'
     A                                      COLOR(BLU)
     A          R C1WIN
     A*%%TS  SD  19991130  235623  AMD         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 66)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF08
     A                                      CA23
     A                                      KEEP
     A                                      OVERLAY
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  1  2'Varenummer. .'
     A                                      COLOR(GRN)
     A            C1VARE    R        O  1 16REFFLD(BDVARE)
     A                                      COLOR(GRN)
     A                                  2 56'Lev'
     A                                  3  2'Antall'
     A                                  3 16'Enh'
     A                                  3 21'Salgspris'
     A                                  3 33'Kostpris'
     A                                  3 42'Rabatt'
     A                                  3 56'Typ'
     A                                  3 60'K'
     A            C1ANTA    R        B  4  2REFFLD(BDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A            C1ENH1    R        B  4 16REFFLD(BDENH1)
     A  31                                  ERRMSGID(BMD0400 *LIBL/QMFSHOP 31)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            C1SAPR         9Y 2B  4 20EDTWRD('     0 ,  ')
     A  32                                  ERRMSGID(BMD1000 *LIBL/QMFSHOP 32)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1KOPR    R        B  4 31REFFLD(BDSELK)
     A                                      EDTWRD('       ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A            C1RAB1    R        B  4 42REFFLD(BDRAB1)
     A                                      EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1RAB2    R        B  4 49REFFLD(BDRAB2)
     A                                      EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1LETY    R        B  4 57REFFLD(BDLETY)
     A                                      COLOR(YLW)
     A            C1KPRI    R        B  4 60REFFLD(BDKPRI)
     A                                      COLOR(YLW)
     A            C1ANT1    R        O  6  2REFFLD(BDANTA)
     A                                      EDTCDE(M)
     A            C1ENH4    R        O  6 16REFFLD(BMENH1)
     A            C1TEK1    R        B  6 20REFFLD(BDTEK1)
     A  33                                  ERRMSGID(BMD0600 *LIBL/QMFSHOP 33)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                  6 60'N=Netto'
     A            C1ANT2    R        O  7  2REFFLD(BDANTA)
     A                                      EDTCDE(M)
     A            C1ENH5    R        O  7 16REFFLD(BMENH1)
     A            C1TEK2    R        B  7 20REFFLD(VVTEK2 *LIBL/FFRF)
     A  33                                  ERRMSGID(BMD0600 *LIBL/QMFSHOP 33)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                  7 60'B=Bunn'
     A            C1ANT3    R        O  8  2REFFLD(BDANTA)
     A                                      EDTCDE(M)
     A            C1ENH6    R        O  8 16REFFLD(BMENH1)
     A                                  8 60'F=Fast'
     A                                 10  2'Lagerbeholdn.:'
     A                                      COLOR(BLU)
     A N86                                  DSPATR(ND)
     A            C1BEHL    R        O 10 17REFFLD(VLBEHL *LIBL/FFRF)
     A N86                                  DSPATR(ND)
     A                                 11  2'Ordre/Pakks .:'
     A                                      COLOR(BLU)
     A N86                                  DSPATR(ND)
     A            C1PAKK    R        O 11 17REFFLD(VLBEHL *LIBL/FFRF)
     A N86                                  DSPATR(ND)
     A                                 12  2'Disp.beholdn.:'
     A                                      COLOR(BLU)
     A N86                                  DSPATR(ND)
     A            C1DISP    R        O 12 17REFFLD(VLBEHL *LIBL/FFRF)
     A N86                                  DSPATR(ND)
     A                                 14  2'F2=Hjelp til kostpris   F3=Gå tilb-
     A                                      ake   F8=Produktinformasjon'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  19950613  091738  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(*DFT 6 63)
     A                                      CA03
     A                                      CA12
     A                                  1  1'Registrering av fri tekst mellom v-
     A                                      arelinjer'
     A                                      COLOR(WHT)
     A                                  2  1'-----------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A            C2TEXT        60A  B  3  1DSPATR(PC)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                  4 31'!'
     A                                      COLOR(WHT)
     A                                  5  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
