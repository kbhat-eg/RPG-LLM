     A*%%TS  SD  20101104  101647  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030813  104013  HKO         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LDOR    R        O  8  4REFFLD(VDLDOR)
     A            B1LNAV        10A  O  8 11
     A            B1OGRP    R        O  8 22REFFLD(VDOGRP)
     A            B1HGRP    R        O  8 25REFFLD(VDHGRP)
     A            B1UGRP    R        O  8 28REFFLD(VDUGRP)
     A            B1UTXT        10A  O  8 32
     A            B1MODN    R        O  8 43REFFLD(VDMODN)
     A            B1MTE1         9A  O  8 52
     A            B1VARE    R   -7   O  8 62REFFLD(VDVARE)
     A            B1TEK1        10A  O  8 71
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101104  101647  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VD100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 59'Sortiment:'
     A                                      COLOR(BLU)
     A            B2VSOR    R        O  4 70REFFLD(VDVSOR)
     A                                      COLOR(YLW)
     A                                  5 59'Lager    :'
     A                                      COLOR(BLU)
     A            B2LAGE    R        O  5 70REFFLD(VDLAGE)
     A                                      COLOR(YLW)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A*                                 7  2'V Lev.              Varegrp.      -
7.xx A*                                            Modulnr            Varenr.'
7.xx A*                                     COLOR(WHT)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  4'Lev.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'OG'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 25'HG'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 28'UG'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 32'Varegrp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 43'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 52'Mtekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 71'Vtekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  2'Levr  +'
7.xx A                                      COLOR(WHT)
7.xx A            B2LDOR    R        B  4 10REFFLD(VDLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  4 20'Varegr +'
7.xx A                                      COLOR(WHT)
7.xx A            B2OGRP    R        B  4 29REFFLD(VDOGRP)
     A  31                                  ERRMSGID(VMD0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMD0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 32'+'
7.xx A                                      COLOR(WHT)
7.xx A            B2HGRP    R        B  4 34REFFLD(VDHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 37'+'
7.xx A                                      COLOR(WHT)
7.xx A            B2UGRP    R        B  4 39REFFLD(VDUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Modul +'
7.xx A                                      COLOR(WHT)
7.xx A            B2MODN    R        B  5 10REFFLD(VDMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 20'Vare   +'
7.xx A                                      COLOR(WHT)
7.xx A            B2VARE    R   -7   B  5 29REFFLD(VDVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  093105  HKO         REL-V5R1M0  5722-WDS
     A                                 22  2'F3=Gå tilbake  F4=Spørring  F5=For-
     A                                      ny  F6=Opprett  F13=Utplukk vareeie-
     A                                      re'
     A                                      COLOR(BLU)
     A                                 23  2'F14=Utplukk varer'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030813  124433  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VD100C1'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Leverandør. . . .  . . . . +'
     A            C1LDOR    R        B  5 31REFFLD(VDLDOR)
     A  38                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 38)
     A  41                                  ERRMSGID(VMD0070 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  6 31REFFLD(VDOGRP)
     A  31                                  ERRMSGID(VMD0050 *LIBL/QMFOFAK 31)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OTXT    R        O  6 41REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  7 31REFFLD(VDHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HTXT    R        O  7 41REFFLD(VGHTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  8 31REFFLD(VDUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT    R        O  8 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Modulnummer. . . . . . . . +'
     A            C1MODN    R        B  9 31REFFLD(VDMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTE1        35A  O  9 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B 10 31REFFLD(VDVARE)
     A  39                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(VMD0060 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19971024  115010  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Utplukkskombinasjonen finnes'
     A                                  3  2'allerede i sortimentet'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030813  124433  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av varesortiment'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VD100C2'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  5 31REFFLD(VDLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  6 31REFFLD(VDOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT    R        O  6 41REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  7 31REFFLD(VDHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT    R        O  7 41REFFLD(VGHTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  8 31REFFLD(VDUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT    R        O  8 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnummer. . . . . . . . .'
     A            C1MODN    R        O  9 31REFFLD(VDMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTE1        35A  O  9 41COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O 10 31REFFLD(VDVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VDEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030813  124433  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette fra sortiment '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør . . . .'
     A            D1LDOR    R        O  4 21REFFLD(VDLDOR)
     A                                  5  2'Overgruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(VDOGRP)
     A                                  6  2'Hovedgruppe. . . .'
     A            D1HGRP    R        O  6 21REFFLD(VDHGRP)
     A                                  7  2'Undergruppe. . . .'
     A            D1UGRP    R        O  7 21REFFLD(VDUGRP)
     A                                  8  2'Modulnummer. . . .'
     A            D1MODN    R        O  8 21REFFLD(VDMODN)
     A                                  9  2'Varenummer . . . .'
     A            D1VARE    R   -7   O  9 21REFFLD(VDVARE)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20030813  124433  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 19 45)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Leverandør . . . +'
     A            K1LDOR    R        B  3 21REFFLD(VDLDOR)
     A  38                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  4  2'Overgruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(VDOGRP)
     A  31                                  ERRMSGID(VMD0050 *LIBL/QMFOFAK 31)
     A  33                                  ERRMSGID(VMD0030 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe. . . +'
     A            K1HGRP    R        B  5 21REFFLD(VDHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Undergruppe. . . +'
     A            K1UGRP    R        B  6 21REFFLD(VDUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Modulnummer. . . +'
     A            K1MODN    R        B  7 21REFFLD(VDMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  8  2'Varenummer . . . +'
     A            K1VARE    R        B  8 21REFFLD(VDVARE)
     A  39                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  2'Leverandør . . . .'
     A            VDLDOR    R        O 11 21OVRDTA
     A                                 12  2'Overgruppe . . . .'
     A            VDOGRP    R        O 12 21OVRDTA
     A                                 13  2'Hovedgruppe. . . .'
     A            VDHGRP    R        O 13 21OVRDTA
     A                                 14  2'Undergruppe. . . .'
     A            VDUGRP    R        O 14 21OVRDTA
     A                                 15  2'Modulnummer. . . .'
     A            VDMODN    R        O 15 21OVRDTA
     A                                 16  2'Varenummer . . . .'
     A            VDVARE    R        O 16 21OVRDTA
     A                                 18  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
