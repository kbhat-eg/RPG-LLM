     A*%%TS  SD  20030101  171342  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RE620D
     A* Beskrivelse . . : Overføre resultatrapport til PC
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       301202 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20030101  171342  KOB         REL-V5R1M0  5722-WDS
     A                                      CA01
     A                                      CA03
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Resultatrapport til PC'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  4'Dato .:'
     A            A2PKDA    R        B  5 12REFFLD(RHPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 22'Regnskapsperiode/år:'
     A            A2PPER    R        B  5 43REFFLD(RKPPER)
     A  35                                  DSPATR(PC)
     A  35                                  ERRMSGID(RMH0040 *LIBL/QMFOKON 35)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            A2PRÅR    R        B  5 46REFFLD(RHPRÅR)
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7 22'Rapport . . . . . .:'
     A            A2PRAP    R        B  7 43REFFLD(RR0RAP)
     A  34                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 22'Avdeling fra - til.:'
     A            A2PAVF    R        B  8 43REFFLD(RKPAVF)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(RMH0130 *LIBL/QMFOKON 33)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 48'-'
     A            A2PAVT    R        B  8 50REFFLD(RKPAVT)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 22'Avdelinger summeres:'
     A            A2PSAL    R        B  9 43REFFLD(RHPNUL)
     A  36                                  DSPATR(PC)
     A  36                                  ERRMSGID(RMH0270 *LIBL/QMFOKON 36)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 46'(J/N = Hele firma/pr. avdeling)'
     A  13                             11  4'Rutinen legger ut member Kxxxaaaa -
     A                                      i register RE02PF med tall i henhol-
     A                                      d  '
     A                                      COLOR(RED)
     A  13                             12  4'til regnskapsperiode/år. Faktisk p-
     A                                      er. og hittil i år, budsjett og i f-
     A                                      jor'
     A                                      COLOR(RED)
     A  13                             14  4'Etter at rutinen er kjørt kan RE02-
     A                                      PF overføres til PC.   '
     A                                      COLOR(RED)
     A  13                             16  4'Membernavnet har følgende verdier:-
     A                                       xxx = firma, aaaa = avdeling'
     A  13                             17  4'Dersom hele firma er valgt blir aa-
     A                                      aa satt = 0000'
     A  13                             19  4'Dersom mappe PCRAPP blir opprettet-
     A                                      , vil Kxxxaaaa bli lagt ut som     -
     A                                         '
     A  13                             20  4'en PCFIL i mappen med dokumentnavn-
     A                                       = Kxxxaaaa.                       -
     A                                         '
     A  14                             11  4'Rutinen legger ut member SKATxxx  -
     A                                      i register RE08PF med tall i henhol-
     A                                      d  '
     A                                      COLOR(RED)
     A  14                             12  4'til regnskapsperiode/år. Faktisk h-
     A                                      ittil i år og i fjor               -
     A                                         '
     A                                      COLOR(RED)
     A  14                             14  4'Etter at rutinen er kjørt kan RE08-
     A                                      PF overføres til PC.   '
     A                                      COLOR(RED)
     A  14                             16  4'Membernavnet har følgende verdier:-
     A                                       xxx = firma'
     A  14                             18  4'Dersom mappe PCRAPP blir opprettet-
     A                                      , vil SKATxxx  bli lagt ut som     -
     A                                         '
     A  14                             19  4'en PCFIL i mappen med dokumentnavn-
     A                                       = SKATxxx.                        -
     A                                         '
     A                                 23  4'F3 tilbake   '
     A                                      COLOR(BLU)
