     A*%%TS  SD  20041102  151635  HKO530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BS510D
     A* Beskrivelse . . : Slettede salgslinjer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041102  151635  HKO530      REL-V5R2M0  5722-WDS
     A            B1SDAT         6Y 0O  7  2EDTWRD('  .  .  ')
     A            B1SUSR    R        O  7 11REFFLD(BIEUSR)
     A            B1STIM    R        O  7 22REFFLD(BIETIM)
     A            B1KASS    R        O  7 32REFFLD(BHKASS)
7.xx A            B1ANTA    R   -2   O  7 36REFFLD(BIANTA)
7.xx A                                      EDTWRD('    0 ,   ')
7.xx A            B1ENHE    R        O  7 48REFFLD(BIENHE)
7.xx A            B1NTPR    R        O  7 52REFFLD(BINTPR)
7.xx A            B1ORNR    R        O  7 64REFFLD(BIORNR)
7.xx A            B1TREF    R        O  7 73REFFLD(BITREF)
     A            B1KODE         1A  O  7 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041102  151635  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Slettede salgslinjer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'BS510B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Bruker-id'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 64'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Bruker-id'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 22'Tidspkt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 31'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 40'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 48'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 53'Nettopris'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 64'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 74'Term.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 80'K'
7.xx A**                                6  4'Dato   Bruker-id  Tidspkt. Kasse  -
7.xx A**                                      Antall    Enh  Nettopris  Ordre  -
7.xx A**                                     Term. K'
     A                                      COLOR(WHT)
7.xx A            B2SDAT         6Y 0B  5  2COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2SUSR    R        B  5 11REFFLD(BIEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2ORNR    R        B  5 64REFFLD(BIORNR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2TREF    R        B  5 73REFFLD(BITREF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20041102  115043  HKO530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
