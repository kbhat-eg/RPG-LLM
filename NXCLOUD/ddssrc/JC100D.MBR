     A*%%TS  SD  20081116  085943  BOF570      REL-V5R4M0  5722-WDS
     A*
7.xx A*   290716 bsa Endringer relatert til ny GUI
     A* 011118 bof : utvidet med filter
6.34 A* 131218 bof : ny feilkode vedr. logistikk-vare
6.35 A* 171218 bof : farge ved logistikkvarer
     A*
7.01 A* 190421 bof : nxs8236 Utvidet verdi på jcsphl 0,1 og 2.
     A*
8.01 A* 211221 bof : nxs9551 utvide med felt + utvide til 132 bilde
8.01 A* 130622 bof : nxs9745 utvide prisgruppe fra 1 til posisjoner
8.02 A* 130334 bof : Justering av bilder vedr. jcsano/jcnobb
8.03 A* 130423 bsa : Manglet kommandotaster
8.05 A* 080124 bsa : Justert posisjoner grunnet overlapp av prisgruppe
8.06 A* 050424 BOF : Justert plassering av sano/nobb i subfile
     A*
     A*---------------------------------------------------------------
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
8.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081116  083838  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 8 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
8.01 A            B1PRGR    R        O  9  4REFFLD(JCPRGR)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1OGRP    R        O  9  7REFFLD(JCOGRP)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1HGRP    R        O  9 10REFFLD(JCHGRP)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1UGRP    R        O  9 13REFFLD(JCUGRP)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1LDOR    R        O  9 17REFFLD(JCLDOR)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1LNAV        14A  O  9 24
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1MODN    R        O  9 40REFFLD(JCMODN)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1VARE    R   -7   O  9 50REFFLD(JCVARE)
6.35 A  50                                  COLOR(BLU)
6.35 A  51                                  COLOR(YLW)
8.01 A            B1KPRI    R        O  9 60REFFLD(JCKPRI)
8.01 A            B1VTYP    R        O  9 63REFFLD(JCVTYP)
8.01 A            B1LETY    R        O  9 66REFFLD(JCLETY)
8.02 A            B1KOFA         7Y 5O  9 80EDTCDE(2)
8.01 A            B1KOBE    R        O  9 90REFFLD(JCKOBE)
     A                                      EDTCDE(M)
8.01 A            B1SAFA         7Y 5O  9101EDTCDE(2)
8.06 A            B1SANO    R        O  9117REFFLD(JCSANO)
8.06 A            B1NOBB    R        O  9125REFFLD(JCNOBB)
8.02 A                                      EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081116  083838  BOF570      REL-V5R4M0  5722-WDS
8.03 A                                      SFLSIZ(0017)
8.03 A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
8.01 A                                      COLOR(WHT)
8.01 A                                  1124TIME
8.01 A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
8.01 A                                  2 38'JC100X1'
8.01 A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
8.01 A                                      COLOR(WHT)
8.01 A                                  2124DATE
8.01 A                                      EDTCDE(Y)
8.01 A                                      COLOR(WHT)
8.01 A                                  3  1'-----------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
8.01 A                                      COLOR(WHT)
7.xx A                                  4  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 10'Varegrp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 24'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 33'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 44'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 55'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 59'T'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 63'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
6.32 A                                       8=Varer under modul'
     A                                      COLOR(BLU)
7.xx A**                                6  2'V Pg Varegr.   Lev.         Moduln-
7.xx A**                                    r  Varenr   P T L Kostpris Kostpris-
7.xx A**                                     Salgspris'
7.xx A**                                    COLOR(WHT)
7.xx A**                                7 54'faktor   beløp    faktor'
7.xx A**                                    COLOR(WHT)
8.01 A                                  7 71'Kostpris'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 81'Kostpris'
7.xx A                                      COLOR(WHT)
8.01 A                                  7102'Salgspris'
7.xx A                                      COLOR(WHT)
8.02 A                                  7112'Påsl.fra'
8.02 A                                      COLOR(WHT)
8.02 A                                  7122'Nobbpris'
8.02 A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'OG'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 10'HG'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'UG'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 24'Navn '
7.xx A                                      COLOR(WHT)
8.01 A                                  8 40'Modulnr'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 50'Varenr'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 59'P'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 62'T'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 65'L'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 80'faktor  '
7.xx A                                      COLOR(WHT)
8.01 A                                  8 91'beløp   '
7.xx A                                      COLOR(WHT)
8.05 A                                  8102'faktor  '
7.xx A                                      COLOR(WHT)
8.02 A                                  8112'nobbpris'
8.02 A                                      COLOR(WHT)
8.05 A                                  8122'som salgsp.'
8.02 A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2PRGR    R        B  5  4REFFLD(JCPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.05 A                                  5  7'+'
8.05 A            B2OGRP    R        B  5  9REFFLD(JCOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A                                  5 12'+'
8.05 A            B2HGRP    R        B  5 14REFFLD(JCHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A                                  5 17'+'
8.05 A            B2UGRP    R        B  5 19REFFLD(JCUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.05 A                                  5 23'+'
8.05 A            B2LDOR    R        B  5 25REFFLD(JCLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.05 A                                  5 32'+'
8.05 A            B2MODN    R        B  5 34REFFLD(JCMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.05 A                                  5 43'+'
8.05 A            B2VARE    R   -7   B  5 45REFFLD(JCVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A                                  5 54'+'
8.05 A            B2KPRI    R        B  5 56REFFLD(JCKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A                                  5 58'+'
8.05 A            B2VTYP    R        B  5 60REFFLD(JCVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A                                  5 62'+'
8.05 A            B2LETY    R        B  5 64REFFLD(JCLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A*                                 7103'Påslag'
8.05 A*                                     COLOR(WHT)
8.05 A*                                 8103'Nobbpris   '
8.05 A*                                     COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020719  105127  HKO         REL-V5R1M0  5722-WDS
8.03 A                                 26  2'F1=Filter  F3=Gå tilbake   F4=Spør-
8.03 A                                      ring   F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050610  112514  HKO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
8.01 A                                      COLOR(WHT)
8.01 A                                  1124TIME
8.01 A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
8.01 A                                      COLOR(WHT)
8.01 A                                  2124DATE
8.01 A                                      EDTCDE(Y)
8.01 A                                      COLOR(WHT)
8.01 A                                  3  1'-----------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Prisgruppe . . . . . . . . +'
     A            C1PRGR    R        B  5 31REFFLD(JCPRGR)
     A  46                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  6 31REFFLD(JCOGRP)
     A  35                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  7 31REFFLD(JCHGRP)
     A  36                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  8 31REFFLD(JCUGRP)
     A  37                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT        35A  O  8 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  9 31REFFLD(JCLDOR)
     A  38                                  ERRMSGID(JML0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV    R        O  9 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Modulnr. . . . . . . . . . +'
     A            C1MODN    R        B 10 31REFFLD(JCMODN)
     A  39                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Varenr . . . . . . . . . . +'
     A            C1VARE    R   -7   B 11 31REFFLD(JCVARE)
     A  40                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 40)
6.34 A  47                                  ERRMSGID(JMV0070 *LIBL/QMFVADM 47)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Priskode . . . . . . . . . +'
     A            C1KPRI    R        B 12 31REFFLD(JCKPRI)
     A  41                                  ERRMSGID(JVA6020 *LIBL/QMFVADM 41)
     A  42                                  ERRMSGID(JVA6040 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        30A  O 12 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Varetype . . . . . . . . . +'
     A            C1VTYP    R        B 13 31REFFLD(JCVTYP)
     A  43                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 43)
     A  44                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 44)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 13 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 14  2'Leveringstype. . . . . . . +'
     A            C1LETY    R        B 14 31REFFLD(JCLETY)
     A  45                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 14 41COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020717  114354  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Påslag finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050610  112514  HKO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
8.01 A                                      COLOR(WHT)
8.01 A                                  1124TIME
8.01 A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
8.01 A                                      COLOR(WHT)
8.01 A                                  2124DATE
8.01 A                                      EDTCDE(Y)
8.01 A                                      COLOR(WHT)
8.01 A                                  3  1'-----------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
8.01 A                                      COLOR(WHT)
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A            C1PRGR    R        O  5 31REFFLD(JCPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  6 31REFFLD(JCOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  7 31REFFLD(JCHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  8 31REFFLD(JCUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  8 41COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  9 31REFFLD(JCLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  9 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O 10 31REFFLD(JCMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Varenr . . . . . . . . . . .'
     A            C1VARE    R   -7   O 11 31REFFLD(JCVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Priskode . . . . . . . . . .'
     A            C1KPRI    R        O 12 31REFFLD(JCKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        30A  O 12 41COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Varetype . . . . . . . . . .'
     A            C1VTYP    R        O 13 31REFFLD(JCVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 13 41COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 14 31REFFLD(JCLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 14 41COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Nobbnr . . . . . . . . . . .'
     A            C1NOBB    R        O 17 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Kostpris-faktor. . . . . . .'
     A            C2KOFA    R        B 19 31REFFLD(JCKOFA)
     A  31                                  ERRMSGID(JMC0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.03 A*                                19 43'Nobbpris som salgspris'
8.03 A*           C2NOBB    R        B 19 66REFFLD(JCNOBB)
8.03 A*                                     VALUES(0 1)
8.03 A*                                     CHKMSGID(JM00010 *LIBL/QMFVADM)
8.03 A*                                     EDTCDE(Z)
8.03 A*                                     COLOR(YLW)
8.03 A* 30                                  DSPATR(PR)
8.03 A*                                     OVRDTA
8.03 A*                                19 69'1=Ja,0=Nei'
     A                                 20  2'Kostpris-beløp . . . . . . .'
     A            C2KOBE    R        B 20 31REFFLD(JCKOBE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.01 A                                 20 43'Påslag fra nobbpris'
8.01 A            C2SANO    R        B 20 66REFFLD(JCSANO)
8.01 A                                      VALUES(0 1)
8.01 A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
8.01 A                                      EDTCDE(Z)
8.01 A                                      COLOR(YLW)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
8.01 A                                 20 69'1=Ja,0=Nei'
     A                                 21  2'Salgspris-faktor . . . . . .'
     A            C2SAFA    R        B 21 31REFFLD(JCSAFA)
     A  32                                  ERRMSGID(JMC0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.02 A                                 21 43'Nobb som salgspris'
8.02 A            C2NOBB    R        B 21 66REFFLD(JCNOBB)
8.02 A                                      VALUES(0 1)
8.02 A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
8.02 A                                      EDTCDE(Z)
8.02 A                                      COLOR(YLW)
8.02 A  30                                  DSPATR(PR)
8.02 A                                      OVRDTA
8.02 A                                 21 69'1=Ja,0=Nei'
     A                                 22  2'Bruk pris fra hoved.lev. . .'
     A            C2SPHL    R        B 22 31REFFLD(JCSPHL)
7.01 A                                      VALUES(0 1 2)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 35'1=Salgsp., 2=Kostp. og Salgsp., 0=-
     A                                      Nei'
     A                                 23  2'Kostpris-faktor 2  . . . . .'
     A            C2KOF2    R        B 23 31REFFLD(JCKOF2)
     A  33                                  ERRMSGID(JMC0100 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 25 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 25 57'/'
     A            C2EDAT         6Y 0O 25 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 25 68'/'
     A            C2EUSR    R        O 25 70REFFLD(JCEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20081116  085943  BOF570      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 19 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette påslag '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgruppe . . . .'
     A            D1PRGR    R        O  4 21REFFLD(JCPRGR)
     A                                  5  2'Varegruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(JCOGRP)
     A            D1HGRP    R        O  5 24REFFLD(JCHGRP)
     A            D1UGRP    R        O  5 27REFFLD(JCUGRP)
     A                                  6  2'Leverandør . . . .'
     A            D1LDOR    R        O  6 21REFFLD(JCLDOR)
     A                                  7  2'Modulnr. . . . . .'
     A            D1MODN    R        O  7 21REFFLD(JCMODN)
     A                                  8  2'Varenr . . . . . .'
     A            D1VARE    R        O  8 21REFFLD(JCVARE)
     A                                  9  2'Priskode . . . . .'
     A            D1KPRI    R        O  9 21REFFLD(JCKPRI)
     A                                 10  2'Varetype . . . . .'
     A            D1VTYP    R        O 10 21REFFLD(JCVTYP)
     A                                 11  2'Leveringstype. . .'
     A            D1LETY    R        O 11 21REFFLD(JCLETY)
     A                                 14  2'Kostpris-faktor. .'
     A            JCKOFA    R        O 14 21
     A                                 15  2'Kostpris-beløp . .'
     A            JCKOBE    R        O 15 21
     A                                 16  2'Salgspris-faktor .'
     A            JCSAFA    R        O 16 21
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)

     A          R K1WIN
     A*%%TS  SD  20050610  113039  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 23 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Prisgruppe . . . +'
     A            K1PRGR    R        B  3 21REFFLD(JCPRGR)
     A  46                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.05 A            K1GTXT        25A  O  3 24COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                  4  2'Varegruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(JCOGRP)
     A  32                                  ERRMSGID(JMC0030 *LIBL/QMFVADM 32)
     A  35                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 24'+'
7.xx A            K1HGRP    R        B  4 26REFFLD(JCHGRP)
     A  36                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 29'+'
7.xx A            K1UGRP    R        B  4 31REFFLD(JCUGRP)
     A  37                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Leverandør . . . +'
     A            K1LDOR    R        B  5 21REFFLD(JCLDOR)
     A  38                                  ERRMSGID(JML0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Modulnr. . . . . +'
     A            K1MODN    R        B  6 21REFFLD(JCMODN)
     A  39                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varenummer . . . +'
     A            K1VARE    R        B  7 21REFFLD(JCVARE)
     A  40                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Priskode . . . . +'
     A            K1KPRI    R        B  8 21REFFLD(JCKPRI)
     A  41                                  ERRMSGID(JVA6020 *LIBL/QMFVADM 41)
     A  42                                  ERRMSGID(JVA6040 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varetype . . . . +'
     A            K1VTYP    R        B  9 21REFFLD(JCVTYP)
     A  43                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 43)
     A  44                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 44)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Leveringstype. . +'
     A            K1LETY    R        B 10 21REFFLD(JCLETY)
     A  45                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 13  2'Prisgruppe . . . .'
     A            JCPRGR    R        O 13 21
     A                                 14  2'Varegruppe . . . .'
     A            JCOGRP    R        O 14 21
     A            JCHGRP    R        O 14 24
     A            JCUGRP    R        O 14 27
     A                                 15  2'Leverandør . . . .'
     A            JCLDOR    R        O 15 21
     A                                 16  2'Modulnr. . . . . .'
     A            JCMODN    R        O 16 21
     A                                 17  2'Varenummer . . . .'
     A            JCVARE    R        O 17 21
     A                                 18  2'Priskode . . . . .'
     A            JCKPRI    R        O 18 21
     A                                 19  2'Varetype . . . . .'
     A            JCVTYP    R        O 19 21
     A                                 20  2'Leveringstype. . .'
     A            JCLETY    R        O 20 21
     A                                 21  2'Nobbpris fra . . .'
     A            JCNOFR    R        O 21 21
     A                                 22  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
6.32 A          R F1WIN
6.32 A*%%TS  SD  20140630  145820  BOF630      REL-V7R1M0  5770-WDS
6.32 A                                      WINDOW(*DFT 10 50)
6.32 A                                      CA03
6.32 A                                      CF04
6.32 A                                      CA12
6.32 A                                      PUTOVR
6.32 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
6.32 A            W_ARCD        10A  H
6.32 A            W_AFLD        10A  H
6.32 A                                  2  2'Filter for visning :'
6.32 A                                      COLOR(WHT)
6.32 A                                  4  2'Prisgruppe  . . . . . .'
6.32 A            F1PRGR    R        B  4 28REFFLD(JCPRGR)
6.32 A                                      OVRDTA
6.32 A                                      COLOR(YLW)
6.32 A                                  5  2'Varegrupper . . . . . . '
6.32 A            F1OGRP    R        B  5 28REFFLD(JCOGRP)
6.32 A                                      OVRDTA
6.32 A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
6.32 A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
6.32 A  36                                  ERRMSGID(JVG0200 *LIBL/QMFVADM 36)
6.32 A                                      COLOR(YLW)
6.32 A            F1HGRP    R        B  5 32REFFLD(JCHGRP)
6.32 A                                      OVRDTA
6.32 A                                      COLOR(YLW)
6.32 A            F1UGRP    R        B  5 36REFFLD(JCUGRP)
6.32 A                                      OVRDTA
6.32 A                                      COLOR(YLW)
6.32 A                                  6  2'Modul . . . . . . . .'
6.32 A            F1MODN    R        B  6 28REFFLD(JCMODN)
6.32 A                                      OVRDTA
6.32 A                                      COLOR(YLW)
6.32 A                                  7  2'Logistikkvare . . . .'
6.32 A            F1LVAR    R   -7   B  7 28REFFLD(JCVARE)
6.32 A                                      OVRDTA
6.32 A                                      COLOR(YLW)
6.32 A                                  9  2'F3=Gå tilbake'
6.32 A                                      COLOR(BLU)
6.32 A          R ASUME                     ASSUME
     A                                 24 79' '
