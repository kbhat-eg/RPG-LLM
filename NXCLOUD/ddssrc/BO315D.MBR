     A*%%TS  SD  20060906  110217  BOF550      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO314D
     A* Beskrivelse . . : Kasseoppgjør rapport detaljer, vis
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050906 bø   Nytt program
7.xx A* 7.xx  260516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HOME(21)
     A          R BASFL                     SFL
     A*%%TS  SD  20060906  110217  BOF550      REL-V5R3M0  5722-WDS
     A            BAVALG         1Y 0B  8  2COLOR(YLW)
     A                                      VALUES(0 5 7)
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A                                      EDTCDE(Z)
     A            BAGRUP    R        O  8  5REFFLD(BOT1ST/B1GRUP *LIBL/BOT1ST)
     A            BAKASS    R        O  8 17REFFLD(BOT1ST/B1KASS *LIBL/BOT1ST)
     A            BABRUK    R        O  8 23REFFLD(BOT1ST/B1BRUK *LIBL/BOT1ST)
     A            BABEHO    R        B  8 36REFFLD(BOT1ST/B1BEHO *LIBL/BOT1ST)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A          R B2CTL                     SFLCTL(BASFL)
     A*%%TS  SD  20060906  110217  BOF550      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre beholdning    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  6'Gruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  6'Gruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 23'Bruker'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 37'Beholdning'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V   Gruppe    Kasse    Bruker     -
7.xx A**                                     Beholdning                 '
7.xx A**                                    COLOR(WHT)
7.xx A            B2GRUP    R        B  5  5REFFLD(BOT1ST/B1GRUP *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20060905  120459  BOF550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny          -
     A                                         '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20060906  110217  BOF550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Kasseoppgjør '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre beholdning    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gruppe   . . . . . . . . . .'
     A            C1GRUP    R        O  5 31REFFLD(BOT1ST/B1GRUP *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kasse  . . . . . . . . . . .'
     A            C1KASS    R        O  6 31REFFLD(BOT1ST/B1KASS *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Bruker . . . . . . . . . . .'
     A            C1BRUK    R        O  7 31REFFLD(BOT1ST/B1BRUK *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Beholdning   . . . . . . . .'
     A            C2BEHO    R        O  9 31REFFLD(BOT1ST/B1BEHO *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2OTIM         6Y 0O 18 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ETIM         6Y 0O 19 41COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(BOT1ST/B1EUSR *LIBL/BOT1ST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
