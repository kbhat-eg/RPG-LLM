     A*%%TS  SD  20080119  172230  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL516D
     A* Beskrivelse . . : Leverandørposteringer, spørring utbet.forslag
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       170305 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20080119  172230  KOBASP      REL-V5R4M0  5722-WDS
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BFNR    R        O  8  4REFFLD(ROBFNR)
     A            B1BILN    R        O  8 14REFFLD(ROBILN)
6.NU A            B1BIDA         6Y 0O  8 23
6.NU A            B1BILK    R        O  8 30REFFLD(ROBILK)
6.NU A            B1TEXT         9   O  8 33
     A            B1BELØ    R        O  8 43REFFLD(ROBELØ)
     A                                      EDTCDE(J)
     A            B1LEVR    R        O  8 59REFFLD(RLLEVR)
     A            B1NAVN    R  -15   O  8 66REFFLD(RLNAVN)
     A            B1BFLI    R        O  8  8REFFLD(ROBFLI)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080119  172230  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på utbetalingsforslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'Fnr  Lnr'
     A                                      COLOR(WHT)
     A                                  7 14'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  7 23'Dato'
     A                                      COLOR(WHT)
6.NU A                                  7 30'Bk'
     A                                      COLOR(WHT)
6.NU A                                  7 33'Tekst'
     A                                      COLOR(WHT)
     A                                  7 49'Beløp'
     A                                      COLOR(WHT)
     A                                  7 59'Lever'
     A                                      COLOR(WHT)
     A                                  7 66'Navn'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for visning
     A*
     A          R C2WIN
     A*%%TS  SD  20050520  002101  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 20 75)
     A                                      CA03
     A                                      CA04
     A                                      PUTOVR
     A                                  2 29'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Regnskaps-per/år:'
     A            C2RPER    R        O  4 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  4 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  1'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Journalliste-nr.:'
     A            C2JOUR    R        O  5 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  6 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  6 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  6 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Betalingsbet.. .:'
     A            C2BETB    R        O  7 19REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Restbeløp. . . .:'
     A            C2REST    R        O  7 61REFFLD(ROREST)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        O  8 19REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Delbetalt. . . .:'
     A            C2DELB    R        O  8 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Samlegiro-nr.. .:'
     A            C2SAMB    R        O  9 19REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Valuta-kode. . .:'
     A            C2VALK    R        O  9 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Betalingsref.-1.:'
     A            C2RRF1    R        O 10 19REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Valutabeløp. . .:'
     A            C2VALB    R        O 10 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Betalingsref.-2.:'
     A            C2RRF2    R        O 11 19REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-rest. . .:'
     A            C2VALR    R        O 11 61REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Attestert av . .:'
     A            C2ATTU    R        O 12 19REFFLD(ROATTU)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Attestert dato .:'
     A            C2ADAT         6Y 0O 12 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 13  1'Lev. fakturanr. :'
     A            C2LFAK    R        O 13 19REFFLD(ROLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'KID . :'
     A            C2KIDI    R        O 13 51REFFLD(ROKIDI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  1'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 19REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 14 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'Forslagsdato . .:'
     A            C2FODA         6Y 0O 15 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 15 43'Returdato. . . .:'
     A            C2RDAT         6Y 0O 15 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 16  1'Forslagstid. . .:'
     A            C2FTIM    R        O 16 19REFFLD(ROFTIM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16 43'Returtid . . . .:'
     A            C2RTIM    R        O 16 61REFFLD(RORTIM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 17  1'Bruker . . . . .:'
     A            C2FSIG    R        O 17 19REFFLD(ROFSIG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 17 43'Bruker . . . . .:'
     A            C2RSIG    R        O 17 61REFFLD(RORSIG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18 43'Til bankkonto'
     A                                 19  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C2TKTO    R        O 19 41REFFLD(ROTKTO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
