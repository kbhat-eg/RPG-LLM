     A*%%TS  SD  20020312  220145  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     A*                                                                 *
     A* System  . . . . : ASOFAK                                        *
     A* Skjermfil . . . : FP430D                                        *
     A* Beskrivelse . . : Vedlikehold av leverandør, spesialkoder       *
     A*                                                                 *
     A* Spesialversjon. :                                               *
     A* Tilpasset for . :                                               *
     A*                                                                 *
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  090316  bsa  Justeringer basert på ny GUI og regler
     A*                                                                 *
     A*                                                                 *
     A*                                                                 *
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020312  215829  HKO         REL-V5R1M0  5722-WDS
     A            B1ALFA    R        H      REFFLD(RLALFA)
     A            B1VALG         1Y 0B  8  2COLOR(YLW)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A            B1LEVR    R        O  8  5REFFLD(RLLEVR)
     A            B1NAVN    R        O  8 13REFFLD(RLNAVN)
     A            B1HYLE    R        O  8 51REFFLD(RLHYLE)
     A            B1LVKO    R        O  8 63REFFLD(RLLVKO)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  220145  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av etikettinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Lever.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 13'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  5'Lever.'
     A                                      COLOR(WHT)
7.xx A                                  7 13'Navn'
     A                                      COLOR(WHT)
7.xx A            B2LEVR    R        B  5  5REFFLD(RLLEVR)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  5 13REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 47'Strekkode'
     A                                      COLOR(WHT)
     A                                  7 58'Informasjon'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020312  132546  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020312  215829  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av etikettinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandørnr . . . . . . . .'
     A            C1LEVR    R        O  5 31REFFLD(RLLEVR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2NAVN    R        O  5 39REFFLD(RLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varenummer som strekkode . .'
     A            C2HYLE    R        B  8 31REFFLD(RLHYLE)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 38'(0=Eget varenummer)'
     A                                  9 38'(1=Leverandørens varenummer)'
     A                                 10 38'(2=GTIN nummer)'
     A                                 11 38'(3=Nobbnummer)'
     A                                 13  2'Varenummer som informasjon .'
     A            C2LVKO    R        B 13 31REFFLD(RLLVKO)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 38'(0=Ikke i bruk)'
     A                                 14 38'(1=Leverandørens varenummer)'
     A                                 15 38'(2=Eget varenummer)'
     A                                 16 38'(3=Nobbnummer)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
