     A*%%TS  SD  20110712  145956  ASK610      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL512D
     A* Beskrivelse . . : Leverandørposteringer, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150802 KOB  Nytt program
7.01 A*       080421 bof  utvidet skjermbilde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20040216  145703  KOB         REL-V5R2M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1RAAR    R        H      REFFLD(RORAAR)
     A            B1RPER    R        H      REFFLD(RORPER)
     A            B1REKL    R        H      REFFLD(ROREKL)
     A            B1BETK    R        H      REFFLD(ROBETK)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(ROBILN)
     A  73                                  COLOR(YLW)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1BIDA         6Y 0O  9 13EDTCDE(Z)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1BILK    R        O  9 20REFFLD(ROBILK)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1TEXT        10A  O  9 23
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1REFN    R        O  9 34REFFLD(ROREFN)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1FDAT         6Y 0O  9 43EDTCDE(Z)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1BELØ        11Y 2O  9 50EDTCDE(J)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1REST        11Y 2O  9 66EDTCDE(J)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110712  145956  ASK610      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på leverandørposter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Lever:'
     A            B2LEVR    R        O  4  9REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                  4 58'Regnskapsår. .:'
     A            B2RAAR         4Y 0B  4 74COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  94        B2NOTB         8A  O  5 16DSPATR(RI)
     A                                      OVRDTA
     A            B2DOKU        10A  O  5 33OVRDTA
     A                                      DSPATR(ND)
     A                                      DFTVAL('C7')
     A                                  5 58'Periode. . . .:'
     A            B2RPER         2Y 0B  5 74COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2'1=Velg  5=Vis  6=Vis motpost  7=Vi-
     A                                      s bilag'
     A                                      COLOR(BLU)
     A                                  7  2'8=Vis faktura  9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  7 58'Saldo:'
     A            B2SALD    R        O  7 66REFFLD(RLSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  8 14'Dato'
     A                                      COLOR(WHT)
6.NU A                                  8 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  8 23'Tekst'
     A                                      COLOR(WHT)
     A                                  8 31'Val.'
     A                                      COLOR(WHT)
     A                                  8 37'Ref.'
     A                                      COLOR(WHT)
     A                                  8 43'Forfall'
     A                                      COLOR(WHT)
     A                                  8 56'Beløp'
     A                                      COLOR(WHT)
     A                                  8 68'Restbeløp'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20040216  145053  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 22  2'F2=Notatark'
     A                                      COLOR(BLU)
     A                                 22 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 22 30'F5=Forny'
     A                                      COLOR(BLU)
     A                                 22 40'F6=Åpne/Alle/Akkum'
     A                                      COLOR(BLU)
     A                                 22 60'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A                                 23  2'F8=Saldo/Kjøp'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20040302  111542  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 29'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  73                                  COLOR(YLW)
6.NU A  73        C2NOTB         8   O  3 28
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        O  6 19REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2CTXT    R        O  6 22REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        O  7 19REFFLD(ROREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlegiro-nr.. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        O  8 19REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        O  9 19REFFLD(ROAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2GTXT    R   18   O  9 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(ROREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjonskode:'
     A            C2ATTS    R        O 10 19REFFLD(ROATTS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        O 11 19REFFLD(ROBETK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        O 12 19REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        O 13 19REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        O 13 61REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        O 14 19REFFLD(RODECK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        O 15 17REFFLD(ROKIDI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 16 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 43'Lev.fakturanr. .:'
     A            C2LFAK    R        O 16 61REFFLD(ROLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A          R E2WIN
     A*%%TS  SD  20021210  230029  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1 22'Reskontrostatistikk'
     A                                      COLOR(WHT)
     A                                  2  1'Kjøp i år .:'
     A            E2KJØP    R        O  2 14REFFLD(RLKJØP)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  2 32'Saldo i år .:'
     A            E2SALD    R        O  2 46REFFLD(RLSALD)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3  1'Kjøp i fjor:'
     A            E2KJØF    R        O  3 14REFFLD(RLKJØF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3 32'Saldo i fjor:'
     A            E2SAIF    R        O  3 46REFFLD(RLSAIF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  5  1'Kredittid .:'
     A            E2KVTI         5Y 0O  5 14EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  6  1'Aldersfordeling:'
     A                                  6 38'Antall poster:'
     A                                  7  1'-Ikke forfalt :'
     A            E2ALD0    R        O  7 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  7 38'- Totalt. . . .:'
     A            E2POST         5Y 0O  7 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  8  1'-Ikke saldert'
     A                                  8 15':'
     A            E2ALDN    R        O  8 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  8 38'- Debet . . . .:'
     A            E2DEBE         5Y 0O  8 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  9  1'-   0 til'
     A            E23LA1    R        O  9 11REFFLD(RA3LA1)
     A                                      DSPATR(UL)
     A                                  9 15':'
     A            E2ALD1    R        O  9 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  9 38'- Kredit. . . .:'
     A            E2KRED         5Y 0O  9 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 10  1'-'
     A            E23LS2    R        O 10  3REFFLD(RA3LA1)
     A                                      DSPATR(UL)
     A                                 10  7'til'
     A            E23LA2    R        O 10 11REFFLD(RA3LA2)
     A                                      DSPATR(UL)
     A                                 10 15':'
     A            E2ALD2    R        O 10 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 10 38'- Åpne poster .:'
     A            E2REST         5Y 0O 10 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 11  1'-'
     A            E23LS3    R        O 11  3REFFLD(RA3LA2)
     A                                      DSPATR(UL)
     A                                 11  7'til'
     A            E23LA3    R        O 11 11REFFLD(RA3LA3)
     A                                      DSPATR(UL)
     A                                 11 15':'
     A            E2ALD3    R        O 11 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 12  1'- Over'
     A            E23LA4    R        O 12 11REFFLD(RA3LA4)
     A                                      DSPATR(UL)
     A                                 12 15':'
     A            E2ALD4    R        O 12 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 13  1'Sum. . . . . .:'
     A            E2SALT    R        O 13 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
