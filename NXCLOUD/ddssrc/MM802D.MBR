     A*%%TS  SD  20030124  103803  MHA         REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020226  120814  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LETY    R        O  7  6REFFLD(VYLETY)
     A            B1BESK    R        O  7 11REFFLD(VYBESK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  120814  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge leveringstype '
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V  Kode  Beskrivelse'
     A                                      COLOR(WHT)
     A            B2LETY    R        B  6  6REFFLD(VYLETY)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2BESK    R        B  6 11REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20030124  103803  MHA         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 18 46)
     A                                 17  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
