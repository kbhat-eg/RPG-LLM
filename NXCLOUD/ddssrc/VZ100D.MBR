     A*%%TS  SD  20110810  143439  BOF620      REL-V7R1M0  5770-WDS
8.01 A* 011222 bof : justering til GUI
8.03 A* 170124 bsa : justering til GUI
8.04 A* 281024 bof : Tillate sletting
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081126  143844  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
8.04 A                                      VALUES(0 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.03 A            B1FPRG    R        O  8 17REFFLD(VPFPRG)
     A            B1OGRP         2Y 0O  8  5
     A            B1HGRP         2Y 0O  8  9
     A            B1UGRP         3Y 0O  8 12
     A            B1TEK1        13A  O  8 32
     A            B1FDAT         6Y 0O  8 47EDTWRD('  .  .  ')
     A            B1TDAT         6Y 0O  8 57EDTWRD('  .  .  ')
     A            B1FSKO    R        O  8 68REFFLD(VPFSKO)
     A            B1FEN1         3A  O  8 80
     A            B1FOR1         9Y 2O  8 85
     A                                      EDTCDE(L)
     A            B1FIN1         9Y 2O  8 98
     A                                      EDTCDE(L)
     A            B1FKO1         9Y 2O  8110
     A                                      EDTCDE(L)
     A            B1FVAR    R   -7   O  8 22REFFLD(VPFVAR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081129  085633  BOF570      REL-V5R4M0  5722-WDS
8.01 A                                      SFLSIZ(0018)
8.01 A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA11
     A                                      CA08
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
8.01 A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Prisforslag'
     A                                      COLOR(WHT)
8.01 A                                  2 37'VZ100B2'
8.01 A                                      COLOR(WHT)
8.01 A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
8.01 A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
8.01 A                                  4 14'Prisgr'
     A                                      COLOR(WHT)
8.01 A                                  4 22'Varenr.'
8.01 A                                      COLOR(WHT)
8.01 A            B2FVAR    R   -7   B  5 22REFFLD(VPFVAR)
     A  31                                  ERRMSGID(VMT0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMT0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.01 A                                  5 15'+'
8.01 A            B2FPRG    R        B  5 17REFFLD(VPFPRG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2FDAT         6Y 0B  5 47COLOR(YLW)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2TDAT         6Y 0B  5 58COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2FSKO    R        B  5 69REFFLD(VPFSKO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.04 A                                  6 13'5=Vis'
8.01 A                                      COLOR(BLU)
8.04 A                                  6  3'4=Slett'
8.04 A                                      COLOR(BLU)
8.01 A                                  7  2'V'
8.01 A                                      COLOR(WHT)
8.01 A                                  7  4'Og'
8.01 A                                      COLOR(WHT)
8.01 A                                  7  9'Hg'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 12'Ugr'
8.01 A                                      COLOR(WHT)
8.03 A                                  7 17'PG'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 22'Varenr.'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 32'Varetekst'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 47'Fra dato'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 57'Til dato'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 68'Sort.kode'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 79'Enh.'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 84'Prisforslag'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 98'Innkj.pris'
8.01 A                                      COLOR(WHT)
8.01 A                                  7111'Kost pris'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 53'Periode'
8.01 A                                      COLOR(WHT)
8.01 A                                  4 68'Sort.kode'
8.01 A                                      COLOR(WHT)
8.01 A                                  5 20'+'
8.01 A                                  5 45'+'
8.01 A                                  5 56'+'
8.01 A                                  5 67'+'
8.01
     A          R B2CMD
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
8.01 A                                 26  2'F1=Filter  F3=Gå tilbake  F4=Spørr-
     A                                      ing   F5=Forny '
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990516  192110  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Prisforslag finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20110810  143439  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
8.01 A            L_FNAV        30A  O  1  2COLOR(WHT)
8.01 A                                  1 46'Ordre/Fakturering'
8.01 A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
8.01 A                                      COLOR(WHT)
8.01 A                                  1124TIME
8.01 A                                      COLOR(WHT)
8.01 A                                  2  2'Vedlikehold av Prisforslag'
8.01 A                                      COLOR(WHT)
8.01 A                                  2 37'VZ100B2'
8.01 A                                      COLOR(WHT)
8.01 A                                  2 46'Bruker:'
8.01 A                                      COLOR(WHT)
8.01 A                                  2 54USER
8.01 A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
8.01 A                                      COLOR(WHT)
8.01 A                                  2124DATE
8.01 A                                      EDTCDE(Y)
8.01 A                                      COLOR(WHT)
8.01 A                                  3  1'-----------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
8.01 A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1FVAR    R   -7   O  5 31REFFLD(VPFVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 42REFFLD(FÅTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe   . . . . . . . .'
     A            C1FPRG    R        O  6 31REFFLD(VPFPRG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT    R        O  6 42REFFLD(FÅTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA

     A                                  8  2'Fra dato . . . . . . . . . .'
     A            C1FDAT         6Y 0O  8 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA

     A                                  9  2'Til dato . . . . . . . . . .'
     A            C1TDAT         6Y 0O  9 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA

     A                                 11  2'Sortimentskode  . . . . . .'
     A            C2FSKO    R        B 11 31REFFLD(VPFSKO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 48'WEB  . . . . . '
     A            C2WEB          1A  O 11 64
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 24'Kostpris'
     A  80                             15 66'Omr.faktor'
     A                                 15  2'Enh'
     A                                 14 11'Prisforslag'
     A                                 14 24'prisf per.'
     A  80                             14 54'Salgspris'
     A  80                             14 66'volum'
     A            C2FEN1    R        B 16  2REFFLD(VPFEN1)
     A  32                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FOR1    R        B 16 11REFFLD(VPFOR1)
     A  31                                  ERRMSGID(VMT0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FKO1    R        B 16 24REFFLD(VPFKO1)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FIN1    R        B 16 37REFFLD(VPFKO1)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP1    R        O 16 52REFFLD(VPFOR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2FEN2    R        B 17  2REFFLD(VPFEN2)
     A  34                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FOR2    R        B 17 11REFFLD(VPFOR2)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FKO2    R        B 17 24REFFLD(VPFKO2)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FIN2    R        B 17 37REFFLD(VPFKO2)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP2    R        O 17 52REFFLD(VPFOR1)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A  80        C2OMR2        12Y 6O 17 67
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A            C2FEN3    R        B 18  2REFFLD(VPFEN3)
     A  36                                  ERRMSGID(VME0200 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(VMT0110 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FOR3    R        B 18 11REFFLD(VPFOR3)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FIN3    R        B 18 37REFFLD(VPFKO3)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A  80        C2SAP3    R        O 18 52REFFLD(VPFOR1)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A  80        C2OMR3        12Y 6O 18 67
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 24  2'F3=Gå tilbake   F4=Spørring'
     A                                 20 41'Sist endret'
     A            C2EDAT         6Y 0O 20 53COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20 63'/'
     A            C2EUSR    R        O 20 65REFFLD(VPFEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2FKO3    R        B 18 24REFFLD(VPFKO3)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 37'Innkjøpspris'
     A                                 15 38'prisf.per'
     A                                 13  2'Sendt dato . . . . . . . . .'
8.01 A            C2SDAT         6Y 0O 13 32COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
8.01 A*  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A                                      OVRDTA
     A                                 21 41'WEB endret'
     A            C2SEDA         6Y 0  21 54COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2SETI         6Y 0  21 66COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 63't'
     A                                 22  2'Solver Ver.'
     A            C2SVER    R   -8   O 22 14REFFLD(VPFVER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22 25'Id:'
     A            C2FFID    R   -8   O 22 31REFFLD(VPFFID)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22 41'Vid:'
     A            C2FTMS    R        O 22 47REFFLD(VPFTMS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 48'Prisprofil . . '
     A            C2PROF         1A  O 12 64COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 18 50)
     A                                  2  2' Slette tilbudspris '
     A                                      DSPATR(RI)
     A            D1FVAR    R        O  7 21REFFLD(VPFVAR)
     A                                  8  2'Prisgruppe . . . .'
     A            D1FPRG    R        O  8 21REFFLD(VPFPRG)

     A                                 10  2'Fra dato/tidspunkt'
     A            D1FDAT         6Y 0O 10 21EDTWRD('  .  .  ')
     A                                 10 30'-'
     A            D1TDAT         6Y 0O 11 21EDTWRD('  .  .  ')
     A                                 11 30'-'
     A            VPFSKO    R        O 13 21
     A                                 14  2'Enhet. . . . . . .'
     A            VPFEN1    R        O 14 21
     A                                 15  2'Salgspris. . . . .'
     A            VPFOR1    R        O 15 21
     A                                 16  2'Kostpris . . . . .'
     A            VPFKO1    R        O 16 21
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  110735  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
6.20 A                                      WINDOW(*DFT 20 50)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  1' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer . . . .'
     A            K1FVAR    R   -7   O  4 21REFFLD(VPFVAR)
     A                                      OVRDTA
     A                                  5  2'Prisgruppe . . . .'
     A            K1PRGR    R        B  5 21REFFLD(VPFPRG)
     A  43                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Fra dato/tidspunkt'
     A            K1FDAT         6Y 0O  7 21
     A                                      OVRDTA
     A                                  8  2'Til dato/tidspunkt'
     A            K1TDAT         6Y 0O  8 21
     A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 13  2'Varenummer . . . .'
     A            VPFVAR    R        O 13 21OVRDTA
     A                                 14  2'Prisgruppe . . . .'
     A            VPFPRG    R        O 14 21OVRDTA

     A                                 16  2'Fra dato/tidspunkt'
     A            K2FDAT         6Y 0O 16 21EDTWRD('  .  .  ')
     A                                      OVRDTA

     A                                 17  2'Til dato/tidspunkt'
     A            K2TDAT         6Y 0O 17 21EDTWRD('  .  .  ')
     A                                      OVRDTA

     A                                 19  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20150722  084508  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 70)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgruppe      . . .+'
     A            F1FPRG    R        B  4 25REFFLD(VPFPRG)
     A  31                                  ERRMSGID(FOM0150 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  6  2'Overgruppe  . . . . +'
     A            F1OGRP         2Y 0B  6 25
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            F1OTXT    R        O  6 35REFFLD(FÅTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  7  2'Hovedgruppe . . . . +'
     A            F1HGRP         2Y 0B  7 25
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            F1HTXT    R           7 35REFFLD(FÅTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  8  2'Undergruppe . . . . +'
     A            F1UGRP         3Y 0B  8 25
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            F1UTXT    R           8 35REFFLD(FÅTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20051021  083406  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'NB Ingen automatisk prisopp-'
     A                                  3  2'datering av alle enheter !!'
     A                                  6  2'ENTER=Fortsett'
     A                                      COLOR(BLU)



