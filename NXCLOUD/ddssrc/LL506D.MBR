     A*%%TS  SD  20020924  130941  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL506D
     A* Beskrivelse . . : Lagerspørring, disponibelt pr. dato
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.NU A* 6.30  061114 BSA  Utvidet pga av nummerutvidelser
7.00 A* K00   060520 BSA  Takler brede skjermbilder
8.01  * 800   211124 ask lagt inn mulighet for å vise
8.01  *                  disponibel beholdning med bekr. NXS-20463
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20001227  094139  ASK         REL-V4R4M0  5769-PW1
6.nu A            B1NUMM         8Y 0O  8  2EDTCDE(Z)
6.nu A            B1SUFF         2Y 0O  8 11EDTCDE(Z)
6.nu A            B1OTYP         2A  O  8 16
6.nu A            B1NAVN        28A  O  8 20
8.01 A            B1DATO         6Y 0O  8 54EDTWRD('  .  .  ')
8.01 A            B1DATB         6Y 0O  8 70EDTWRD('  .  .  ')
8.01 A N30                                  DSPATR(ND)
8.01 A            B1ANTA         9Y 2O  8 87EDTWRD('     0 ,  -')
8.01 A            B1ANTC         9Y 2O  8103EDTWRD('     0 ,  -')
     A  41                                  COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20001227  101353  ASK         REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Disponibel saldo pr. dato'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL506B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
8.01 A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare  '
     A            B2VARE    R   -7   B  4  9REFFLD(LDVARE)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A            B2TEK1    R        O  4 18REFFLD(LDTEK1)
     A                                      COLOR(YLW)
     A                                  5  2'Lager'
     A            B2LAGE         2S 0B  5  9COLOR(YLW)
     A            B2LTEK        20A  O  5 18COLOR(YLW)
     A                                  5 54'Lagerenhet'
     A            B2ENHL         3A  O  5 65COLOR(YLW)
7.xx A                                  7  2'Ord/bes '
     A                                      COLOR(WHT)
7.xx A                                  7 11'Su'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Ty'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Kunde/Leverandør            '
8.01 A                                      COLOR(WHT)
8.01 A  30                              7 50'Ubekreftet dato'
8.01 A  30                                  COLOR(WHT)
8.01 A N30                              7 55'Dato'
8.01 A N30                                  COLOR(WHT)
8.01 A  30                              7 68'Bekreftet dato'
8.01 A  30                                  COLOR(WHT)
8.01 A N30                              7 68'              '
8.01 A N30                                  COLOR(WHT)
8.01 A                                  7 91'Antall   '
8.01 A                                      COLOR(WHT)
8.01 A                                  7103'Disp.saldo'
8.01 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020924  130941  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 58' '
     A          R ASUME                     ASSUME
     A                                 24 79' '
