     A*%%TS  SD  20030206  130502  HKO         REL-V5R1M0  5722-WDS
     A*------------------------------------------------------------*
     A* 6.30 181016 bsa Nytt program                               *
     A*                                                            *
7.xx A*      131118 bsa Skrevet om til ny GUI.                     *
     A*                                                            *
8.01 A*K0000 301122 bsa Lagt til RDS lager                         *
     A*                                                            *
     A*                                                            *
     A*------------------------------------------------------------*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030206  130046  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1OLAG    R        O  8  4REFFLD(VVOGRP)
     A            B1OOGR    R        O  8  7REFFLD(VVOGRP)
     A            B1OHGR    R        O  8 11REFFLD(VVHGRP)
     A            B1OUGR    R        O  8 15REFFLD(VVUGRP)
     A            B1OMOD    R        O  8 19REFFLD(VVNMOD)
     A            B1OVAR    R   -7   O  8 28REFFLD(VVVARE)
     A            B1OPOM        10A  O  8 37
8.01 A            B1ORLA    R        O  8 48REFFLD(VLLAGE)
8.01 A            B1OTXT        27A  O  8 51
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030206  130133  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF04
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av plukkområdeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Lg Ogr Hgr Ugr  Modul    Vare  -
7.xx A                                        Pl.område'
     A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A*                                 6  2'V Lg Ogr Hgr Ugr  Modul    Vare  -
7.xx A*                                       Pl.område  Beskrivelse'
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  7'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Modul'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 29'Vare  '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 37'Pl.område'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 48'Rl'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 51'Beskrivelse           '
     A                                      COLOR(WHT)
7.xx A            B2OLAG    R        B  5  4REFFLD(VVOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2OOGR    R        B  5  7REFFLD(VVOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2OHGR    R        B  5 11REFFLD(VVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2OUGR    R        B  5 15REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2OMOD    R        B  5 19REFFLD(VVNMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2OVAR    R   -7   B  5 28REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OPOM        10A  B  5 37
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20030204  085104  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av plukkområdeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
8.01 A                                  5  2'Lager. . . . . . . . . . . +'
     A            C1OLAG    R        B  5 31REFFLD(VLLAGE)
     A  31                                  ERRMSG('Fyll inn verdier i minst et-
     A                                       felt')
     A  35                                  ERRMSG('Ugyldig kombinasjon av vare-
     A                                      grupperinger')
     A  40                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        35A  O  5 40COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6  2'Overgruppe . . . . . . . . +'
     A            C1OOGR    R        B  6 31REFFLD(VVOGRP)
     A  32                                  ERRMSGID(LM00210 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  6 40COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  7  2'Hovedgruppe. . . . . . . . +'
     A            C1OHGR    R        B  7 31REFFLD(VVHGRP)
     A  33                                  ERRMSGID(LM00220 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  7 40COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  8  2'Undergruppe. . . . . . . . +'
     A            C1OUGR    R        B  8 31REFFLD(VVUGRP)
     A  34                                  ERRMSGID(LM00230 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  8 40COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  9  2'Modulnummer. . . . . . . . +'
     A            C1OMOD    R        B  9 31REFFLD(VVNMOD)
     A  36                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTXT        35A  O  9 40COLOR(YLW)
     A                                      OVRDTA
8.01 A                                 10  2'Varenummer . . . . . . . . +'
     A            C1OVAR    R   -7   B 10 31REFFLD(VVVARE)
     A  37                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT        35A  O 10 40COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020821  091008  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Områdeknytning finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av plukkområdeknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager. . . . . . . . . . . .'
     A            C1OLAG    R        O  5 31REFFLD(VLLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT        35A  O  5 40COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Overgruppe . . . . . . . . .'
     A            C1OOGR    R        O  6 31REFFLD(VVOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  6 40COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Hovedgruppe. . . . . . . . .'
     A            C1OHGR    R        O  7 31REFFLD(VVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  7 40COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Undergruppe. . . . . . . . .'
     A            C1OUGR    R        O  8 31REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  8 40COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnummer. . . . . . . . .'
     A            C1OMOD    R        O  9 31REFFLD(VVNMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTXT        35A  O  9 40COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenummer . . . . . . . . .'
     A            C1OVAR    R   -7   O 10 31REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT        35A  O 10 40COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Plukkområde. . . . . . . . .'
     A            C2OPOM        10A  B 12 31
     A  41                                  ERRMSG('Plukkområde og beskrivelse -
     A                                      må fylles ut')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Beskrivelse. . . . . . . . .'
     A            C2OTXT        30A  B 13 31
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
8.01 A                                 14  2'RDS lager. . . . . . . . . .'
8.01 A            C2ORLA    R        B 14 31REFFLD(VLLAGE)
8.01 A  42                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 42)
8.01 A                                      COLOR(YLW)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
8.01 A            C2RLTX        35A  O 14 40COLOR(YLW)
8.01 A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2OODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2OEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2OEUS    R        O 20 31REFFLD(VKEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030206  130307  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette knytning '
     A                                      DSPATR(RI)
     A                                  4  2'Lager. . . . . . .'
     A            D1OLAG    R        O  4 21REFFLD(VLLAGE)
     A                                  5  2'Overgruppe . . . .'
     A            D1OOGR    R        O  5 21REFFLD(VVOGRP)
     A                                  6  2'Hovedgruppe. . . .'
     A            D1OHGR    R        O  6 21REFFLD(VVHGRP)
     A                                  7  2'Undergruppe. . . .'
     A            D1OUGR    R        O  7 21REFFLD(VVUGRP)
     A                                  8  2'Modul. . . . . . .'
     A            D1OMOD    R        O  8 21REFFLD(VVNMOD)
     A                                  9  2'Varenummer . . . .'
     A            D1OVAR    R   -7   O  9 21REFFLD(VVVARE)
     A                                 10  2'Plukkområde. . . .'
     A            D1OPOM        10A  O 10 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20030206  130502  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 19 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  3  2'Lager. . .  . . .'
     A            K1OLAG    R        B  3 20REFFLD(VLLAGE)
     A  31                                  ERRMSG('Fyll inn verdier i minst et-
     A                                       felt')
     A  35                                  ERRMSG('Ugyldig kombinasjon av vare-
     A                                      grupperinger')
     A  40                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4  2'Overgruppe  . . .'
     A            K1OOGR    R        B  4 20REFFLD(VVOGRP)
     A  32                                  ERRMSGID(LM00210 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Hovedgruppe . . .'
     A            K1OHGR    R        B  5 20REFFLD(VVHGRP)
     A  33                                  ERRMSGID(LM00220 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Undergruppe . . .'
     A            K1OUGR    R        B  6 20REFFLD(VVUGRP)
     A  34                                  ERRMSGID(LM00230 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Modulnummer . . .'
     A            K1OMOD    R        B  7 20REFFLD(VVNMOD)
     A  36                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varenummer. . . .'
     A            K1OVAR    R   -7   B  8 20REFFLD(VVVARE)
     A  37                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  2'Lager. . .  . . .'
     A            LPOLAG    R        O 11 20REFFLD(VLLAGE)
     A                                      OVRDTA
     A                                 12  2'Overgruppe. . . .'
     A            LPOOGR    R        O 12 20REFFLD(VVOGRP)
     A                                      OVRDTA
     A                                 13  2'Hovegruppee . . .'
     A            LPOHGR    R        O 13 20REFFLD(VVOGRP)
     A                                      OVRDTA
     A                                 14  2'Undergruppe . . .'
     A            LPOUGR    R        O 14 20REFFLD(VVUGRP)
     A                                      OVRDTA
     A                                 15  2'Modulnummer . . .'
     A            LPOMOD    R        O 15 20REFFLD(VVNMOD)
     A                                      OVRDTA
     A                                 16  2'Varenummer. . . .'
     A            LPOVAR    R        O 16 20REFFLD(VVVARE)
     A                                      OVRDTA
     A                                 18  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
