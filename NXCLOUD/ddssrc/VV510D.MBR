     A*%%TS  SD  20140821  140732  BOF630      REL-V7R1M0  5770-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A* BOF 21.08.14 : Utvidet med erstattet av varenr.
7.xx A* 7.xx  110117  bsa  Justeringer basert på ny GUI og regler
7.02 A* R6.30 270616 BSA  Lagt inn F tast for VA info
     A*
8.01 A*PRG2   150622 bsa  Utvidet prisgruppe far 1 til 2 posisjoner
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20090505  131653  X50TEST     REL-V5R3M0  5722-WDS
8.01 A            B1PRGR    R        O 15  2REFFLD(VPPRGR)
     A                                      COLOR(YLW)
     A            B1ENHE    R        O 15  5REFFLD(VPENHE)
     A                                      COLOR(YLW)
     A            B1LETY    R        O 15  9REFFLD(VPLETY)
     A                                      COLOR(YLW)
     A            B1PDAT         6Y 0O 15 11COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A            B1SAPR    R        O 15 20REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A            B1KOPR    R   -1   O 15 31REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            B1INPR    R   -1   O 15 41REFFLD(VPINPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            B1BUPR    R   -1   O 15 51REFFLD(VPBUPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            B1DEKN         4Y 2O 15 61COLOR(YLW)
     A                                      EDTCDE(M)
     A            B1OMRE    R        O 15 68REFFLD(VEOMRE)
     A                                      COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140821  140732  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF02
     A                                      CA03
7.02 A                                      CF10
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
6.32 A                                      CF21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise vare'
     A                                      COLOR(WHT)
7.xx A                                  2 38'VV510B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer. . .'
     A            B2VARE    R   -7   O  4 18REFFLD(VVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 56'Varetype. . . .'
     A            B2VTYP    R        O  4 72REFFLD(VVVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  80                              4 77' N '
     A                                      COLOR(YLW)
     A                                      DSPATR(RI)
     A                                  5  2'Varetekst . . .'
     A            B2TEK1    R        O  5 18REFFLD(VVTEK1)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 56'Priskode. . . .'
     A            B2KPRI    R        O  5 72REFFLD(VVKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Varetekst 2 . .'
     A            B2TEK2    R        O  6 18REFFLD(VVTEK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 56'Modulnummer . .'
     A            B2NMOD    R        O  6 72REFFLD(VVNMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe. . .'
     A            B2OGRP    R        O  7 18REFFLD(VVOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2HGRP    R        O  7 21REFFLD(VVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UGRP    R        O  7 24REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UTXT        24A  O  7 29COLOR(YLW)
     A                                      OVRDTA
     A                                  7 56'Nobbnummer. . .'
     A            B2NOBB    R        O  7 72REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør. . .'
     A            B2LDOR    R        O  8 18REFFLD(VVLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LNAV        24A  O  8 29COLOR(YLW)
     A                                      OVRDTA
     A                                  8 56'Nobb prisgiver.'
     A            B2NLEV    R        O  8 72REFFLD(VVNLEV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Lev. varenummer'
     A            B2LVAR    R        O  9 18REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 56'Erstattes av  .'
     A            B2ENOB    R        O  9 72REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 56'Nobb utgår-dato'
     A            B2NUDA         6Y 0O 10 72COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Sammenlign.enh.'
     A            B2ENPS    R        O 10 18REFFLD(VVENPS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 56'Utgår dato. . .'
     A            B2UDAT         6Y 0O 11 72COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 11  2'Stat/lagerenhet'
     A            B2ENHS    R        O 11 18REFFLD(VVENHS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ENHL    R        O 11 22REFFLD(VVENHL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Basisenhet. . .'
     A            B2ENHE    R        O 12 18REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2ODAT         6Y 0O 12 50COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12 59'/'
     A            B2EDAT         6Y 0O 12 61COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12 70'/'
     A            B2EUSR    R   -1   O 12 72REFFLD(VVEUSR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 14  2'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                 14  5'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 14  9'L'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 11'Dato    '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 20'Salgspris '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 31'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 41'Innk.pris'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 51'But.pris'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 61'DG    '
7.xx A                                      COLOR(WHT)
7.xx A                                 14 68'Omr.fakt vol.'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20000321  131222  HKO         REL-V4R4M0  5769-PW1
7.02 A                                 22  2'F3=Gå tilbake  F2=Notat  F10=VA In-
7.02 A                                      fo  F13=Bilde-2  F14=Enheter  F15=-
7.02 A                                      Priser'
     A                                      COLOR(BLU)
     A                                 23  2'F16=Søketekster   F17=GTIN-nummer -
6.32 A                                       F18=Tilbudspriser  F21=Vare-lev.'
     A                                      COLOR(BLU)
