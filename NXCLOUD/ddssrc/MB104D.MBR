     A*%%TS  SD  20050902  103045  BOF540      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSERV
     A* Format. . . . . : MB104D
     A* Beskrivelse . . : ABC-detaljer alle
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       080904 bø   Nytt program
7.xx A* 7.xx  170316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050307  205044  BOF530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFLAGR)
     A            B1KABC    R        O 10  5REFFLD(MBKABC)
     A            B1VARE    R   -7   H      REFFLD(MBVARE)
     A            B1LABC         1A  H
     A            B1NAVN        35A  H
     A            B1ANTO    R   -2   O 10 48REFFLD(MBANTO)
     A                                      EDTCDE(L)
     A            B1OMSE    R        O 10 59REFFLD(MBOMSE)
     A                                      EDTCDE(L)
     A            B1ABPR         6Y 3O 10 73EDTCDE(L)
     A            B1TEXT        35A  O 10  8
     A            B1OABC    R        O 10 44REFFLD(MBOABC)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050902  103045  BOF540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA08
     A                                      CA09
     A                                      CF06
     A                                      CF11
     A                                      CF13
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'ABC-analyse detaljer - Totalt'
     A                                      COLOR(WHT)
7.xx A                                  2 38'MB104B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varesort.'
     A            B2VSOR    R        O  4 12REFFLD(MBVSOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Ogrp.'
     A            B2OGRP    R        O  5  8REFFLD(MBOGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 11'Hgrp.'
     A            B2HGRP    R        O  5 17REFFLD(MBHGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 20'Ugrp.'
     A            B2UGRP    R        O  5 26REFFLD(MBUGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 31'Lager'
     A            B2LAGK    R        O  5 37REFFLD(MBLAGK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  4 50'Prosentfordeling'
     A                                      COLOR(WHT)
7.xx A                                  4 68'ABC'
7.xx A                                  4 75'XYZ'
7.xx A                                  5 51'A / X'
7.xx A            B2ABAP    R        O  5 68REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2XYXP    R        O  5 75REFFLD(MAXYXP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 51'B / Y'
7.xx A            B2ABBP    R        O  6 68REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2XYYP    R        O  6 75REFFLD(MAXYYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 51'C / Z'
7.xx A            B2ABCP    R        O  7 68REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2XYZP    R        O  7 75REFFLD(MAXYZP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  4'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 19'Ord.linjer'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 31'Omsetning'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'2=Endre kode  5=Mer info         '
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'ABC'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  8'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 10'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 19'Varenavn     '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 45'OAbc'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 50'Ord.linjer'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 61'Omsetning  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 74' %vis'
     A                                      COLOR(WHT)
7.xx A            B2KABC    R        B  7  5REFFLD(MBKABC)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  7  8'+'
7.xx A            B2VARE    R   -7   B  7 10REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ANTO    R   -2   B  7 19REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OMSE    R        B  7 30REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A**                                9 19'F4'
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
7.xx A                                 23  2'F1=Bla-alt F3=Gå tilbake F5=Forny-
7.xx A                                       F8=Omsetning F9=Ordrel. F11=Skift-
7.xx A                                       F13=Info'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20041217  103407  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre kode på ABC analyse  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenavn   . . . . . . . . .'
     A            C2NAVN        30A  O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Antall ordrelinjer . . . . .'
     A            C2ANTO    R        O  8 31REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A            C2ENHS    R        O  8 45REFFLD(LÅENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Omsetning  . . . . . . . . .'
     A            C2OMSE    R        O  9 31REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 10  2'ABC-kode - analyse . . . . .'
     A            C2OABC    R        O 10 31REFFLD(MBOABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'ABC-kode - endre . . . . . .'
     A            C2EABC    R        B 11 31REFFLD(MBKABC)
     A  31                                  ERRMSGID(MMB0070 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KLAB    R        B 11 35REFFLD(LÅJANE)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 38'1=Låst 0=Ikke låst'
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2USER    R        O 20 31REFFLD(MBUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20050307  160128  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre kode på ABC analyse  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenavn   . . . . . . . . .'
     A            C3NAVN        30A  O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Antall ordrelinjer . . . . .'
     A            C3ANTO    R        O  8 31REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                  9  2'Antall solgt . . . . . . . .'
     A            C3ANTA    R        O  9 31REFFLD(MBANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A            C3ENHS    R        O  9 45REFFLD(LÅENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Kostpris . . . . . . . . . .'
     A            C3PRIS        11Y 2O 10 31COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 11  2'Omsetning  . . . . . . . . .'
     A            C3OMSE    R        O 11 31REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 12  2'ABC-kode opprinnelig . . . .'
     A            C3OABC    R        O 12 31REFFLD(MBOABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'ABC-kode endret  . . . . . .'
     A            C3EABC    R        O 13 31REFFLD(MBKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'ABC-kode låst  . . . . . . .'
     A            C3KLAB    R        O 14 31REFFLD(MBKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C3ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C3EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C3USER    R        O 20 31REFFLD(MBUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A**                               23  2'F3=Gå tilbake   F4=Spørring'
7.xx A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050131  183644  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg ABC-kode .:'
     A            H1KABC    R        B  4 19REFFLD(MBKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 26'Blank gir alle'
     A                                  5 26'Eks. AX,A , X'
     A                                  6  2'Bare låste  . .:'
     A            H1BARL    R        B  6 19REFFLD(LÅJANE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6 26'1=Ja'
     A                                  7  2'Låst til ABC   :'
     A            H1LABC    R        B  7 19REFFLD(MBKABC)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20050308  082917  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                  2  2'Forklaring :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'* '
     A                                  4  5'Hvis en vare har låst ABC-kode vil-
     A                                         '
     A                                  5  5'disse være merket med *           -
     A                                          '
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
