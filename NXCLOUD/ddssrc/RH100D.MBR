     A*%%TS  SD  20210609  103603  ASP_BMS     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH100D
     A* Beskrivelse . . : Kontoplan, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
7.01 A*       290320  bof  Lagt inn saft endringer
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  235714  KOB         REL-V5R1M0  5722-WDS
     A            B1ALFA    R        H      REFFLD(RHALFA)
7.xx A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 8)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1KONT    R        O  9  5REFFLD(RHKONT)
     A                                      EDTCDE(Z)
7.xx A            B1SPES    R        O  9 11REFFLD(RHSPES)
     A                                      EDTCDE(Z)
7.xx A            B1AVDE    R        O  9 16REFFLD(RHAVDE)
     A                                      EDTCDE(Z)
7.xx A            B1SPRK    R        O  9 22REFFLD(RHSPRK)
7.xx A            B1MVAK    R        O  9 28REFFLD(RHMVAK)
7.xx A            B1MVAO    R        O  9 35REFFLD(RHMVAO)
7.xx A            B1MVAG    R        O  9 39REFFLD(RHMVAG)
7.xx A            B1TEXT    R        O  9 49REFFLD(RHTEXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020328  150823  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontoplan'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 21'Tekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       8=Poster'
     A                                      COLOR(BLU)
7.xx A*                                 7 51'Sperr'
7.xx A*                                     COLOR(WHT)
7.xx A**                                7 58'<----- MVA ----->'
7.xx A**                                    COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'Tekst          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 22'Sper'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 28'Mva'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 35'O'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 39'Overstyr'
     A                                      COLOR(WHT)
7.xx A            B2KONT    R        B  5  5REFFLD(RHKONT)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2SPES    R        B  5 11REFFLD(RHSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2AVDE    R        B  5 16REFFLD(RHAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  5 21REFFLD(RHALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                                7 51'kode   kode overstyring'
     A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020328  151504  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontoplan'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Konto. . . . . . . . . . . .'
     A            C1KONT    R        B  5 31REFFLD(RHKONT)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C1SPES    R        B  5 37REFFLD(RHSPES)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            C1AVDE    R        B  5 42REFFLD(RHAVDE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020328  151504  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kontonummer finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20210609  103603  ASP_BMS     REL-V7R3M0  5770-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontoplan'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontonummer. . . . . . . . .'
     A            C1KONT    R        O  5 31REFFLD(RHKONT)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            C1SPES    R        O  5 37REFFLD(RHSPES)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            C1AVDE    R        O  5 42REFFLD(RHAVDE)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  94        C2NOTB         8A  O  5 50DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2TEXT    R        B  7 31REFFLD(RHTEXT)
     A  36 81                               ERRMSGID(RMH0200 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(PC)
     A                                      OVRDTA
     A            C2TXT2    R        B  8 31REFFLD(RHTXT2)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'Alfabetisk sortering . . . .'
     A            C2ALFA    R        B  9 31REFFLD(RHALFA)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'MVA-kode . . . . . . . . . .'
     A            C2MVAK    R        B 10 31REFFLD(RHMVAK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RMH0240 *LIBL/QMFOKON 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 33'(kode)'
     A            C2ETXT    R        O 10 40REFFLD(RAETXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Overstyring. . . . . . . . .'
     A            C2MVAO    R        B 11 31REFFLD(RHMVAO)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A*                                     VALUES('J' 'N')
     A                                 11 33'J/N'
     A            C2MVAG    R        B 11 37REFFLD(RHMVAG)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Budsjett-nøkkel. . . . . . .'
     A            C2BNØK    R        B 12 31REFFLD(RHBNØK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32 81                               ERRMSGID(RMH0210 *LIBL/QMFOKON 32)
     A                                      OVRDTA
     A            C2MTXT    R        O 12 34REFFLD(RAMTXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Valutakode . . . . . . . . +'
     A            C2VALK    R        B 13 31REFFLD(RHVALK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RMH0220 *LIBL/QMFOKON 34)
     A                                      OVRDTA
     A            C2PMNA    R        O 13 35REFFLD(RAPMNA)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Mengdekode . . . . . . . . +'
     A            C2MNGD    R        B 14 31REFFLD(RHMNGD)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RMH0220 *LIBL/QMFOKON 33)
     A                                      OVRDTA
     A            C2OTXT    R        O 14 34REFFLD(RAOTXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Prosjektkode . . . . . . . .'
     A            C2PROJ    R        B 15 31REFFLD(RHPROJ)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N' ' ')
     A                                 15 34'J/N/ '
     A                                 16  2'Kostnadsbærerkode. . . . . .'
     A            C2BÆRK    R        B 16 31REFFLD(RHBÆRK)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES('J' 'N' ' ')
     A                                 16 34'J/N/ '
     A                                 17  2'Avdelingskode. . . . . . . .'
     A            C2AVDK    R        B 17 31REFFLD(RHAVDK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      VALUES('J' 'N' ' ')
     A                                 17 34'J/N/ '
     A                                 18  2'Nøkkeltall-kode. . . . . . .'
     A            C2NØKK    R        B 18 31REFFLD(RHNØKK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 34'0 - 9'
     A                                 19  2'Sperret for postering. . . .'
     A            C2SPRK    R        B 19 31REFFLD(RHSPRK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      VALUES('J' ' ')
     A                                 19 34'(J=Ja)'
     A                                 20  2'Std.konto (SAF-T). . . . . +'
     A            C2REFN    R        B 20 31REFFLD(RHREFN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  37
     AO 38                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RMH0470 *LIBL/QMFOKON 37)
     A  38 81                               ERRMSGID(RMH0480 *LIBL/QMFOKON 38)
     A                                      OVRDTA
     A            C2REFT    R        B 20 36REFFLD(RHREFT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 21  2'Kontoplangruppe. . . . . . .'
     A            C2KPG1    R        B 21 31REFFLD(RHKPG1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KPG2    R        B 21 35REFFLD(RHKPG2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2KPG3    R        B 21 39REFFLD(RHKPG3)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KPG4    R        B 21 43REFFLD(RHKPG4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KPG5    R        B 21 47REFFLD(RHKPG5)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'Sist endret dato .'
     A            C2EDAT         6Y 0O 22 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 31'Endret av. . .'
     A            C2EUSR    R        O 22 46REFFLD(RHEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F2=Notatark'
     A                                      COLOR(BLU)
     A                                 23 16'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 32'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020821  225320  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette konto '
     A                                      DSPATR(RI)
     A                                  4  2'Konto. . . . . . .'
     A            D1KONT    R        O  4 21REFFLD(RHKONT)
     A  32                                  ERRMSGID(RMH0250 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMH0260 *LIBL/QMFOKON 33)
     A            D1SPES    R        O  4 27REFFLD(RHSPES)
     A            D1AVDE    R        O  4 32REFFLD(RHAVDE)
     A                                  6  2'Beskrivelse. . . .'
     A            RHTEXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020328  153110  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Konto. . . . . . .'
     A            K1KONT    R        B  4 21REFFLD(RHKONT)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            K1SPES    R        B  4 27REFFLD(RHSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            K1AVDE    R        B  4 32REFFLD(RHAVDE)
     A  32                                  ERRMSGID(RMA0020 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Konto. . . . . . .'
     A            RHKONT    R        O  8 21
     A            RHSPES    R        O  8 27
     A            RHAVDE    R        O  8 32
     A                                  9  2'Beskrivelse. . . .'
     A            RHTEXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
