     A*%%TS  SD  20171002  115957  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP602D
     A* Beskrivelse . . : Arb.ordre,spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       181110 NHO  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A          R B1SFL                     SFL
     A*%%TS  SD  20170927  124207  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B14TXT        23A  O  7 25
     A            B14PRO    R        O  7  4REFFLD(RP3PRO)
     A  42                                  COLOR(RED)
     A            B14UPR    R        O  7 11REFFLD(RP3UPR)
     A  43                                  COLOR(RED)
     A            B14AKT    R        O  7 18REFFLD(RP3AKT)
     A  44                                  COLOR(RED)
     A            B14STS    R        O  7 50REFFLD(RP3STS)
     A            B14TX1    R        H      REFFLD(RP3TXT)
     A            B14TX2    R        H      REFFLD(RP3TX2)
     A            B14KUN    R        H      REFFLD(RP3KNR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20171002  115957  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Spørring aktivitet'
     A                                      DSPATR(RI)
     A                                  3  4'Prosj. Uprosj Aktiv. Beskrivelse-
     A                                                   St.         '
     A                                      COLOR(WHT)
     A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Uprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 18'Aktiv.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 25'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 50'St.'
7.xx A                                      COLOR(WHT)
     A            B2ALFA        23A  B  4 25COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            B24PRO    R        B  4  4REFFLD(RP3PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B24UPR    R        B  4 11REFFLD(RP3UPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B24AKT    R        B  4 18REFFLD(RP3AKT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B24STS    R        B  4 50REFFLD(RP3STS)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020106  234033  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 60)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
