     A*%%TS  SD  20071014  092809  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : SF502D
     A* Beskrivelse . . : Spørring på salg av vare
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
9.01 A* 6.30  180517 BSA  Lagt inn oppslag mot arkiv
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20071014  092809  KOB550      REL-V5R4M0  5722-WDS
     A            B1LINE    R        H      REFFLD(SFLINE)
     A            B1WSID    R        H      REFFLD(SFWSID)
     A            B1BONG    R        H      REFFLD(SFBONG)
     A            B1VALG         1Y 0B 10  2COLOR(YLW)
9.01 A                                      VALUES(0 5 6 7 9)
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A                                      EDTCDE(Z)
     A            B1VKUN    R        O 10  4REFFLD(SFVKUN)
6.nu A            B1KNAV        13A  O 10 11
6.nu A            B1KPRO    R        O 10 25REFFLD(SFKPRO)
6.nu A            B1PNAV         9A  O 10 32
6.nu A            B1BIDA         6Y 0O 10 42EDTCDE(Z)
6.nu A            B1BINR    R        O 10 49REFFLD(SFBINR)
6.nu A            B1ORNR    R        O 10 58REFFLD(SFORNR)
     A            B1OTYP    R        O 10 67REFFLD(SFOTYP)
     A            B1ANTA    R   -2   O 10 70REFFLD(SFANTA)
     A                                      EDTCDE(M)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20071012  133224  KOB550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
6.31 A  40                                  CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på salg på vare'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SF502B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Vare . .'
     A                                      COLOR(BLU)
7.xx A            B2VARE    R   -7   O  4 11REFFLD(SFVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2TEK1    R     A  O  4 20REFFLD(FFRFR/VVTEK1 *LIBL/FFRF)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  4 65'År . . .'
     A                                      COLOR(BLU)
     A            B2PAAR    R        O  4 74REFFLD(SFPAAR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5 65'Dato . .'
     A                                      COLOR(BLU)
     A            B2BID1         6Y 0O  5 74COLOR(BLU)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  5  2'Kunde. .'
     A                                      COLOR(BLU)
7.xx A            B2VKU1    R        O  5 14REFFLD(SFVKUN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  8  2'5=Vis  6=Skriv fakturakopi  7=Vise-
9.01 A                                       faktura/bong  9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                  5 25'Lev.adr.'
     A                                      COLOR(BLU)
7.xx A            B2LAD1    R        O  5 34REFFLD(SFKPRO)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  6  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 25'K.prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 43'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 49'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 58'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 25'K.prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 43'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 49'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 58'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 67'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 72'Antall'
7.xx A**                                8  2'Kunde                    Kundepros-
7.xx A**                                    jekt   Dato  Faktnr   Ordre    OT  -
7.xx A**                                     Antall'
     A                                      COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A            B2VKUN    R        B  7  4REFFLD(SFVKUN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 23'+'
7.xx A            B2KPRO    R        B  7 25REFFLD(SFKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BIDA         6Y 0B  7 42COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BINR    R        B  7 49REFFLD(SFBINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ORNR    R        B  7 58REFFLD(SFORNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  19980615  155607  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
6.31 A                                 23 30'F9=Eksport til excel'
6.31 A                                      COLOR(BLU)
