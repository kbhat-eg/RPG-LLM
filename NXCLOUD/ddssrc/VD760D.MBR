     A*%%TS  SD  20101111  121632  BOF610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VD760D
     A* Beskrivelse . . : Innlesing av varesortiment fra fil
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20101111  121632  BOF610      REL-V5R4M0  5722-WDS
     A*                                     WINDOW(*DFT 22 54)
     A                                      CA03
     A                                      CA12
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Innlesing av Varesortiment CSV-fi-
     A                                      l '
     A                                      DSPATR(RI)
7.xx A                                  5  2'Varesortiment . . +'
     A            B1VSOR    R        B  5 23REFFLD(VCVSOR)
     A  31                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A            B1BESK    R  -15   O  5 34REFFLD(VCBESK)
7.xx A                                  6  2'Lager   . . . . . +'
     A            B1LAGE    R        B  6 23REFFLD(VCLAGE)
     A  32                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LTXT    R  -15   O  6 34REFFLD(VCBESK)
     A                                  7  2'Type  . . . . . . .'
     A            B1TYPE    R        B  7 23REFFLD(VCTYPE)
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 12  2'Filnavn   (CSV) . .'
     A            B1FILE        20A  B 12 22COLOR(YLW)
     A  34                                  ERRMSGID(FM00600 *LIBL/QMFOFAK 34)
     A                                 13  2'Mappenavn . . . . .'
     A            B1FLDR        20A  B 13 22COLOR(YLW)
     A  35                                  ERRMSGID(FM00605 *LIBL/QMFOFAK 35)
     A                                 10  2'Fjern eksisterende.'
     A            B1SLET    R        B 10 23REFFLD(FÅJANE)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 10 26'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                  7 26' /V/C/P '
     A          R B1MSG
     A*%%TS  SD  20010904  091133  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 12 36)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Valgt mappenavn er ugyldig, filene'
     A                                  3  2'ble ikke funnet i mappen, eller du'
     A                                  4  2'mangler tilgang for behandling av'
     A                                  5  2'mapper.'
     A                                  7  2'Sjekk at filene finnes i mappen.'
     A                                  8  2'Dersom du mangler tilgang må du'
     A                                  9  2'kontakte systemansvarlig.'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
