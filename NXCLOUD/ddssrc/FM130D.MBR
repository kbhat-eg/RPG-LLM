     A*%%TS  SD  20050210  135926  GRO530      REL-V5R2M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FM130D
     A* Beskrivelse . . : Vise fakturalogg
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050210  135041  GRO530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A            B1BINR    R        O  7  4REFFLD(FNUBIN)
6.NU A            B1FATY    R        O  7 13REFFLD(FNUFAT)
6.NU A            B1BESK        15A  O  7 16
6.NU A            B1KUND    R        O  7 32REFFLD(FNUKUN)
6.NU A            B1KNAV        20A  O  7 39
6.NU A            B1KDAT         6Y 0O  7 60EDTWRD('  .  .  ')
6.NU A            B1KTIM         6Y 0O  7 70EDTWRD('  :  :  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050210  121148  GRO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fakturalogg'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  4 63'Årstall :'
     A            B2AARR    R        B  4 73REFFLD(FNUAAR)
     A                                      COLOR(YLW)
7.xx A                                  5 60'----  Kjørt  ----'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Faktnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 13'Tp'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 16'Beskr.    '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 33'Kunde     '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 61' Dato '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 71' Tid  '
7.xx A**                                6  2'V Faktnr Tp Beskr.           Kunde-
7.xx A**                                                           ----  Kjørt -
7.xx A**                                     -----'
     A                                      COLOR(WHT)
7.xx A**                                6  2'                                  -
7.xx A**                                                           Dato        -
7.xx A**                                       Tid'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050210  134706  GRO530      REL-V5R2M0  5722-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake  F5=Forny-
     A                                          '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050210  124544  GRO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fakturalogg'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  5  2'År . . . . . . . . . . . . .'
     A            C2AARR    R        O  5 31REFFLD(FNUAAR)
     A                                      COLOR(YLW)
     A                                  6  2'Fakturanummer. . . . . . . .'
     A            C2BINR    R        O  6 31REFFLD(FNUBIN)
     A                                      COLOR(YLW)
     A                                  7  2'Fakturatype. . . . . . . . .'
     A            C2FATY    R        O  7 31REFFLD(FNUFAT)
     A                                      COLOR(YLW)
     A            C2BESK    R        O  7 40REFFLD(FMTBES)
     A                                      COLOR(YLW)
     A                                  8  2'Kundenummer. . . . . . . . .'
     A            C2KUND    R        O  8 31REFFLD(FNUKUN)
     A                                      COLOR(YLW)
     A            C2KNAV        20A  O  8 40COLOR(YLW)
     A                                 10  2'Kjørt dato . . . . . . . . .'
     A            C2KDAT         6Y 0O 10 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                 11  2'Kjørt tid. . . . . . . . . .'
     A            C2KTIM         6Y 0O 11 31EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A                                 12  2'Kjørt arbeidsstasjon . . . .'
     A            C2KWSI    R        O 12 31REFFLD(FNUKWS)
     A                                      COLOR(YLW)
     A                                 14  2'Antall kjøringer . . . . . .'
     A            C2TELL    R        O 14 31REFFLD(FNUTEL)
     A                                      COLOR(YLW)
     A                                 16  2'Opprettet dato / tid . . . .'
     A            C2ODAT         6Y 0O 16 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                 16 41'/'
     A            C2OTIM         6Y 0O 16 43EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A                                 17  2'Opprettet arbeidsstasjon . .'
     A            C2OWSI    R        O 17 31REFFLD(FNUWSI)
     A                                      COLOR(YLW)
     A                                 19  2'Endret dato / tid. . . . . .'
     A            C2EDAT         6Y 0O 19 31EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                 19 41'/'
     A            C2ETIM         6Y 0O 19 43EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(FNUEUS)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F10=Se faktura   F-
     A                                      11=Reset kjøredato'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050210  124823  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 12 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette fakturaloggelement'
     A                                      DSPATR(RI)
     A                                  4  2'År . . . . . . . .'
     A            D1AARR    R        O  4 21REFFLD(FNUAAR)
     A                                  5  2'Fakturanummer. . .'
     A            D1BINR    R        O  5 21REFFLD(FNUBIN)
     A                                  6  2'Fakturatype. . . .'
     A            D1FATY    R        O  6 21REFFLD(FMTFAT)
     A            D1BESK    R        O  6 29REFFLD(FMTBES)
     A                                  7  2'Kundenummer. . . .'
     A            D1KUND    R        O  7 21REFFLD(FNUKUN)
     A            D1KNAV        20A  O  7 29
     A                                  9  2'NB! Faktura vil ikke bli sendt ele-
     A                                      ktronisk !!'
     A                                      COLOR(RED)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  20050210  125134  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 12 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Reset av fakturaloggelement'
     A                                      DSPATR(RI)
     A                                  4  2'År . . . . . . . .'
     A            E1AARR    R        O  4 21REFFLD(FNUAAR)
     A                                  5  2'Fakturanummer. . .'
     A            E1BINR    R        O  5 21REFFLD(FNUBIN)
     A                                  6  2'Fakturatype. . . .'
     A            E1FATY    R        O  6 21REFFLD(FMTFAT)
     A            E1BESK    R        O  6 29REFFLD(FMTBES)
     A                                  7  2'Kundenummer. . . .'
     A            E1KUND    R        O  7 21REFFLD(FNUKUN)
     A            E1KNAV        20A  O  7 29
     A                                  9  2'NB! Faktura vil bli sendt på nytt -
     A                                      !!'
     A                                      COLOR(RED)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050210  135926  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 12 55)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Kun usendte . .'
     A                                  4 28'1=Ja,0=Nei'
     A            H1USEN         1A  B  4 18COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Fakturatype . .'
     A            H1FATY         2Y 0B  5 18COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            H1BESK        20A  O  5 28OVRDTA
     A                                  6  2'Kundenummer . .'
     A            H1KUND         6Y 0B  6 18CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            H1KNAV        20A  O  6 28OVRDTA
     A                                  7  2'Kjørt dato. . .'
     A            H1KDAT         6Y 0B  7 18COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  8  2'Fakturanummer .'
6.NU A            H1BINR         8Y 0B  8 18COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
