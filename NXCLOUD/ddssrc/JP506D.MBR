     A*%%TS  SD  20150925  091247  ASP_G0X     REL-V7R1M0  5770-WDS
     A*%%EC
8.01 A*PRG2 140622 bof Utvidet prisgruppe fra 1 til 2 posisjoner
8.02 A*PRG2 030424 bof Gjort nødv. endringer for GUI
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20150924  130714  ASP_G0X     REL-V7R1M0  5770-WDS
     A            B1RSTS    R        H      REFFLD(JPRSTS)
     A            B1LINE    R        H      REFFLD(JPLINE)
     A            B1VALG    R        B 15  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 5 7 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1PRGR    R        O 15  4REFFLD(JPPRGR)
8.02 A            B1BETI    R   -1   O 15  7REFFLD(JPBETI)
8.01 A            B1NTPR         9Y 2O 15 43EDTCDE(M)
8.01 A            B1FAKT         8Y 5O 15 55EDTCDE(M)
8.01 A            B1RABA         5Y 2O 15 66EDTCDE(M)
8.01 A            B1AKTI         1A  O 15 74
8.01 A            B1RKOD         1A  O 15 76
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20150925  091247  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      ROLLUP(10)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge betingelse - innenfor nivå -
     A                                      som gjelder for varen '
     A                                      DSPATR(RI)
     A                                  4  2'Vare . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2VAR2    R   -7   O  4 27REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A            B2VTE2    R        O  4 37REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Betingelsesnivå:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  6  2'Leverandør . . . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  6 27REFFLD(JPLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2LNAV    R        O  6 37REFFLD(JLNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Varegruppe . . . . . . .'
     A                                      COLOR(BLU)
     A            B2OGRP    R        O  7 27REFFLD(JPOGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2HGRP    R        O  7 30REFFLD(JPHGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2UGRP    R        O  7 33REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UTXT        35A  O  7 37COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Modulnummer. . . . . . .'
     A                                      COLOR(BLU)
     A            B2MODN    R        O  8 27REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2MTE1    R        O  8 37REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer/Nobbnummer. .'
     A                                      COLOR(BLU)
     A            B2VARE    R   -7   O  9 27REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A            B2NOBB    R        O  9 37REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Prisgruppe . . . . . . .'
     A                                      COLOR(BLU)
     A            B2PRGR    R        O 10 27REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Varetype . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2VTYP    R        O 11 27REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Leveringstype. . . . . .'
     A                                      COLOR(BLU)
     A            B2LETY    R        O 12 27REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'1=Velg  5=Vis betingelse  7=Vis ra-
     A                                      battelement'
     A                                      COLOR(BLU)
8.02 A*                                14  2'V PG Betingelse                   -
8.02 A*                                            Nettopris    Faktor   Rabatt-
8.02 A*                                         R'
8.02 A*                                     COLOR(WHT)
8.02 A                                 14  2'V'
8.02 A                                      COLOR(WHT)
8.02 A                                 14  4'PG'
8.02 A                                      COLOR(WHT)
8.02 A                                 14  7'Betingelse'
8.02 A                                      COLOR(WHT)
8.02 A                                 14 43'Nettopris'
8.02 A                                      COLOR(WHT)
8.02 A                                 14 55'Faktor'
8.02 A                                      COLOR(WHT)
8.02 A                                 14 66'Rabatt'
8.02 A                                      COLOR(WHT)
8.02 A                                 14 76'R'
8.02 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990902  202428  HKO         REL-V4R2M0  5769-PW1
     A                                 21  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20150924  131050  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Aktiviser betingelse for betingels-
     A                                      esnivå'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . . . . . . . . .'
     A            C2LDOR    R        O  4 31REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LNAV    R        O  4 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  5 31REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A            C2PTXT        35A  O  5 41COLOR(YLW)
     A                                  6  2'Overgruppe . . . . . . . . .'
     A            C2OGRP    R        O  6 31REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2OTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Hovedgruppe. . . . . . . . .'
     A            C2HGRP    R        O  7 31REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2HTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Undergruppe. . . . . . . . .'
     A            C2UGRP    R        O  8 31REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UTXT        35A  O  8 41COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnummer. . . . . . . . .'
     A            C2MODN    R        O  9 31REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2MTE1    R        O  9 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O 10 31REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2VTEK    R        O 10 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Varetype . . . . . . . . . .'
     A            C2VTYP    R        O 11 31REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O 12 31REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Nobb-nummer. . . . . . . . .'
     A            C2NOBB    R        O 14 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Betingelse . . . . . . . . .'
     A            C2BETI    R        O 16 31REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Fra-dato . . . . . . . . . .'
     A            C2FDAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Til-dato . . . . . . . . . .'
     A            C2TDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endringsdato avtaleeier. . .'
     A            C2DATO         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(JPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
