     A*%%TS  SD  20140820  152811  BOF630      REL-V7R1M0  5770-WDS
     A* 200814 BOF : Innført bytt mellom butikkpris og innk.pris2
     A*%%EC
7.xx A*          220216 bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081105  082306  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.01 A            B1PRGR    R        O  8  4REFFLD(VPPRGR)
     A  50                                  COLOR(BLU)
     A            B1ENHE    R        O  8  7REFFLD(VPENHE)
     A  50                                  COLOR(BLU)
     A            B1LETY    R        O  8 12REFFLD(VPLETY)
     A  50                                  COLOR(BLU)
     A            B1PDAT         6Y 0O  8 15EDTWRD('  .  .  ')
     A  50                                  COLOR(BLU)
     A            B1SAPR    R        O  8 25REFFLD(VPSAPR)
     A  50                                  COLOR(BLU)
     A            B1KOPR    R        O  8 37REFFLD(VPKOPR)
     A  50                                  COLOR(BLU)
     A            B1INPR    R        O  8 49REFFLD(VPINPR)
     A  50                                  COLOR(BLU)
     A            B1PRIS    R        O  8 61REFFLD(VPBUPR)
     A  50                                  COLOR(BLU)
     A            B1DEKN         5Y 2O  8 73EDTCDE(M)
     A  50                                  COLOR(BLU)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110401  092551  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise/slette priser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2VARE    R   -7   O  4 23REFFLD(VPVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2TEK1    R        O  4 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5  2'Leverandør . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  5 23REFFLD(VPLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV    R        O  5 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  6  2'4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  7'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17' Dato '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 26'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 39'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Innkj.pris'
     A                                      COLOR(WHT)
     A       N80                        7 61'Butikkpris'
     A                                      COLOR(WHT)
     A        80                        7 61'Innkj.pris 2'
     A                                      COLOR(WHT)
7.xx A                                  7 74'  DG '
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990601  192528  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F1=Filter   F3=Gå tilbake   F5=For-
                                            ny   F23=Bytt   '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140820  152811  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise prisinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O  5 31REFFLD(VPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1    R        O  5 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leverandør . . . . . . . . .'
     A            C2LDOR    R   -7   O  6 31REFFLD(VPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  7 31REFFLD(VPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PTXT    R        O  7 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O  8 31REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ETXT    R        O  8 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O  9 31REFFLD(VPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LTXT    R        O  9 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Dato . . . . . . . . . . . .'
     A            C2PDAT         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12  2'Dekningsgrad . . . . . . . .'
     A            C2DEKN         5Y 2O 12 31COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 14  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        O 14 31REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Kostpris . . . . . . . . . .'
     A            C2KOPR    R        O 15 31REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Innkjøpspris innkl.frakt.bet'
     A            C2INP2    R        O 16 31REFFLD(VPINP2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Innkjøpspris . . . . . . . .'
     A            C2INPR    R        O 17 31REFFLD(VPINPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Butikkpris . . . . . . . . .'
     A            C2BUPR    R        O 18 31REFFLD(VPBUPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Nobb Pris  . . . . . . . . .'
     A            C2NOPR    R        O 19 31REFFLD(VPNOPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Nobb Prisgiver . . . . . . .'
     A            C2NLEV    R        O 20 31REFFLD(VPNLEV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Prisgivers Varenr. . . . . .'
     A            C2LVAR    R        O 21 31REFFLD(VPLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  19991102  203848  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette pris '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer . . . .'
     A            D1VARE    R        O  4 21REFFLD(VPVARE)
     A  31                                  ERRMSGID(VMP0200 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMP0210 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMP0220 *LIBL/QMFOFAK 33)
6.25 A  34                                  ERRMSG('Vare/enhet ligger på -
6.25 A                                      bestilling')
     A                                  5  2'Prisgruppe . . . .'
     A            D1PRGR    R        O  5 21REFFLD(VPPRGR)
     A                                  6  2'Enhet. . . . . . .'
     A            D1ENHE    R        O  6 21REFFLD(VPENHE)
     A                                  7  2'Leveringstype. . .'
     A            D1LETY    R        O  7 21REFFLD(VPLETY)
     A                                  8  2'Dato . . . . . . .'
     A            D1PDAT         6Y 0O  8 21EDTWRD('  .  .  ')
     A                                 10  2'Salgspris. . . . .'
     A            VPSAPR    R        O 10 21
     A                                 11  2'Kostpris . . . . .'
     A            VPKOPR    R        O 11 21
     A                                 12  2'Innkjøpspris . . .'
     A            VPINPR    R        O 12 21
     A                                 13  2'Butikkpris . . . .'
     A            VPBUPR    R        O 13 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20100303  142229  ASP_A04     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Blank + prisgrupper.  +'
     A            H1PRG1    R        B  4 26REFFLD(VPPRGR)
     A                                      DLTCHK
     A  31                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
8.01 A                                  4 29'+'
8.01 A            H1PRG2    R        B  4 31REFFLD(VPPRGR)
     A                                      DLTCHK
     A  32                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  4 34'+'
8.01 A            H1PRG3    R        B  4 36REFFLD(VPPRGR)
     A                                      DLTCHK
     A  33                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  4 39'+'
8.01 A            H1PRG4    R        B  4 41REFFLD(VPPRGR)
     A                                      DLTCHK
     A  34                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  4 44'+'
8.01 A            H1PRG5    R        B  4 46REFFLD(VPPRGR)
     A                                      DLTCHK
     A  35                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 24'+'
     A            H1PRG6    R        B  5 26REFFLD(VPPRGR)
     A                                      DLTCHK
     A  36                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  5 29'+'
8.01 A            H1PRG7    R        B  5 31REFFLD(VPPRGR)
     A                                      DLTCHK
     A  37                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  5 34'+'
8.01 A            H1PRG8    R        B  5 36REFFLD(VPPRGR)
     A                                      DLTCHK
     A  38                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  5 39'+'
8.01 A            H1PRG9    R        B  5 41REFFLD(VPPRGR)
     A                                      DLTCHK
     A  39                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.01 A                                  5 44'+'
8.01 A            H1PR10    R        B  5 46REFFLD(VPPRGR)
     A                                      DLTCHK
     A  40                                  ERRMSGID(FMO0300 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Prisdatoer . . . . .:'
7.xx A            H1DAT1         6Y 0B  7 24COLOR(YLW)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            H1DAT2         6Y 0B  7 34COLOR(YLW)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)

