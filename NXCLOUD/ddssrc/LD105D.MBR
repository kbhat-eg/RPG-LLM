6.20 A*  NHO  16.02.11   Lagt inn vis av VA info
6.20 A*
6.21 A*  NHO  11.02.11   Prosjekt og aktivitet fjernet fra bildet.
6.32 A*  BSA  15.09.14   MVA kode lagt på linjenivå
7.xx A* 7.00  030316 bsa Endringer relatert til ny GUI
7.03 A*  BOF  01.03.17   Tillate levtype = 1 under visse forutsetninger
7.05 A*  ASK  05.05.20   Lagt inn tilpasninger for bedre NewLook bilde
8.02 A* 8.00  300421 ask Lagt inn visning av linjesum i NOK ved valuta
     A*----------------------------------------------------------------
     A*%%TS  SD  20101127  111019  ASP_X50     REL-V6R1M0  5761-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3 -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
7.xx A*                                     WINDOW(*DFT 22 64)
     A          R C1WIN
     A*%%TS  SD  20101127  111019  ASP_X50     REL-V6R1M0  5761-WDS
7.xx A*                                     WINDOW(*DFT 22 64)
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF08
     A                                      CF09
6.20 A                                      CF10
6.33 A                                      CF18
     A                                      CA23
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
6.32 A            C1OMVA    R        H      REFFLD(LDOMVA)
7.xx A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
7.xx A                                      COLOR(WHT)
7.xx A                                  1 72TIME
7.xx A                                      COLOR(WHT)
     A                                  2  2' Reg./vedlikehold av varel-
     A                                      inje '
     A                                      DSPATR(RI)
7.xx A                                  2 38'LD105C1'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 54USER
7.xx A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
7.xx A                                      COLOR(WHT)
7.xx A                                  2 72DATE
7.xx A                                      EDTCDE(Y)
7.xx A                                      COLOR(WHT)
7.xx A                                  3  1'-----------------------------------
7.xx A                                      ------------------------------------
7.xx A                                      -----------'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  2'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 16'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 21'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 29'T'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 32'Nobbnr'
7.xx A**                                4  2'Antall        Enh  Varenr  T  Nobb-
7.xx A**                                    nr'
     A                                      COLOR(WHT)
     A            C1ANT1    R        B  5  2REFFLD(LDANTA)
     A  33                                  ERRMSGID(FMD0150 *LIBL/QMFOFAK 33)
     A  49                                  ERRMSGID(FM00630 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENH1    R        B  5 16REFFLD(LDENH1)
     A  31                                  ERRMSGID(VMA1010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 32)
     A  81                                  COLOR(YLW)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1VARE    R   -7   O  5 20REFFLD(LDVARE)
     A                                      OVRDTA
     A            C1VTYP    R        O  5 29REFFLD(VVVTYP *LIBL/FFRF)
     A                                      OVRDTA
     A            C1NOBB    R        O  5 31REFFLD(LDNOBB)
     A                                      OVRDTA
     A            C1NTSU        11Y 2O  5 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTPR    R        O  5 54REFFLD(LDIPNY)
     A                                      COLOR(BLU)
     A                                      OVRDTA
8.02 A            C1NTSN        11Y 2O  5 65COLOR(BLU)
8.02 A  84                                  DSPATR(ND)
8.02 A                                      EDTCDE(4)
8.02 A                                      OVRDTA
8.02 A                                  5 78'NOK'
8.02 A  84                                  DSPATR(ND)
8.02 A                                      COLOR(BLU)
     A            C1ANT2    R        B  6  2REFFLD(LDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  82                                  DSPATR(ND)
     A  82                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH2    R        O  6 16REFFLD(LDENH1)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1NISU        11Y 2O  6 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTIM    R        O  6 54REFFLD(LDIPNY)
     A                                      COLOR(BLU)
     A                                      OVRDTA
8.02 A            C1NISN        11Y 2O  6 65COLOR(BLU)
8.02 A  84                                  DSPATR(ND)
8.02 A                                      EDTCDE(4)
8.02 A                                      OVRDTA
8.02 A                                  6 78'NOK'
8.02 A  84                                  DSPATR(ND)
8.02 A                                      COLOR(BLU)
     A            C1ANT3    R        B  7  2REFFLD(LDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  83                                  DSPATR(ND)
     A  83                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH3    R        O  7 16REFFLD(LDENH1)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                  8 20'Innkj.pris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Rab-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Rab-2'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 47'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'K'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 61'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'LT'
7.xx A**                                8 20'Innkj.pris  Rab-1   Rab-2  L K   K-
7.xx A**                                    ostpris LT'
     A                                      COLOR(WHT)
     A            C1ANTA    R        O  9  2REFFLD(LDANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENHE    R        O  9 16REFFLD(LDENH1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1INPR         9Y 2B  9 20EDTWRD('     0 ,  ')
     A  39                                  ERRMSGID(FMD0170 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1RAB1    R        B  9 31REFFLD(LDRAB1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1RAB2    R        B  9 39REFFLD(LDRAB2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1LETY    R        O  9 47REFFLD(LDLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KPLJ    R        B  9 49REFFLD(LDKPLJ)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 51'(kode)'
     A            C1KOPR    R        B  9 59REFFLD(LDKPNY)
     A                                      COLOR(YLW)
7.02 A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1LTYP    R        O  9 70REFFLD(LDLTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        B 10 20REFFLD(LDTEK1)
     A  35                                  ERRMSGID(FMD0180 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            C1TEK2    R        B 11 20REFFLD(LDTEK2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Innkjøpspris inkl'
     A            C1INIM         9Y 2B 13 20EDTWRD('       ,  ')
     A  36                                  ERRMSGID(FMD0210 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 37'Lagerbeholdn.'
     A N87                                  DSPATR(ND)
     A            C1BEHL    R        O 13 51REFFLD(VLBEHL *LIBL/FFRF)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 14  2'Lev.dato. . . . .'
     A            C1LDAT         6Y 0B 14 20COLOR(YLW)
     A  40                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(FMD0200 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14 37'I bestilling.'
     A N87                                  DSPATR(ND)
     A            C1BBES    R        O 14 51REFFLD(VLBBES *LIBL/FFRF)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15  2'Utgår dato  . . .'
     A            C1UDAT         6Y 0O 15 20COLOR(YLW)
     A  42                                  ERRMSGID(LM00400 *LIBL/QMFLAGR 42)
     A  43                                  ERRMSGID(LM00410 *LIBL/QMFLAGR 43)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 15 37'Ordre/pakks..'
     A N87                                  DSPATR(ND)
     A            C1PAKK    R        O 15 51REFFLD(VLBEHL *LIBL/FFRF)
     A                                      DSPATR(UL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
6.21 A**                               16  2'Prosjekt/akt. . .'
6.21 A**          C1PROJ    R        B 16 20REFFLD(LDPROJ)
6.21 A            C1PROJ         6A  H
6.21 A**45                                  ERRMSGID(FRH0220 *LIBL/QMFOFAK 45)
6.21 A**                                    CHECK(RB)
6.21 A**                                    COLOR(YLW)
6.21 A**                                    OVRDTA
6.21 A**          C1AKTI    R        B 16 27REFFLD(LDAKTI)
6.21 A            C1AKTI         3A  H
6.21 A**46                                  ERRMSGID(FMA0220 *LIBL/QMFOFAK 46)
6.21 A**                                    COLOR(YLW)
6.21 A**                                    OVRDTA
     A                                 16 37'Disp.beholdn.'
     A N87                                  DSPATR(ND)
     A            C1DISP    R        O 16 51REFFLD(VLBEHL *LIBL/FFRF)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17  2'Modulnr . . . . .'
     A            C1NMOD    R        O 17 20REFFLD(VVNMOD *LIBL/FFRF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 18  2'GTIN-nr . . . . .'
     A            C1EANN    R        O 18 20REFFLD(VXEANN *LIBL/FFRF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 18 37'Cross docking vare'
     A            C1CRDO    R        O 18 56REFFLD(VVCRDO *LIBL/FFRF)
     A                                 19  2'Erstattes av  . .'
     A            C1ENOB    R        O 19 20REFFLD(VVNOBB *LIBL/FFRF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  50        C1SESS        28   O 19 37OVRDTA
     A                                      COLOR(RED)
6.33 A                                 22  2'F2=Kostpris  F3=Gå tilbake  F4=Spø-
6.33 A                                      ring  F8=Produktinfo'
     A                                      COLOR(BLU)
6.33 A                                 23  2'F9=Lagerinformasjon  F10=VA-info  -
6.33 A                                      F18=NOBB info  F23=Vis kost'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20021120  093429  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 41)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Kost-/innkjøpspris oppdateringskod-
     A                                      e er'
     A                                  4  2'endret.'
     A                                  6  2'Bekreft endringen med Enter eller -
     A                                      <OK>,'
     A                                  7  2'eller gå tilbake til registrering -
     A                                      ved å'
     A                                  8  2'velge <Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.25 A          R C2MSG
6.25 A*%%TS  SD  20101111  092820  BOF610      REL-V5R4M0  5722-WDS
6.25 A                                      WINDOW(*DFT 8 43)
6.25 A                                      CA03
6.25 A                                      CA07
6.25 A                                      CA12
6.25 A                                  3  2'Varen har utgått. Overstyring av+
6.25 A                                        dato må'
6.25 A                                  4  2'bekreftes med F7 !!'
6.26 A                                  7  2'F3=Gå tilbake  F7=-
6.26 A                                      Bekreft'
6.26 A                                      COLOR(BLU)
7.xx A*         R ASUME                     ASSUME
7.xx A*                                24 79' '
