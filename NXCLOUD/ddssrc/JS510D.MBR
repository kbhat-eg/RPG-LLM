     A*%%TS  SD  20091104  104910  BOF610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JS510D
     A* Beskrivelse . . : Kjede sortiment, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       041109 bof  Nytt program
7.xx A* 040316 bsa Endringer relatert til ny GUI
     A*---------------------------------------------------------------
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20091104  104910  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(MM00010 *LIBL/QMFVADM)
     A            B1OTXT    R        O  7 17REFFLD(JSOTXT)
     A            B1OSOR    R        O  7  5REFFLD(JSOSOR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020228  092906  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge sortiment '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Sortiment'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Sortiment'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 17'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2OSOR    R        B  4  5REFFLD(JSOSOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2OTXT    R        B  4 17REFFLD(JSOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20051110  104501  SST540S     REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
