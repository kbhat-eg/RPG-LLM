     A*%%TS  SD  20081130  084024  BOF570      REL-V5R4M0  5722-WDS
     A*
8.01 A*8.xx      101122 bsa  Skrevet om til newlook håndtering
8.02 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*
     A*%%EC
8.01 A                                      DSPSIZ(24 80 *DS3 -
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081130  084024  BOF570      REL-V5R4M0  5722-WDS
8.01 A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.02 A            B1PRGR    R        O  9  4REFFLD(VTPRGR)
8.01 A            B1LETY    R        O  9  7REFFLD(VTLETY)
8.01 A            B1FDAT         6Y 0O  9  9EDTWRD('  .  .  ')
8.01 A            B1FTIM         4Y 0O  9 18EDTWRD('  :  ')
8.01 A            B1TDAT         6Y 0O  9 25EDTWRD('  .  .  ')
8.01 A            B1TTIM         4Y 0O  9 34EDTWRD('  :  ')
8.01 A            B1KAMP    R        O  9 40REFFLD(VTKAMP)
8.01 A            B1ENH1    R        O  9 46REFFLD(VTENH1)
8.01 A            B1TIP1    R        O  9 50REFFLD(VTTIP1)
8.01 A            B1KOP1    R        O  9 61REFFLD(VTKOP1)
8.01 A            B1ENH2    R        O  9 73REFFLD(VTENH2)
8.01 A            B1ENH3    R        O  9 77REFFLD(VTENH3)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081130  084024  BOF570      REL-V5R4M0  5722-WDS
8.01 A                                      SFLSIZ(0014)
8.01 A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise tilbudspriser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
8.01 A                                  4  2'Vare . . . . . . . .'
     A                                      COLOR(BLU)
8.01 A            B2VARE    R   -7   O  4 23REFFLD(VTVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
8.01 A            B2TEK1    R        O  4 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
8.01 A                                  5  4'Pg'
8.01 A                                      COLOR(WHT)
8.01 A                                  5  7'L'
8.01 A                                      COLOR(WHT)
8.01 A                                  5  9'Fradato'
8.01 A                                      COLOR(WHT)
8.01 A                                  5 25'Tildato'
8.01 A                                      COLOR(WHT)
8.01 A                                  5 40'Kamp.'
8.01 A                                      COLOR(WHT)
8.02 A            B2PRGR    R        B  6  4REFFLD(VTPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.01 A            B2LETY    R        B  6  7REFFLD(VTLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A            B2FDAT         6Y 0B  6  9COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2TDAT         6Y 0B  6 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            B2KAMP    R        B  6 40REFFLD(VTKAMP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  7  2'5=Vis'
     A                                      COLOR(BLU)
8.01 A                                  8  2'V'
8.01 A                                      COLOR(WHT)
8.01 A                                  8  4'Pg'
8.01 A                                      COLOR(WHT)
8.01 A                                  8  7'L'
8.01 A                                      COLOR(WHT)
8.01 A                                  8  9'Fradato'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 25'Tildato'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 40'Kamp.'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 46'Enh'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 51'Tilb.pris'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 62'Kostpris'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 73'Enh'
8.01 A                                      COLOR(WHT)
8.01 A                                  8 77'Enh'
8.01 A                                      COLOR(WHT)
8.01 A**                                8  2'V Pg L -----  Tilbudsperiode  -----
8.01 A**                                    -  Kamp.  Enh  Tilb.pris  Kostpris -
8.01 A**                                      Enh Enh'
8.01 A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19991111  230146  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020226  111207  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise tilbudspris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  5 31REFFLD(VTVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 42REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C1PRGR    R        O  6 31REFFLD(VTPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT    R        O  6 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O  7 31REFFLD(VTLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LTXT    R        O  7 42REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Fra dato/tidspunkt . . . . .'
     A            C1FDAT         6Y 0O  9 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  9 40'-'
     A            C1FTIM         4Y 0O  9 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 10  2'Til dato/tidspunkt . . . . .'
     A            C1TDAT         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 40'-'
     A            C1TTIM         4Y 0O 10 42COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                      OVRDTA
     A                                 12  2'Kampanje . . . . . . . . . .'
     A            C2KAMP    R        O 12 31REFFLD(VTKAMP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 22'Kostpris'
     A                                 16  2'Enh'
     A                                 16  8'Tilbudspris'
     A                                 16 22'tilb. per.'
     A            C2ENH1    R        O 17  2REFFLD(VTENH1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TIP1    R        O 17  8REFFLD(VTTIP1)
     A  31                                  ERRMSGID(VMT0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KOP1    R        O 17 22REFFLD(VTKOP1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ENH2    R        O 18  2REFFLD(VTENH2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TIP2    R        O 18  8REFFLD(VTTIP2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KOP2    R        O 18 22REFFLD(VTKOP2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ENH3    R        O 19  2REFFLD(VTENH3)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TIP3    R        O 19  8REFFLD(VTTIP3)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KOP3    R        O 19 22REFFLD(VTKOP3)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VTEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
