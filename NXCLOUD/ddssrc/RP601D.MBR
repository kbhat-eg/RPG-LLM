     A*%%TS  SD  20210317  144748  SST_NXC     REL-V7R3M0  5770-WDS
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP601D
     A* Beskrivelse . . : Underprosjekt/spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       121110 NHO  Nytt program
7.01 A* K00   060520  bsa  Takler brede skjermer.
7.xx A* K00   080520  bsa  Skrevet om til cloud
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20210317  144748  SST_NXC     REL-V7R3M0  5770-WDS
     A            B12ALF        20A  H
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1PROS    R        O  7  4REFFLD(RP2PRO)
     A            B1UPRO    R        O  7 13REFFLD(RP2UPR)
     A            B1TXT1    R   23   O  7 27REFFLD(RP2TXT)
     A            B1RAKT    R        H      REFFLD(RP2RAK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20210317  144748  SST_NXC     REL-V7R3M0  5770-WDS
     A  *DS3                                SFLSIZ(0009)
     A  *WIDE                               SFLSIZ(0009)
     A  *DS3                                SFLPAG(0008)
     A  *WIDE                               SFLPAG(0008)
     A  *DS3                                WINDOW(B2CMD)
     A  *WIDE                               WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Spørring underprosjekt'
     A                                      DSPATR(RI)
     A                                  3  4'Prosj'
     A                                      COLOR(WHT)
     A                                  3 13'Underprosj.'
     A                                      COLOR(WHT)
     A                                  3 27'Beskrivelse'
     A                                      COLOR(WHT)
     A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Prosj'
     A                                      COLOR(WHT)
     A                                  6 13'Underprosj.'
     A                                      COLOR(WHT)
     A                                  6 27'Beskrivelse'
     A                                      COLOR(WHT)
     A            B22PRO    R        B  4  4REFFLD(RP2PRO)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A* MÅ LEGGES INN NYE SØKEBEGREP PÅ DISSE I PROGRAM
     A            B22UPR    R        B  4 13REFFLD(RP2UPR)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            B22ALF        20A  B  4 27COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020106  234033  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F5=Forny'
     A                                      COLOR(BLU)
     A                                 16 29'F6=OPPRETT'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
