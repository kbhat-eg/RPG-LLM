     A*%%TS  SD  20150915  145210  BOFBMP      REL-V7R1M0  5770-WDS
8.01 A*PRG2 1506022 bof : utvidet prisgruppe fra 1 til 2 posisjoner
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C1BLD
     A*%%TS  SD  20150915  145210  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      KEEP
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kalkulasjon av vare'
     A                                      COLOR(WHT)
     A                                  2 38'JV171C1'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . . . . .'
     A            C1VARE    R   -7   O  4 27REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 41'Pris fra-dato. . . . . .'
     A            C1GDAT         6Y 0O  4 66COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  5  2'Nobbnr . . . . . . . . .'
     A            C1NOBB    R        O  5 27REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 41'Nobb-pris. . . . . . . .'
     A            C1GRPR    R        O  5 66REFFLD(JXPRIS)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                  6  2'Modulnr. . . . . . . . .'
     A            C1MODN    R        O  6 27REFFLD(JVMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 41'Priskode . . . . . . . .'
     A            C1KPRI         1A  O  6 66COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Prisgivende leverandør .'
     A            C1LDOR    R        O  8 27REFFLD(JXLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  8 34REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Betingelse:'
     A                                      DSPATR(UL)
     A                                 10 41'Påslag:'
     A                                      DSPATR(UL)
     A            C1BLDO    R        O 11  2REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BOGR    R        O 11  9REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BHGR    R        O 11 12REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BUGR    R        O 11 15REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BMOD    R        O 11 19REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BVAR    R   -7   O 11 28REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BVTY    R        O 11 37REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BLET    R        O 11 39REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1POGR    R        O 11 41REFFLD(JCOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PHGR    R        O 11 44REFFLD(JCHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PUGR    R        O 11 47REFFLD(JCUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PLDO    R        O 11 51REFFLD(JCLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PMOD    R        O 11 58REFFLD(JCMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PVAR    R   -7   O 11 67REFFLD(JCVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PKPR    R        O 11 76REFFLD(JCKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PVTY    R        O 11 78REFFLD(JCVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PLET    R        O 11 80REFFLD(JCLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BBET    R        O 12  2REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                 12 42'Kostpris-faktor. . . .'
8.01 A            C1KFAK         8Y 6B 12 66COLOR(YLW)
     A  31                                  ERRMSGID(JMV0310 *LIBL/QMFVADM 31)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A                                 13  2'Innkjøpspris-faktor. . .'
     A            C1IFAK         8Y 6B 13 27COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A                                 13 42'Kostpris-beløp . . . .'
8.01 A            C1KBEL    R        B 13 66REFFLD(JCKOBE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A            C1PRUT    R        O 13 78REFFLD(JCPRGR)
     A                                      OVRDTA
8.01 A                                 14  2'Nettopris. . . . . . . .'
     A            C1NTPR    R        B 14 27REFFLD(JBINPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
8.01 A                                 14 42'Salgspris-faktor . . .'
8.01 A            C1SFAK         8Y 6B 14 66COLOR(YLW)
     A  32                                  ERRMSGID(JMV0330 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JMV0320 *LIBL/QMFVADM 33)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 15  2'Innkjøpspris . . . . . .'
     A            C1INPR    R        O 15 27REFFLD(JBINPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1PGBE    R        O 15 39REFFLD(JCPRGR)
     A                                      OVRDTA
8.01 A                                 15 42'Dekningsgrad (DG). . .'
     A            C1DEKN         5Y 2B 15 66COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(M)
     A                                 16  2'Frakt-Faktor . . . . . .'
     A            C1FFAK         8Y 6B 16 27COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'Innkjøpspris 2 (frakt) .'
     A            C1INP2    R        O 17 27REFFLD(JBINPR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1PGFR    R        O 17 39REFFLD(JCPRGR)
     A                                      OVRDTA
     A                                 18  2'Antall        Enh'
     A                                 18 25'Antall'
     A                                 18 34'Enh'
     A                                 18 39'Salgspris'
     A                                 18 50'Rab-1'
     A                                 18 57'Rab-2'
     A                                 18 63'L'
     A                                 18 67'Kostpris'
     A            C1ANT1        11Y 3B 19  2COLOR(YLW)
     A  35                                  ERRMSGID(JFD0150 *LIBL/QMFVADM 35)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      FLDCSRPRG(C1ANT2)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENH1    R        O 19 16REFFLD(JWENHE)
     A                                      OVRDTA
     A            C1ANTA        11Y 3O 19 20EDTCDE(M)
     A                                      OVRDTA
     A            C1ENHE    R        O 19 34REFFLD(JWENHE)
     A                                      OVRDTA
     A            C1SAPR    R        B 19 38REFFLD(JXPRIS)
     A  36                                  ERRMSGID(JFD0110 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1RAB1         5Y 2B 19 49COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1RAB2         5Y 2B 19 56COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1LETY         1Y 0B 19 63COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            C1KOPR    R        B 19 65REFFLD(JXPRIS)
     A  38                                  ERRMSGID(JFD0130 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1ANT2        11Y 3B 20  2COLOR(YLW)
     A                                      CHECK(RB)
     A                                      FLDCSRPRG(C1ANT3)
     A                                      EDTCDE(M)
     A  83                                  DSPATR(ND)
     A  83                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH2    R        O 20 16REFFLD(JWENHE)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1TEK1    R        O 20 38REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1ANT3        11Y 3B 21  2COLOR(YLW)
     A                                      CHECK(RB)
     A                                      FLDCSRPRG(C1SAPR)
     A                                      EDTCDE(M)
     A  84                                  DSPATR(ND)
     A  84                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH3    R        O 21 16REFFLD(JWENHE)
     A  84                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1TEK2    R        O 21 38REFFLD(JVTEK2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'F3=Gå tilbake  F5=Forny  F8=Produk-
     A                                      tinfo  F13=Velg prisg lev  F14=Velg-
     A                                       bet'
     A                                      COLOR(BLU)
     A                                 23  2'F15=Kundeinfo  F16=Rabattmatrise  -
     A                                      F17=Spesialpriser  F18=NOBB info'
     A                                      COLOR(BLU)
