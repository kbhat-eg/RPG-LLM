     A*%%TS  SD  20141007  135028  NHO630      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A*     bsa  110117    Endringer relatert til ny GUI
     A*-------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20051017  131436  HKO530      REL-V5R3M0  5722-WDS
     A            B1LINE    R        H      REFFLD(SFLINE)
     A            B1WSID    R        H      REFFLD(SFWSID)
     A            B1BONG    R        H      REFFLD(SFBONG)
     A            B1SAPR         9  2H
     A            B1KOPR         9  2H
     A            B1LDOR    R        H      REFFLD(SFLDOR)
     A            B1LVAR    R        H      REFFLD(SFLVAR)
7.xx A            B1VALG         1Y 0B 10  2COLOR(YLW)
     A                                      VALUES(0 5 6 7)
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A                                      EDTCDE(Z)
7.xx A            B1ANTA         7Y 2B 10  5COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
7.xx A            B1VARE    R   -7   O 10 14REFFLD(SFVARE)
7.xx A            B1TEK1         9A  O 10 23
7.xx A            B1KPRO    R        O 10 33REFFLD(SFKPRO)
7.xx A            B1BIDA         6Y 0O 10 40EDTCDE(Z)
7.xx A            B1BINR    R        O 10 47REFFLD(SFBINR)
7.xx A            B1ORNR    R        O 10 56REFFLD(SFORNR)
7.xx A            B1OTYP    R        O 10 65REFFLD(SFOTYP)
7.xx A            B1ENHE    R        O 10 68REFFLD(SFENHE)
7.xx A            B1ANTS         7Y 2O 10 72EDTCDE(M)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20141007  135028  NHO630      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring/registrering av varelinje-
     A                                      r'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SF161B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre.'
     A                                      COLOR(BLU)
     A            B2NUMM         8S 0O  4  9OVRDTA
     A                                      COLOR(BLU)
     A                                  4 19'Kunde .'
     A                                      COLOR(BLU)
     A            B2VKUN    R        O  4 27REFFLD(SFVKUN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2KNAV        30A  O  4 34COLOR(BLU)
     A                                      OVRDTA
     A                                  4 66'År. . .'
     A                                      COLOR(BLU)
     A            B2PAAR    R        O  4 74REFFLD(SFPAAR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 19'K.prosj'
     A                                      COLOR(BLU)
     A            B2LAD1    R        O  5 27REFFLD(SFKPRO)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2PNAV        30A  O  5 34COLOR(BLU)
     A                                      OVRDTA
     A                                  5 66'Dato. .'
     A                                      COLOR(BLU)
     A            B2BID1         6Y 0O  5 74COLOR(BLU)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  6  3'Vare +'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 20'Varetekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 42'K.proj +'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  3'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 42'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'5=Vis  6=Skriv fakturakopi  7=Vise-
     A                                       faktura/bong'
     A                                      COLOR(BLU)
     A                                  6 63'Tot:'
     A                                      COLOR(WHT)
6.22 A            B2TOTS        11Y 2O  6 69EDTCDE(4)
     A                                      COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  6'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 14'Vare  '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 23'Varetekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 33'K.proj'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 40'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 47'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 56'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 65'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 68'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 72'Ant.slgt'
7.xx A**                                9  2'V   Antall  Vare     Varetekst K-p-
7.xx A**                                    roj Dato   Faktnr   Ordre    OT Enh-
7.xx A**                                     Ant.slgt'
     A                                      COLOR(WHT)
7.xx A            B2VARE         8Y 0B  6 10COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2TEK1         9A  B  6 31COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2KPRO    R        B  6 51REFFLD(SFKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BIDA         6Y 0B  7 10COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BINR    R        B  7 31REFFLD(SFBINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ORNR    R        B  7 51REFFLD(SFORNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20051014  140040  HKO530      REL-V5R3M0  5722-WDS
     A                                 23  2'F2=Tekstlinje   F3=Gå tilbake   F4-
     A                                      =Spørring'
     A                                      COLOR(BLU)
