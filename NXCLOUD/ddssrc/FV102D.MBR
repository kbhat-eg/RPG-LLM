     A*%%TS  SD  20211206  114201  SST_NXC     REL-V7R3M0  5770-WDS
6.20 A*
6.20 A*     NHO  20110309  SJEKK MOT SKAFFEVARE
6.21 A*     NHO  20130829  ENDRET TEKSTER VEDR PARAMETER FOR VISMA
6.25 A*     BSA  20140225  KAN IKKE HA OVERLAPPENDE FELT, + DIV SOM
6.25 A*                    SKAPER PROBLEMER FOR JACADA.
7.xx A*  7.xx 220116 bsa   Endringer relatert til ny GUI
Q.01 A*  7.xx 020419 bsa   Utvidet skjermstørrelse
Q.02 A*  7.xx 020419 bsa   Krav til leveringsdatoe
7.00 A*  7.00 251019 nho   Mamut
7.01 A*  7.00 070220 nho   CMD4 til vedlikehold av konv.bilagskoder
7.01 A*                    vedr Visma
7.02 A*K0000  110920 bsa   Valg 2 på bestillingsforslag.
7.03 A*K00    160920 sst   Mulighet for å sette opp Saldooverføring fra ekst. sy
7.04 A*K00    031120 sst   Integrasjon mot Uni Micro
     A*
7.fb A*  7.00 190220 bsa   Forskudsbetaling
7.f1 A* K0000 300920 bsa   Fargevalg på ordre med depositumskrav.
7.f2 A* K0000 021120 bsa   Fjernet fanen for hurtigopprett. Er erstattet
7.f2 A* K0000              av andre funksjoner i NX.
7.09 A*       070922 MST   Kode for økonomisystem er lagt ut i  fil XTILPF
7.09 A*                    Ingen endring i DDS, endring skjer i RPG program
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      CA03(03)
     A                                      CA04(04)
     A                                      CA05(05)
     A                                      CA06(06)
     A                                      CA07(07)
     A          R A1BLD
     A*%%TS  SD  20111220  133717  BOF620      REL-V7R1M0  5770-WDS
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.fb A                                  1 42'Ordre/Fakturering 1:4'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Diverse koder'
     A                                      COLOR(WHT)
7.xx A                                  2 34'FV102A1'
7.xx A                                      COLOR(WHT)
6.25 A                                  2 42'Bruker:'
     A                                      COLOR(WHT)
6.25 A                                  2 50USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre/fakturering :'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  5  2'Kode for ordremerking . . . .'
     A            A1ABND    R        B  5 32REFFLD(FAABND)
     A                                      COLOR(YLW)
     A                                  5 35'J, N, X'
     A                                  6  2'Antall desimaler ved utskrift'
     A            A1ADES    R        B  6 32REFFLD(FAADES)
     A                                      COLOR(YLW)
     A                                  6 35'0, 1, 2, 3'
     A                                  7  2'Skal mva vises i registrering'
     A            A1AMVA    R        B  7 32REFFLD(FAAMVA)
     A                                      COLOR(YLW)
     A                                  7 35'1=Ja,0=Nei'
     A                                  8  2'Benytte bare store bokstaver.'
     A            A1AHOY    R        B  8 32REFFLD(FAAHOY)
     A                                      COLOR(YLW)
     A                                  8 35'1=Ja,0=Nei'
     A                                  9  2'Tillate manuelle ordrenummer.'
     A            A1AMAN    R        B  9 32REFFLD(FAAMAN)
     A                                      COLOR(YLW)
     A                                  9 35'1=Ja,0=Nei'
     A                                 10  2'Kontroll av neste ordretype .'
     A            A1ANXT    R        B 10 32REFFLD(FAANXT)
     A                                      COLOR(YLW)
     A                                 10 35'1=Ja,0=Nei'
     A                                 11  2'Kontroll mot beholdning ? . .'
     A            A1ABEH    R        B 11 32REFFLD(FAABEH)
     A                                      COLOR(YLW)
6.25 A                                 11 35'1=Ja,0=Nei,2=Krav,3=Sjekk så vid-
     A                                      ere.    '
     A                                 12  2'Kontroll mot skaffevare ? . .'
     A            A1ALBE    R        B 12 32REFFLD(FAALBE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
6.25 A                                 12 35'1=Ja,0=Nei,2=Krav,3=Sjekk mail s-
     A                                      endes'
     A                                 13  2'Skaffeordre gir best.forslag.'
     A            A1ABFS    R        B 13 32REFFLD(FAABFS)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.02 A                                 13 35'1=Ja,0=Nei,2=Pr leverandør'
     A                                 14  2'Tvungen leveringsdato . . . .'
     A            A1ALDS    R        B 14 32REFFLD(FAALDS)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 14 35'1=Ja,0=Nei'
Q.02 A                                 15  2'Levdato settes lik dd på OBKR'
Q.02 A            A1AK06    R        B 15 32REFFLD(FAALDS)
Q.02 A                                      COLOR(YLW)
Q.02 A                                      VALUES(0 1)
Q.02 A                                      EDTCDE(Z)
Q.02 A                                 15 35'1=Ja,0=Nei'
Q.02 A                                 16  2'Levdato settes lik dd på PAKK'
Q.02 A            A1AK07    R        B 16 32REFFLD(FAALDS)
Q.02 A                                      COLOR(YLW)
Q.02 A                                      VALUES(0 1)
Q.02 A                                      EDTCDE(Z)
Q.02 A                                 16 35'1=Ja,0=Nei'
     A                                 18  2'Lager :'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 19  2'Oppdatering av lager  . . . .'
     A            A1ALAG    R        B 19 32REFFLD(FAALAG)
     A                                      COLOR(YLW)
     A                                 19 35'1=Ja,0=Nei'
     A                                 20  2'Hvilket lager er hovedlager ?'
     A            A1AHLA    R        B 20 32REFFLD(FAAHLA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 21  2'Std. varetype for lager      '
     A            A1AVTY    R        B 21 32REFFLD(FAAVTY)
     A                                      COLOR(YLW)
     A                                      VALUES('S' 'L')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A2BLD
     A*%%TS  SD  20211206  114201  SST_NXC     REL-V7R3M0  5770-WDS
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 42'Ordre/Fakturering 2:4'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Diverse koder'
     A                                      COLOR(WHT)
     A                                  2 42'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 50USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Hovedbok :'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  5  2'Spesifiser kost . . . . . . .'
     A            A2ASAK    R        B  5 32REFFLD(FFRFR/FAASAK *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                  5 35'1=Ja,0=Nei'
     A                                  6  2'Spesifiser linjerabatt. . . .'
     A            A2ASA1    R        B  6 32REFFLD(FFRFR/FAASA1 *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                  6 35'1=Ja,0=Nei'
     A                                  7  2'Spesifiser almenningsrabatt .'
     A            A2ASA2    R        B  7 32REFFLD(FFRFR/FAASA2 *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                  7 35'1=Ja,0=Nei'
     A                                  8  2'Spesifiser ordrerabatt. . . .'
     A            A2ASAO    R        B  8 32REFFLD(FFRFR/FAASAO *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                  8 35'1=Ja,0=Nei'
     A                                  9  2'Økonomi system  . . . . . . +'
     A            A2AK03         2Y 0B  9 32COLOR(YLW)
     A            A2AK03TXT     20A  O  9 35COLOR(YLW)
     A*
     A                                 11  2'Droppe oppd. Nexstep økonomi '
     A            A2NXUT    R        B 11 32REFFLD(FFRFR/FAAK04 *LIBL/FFRF)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                 11 35'1=Ja,0=Nei'
     A                                 13  2'Andre koder :'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 14  2'Farge på sentrallager-varer .'
     A            A2AFSL    R        B 14 32REFFLD(FFRFR/FAAFSL *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                 14 35'1=Ja,0=Nei'
     A                                 15  2'Skaffevare i varesøk  . . . .'
     A            A2ASSV    R        B 15 32REFFLD(FFRFR/FAASSV *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                 15 35'1=Ja,0=Nei'
     A                                 16  2'Selgers mobil på div.lister .'
     A            A2AMSA    R        B 16 32REFFLD(FFRFR/FAAMSA *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                 16 35'1=Ja,0=Nei'
     A                                 17  2'SMS ved varemottak  . . . . .'
     A            A2AK01    R        B 17 32REFFLD(FFRFR/FAAK01 *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                 17 35'1=Ja,0=Nei'
     A                                 22  2'F3=Gå tilbake   F5=Konv. av momsko-
     A                                      der           F6=Sett opp Saldoover-
     A                                      føring'
     A                                      COLOR(BLU)
     A                                 23 18'F7=Konv. av bilagskoder'
     A                                      COLOR(BLU)
     A          R A3BLD
     A*%%TS  SD  20201002  114733  SST700      REL-V7R3M0  5770-WDS
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.fb A                                  1 42'Ordre/Fakturering 3:4'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Saldo overføring'
     A                                      COLOR(WHT)
     A                                  2 42'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 50USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Overføring av saldo fra eksternt Ø-
     A                                      konomisystem:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  6  4'Eksternt system legger filene'
     A                                  7  6'inn i vårt ifs-område . .'
     A                                  7 32'.:'
     A                                  7 48'J=Ja'
     A            A3FAA          1A  B  7 36VALUES(' ' 'J')
     A                                      COLOR(YLW)
     A                                  8  4'Vi henter filene fra eksternt'
     A                                  9  6'økonomisystem . . . . . . .:'
     A            A3HENT         1A  B  9 36VALUES(' ' 'J')
     A                                      COLOR(YLW)
     A                                  9 48'J=Ja'
     A                                 10  4'FTP-anmodning . . . . . . . .:'
     A            A3FTPA        10A  B 10 36COLOR(YLW)
     A                                 10 48'(F4)'
     A                                 11  8'Denne må alltid legges inn. Ev. ba-
     A                                      re med filnavn'
     A                                 13  2'Overføring/Oppdatering av saldo ti-
     A                                      l kunderegister:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 14  4'Jobbnavn i Jobbplanlegger :'
     A            A3JNAV        10A  B 14 32COLOR(YLW)
     A                                 14 49'(eks. xxxVisma)'
     A                                 17  4'Overføring fra lev. kl. . :'
     A            A3STOV         6A  B 17 32COLOR(YLW)
     A                                 17 40'(eks. 071500)'
     A                                 23  6'F3=Gå  tilbake   F6=Sett opp oppda-
     A                                      teringsjobb  F7=Se på Oppdat.jobb  -
     A                                         '
     A                                      COLOR(BLU)
     A            A3STAT        10A  O 14 66COLOR(YLW)
     A            A3DAGL         1A  B 15 32COLOR(YLW)
     A                                 15  4'Kjøres daglig . . . . . . :'
     A                                 15 56'(man/tir/ons/tor/-'
     A                                 16  4'Kjøres faste dager. . . . :'
     A            A3DAG1         3A  B 16 32COLOR(YLW)
     A            A3DAG2         3A  B 16 36COLOR(YLW)
     A            A3DAG3         3A  B 16 40COLOR(YLW)
     A            A3DAG4         3A  B 16 44COLOR(YLW)
     A            A3DAG5         3A  B 16 48COLOR(YLW)
     A            A3DAG6         3A  B 16 52COLOR(YLW)
     A                                 16 62'-fre/lør/søn)'
     A                                 17 55'Oppdat.kl:'
     A            A3STOP         6A  B 17 66COLOR(YLW)
     A                                 18  4'Stopp kl. . . . . . . . . :'
     A            A3SLUT         6A  B 18 32COLOR(YLW)
     A                                 18 40'(eks. 210000)'
     A                                 19  4'Kjøres hvert n. . . . . . :'
     A            A3REPP         3A  B 19 32COLOR(YLW)
     A                                 19 37'minutt'
     A                                 20  4'Kjøres månedlig . . På dag:'
     A            A3MDAG         3A  B 20 32COLOR(YLW)
     A            A3MDAN         1A  B 20 45COLOR(YLW)
     A                                 20 37'Dag nr.'
     A                                 20 49'(1-5)'
     A                                 21  4'Kjøres årlig. . . . På mnd:'
     A            A3AMND         3A  B 21 32COLOR(YLW)
     A                                 21 37'Dag:'
     A            A3ADAG         3A  B 21 43COLOR(YLW)
     A                                 21 49'Dag nr.'
     A            A3ADAN         1A  B 21 57COLOR(YLW)
     A                                 21 60'(1-5)'
     A                                 22  4'Endret av:'
     A            A3EUSR        10A  B 22 15COLOR(YLW)
     A            A3EDAT        10A  B 22 26COLOR(YLW)
     A                                 22 37'/'
     A            A3ETIM         8A  B 22 39COLOR(YLW)
      *
7.fb A          R A4BLD
7.fb A*%%TS  SD  20140528  150253  ASK620      REL-V7R1M0  5770-WDS
7.fb A            L_FNAV        30A  O  1  2COLOR(WHT)
7.fb A                                  1 42'Ordre/Fakturering 4:4'
7.fb A                                      COLOR(WHT)
7.fb A                                  1 66'Kl. :'
7.fb A                                      COLOR(WHT)
7.fb A                                  1 72TIME
7.fb A                                      COLOR(WHT)
7.fb A                                  2  2'Diverse koder'
7.fb A                                      COLOR(WHT)
7.fb A                                  2 42'Bruker:'
7.fb A                                      COLOR(WHT)
7.fb A                                  2 50USER
7.fb A                                      COLOR(WHT)
7.fb A                                  2 66'Dato:'
7.fb A                                      COLOR(WHT)
7.fb A                                  2 72DATE
7.fb A                                      EDTCDE(Y)
7.fb A                                      COLOR(WHT)
7.fb A                                  3  2'-----------------------------------
7.fb A                                      ------------------------------------
7.fb A                                      ---------'
7.fb A                                      COLOR(WHT)
7.fb A                                  4  2'Forskuddsbetaling/Depositum'
7.fb A                                      COLOR(BLU)
7.f1 A**                                    DSPATR(UL)
7.fb A                                  5  2'Forskudd/depositum brukes .  . .'
7.fb A            A4AK11    R        B  5 37REFFLD(FFRFR/FAAK11 *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                  5 39'1=Ja,0=Nei'
7.fb A                                  6  2'Krav til Dep. Kont.ord. til best'
7.fb A            A4AK12    R        B  6 37REFFLD(FFRFR/FAAK12 *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                  6 39'1=Ja,0=Nei'
7.fb A                                  7  2'Minimums andel % . . . . . . . .'
7.fb A            A4AMAP    R        B  7 37REFFLD(FFRFR/FAAMAP *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                      CHECK(RB)
7.fb A                                      EDTCDE(L)
7.fb A                                  8  2'Krav til beløp større enn. . . .'
7.fb A            A4AKBS    R        B  8 37REFFLD(FFRFR/FAAKBS *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                      CHECK(RB)
7.fb A                                      EDTCDE(L)
7.fb A                                  9  2'Tillatt justering ordre %. . . .'
7.fb A            A4ATJP    R        B  9 37REFFLD(FFRFR/FAATJP *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                      CHECK(RB)
7.fb A                                      EDTCDE(L)
7.fb A                                 10  2'Tillatt justering ordre bel. . .'
7.fb A            A4ATJB    R        B 10 37REFFLD(FFRFR/FAATJB *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.fb A                                      CHECK(RB)
7.fb A                                      EDTCDE(L)
7.fb A                                 11  2'Bilagskode forskudds/deposit.. .'
7.fb A            A4AFBK    R        B 11 37REFFLD(FFRFR/FAAFBK *LIBL/FFRF)
7.fb A                                      COLOR(YLW)
7.f1 A                                 12  2'Depositumskrav ikke betalt . . .'
7.f1 A            A4APRN    R        B 12 37REFFLD(FFRFR/FAAPRN *LIBL/FFRF)
7.f1 A                                      COLOR(YLW)
7.f1 A                                 12 40'(R)ød'
7.f1 A                                      COLOR(RED)
7.f1 A                                 12 46'R(o)sa'
7.f1 A                                      COLOR(PNK)
7.f1 A                                 13  2'Depositumskrav betalt. . . . . .'
7.f1 A            A4APRP    R        B 13 37REFFLD(FFRFR/FAAPRP *LIBL/FFRF)
7.f1 A                                      COLOR(YLW)
7.f1 A                                 13 40'(B)lå'
7.f1 A                                      COLOR(BLU)
7.f1 A                                 13 46'(T)urkis'
7.f1 A                                      COLOR(TRQ)
7.f2 A**                               16  2'Hurtigopprett ordre '
7.f2 A**                                    COLOR(BLU)
7.f1 A*                                     DSPATR(UL)
7.f2 A**                               17  2'BM Card kan opprettes. . . . . .'
7.f2 A**          A3AK13    R        B 17 37REFFLD(FFRFR/FAAK13 *LIBL/FFRF)
7.f2 A**                                    COLOR(YLW)
7.f2 A**                               17 39'1=Ja,0=Nei'
7.f2 A**                               18  2'Krav til avdeling. . . . . . . .'
7.f2 A**          A3AK14    R        B 18 37REFFLD(FFRFR/FAAK14 *LIBL/FFRF)
7.f2 A**                                    COLOR(YLW)
7.f2 A**                               18 39'1=Ja,0=Nei'
7.f2 A**                               19  2'Spes.avtale tilgjengelig . . . .'
7.f2 A**          A3AK15    R        B 19 37REFFLD(FFRFR/FAAK01 *LIBL/FFRF)
7.f2 A**                                    COLOR(YLW)
7.f2 A**                               19 39'1=Ja,0=Nei'
7.f3 A                                 23  2'F3=Gå tilbake'
7.f3 A                                      COLOR(BLU)
