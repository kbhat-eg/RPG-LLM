     A*%%TS  SD  20120110  152814  ASK800      REL-V7R1M0  5770-WDS
     A*%%EC
     A*800       021222 ask  Nytt skjermbilde for spørring på masterdata feillog
     A*
     A*-------------------------------------------------------------------------
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      PRINT
     A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070328  124845  AMD550      REL-V5R4M0  5722-WDS
     A            B1UID          9Y 0H
     A            B1VALG         1Y 0B  6  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 2 4 5)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1TRTY         4A  O  6  4
     A  35                                  COLOR(RED)
     A            B1DATE         8A  O  6  9
     A  35                                  COLOR(RED)
     A            B1TIME         8A  O  6 18
     A  35                                  COLOR(RED)
     A            B1FEIL        78A  O  6 28
     A  35                                  COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070328  151100  AMD550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NEXSTEP'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Feillogg masterdata transaksjoner -
     A                                             '
     A                                      COLOR(WHT)
     A                                  2 38'RF520Z2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2' 2=vedlikehold'
     A                                      COLOR(BLU)
     A                                  4 17' 4=Slett'
     A                                      COLOR(BLU)
     A                                  4 27' 5=Vis detaljer  '
     A                                      COLOR(BLU)
     A                                  5  2'V'
     A                                      COLOR(WHT)
     A                                  5  4'Type'
     A                                      COLOR(WHT)
     A                                  5  9'Dato  '
     A                                      COLOR(WHT)
     A                                  5 18'Tid'
     A                                      COLOR(WHT)
     A                                  5 28'Feilmelding'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050621  150935  ASK530      REL-V5R2M0  5722-WDS
     A                                 26  2'F1=Filter '
     A                                      COLOR(BLU)
     A                                 26 23'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(02 02 20 70)
     A                                      CA03
     A                                      CA08
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Detaljer fra feillogg'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 49'RF520B3'
     A                                      COLOR(WHT)
     A                                  4  2'Type'
     A            B3TRTY        25A     4 20
     A                                  5  2'Dato'
     A            B3DATE         8A     5 20
     A                                  5 30'Tid'
     A            B3TIME         8A     5 34
     A  31        B3KTX1        15A     6  2
     A  31        B3FLD1         6Y 0O  6 20EDTCDE(Z)
     A  32        B3KTX2        15A     7  2
     A  32        B3FLD2         6Y 0O  7 20EDTCDE(Z)
     A  33        B3KTX3        15A     8  2
     A  33        B3FLD3         6A  O  8 20
     A                                 10  2'Feilmelding'
     A            B3FEIL1       50A  O 11  2
     A            B3FEIL2       50A  O 12  2
     A            B3FEIL3       50A  O 13  2
     A            B3FEIL4       50A  O 14  2
     A                                 16  2'Serverid'
     A            B3SRVID       30A  O 16 18
     A                                 17  2'Abonementsid'
     A            B3ABBID       10A  O 17 18
     A                                 19  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 19 16'F8=Vedlikehold'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(02 02 10 50)
     A                                      CA03
     A                                      CA08
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Slette loggtrans'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 39'RF520B4'
     A                                      COLOR(WHT)
     A                                  4  2'Type'
     A            B4TRTY        25A     4 20
     A                                  5  2'Dato'
     A            B4DATE         8A     5 20
     A                                  5 30'Tid'
     A            B4TIME         8A     5 34
     A                                  8  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(02 02 14 55)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Filter'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 49'RF520H1'
     A                                      COLOR(WHT)
     A                                  4  2'Transaksjonstype +'
     A            H1TYPE         4A  B  4 23
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Dato fra'
     A            H1DATO         6Y 0B  6 23EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'Ta med behandlede J'
     A            H1SLET         1A  B  8 23
     A                                      COLOR(YLW)
     A                                 13  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(02 02 20 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Transaksjonstype'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 49'RF520H2'
     A                                      COLOR(WHT)
     A                                  3  2 'Valg'
     A            H2VA01         1A  B  4  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  4  7'KUND - Kunde'
     A            H2VA02         1A  B  5  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  7'LEVR - Leverandør'
     A            H2VA03         1A  B  6  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  7'KONT - Kontaktperson'
     A            H2VA04         1A  B  7  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  7'PROS - Kunde prosjekt'
     A            H2VA05         1A  B  8  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  7'PRIS - Arikkel pris'
     A            H2VA06         1A  B  9  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  7'KAMP - Kampanje'
     A            H2VA07         1A  B 10  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10  7'KPRI - Kampanje pris'
     A            H2VA08         1A  B 11  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  7'BONG - Kassebong'
     A            H2VA09         1A  B 12  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 12  7'KORD - Kundeordre'
     A            H2VA10         1A  B 13  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  7'LAGE - Lagersaldo'
     A            H2VA11         1A  B 14  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 14  7'PENH - SPrisenhet'
     A            H2VA12         1A  B 15  3
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 15  7'SALD - Saldoinformasjon'
     A*           H2VA13         1A  B 16  3
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
     A                                 16  7'            '
     A*           H2VA14         1A  B 17  3
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
     A                                 17  7'            '
     A                                 19  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
