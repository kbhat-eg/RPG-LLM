     A*%%TS  SD  20030614  000551  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK110D
     A* Beskrivelse . . : Kundeposteringer, vedlikehold bilag/kunde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050802 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19960228  123916  KB          REL-V3R1M0  5763-PW1
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(RNBILN)
6.NU A            B1BIDA         6Y 0O  9 13
6.NU A            B1BILK    R        O  9 20REFFLD(RNBILK)
6.NU A            B1TEXT        16A  O  9 23
     A            B1BELØ         9Y 2O  9 40EDTCDE(J)
     A            B1KUND    R        O  9 54REFFLD(RKKUND)
     A            B1NAVN    R  -10   O  9 61REFFLD(RKNAVN)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020810  220706  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundeposteringer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  5=Vis'
     A                                      COLOR(BLU)
     A                                  5 61'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 77REFFLD(RNRAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  7 14'Dato'
     A                                      COLOR(WHT)
6.NU A                                  7 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  7 23'Tekst'
     A                                      COLOR(WHT)
     A                                  7 44'Beløp'
     A                                      COLOR(WHT)
     A                                  7 55'Kunde'
     A                                      COLOR(WHT)
     A                                  7 61'Navn'
     A                                      COLOR(WHT)
     A            B2BILN    R        B  8  4REFFLD(RNBILN)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2KUND    R        B  8 54REFFLD(RKKUND)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020810  231330  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20030614  000551  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 75)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2 32'Kundeposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A  30                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(RNBILK)
     A  33                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            C2TEXT    R        B  4 22REFFLD(RNTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0B  5 19DSPATR(UL)
     A  32                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 32)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RZ)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        B  6 19REFFLD(RNREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        B  7 19REFFLD(RNREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 43'Generalnota-nr. :'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        B  8 19REFFLD(RNANTP)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 24'Dato:'
     A            C2PDAT         6Y 0B  8 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A  39 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        B  9 19REFFLD(RNRENK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 24'Dato:'
     A            C2RDAT         6Y 0B  9 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        B  9 61REFFLD(RNREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(L)
     A                                      COLOR(YLW)
     A                                 10  1'Inkassokode. . .:'
     A            C2INKO    R        B 10 19REFFLD(RNINKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10 24'Dato:'
     A            C2IDAT         6Y 0B 10 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A  39 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            C2VALK    R        O 10 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            C2AVDE    R        B 11 19REFFLD(RNAVDE)
     A  34                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  34 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            C2GTXT    R   18   O 11 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                 11 43'Valutabeløp. . .:'
     A            C2VALB    R        O 11 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12  1'Selger . . . . .:'
     A            C2SLGR    R        B 12 19REFFLD(RNSLGR)
     A  35                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  35 81                               ERRMSGID(RM01090 *LIBL/QMFOKON 81)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            C2ITXT    R   18   O 12 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                 12 43'Valuta-rest. . .:'
     A            C2VALR    R        B 12 61REFFLD(RNVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(L)
     A                                      COLOR(YLW)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 17'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
