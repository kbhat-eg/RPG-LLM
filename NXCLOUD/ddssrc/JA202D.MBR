     A*%%TS  SD  20140213  104211  SST620      REL-V7R1M0  5770-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       010816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140131  094805  SST620      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1REGL    R        O  9  5REFFLD(JAREGL)
     A            B1FRPR    R        O  9 11REFFLD(JAFRPR)
     A            B1OKNI    R        O  9 23REFFLD(JAOKNI)
     A                                      EDTCDE(4)
     A            B1AVRP    R        O  9 35REFFLD(JAAVRP)
     A                                      EDTCDE(4)
     A            B1RTXT        30A  O  9 43
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140131  101220  SST620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Avrundingsregler'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 54'Filter: Regel:'
     A            B2FRGL    R        O  4 75REFFLD(JAREGL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  5'Regel'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Frapris'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Regel'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Frapris '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 27'Økning'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 36'Avr.%'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 43'Beskrivelse    '
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V  Regel    Frapris      Økning   -
7.xx A**                                    Avr.%                              -
7.xx A**                                            '
7.xx A**                                    COLOR(WHT)
7.xx A            B2REGL    R        B  6  5REFFLD(JAREGL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FRPR    R        B  6 11REFFLD(JAFRPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20140121  122422  SST620      REL-V7R1M0  5770-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake  F4=Spørr-
     A                                      ing  F5=Forny  F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20140212  115359  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Avrundingsregler  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C1RTXT        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Regel  . . . . . . . . . . +'
     A                                  6  2'Fra Pris (innkl. mva). . . .'
     A            C1REGL    R        B  5 31REFFLD(JAREGL)
     A  41                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1FRPR    R        B  6 31REFFLD(JAFRPR)
     A  42                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spøring '
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20140128  090350  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Avrundings-regel finnes  '
     A                                  3  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140212  115643  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Avrundingsregel   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2RTXT        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Regel nummer . . . . . . . .'
     A                                  6  2'Fra Pris (innkl. mva). . . .'
     A                                  9  2'Økning (i kr.) . . . . . . .'
     A            C2REGL    R        O  5 31REFFLD(JAREGL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(FMR0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A            C2ODAT         6Y 0O 22 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 58'/'
     A            C2EDAT         6Y 0O 22 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 69'/'
     A            C2EUSR    R        O 22 71REFFLD(JAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spøring '
     A                                      COLOR(BLU)
     A            C2FRPR    R        O  6 31REFFLD(JAFRPR)
     A                                      EDTCDE(4)
     A  35                                  ERRMSGID(FMR0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Avrundings%  . . . . . . . .'
     A            C2OKNI    R        B  9 31REFFLD(JAOKNI)
     A                                      EDTCDE(4)
     A  35                                  ERRMSGID(FMR0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A  49                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A            C2AVRP    R        B 10 31REFFLD(JAAVRP)
     A                                      EDTCDE(4)
     A  35                                  ERRMSGID(FMR0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A  49                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A          R D1WIN
     A*%%TS  SD  20140128  090350  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 22 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette avrundings regel   '
     A                                      DSPATR(RI)
     A                                  4  2'Regel nummer . . .'
     A                                  5  2'Fra Pris . . . . .'
     A                                  8  2'Økning . . . . . .'
     A            D1AVRP    R        O  9 21REFFLD(JAAVRP)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            D1REGL    R        O  4 21REFFLD(JAREGL)
     A            D1FRPR    R        O  5 21REFFLD(JAFRPR)
     A                                  9  2'Avrundings%. . . .'
     A            D1OKNI    R        O  8 21REFFLD(JAOKNI)
     A          R K1WIN
     A*%%TS  SD  20140212  115643  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 22 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Regel nummer . . +'
     A                                  4  2'Fra Pris (ink.mva)'
     A                                  6  2'Økning (i kr.) . .'
     A            K1REGL    R        B  3 21REFFLD(JAREGL)
     A  47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11  2'Regel nummer . . .'
     A                                 12  2'Fra Pris (innk.mva'
     A            JAREGL    R        O 11 21REFFLD(JAREGL)
     A                                      OVRDTA
     A                                 21  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A            K1FRPR    R        B  4 21REFFLD(JAREGL)
     A  47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Avrundings%  . . .'
     A            K1OKNI    R        B  6 21REFFLD(JAOKNI)
     A  47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1AVRP    R        B  7 21REFFLD(JAAVRP)
     A  47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  9  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A            JAFRPR    R        O 12 21REFFLD(JAFRPR)
     A                                      OVRDTA
     A          R F1WIN
     A*%%TS  SD  20140128  090350  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 13 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            F1REG     R        H      REFFLD(JAREGL)
     A                                  2  2' Filter '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Regel. . . . . . +'
     A            F1REGL    R        B  4 21REFFLD(JAREGL)
     A  31                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            F1RTXT        30A  O  4 28COLOR(YLW)
     A                                 12  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20140121  152148  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Avrundings-nøkkel finnes '
     A                                  3  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R NYREGEL
     A*%%TS  SD  20140121  152148  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  2  2'Avrundings-regel finnes ikke'
     A                                  3  2'Denne må opprettes          '
     A                                  4 17'Tryk ENTER'
