     A*%%TS  SD  20100305  135445  ASP_A04     REL-V6R1M0  5761-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EM102
     A* Beskrivelse . . : BONUS
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.21 A*       081113 BSA  Lagt tilbake bruk av varenr og lev.type
6.23 A*       041213 BSA  Endringer i skjermbildet pga jacada
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A*--------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051021  082418  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1OGRP    R        O  9  4REFFLD(EKOGRP)
     A            B1HGRP    R        O  9  7REFFLD(EKHGRP)
     A            B1UGRP    R        O  9 10REFFLD(EKUGRP)
     A            B1LDOR    R        O  9 14REFFLD(EKLDOR)
     A            B1MODN    R        O  9 21REFFLD(EKMODN)
     A            B1VARE    R   -7   O  9 30REFFLD(EKVARE)
6.23 A            B1TEXT        21A  O  9 41
     A**          B1ENHE    R        O  9 39REFFLD(EKENHE)
6.21 A            B1LETY    R        O  9 39REFFLD(EKLETY)
     A*           B1BOTY    R        O  9 45REFFLD(EKBOTY)
6.23 A            B1FDAT         6Y 0O  9 64EDTCDE(Z)
6.23 A            B1TDAT         6  0O  9 71EDTCDE(Z)
6.23 A            B1MERK         1A  O  9 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051021  131856  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
6.20 A**                                    CF07
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonuselement - mal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Mal. . .'
     A            B2HBOT         2A  O  4 11
     A            B2HBES        35A  O  4 14
7.xx A                                  5  4'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  9'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 20'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 29'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 40'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 51'Lt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Bonusdetaljer'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  7'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 10'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 21'Modulnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 30'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 38'Lt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'Fra'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 74'Til'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'M'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 66'dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 73'dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2OGRP    R        B  6  4REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  7'+'
7.xx A            B2HGRP    R        B  6  9REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 12'+'
7.xx A            B2UGRP    R        B  6 14REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 18'+'
7.xx A            B2LDOR    R        B  6 20REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 27'+'
7.xx A            B2MODN    R        B  6 29REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 38'+'
7.xx A            B2VARE    R   -7   B  6 40REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A**          B2ENHE    R        B  8 39REFFLD(EKENHE)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
7.xx A                                  6 49'+'
7.xx A            B2LETY    R        B  6 51REFFLD(EKLETY)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
     A*           B2BOTY    R        B  8 48REFFLD(EKBOTY)
     A*                                     COLOR(YLW)
     A*                                     OVRDTA
     A          R B2CMD
     A*%%TS  SD  20051026  223816  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
6.20 A                                      orny   F6=Opprett'
6.20 A**                                    orny   F6=Opprett   F7=Hent fra R&A'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051028  085705  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprette bonusnivå for mal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Varegruppe . . . . . . +'
7.xx A            C1OGRP    R        B  5 27REFFLD(EKOGRP)
     A  41                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 30'+'
7.xx A            C1HGRP    R        B  5 32REFFLD(EKHGRP)
     A  42                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 35'+'
     A            C1UGRP    R        B  5 37REFFLD(EKUGRP)
     A  43                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1GTXT    R        O  5 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Leverandør . . . . . . +'
7.xx A            C1LDOR    R        B  6 27REFFLD(EKLDOR)
     A  44                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV        30A  O  6 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Modulnr. . . . . . . . +'
7.xx A            C1MODN    R        B  7 27REFFLD(EKMODN)
     A  34                                  ERRMSGID(EMK0070 *LIBL/QMFOFAK 34)
     A  45                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Varenr . . . . . . . . +'
7.xx A            C1VARE    R   -7   B  8 27REFFLD(EKVARE)
     A  35                                  ERRMSGID(EMK0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O  8 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                                9  2'Enhet. . . . . . . . . . . .'
     A**          C1ENHE    R        B  9 31REFFLD(EKENHE)
     A**47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A**          C1ETXT    R        O  9 41REFFLD(VAETXT)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
7.xx A                                  9  2'Leveringstype. . . . . +'
7.xx A            C1LETY    R        B  9 27REFFLD(EKLETY)
6.21 A  48                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 48)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A            C1LTXT    R        O  9 41REFFLD(VYBESK)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
7.xx A                                 10  2'Fra-dato . . . . . . . .'
7.xx A            C1FDAT         6Y 0B 10 27COLOR(YLW)
     A  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A  52                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 52)
     A  53                                  ERRMSGID(EMK0100 *LIBL/QMFOFAK 53)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                 11  2'Til-dato . . . . . . . .'
7.xx A            C1TDAT         6Y 0B 11 27COLOR(YLW)
     A  51                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 51)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20051021  083406  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bonus-nivå finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051026  142713  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF20
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonus-nivå'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varegruppe . . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGRP    R        O  5 34REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGRP    R        O  5 37REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT    R        O  5 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  6 31REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O  7 31REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varenr . . . . . . . . . . .'
     A            C1VARE    R   -7   O  8 31REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O  8 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                                9  2'Enhet. . . . . . . . . . . .'
     A**          C1ENHE    R        O  9 31REFFLD(EKENHE)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A**          C1ETXT    R        O  9 41REFFLD(VAETXT)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
6.21 A                                  9  2'Leveringstype. . . . . . . .'
6.21 A            C1LETY    R        O  9 31REFFLD(EKLETY)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A            C1LTXT    R        O  9 41REFFLD(VYBESK)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A                                 10  2'Fra-dato . . . . . . . . . .'
6.21 A            C1FDAT         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 11  2'Til-dato . . . . . . . . . .'
6.21 A            C1TDAT         6Y 0O 11 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 12  2'Bonusbyggende. . . . . . . .'
6.21 A                                 12 33'1=Nei'
6.21 A            C2BYGG    R        B 12 31REFFLD(EKLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                 13  2'Merknad. . . . . . . . . . .'
6.21 A            C2MERK    R        B 13 31REFFLD(EKMERK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . '
     A            C2EEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Sist endret av . . . . . . .'
     A            C2EEUS         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C7BLD
     A*%%TS  SD  20051021  083406  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bekreft import av informasjon'
     A                                  3  2'fra Rapport&Analyse med ENTER'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051027  213608  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bonus-nivå '
     A                                      DSPATR(RI)
     A                                  5  2'Varegruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(EKOGRP)
     A            D1HGRP    R        O  5 24REFFLD(EKHGRP)
     A            D1UGRP    R        O  5 27REFFLD(EKUGRP)
     A                                  6  2'Leverandør . . . .'
     A            D1LDOR    R        O  6 21REFFLD(EKLDOR)
     A                                  7  2'Modulnr. . . . . .'
     A            D1MODN    R        O  7 21REFFLD(EKMODN)
     A                                  8  2'Varenr . . . . . .'
     A            D1VARE    R        O  8 21REFFLD(EKVARE)
     A**                                9  2'Enhet. . . . . . .'
     A**          D1ENHE    R        O  9 21REFFLD(EKENHE)
6.21 A                                  9  2'Leveringstype. . .'
6.21 A            D1LETY    R        O  9 21REFFLD(EKLETY)
6.21 A                                 10  2'Fra-dato . . . . .'
6.21 A            D1FDAT         6Y 0O 10 21EDTWRD('  .  .  ')
6.21 A                                 11  2'Til-dato . . . . .'
6.21 A            D1TDAT         6Y 0O 11 21EDTWRD('  .  .  ')
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051021  122950  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 22 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varegruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(EKOGRP)
     A  41                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 24'+'
7.xx A            K1HGRP    R        B  4 26REFFLD(EKHGRP)
     A  42                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 29'+'
7.xx A            K1UGRP    R        B  4 31REFFLD(EKUGRP)
     A  43                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Leverandør . . . +'
     A            K1LDOR    R        B  5 21REFFLD(EKLDOR)
     A  44                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Modulnr. . . . . +'
     A            K1MODN    R        B  6 21REFFLD(EKMODN)
     A  34                                  ERRMSGID(EMK0070 *LIBL/QMFOFAK 34)
     A  45                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varenr . . . . . +'
     A            K1VARE    R   -7   B  7 21REFFLD(EKVARE)
     A  35                                  ERRMSGID(EMK0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                                8  2'Enhet. . . . . . .'
     A**          K1ENHE    R        B  8 21REFFLD(EKENHE)
     A**47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A**                                    COLOR(YLW)
     A**                                    CHECK(RB)
     A**                                    OVRDTA
7.xx A                                  8  2'Leveringstype. . +'
6.21 A            K1LETY    R        B  8 21REFFLD(EKLETY)
6.21 A  48                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 48)
6.21 A                                      COLOR(YLW)
6.21 A                                      CHECK(RB)
6.21 A                                      OVRDTA
6.21 A                                  9  2'Fra-dato . . . . .'
6.21 A            K1FDAT         6Y 0B  9 21COLOR(YLW)
     A  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A  52                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 52)
     A  53                                  ERRMSGID(EMK0100 *LIBL/QMFOFAK 53)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 10  2'Til-dato . . . . .'
6.21 A            K1TDAT         6Y 0B 10 21COLOR(YLW)
     A  51                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 51)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 13  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 14  2'Varegrp/Lev./Modul'
     A            EMEOGR         2S 0O 14 21OVRDTA
     A            EMEHGR         2S 0O 14 24OVRDTA
     A            EMEUGR         3S 0O 14 27OVRDTA
     A                                 14 31'/'
     A            EMELDO         6S 0O 14 33OVRDTA
     A                                 14 40'/'
     A            EMEMOD         8S 0O 14 42OVRDTA
     A**                               15  2'Varenr/Enhet . . .'
     A                                 15  2'Varenr . . . . . .'
     A            EMEVAR        15A  O 15 21OVRDTA
     A**                               15 37'/'
     A**          EMEENH         3A  O 15 39OVRDTA
6.23 A                                 16  2'Leveringstype. . .'
6.23 A            EMELET         1S 0O 16 21OVRDTA
6.23 A                                 17  2'Fra-dato/Til-dato.'
6.23 A            K1FDA2         6Y 0O 17 21EDTWRD('  .  .  ')
     A                                      OVRDTA
6.23 A                                 17 30'/'
6.23 A            K1TDA2         6Y 0O 17 32EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
