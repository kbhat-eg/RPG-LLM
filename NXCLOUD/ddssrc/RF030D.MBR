     A*%%TS  SD  20021107  113510  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF030D
     A* Beskrivelse . . : Fordelingsparametre, vedlikehold - hovedrec.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       071102 KOB  Nytt program
7.xx A* 7.xx  100516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021107  102025  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        O  8  4REFFLD(RFKONT)
     A            B1SPES    R        O  8 10REFFLD(RFSPES)
     A            B1AVDE    R        O  8 15REFFLD(RFAVDE)
     A            B1TEXT    R        O  8 20REFFLD(RHTEXT)
     A            B1KODE    R        O  8 55REFFLD(RFKODE)
     A            B1PROS    R        O  8 60REFFLD(RFPROS)
     A            B1PRIS    R        O  8 69REFFLD(RFPRIS)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20021107  113338  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 10'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Til konti'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 10'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Kontotekst      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 59'Prosent'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 71'Pris'
7.xx A**                                7  2'V Konto Bærer Avd Kontotekst      -
7.xx A**                                                     Kode  Prosent     -
7.xx A**                                    Pris'
     A                                      COLOR(WHT)
7.xx A            B2KONT    R        B  5  4REFFLD(RFKONT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2SPES    R        B  5 10REFFLD(RFSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2AVDE    R        B  5 15REFFLD(RFAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20021107  102836  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Konto. . . . . . . .'
     A            C1KONT    R        B  5 23REFFLD(RFKONT)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1SPES    R        B  5 29REFFLD(RFRFR/RFSPES *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1AVDE    R        B  5 34REFFLD(RFRFR/RFAVDE *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20021107  100205  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Fordelingsparameter finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20021107  102836  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av fordelingsparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Konto. . . . . . . . . . . .'
     A            C1KONT    R        O  5 31REFFLD(RFKONT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SPES    R        O  5 38REFFLD(RFSPES)
     A  31                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C1AVDE    R        O  5 44REFFLD(RFAVDE)
     A  31                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Kode . . . . . . . . . . . .'
     A            C2KODE    R        B  7 31REFFLD(RFKODE)
     A  32                                  ERRMSGID(RMF0060 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMF0071 *LIBL/QMFOKON 33)
     A  34                                  ERRMSGID(RMF0072 *LIBL/QMFOKON 34)
     A  35                                  ERRMSGID(RMF0073 *LIBL/QMFOKON 35)
     A  36                                  ERRMSGID(RMF0074 *LIBL/QMFOKON 36)
     A  37                                  ERRMSGID(RMF0075 *LIBL/QMFOKON 37)
     A  38                                  ERRMSGID(RMF0076 *LIBL/QMFOKON 38)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                  7 34'(B=Beregning, O=Omfordeling)'
     A                                  8  2'Prosent andel. . . . . . . .'
     A            C2PROS    R        B  8 31REFFLD(RFPROS)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                  9  2'Pris . . . . . . . . . . . .'
     A            C2PRIS    R        B  9 31REFFLD(RFPRIS)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                 12  2'Kontotekst . . . . . . . . .'
     A            C2TEXT    R        B 12 31REFFLD(RHTEXT)
     A  31                                  ERRMSGID(RMF0100 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RFEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20021107  113510  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette fordelingsparameter '
     A                                      DSPATR(RI)
     A*                                 4  4'Konto  Bærer  Avd  Kode   Prosent -
     A*                                          Pris '
     A*                                     COLOR(WHT)
     A                                  4  2'Konto. . . . . . .'
     A            D1KONT    R        O  4 21REFFLD(RFKONT)
     A            D1SPES    R        O  4 27REFFLD(RFSPES)
     A            D1AVDE    R        O  4 32REFFLD(RFAVDE)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20021107  104103  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  4  4' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  6  4'Konto  Bærer  Avd'
     A                                      COLOR(WHT)
     A            K1KONT    R        B  7  4REFFLD(RFKONT)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 31)
     A  39                                  ERRMSGID(RMF0050 *LIBL/QMFOKON 32)
     A            K1SPES    R        B  7 11REFFLD(RFSPES)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            K1AVDE    R        B  7 17REFFLD(RFAVDE)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(RMF0030 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9  4' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  4'Konto  Bærer  Avd'
     A                                      COLOR(WHT)
     A            RFKONT    R        O 12  4REFFLD(RFKONT)
     A            RFSPES    R        O 12 11REFFLD(RFSPES)
     A            RFAVDE    R        O 12 17REFFLD(RFAVDE)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
