     A*%%TS  SD  20070609  104909  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RM110D
     A* Beskrivelse . . : Kunder, vedlikehold av kundeinformasjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160303 KOB  Nytt program
7.xx A*       120516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20030316  205405  KOB         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 5 6 7 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O 10  4REFFLD(RKKUND)
     A            B1NAVN    R        O 10 11REFFLD(RKNAVN)
     A            B1STED        23A  O 10 42
     A            B1TLFN    R        O 10 66REFFLD(RKTLFN)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030316  205405  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på kundeinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Frisøk .'
     A            B2STEK        35A  B  5 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Kundenavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  8 11'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8 18'7=Eiendommer'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Kundenavn                '
     A                                      COLOR(WHT)
7.xx A                                  9 42'Postnr.'
     A                                      COLOR(WHT)
7.xx A                                  9 70'Telefon'
     A                                      COLOR(WHT)
7.xx A            B2KUND    R        B  7  4REFFLD(RKKUND)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  7 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PONR    R        B  7 42REFFLD(RKPONR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20030222  103710  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Aktiv/Alle'
     A                                      COLOR(BLU)
     A                                 23 32'F9=Gjenta søk'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for maskininformasjon
     A*
     A          R C2WIN
     A*%%TS  SD  20070609  104909  KOB550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 20 60)
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Kunde . . . . .:'
     A            C2KUND    R        O  2 19REFFLD(RKKUND)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2NAVN    R        B  2 26REFFLD(RKNAVN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RMK3900 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  4  2'Kontaktperson .:'
     A            C2PERS    R        B  4 19REFFLD(RFRFR/RKPERS *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  5  2'Telefon . . . .:'
     A            C2TLFN    R        B  5 19REFFLD(RKTLFN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  5 33'Fax:'
     A            C2TLFX    R        B  5 38REFFLD(RKTLFX)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6  2'E-mail :'
     A            C2EMAL    R        B  6 11REFFLD(RFRFR/RKEMAL *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  8  2'Fødselsdato . .:'
     A            C2KFDA         6Y 0B  8 19OVRDTA
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  9  2'Beløpsgrense. .:'
     A            C2GRAL    R        B  9 19REFFLD(RFRFR/RKGRAL *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
