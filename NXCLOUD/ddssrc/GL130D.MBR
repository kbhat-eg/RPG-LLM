     A*%%TS  SD  20030120  125616  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL130D
     A* Beskrivelse . . : Forespørsel på utbetalinger pr. dag
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1SFL                     SFL
     A*%%TS  SD  19960715  115826  RHO         REL-V3R6M0  5716-PW1
     A                                      TEXT('Subfile')
     A            B1DATO         8Y 0O  8  2EDTWRD('  .  .    ')
     A                                      TEXT('Betalingsdato')
     A            B1MORE    R        O  8 14REFFLD(ROBELØ)
     A                                      TEXT('Beløp')
     A                                      EDTCDE(K)
     A            B1MARE    R        O  8 31REFFLD(ROBELØ)
     A                                      TEXT('Beløp')
     A                                      EDTCDE(K)
     A            B1IKOV    R        O  8 48REFFLD(ROBELØ)
     A                                      TEXT('Beløp')
     A                                      EDTCDE(K)
     A            B1TOTA    R        O  8 64REFFLD(ROBELØ)
     A                                      TEXT('Beløp')
     A                                      EDTCDE(K)
E5.00A            B1STAT    R        O  8 80REFFLD(RTSTAT)
     A                                      TEXT('Status kode m.h.p. om sum inn-
     A                                      eholder valuta')
     A                                      COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030120  125616  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('Hovedbilde')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      PRINT
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  39                                  SFLMSGID(RML0370 *LIBL/QMFOKON 39)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oversikt over utbetalinger pr. dat-
     A                                      o'
     A                                      COLOR(WHT)
7.xx A                                  2 38'GL130B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A*                                 5  2'           ______ Oversendt til ba-
7.xx A*                                     nk ______   Ikke oversendt         -
7.xx A*                                               '
7.xx A*                                     COLOR(WHT)
7.xx A                                  6 14'Oversendt bank '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 31'Oversendt bank '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 48'Ikke oversendt'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'Dato      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Mottatt 1.retur'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 31'Mangler 1.retur'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 48'til bank      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 64'Totalt       '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 80'S'
7.xx A                                      COLOR(WHT)
7.xx A*                                 6  2'Dato       Mottatt 1.retur  Mangle-
7.xx A*                                     r 1.retur      til bank            -
7.xx A*                                      Totalt  S'
7.xx A*                                     COLOR(WHT)
7.xx A*                                     DSPATR(UL)
     A          R B2CMD
     A*%%TS  SD  19960715  115336  RHO         REL-V3R6M0  5716-PW1
     A                                      TEXT('Kommando taster')
     A                                      PRINT
     A                                 24  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME
     A*%%TS  SD  19941222  135431  RH          REL-V2R3M0  5738-PW1
     A                                      KEEP
     A                                      ASSUME
     A                                 24 79' '
