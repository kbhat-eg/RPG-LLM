     A*%%TS  SD  20020819  152002  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JR500D
     A* Beskrivelse . . : Velge rapportør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 030616 bsa Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19980326  120225  HKO         REL-V3R7M0  5716-PW1
     A            B1VALG    R        B  7  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1NAVN    R        O  7  5REFFLD(JRNAVN)
     A            B1RAPP    R        O  7 43REFFLD(JRRAPP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020819  152002  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge rapportør '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Navn'                          -
7.xx A                                      COLOR(WHT)
7.xx A                                  3 42'Rapp'                          -
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Navn                             '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 42'Rapp'
7.xx A                                      COLOR(WHT)
7.xx A**                                5  2'V  Navn                           -
7.xx A**                                          Rapp.'
7.xx A**                                    COLOR(WHT)
7.xx A            B2NAVN    R        B  4  5REFFLD(JRNAVN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2RAPP    R        B  4 43REFFLD(JRRAPP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19940410  165946  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(*DFT 17 47)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 78' '
