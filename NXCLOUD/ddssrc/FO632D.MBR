     A*%%TS  SD  20040915  155957  GRO         REL-V5R2M0  5722-WDS
     A*
7.xx A*          160216 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      CF03
     A                                      CF12
     A          R B1BLD
     A*%%TS  SD  20040915  155957  GRO         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av kontroll-liste'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO632B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Seleksjonskriterier :'
7.xx A                                  7  2'Ordre-type . . . . . . . . +'
     A            B1OTYP    R        B  7 31REFFLD(FOOTYP)
     A  31                                  ERRMSGID(FMA0260 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                  8  2'Sekvenskoder . . . . . . . .'
     A            B1SEKV         2Y 0B  8 31EDTWRD('  ')
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  8 40'(Skriver alle med SK mindre enn,'
     A                                  9 40'og alle lik oppgitt SK)'
7.xx A                                 10  2'Avdeling . . . . . . . . . +'
     A            B1AVDE    R        B 10 31REFFLD(FOAVDE)
     A  32                                  ERRMSGID(FRA0720 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 12 32'Fra'
     A                                 12 44'Til'
     A                                 13  2'Ordrenummer. . . . . . . . .'
     A            B1FONR    R        B 13 31REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(FM09990 *LIBL/QMFOFAK 37)
     A            B1TONR    R        B 13 42REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(FM09990 *LIBL/QMFOFAK 37)
7.xx A                                 14  2'Kundenummer. . . . . . . . +'
     A            B1FKUN    R        B 14 31REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 14 40'+'
7.xx A            B1TKUN    R        B 14 42REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 15  2'Ordre-dato . . . . . . . . .'
     A            B1FDAT         6Y 0B 15 31COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(FM00160 *LIBL/QMFOFAK 35)
7.xx A            B1TDAT         6Y 0B 15 42COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(FM00160 *LIBL/QMFOFAK 35)
     A                                 16  2'Pakks-dato t.o.m.  . . . . .'
7.xx A            B1LDAT         6Y 0B 16 42COLOR(YLW)
     A                                      CHECK(RB)
     A                                 17  2'Info-kode. . . . . . . . . .'
     A            B1ITYP    R        B 17 31REFFLD(FOITYP)
     A                                      COLOR(YLW)
     A                                 18  2'Alt. fakturering . . . . . .'
     A            B1ALTF         2Y 0B 18 31COLOR(YLW)
     A                                      CHECK(RB)
     A            B1ALTN        20A  O 18 42COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020312  204033  HKO         REL-V5R1M0  5722-WDS
     A                                      CF04
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av kontroll-liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  2'Utskriftvariant. . . . . . . . .'
     A            C1VAL1         1Y 0B  5 35VALUES(1 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  5 40'1 = Kontroll-liste med DB'
     A                                  6 40'2 = Kontroll-liste med tekster'
     A                                  7 40'3 = Kontroll-liste med verdier'
     A                                  8 40'5 = Kontroll oversikt'
     A                                 10  2'Sorteringsrekkefølge . . . . . .'
     A            C1VAL2         1Y 0B 10 35CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2)
     A                                 10 40'1=Registreringsrekkefølge'
     A                                 11 40'2=Vareadresse'
     A                                 13  2'Ordre som skal skrives ut. . . .'
     A            C1VAL3         1Y 0B 13 35VALUES(0 1 2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 13 40'0=Alle, 1=De som er plukket'
     A                                 14 48'2=Kontrollkode=J'
     A                                 15  2'Bare ordre fra denne skjerm. . .'
     A            C1VAL4         1Y 0B 15 35VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 15 40'1=Ja,0=Nei'
     A                                 16  2'Bare ordre for denne bruker. . .'
     A            C1VAL5         1Y 0B 16 35VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 16 40'1=Ja,0=Nei'
     A                                 17  2'Skal kontrollkode slettes. . . .'
     A            C1VAL6         1Y 0B 17 35VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 17 40'1=Ja,0=Nei'
     A                                 19  2'Skriv varelinjer der :'
     A                                      DSPATR(HI)
     A                                 20  2'Salgspris og rabatt ikke er std'
     A            C1VAL7         1Y 0B 20 35VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 20 40'1=Ja,0=Nei'
     A                                 21  2'DB er under . . . . . . . . . .'
     A            C1PROS         2Y 0B 21 35EDTWRD('  ')
     A                                      COLOR(YLW)
     A                                 21 40'Oppgi DB i hele prosenter'
     A                                 22 40'NB! Skriver ALLE med 100% DB'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Jobbkø'
     A                                      COLOR(BLU)
     A*
     A          R F1WIN
     A                                      WINDOW(*DFT 16 30)
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Velg alt.fakturering'
     A                                      DSPATR(RI)
     A                                  4  2'1=Valg'
     A                                      COLOR(BLU)
     A                                  5  2'V'
     A                                      COLOR(WHT)
     A            F1ALTH        13A  O  5  5COLOR(WHT)
     A            F1VAL1         1A  B  6  2COLOR(YLW)
     A            F1ALT1        25A  O  6  5
     A            F1VAL2         1A  B  7  2COLOR(YLW)
     A            F1ALT2        25A  O  7  5
     A            F1VAL3         1A  B  8  2COLOR(YLW)
     A            F1ALT3        25A  O  8  5
     A            F1VAL4         1A  B  9  2COLOR(YLW)
     A            F1ALT4        25A  O  9  5
     A            F1VAL5         1A  B 10  2COLOR(YLW)
     A            F1ALT5        25A  O 10  5
     A            F1VAL6         1A  B 11  2COLOR(YLW)
     A            F1ALT6        25A  O 11  5
     A            F1VAL7         1A  B 12  2COLOR(YLW)
     A            F1ALT7        25A  O 12  5
     A            F1VAL8         1A  B 13  2COLOR(YLW)
     A            F1ALT8        25A  O 13  5
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
