     A*%%TS  SD  20081102  123424  KOB560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK120D
     A* Beskrivelse . . : Kundeposteringer, vedlikehold - renter/inkasso
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
6.20 A*       030313 NHO  Lagt inn vis av notatblokk
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20020813  135525  KOB         REL-V5R1M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1WTIS    R        H      REFFLD(RWTIST)
     A            B1RAAR    R        H      REFFLD(RNRAAR)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 4 5 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(RNBILN)
6.20 A  73                                  COLOR(YLW)
6.NU A            B1BIDA         6Y 0O  9 13EDTCDE(Z)
6.NU A            B1BILK    R        O  9 20REFFLD(RNBILK)
6.NU A            B1TEXT        10A  O  9 23
6.NU A            B1REFN    R        O  9 34REFFLD(RNREFN)
     A            B1FDAT         6Y 0O  9 43EDTCDE(Z)
     A            B1BELØ        11Y 2O  9 50EDTCDE(J)
     A            B1REST        11Y 2O  9 66EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050723  184309  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundeposteringer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde:'
     A            B2KUND    R        O  4  9REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
6.20 A  94        B2NOTB         8A  O  4 48DSPATR(RI)
     A                                  5 58'Saldo:'
     A            B2SALD    R        O  5 66REFFLD(RKSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  6  2'2=Endre  4=Slett  5=Vis  8=Vis fak-
     A                                      tura'
     A                                      COLOR(BLU)
     A                                  6 58'Sum. :'
     A            B2SUMP    R        O  6 66REFFLD(RKSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  7  2'9=Slett og merk post'
     A                                      COLOR(BLU)
     A  38                                  DSPATR(ND)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  8 14'Dato'
     A                                      COLOR(WHT)
6.NU A                                  8 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  8 23'Tekst'
     A                                      COLOR(WHT)
     A                                  8 31'Val.'
     A                                      COLOR(WHT)
     A                                  8 37'Ref.'
     A                                      COLOR(WHT)
     A                                  8 43'Forfall'
     A                                      COLOR(WHT)
     A                                  8 56'Beløp'
     A                                      COLOR(WHT)
     A                                  8 68'Restbeløp'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020813  090005  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
6.20 A                                 23  2'F2=Notatark'
6.20 A                                      COLOR(BLU)
6.20 A*                                23  2'F3=Gå tilbake'
6.20 A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.20 A*                                23 17'F5=Forny'
6.20 A                                 23 30'F5=Forny'
     A                                      COLOR(BLU)
6.20 A*                                23 27'F7=Valuta/NOK'
6.20 A                                 23 40'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20081031  141049  KOB560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
6.20 A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 32'Kundeposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(RNBILK)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  4 22REFFLD(RNTEXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0B  5 19
     A  37                                  DSPATR(PC)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A  40                                  ERRMSG('Forfallsdato er ugyldig')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        B  6 19REFFLD(RNREKL)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        B  7 19REFFLD(RNREFN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7 43'Generalnota-nr. :'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        B  8 19REFFLD(RNANTP)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 24'Dato:'
     A            C2PDAT         6Y 0B  8 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  41                                  ERRMSG('Purret dato er ugyldig')
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        B  9 19REFFLD(RNRENK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 24'Dato:'
     A            C2RDAT         6Y 0B  9 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A  42                                  ERRMSG('Renteberegnet dato er ugyld-
     A                                      ig')
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(RNREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Rente fra . . . :'
     A                                 10 24'Dato:'
     A            C2RFDA         6Y 0B 10 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  43                                  ERRMSG('Rente fra dato er ugyldig')
     A                                 10 43'Rentegrunnlag. .:'
     A            C2RGRL    R        B 10 61REFFLD(RFRFR/RWRGRL *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11  1'Rente til . . . :'
     A                                 11 24'Dato:'
     A            C2RTDA         6Y 0B 11 30EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  44                                  ERRMSG('Rente til dato er ugyldig')
     A                                 11 43'Beregnet rente .:'
     A            C2RENT    R        O 11 61REFFLD(RFRFR/RWRENT *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Inkassokode. . .:'
     A            C2INKO    R        B 12 19REFFLD(RNINKO)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 24'Dato:'
     A            C2IDAT         6Y 0B 12 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  45                                  ERRMSG('Inkassodato er ugyldig')
     A                                 12 43'Valuta-kode. . .:'
     A            C2VALK    R        O 12 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13  1'Avdeling . . . .:'
     A            C2AVDE    R        B 13 19REFFLD(RNAVDE)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2GTXT    R   18   O 13 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valutabeløp. . .:'
     A            C2VALB    R        O 13 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  1'Selger . . . . .:'
     A            C2SLGR    R        B 14 19REFFLD(RNSLGR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01090 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2ITXT    R   18   O 14 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Valuta-rest. . .:'
     A            C2VALR    R        O 14 61REFFLD(RNVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
6.20 A                                 16  2'F2=Noteark bilag'
6.20 A                                      COLOR(BLU)
6.20 A*                                16  2'F3=Gå tilbake'
6.20 A                                 16 20'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.20 A*                                16 18'F4=Spørring'
6.20 A                                 16 35'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20081102  123424  KOB560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 17 48)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette postering '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde . . . . .'
     A            D1KUND    R        O  4 18REFFLD(RKKUND)
     A  32                                  ERRMSGID(RMK0360 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMK0370 *LIBL/QMFOKON 33)
     A  34                                  ERRMSGID(RMK0380 *LIBL/QMFOKON 34)
     A                                  5  2'Bilagskode. . :'
     A            D1BILK    R        O  5 18REFFLD(RNBILK)
     A                                      OVRDTA
     A                                  6  2'Tekst . . . . :'
     A            D1TEXT    R        O  6 18REFFLD(RNTEXT)
     A                                      OVRDTA
     A                                  7  2'Dato. . . . . :'
     A            D1BDAT    R        O  7 18REFFLD(RNBDAT)
     A                                      OVRDTA
     A                                  8  2'Beløp . . . . :'
     A            D1BELØ    R        O  8 18REFFLD(RNBELØ)
     A                                      OVRDTA
     A                                  9  2'Restbeløp . . :'
     A            D1REST    R        O  9 18REFFLD(RNREST)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
