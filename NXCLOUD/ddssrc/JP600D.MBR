     A*%%TS  SD  20050920  092816  HKO530      REL-V5R3M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       010816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20050920  092816  HKO530      REL-V5R3M0  5722-WDS
     A*                                     WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av betingelser med rabat-
     A                                      telement '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Leverandør . . . . . . . +'
     A            B1LDOR    R        B  4 29REFFLD(JPLDOR)
     A  31                                  ERRMSGID(JML0020 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  4 38'Blank=alle'
     A                                  6  2'Dato . . . . . . . . . . .'
     A            B1DATO         6Y 0B  6 29EDTWRD('  .  .  ')
     A  32                                  ERRMSGID(JM00250 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  8  2'Overstyringer:'
     A                                      DSPATR(UL)
     A                                  9  2'Utelate rabattelement. . .'
     A            B1VALR    R        B  9 29REFFLD(JÅVALG)
     A  33                                  ERRMSGID(JM00010 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                  9 38'1=ja,0=nei'
     A                                 10  2'Kun aktive betingelser . .'
     A            B1VALA    R        B 10 29REFFLD(JÅVALG)
     A  34                                  ERRMSGID(JM00010 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                 10 38'1=ja,0=nei'
     A                                 11  2'Kun utgåtte bet./element .'
     A            B1VALU    R        B 11 29REFFLD(JÅVALG)
     A  35                                  ERRMSGID(JM00010 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                 11 38'1=ja,0=nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
