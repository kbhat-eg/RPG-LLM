     A*%%TS  SD  20030120  112934  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL113D
     A* Beskrivelse . . : Spørring på oppdaterte returer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1SFL                     SFL
     A*%%TS  SD  19950216  191357  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Subfile')
     A            B1MRID    R        H      REFFLD(GDMRID)
     A            B1VALG    R        B 10  2REFFLD(AAVALG)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      VALUES(' ' '4')
     A  30                                  DSPATR(ND)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            B1MFGR    R        O 10  5REFFLD(GDMFGR)
     A                                      TEXT('Filgruppe')
     A            B1MFIR    R        O 10 10REFFLD(GDMFIR)
     A                                      TEXT('Firmanummer')
     A            B1MUBF    R        O 10 16REFFLD(GDMUBF)
     A                                      TEXT('Forslagsnummer')
     A                                      EDTCDE(Z)
     A            B1MRKO    R        O 10 22REFFLD(GDMRKO)
     A                                      TEXT('Returkode')
     A            B1INUT        10A  O 10 25TEXT('Innland/Utland')
     A            B1DATO         8Y 0O 10 37EDTWRD('  .  .    ')
     A                                      TEXT('Mottatt dato')
     A            B1KLOK    R        O 10 49REFFLD(GDMTIM)
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Mottatt klokken')
     A            B1KOMM    R        O 10 60REFFLD(GDMTXT)
     A                                      TEXT('Innland/Utland')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030120  112934  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('Hovedbilde')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på oppdaterte returer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 37'Filgr:'
     A            A1FGRP    R        O  4 44REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A                                      DSPATR(UL)
     A                                  4 49'Firma:'
     A            A1FIRM    R        O  4 56REFFLD(GCFIRM)
     A                                      TEXT('Firmanr')
     A                                      DSPATR(UL)
     A            A1FNAV        20A  O  4 60DSPATR(UL)
     A                                  5  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'4=Slette'
     A                                      COLOR(BLU)
     A                                  7  2'                                  -
     A                                       ______Mottatt_______ '
     A                                      COLOR(WHT)
     A                                  8  2'V  Fgr. Fir.  For. Rk. Type       -
     A                                       Dato        Klokken    Kommentar  -
     A                                               '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A          R B2CMD
     A*%%TS  SD  20030120  112934  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Kommando taster')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A            CMDF06        20A  O 23 29DSPATR(ND)
     A                                      COLOR(BLU)
     A          R C1WIN
     A*%%TS  SD  19950809  090341  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Slette post')
     A                                      CA03
     A                                      WINDOW(&W_AROW &W_ACOL 13 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Trykk Enter for å slette post.'
     A                                  4  2'Filgruppe. . .:'
     A            C1MFGR    R        O  4 18REFFLD(GDMFGR)
     A                                      DSPATR(UL)
     A                                  5  2'Firma. . . . .:'
     A            C1MFIR    R        O  5 18REFFLD(GDMFIR)
     A                                      DSPATR(UL)
     A                                  6  2'Forslagsnummer:'
     A            C1MUBF    R        O  6 18REFFLD(GDMUBF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                  7  2'Returkode. . .:'
     A            C1MRKO    R        O  7 18REFFLD(GDMRKO)
     A                                      DSPATR(UL)
     A                                  8  2'Type . . . . .:'
     A            C1INUT        10A  O  8 18DSPATR(UL)
     A                                  9  2'Dato . . . . .:'
     A            C1DATO         8Y 0O  9 18DSPATR(UL)
     A                                      EDTWRD('  .  .    ')
     A                                 10  2'Klokken. . . .:'
     A            C1KLOK    R     Y  O 10 18REFFLD(GDMTIM)
     A                                      DSPATR(UL)
     A                                      EDTWRD('  :  :  ')
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
