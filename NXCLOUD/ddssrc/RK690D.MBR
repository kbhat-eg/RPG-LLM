     A*%%TS  SD  20020128  145656  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK690D
     A* Beskrivelse . . : Kunder, utskrift generalnota - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240702 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20020122  203232  KOB         REL-V5R1M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av generalnota'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  8 10'Bilagsdato. . . . . :'
     A            A2PKDA    R        B  8 32REFFLD(RKPKDA)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 10'Periode . . . . . . :'
     A            A2PPER    R        B  9 32REFFLD(RKPPER)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 10 10'Regnskapsår . . . . :'
     A            A2PRÅR    R        B 10 32REFFLD(RKPRÅR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 12 10'Kundenr.. . . . . . :'
     A            A2PKNF    R        B 12 32REFFLD(RKPKNR)
     A  34                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMK0020 *LIBL/QMFOKON 34)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 12 40'(Blank=Alle kunder)'
     A                                 14 10'Bilagstype. . . . . :'
     A            A2BTYP    R        B 14 32REFFLD(RKPSTY)
     A                                      COLOR(YLW)
7.xx A                                 14 34'(k)'
7.xx A                                      COLOR(WHT)
7.xx A                                 14 39'J: Kun bilagtype 01 og 03 tas med'
7.xx A                                      COLOR(WHT)
7.xx A                                 15 39'Blank: Alle bilagstyper tas med'
7.xx A                                      COLOR(WHT)
     A                                 17 10'Kun forfalte poster.:'
     A            A2FORF    R        B 17 32REFFLD(RKPSFP)
     A                                      COLOR(YLW)
7.xx A                                 17 34'(k)'
7.xx A                                      COLOR(WHT)
7.xx A                                 17 39'J: Kun forfalte poster tas med'
7.xx A                                      COLOR(WHT)
7.xx A                                 18 39'Blank: Alle poster tas med'
7.xx A                                      COLOR(WHT)
     A                                 20 10'Betalingsbetingelse.:'
     A            A2BETB    R        B 20 32REFFLD(RKPSBB)
     A                                      COLOR(YLW)
7.xx A                                 20 34'(k)'
7.xx A                                      COLOR(WHT)
7.xx A                                 20 39'J: Hentes fra kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                 21 39'Blank: Betalingsbet. 99'
7.xx A                                      COLOR(WHT)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
