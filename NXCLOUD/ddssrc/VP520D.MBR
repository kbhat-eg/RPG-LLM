     A*%%TS  SD  20081105  130555  BOF570      REL-V5R4M0  5722-WDS
     A*
8.01 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081105  130555  BOF570      REL-V5R4M0  5722-WDS
     A            B1VARE    R        H      REFFLD(VVVARE)
8.01 A            B1TEK1    R   -2   O  7  2REFFLD(VVTEK1)
8.01 A            B1VAR8         8A  O  7 36
8.01 A            B1PRGR    R        O  7 45REFFLD(VPPRGR)
     A            B1NOBB    R        O  7 48REFFLD(VVNOBB)
     A            B1PDAT         6Y 0O  7 57EDTWRD('  .  .  ')
     A            B1GDAT         6Y 0O  7 66EDTWRD('  .  .  ')
     A            B1DIFF         4Y 0O  7 75EDTCDE(M)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081105  130555  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Varer med eldre prisdato i EVR enn-
     A                                       i VA'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 23REFFLD(VVLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV        30A  O  4 30COLOR(BLU)
     A                                      OVRDTA
     A                                  5 56'   EVR'
     A                                      COLOR(WHT)
     A                                  5 68'VA'
     A                                      COLOR(WHT)
     A                                  6  2'Varetekst                         -
8.01 A                                       Varenr  Pg Nobbnr  Prisdato Prisd-
     A                                      ato Diff.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990601  192528  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020226  104245  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise prisinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O  5 31REFFLD(VPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1    R        O  5 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  6 31REFFLD(VPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PTXT    R        O  6 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O  7 31REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ETXT    R        O  7 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O  8 31REFFLD(VPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LTXT    R        O  8 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Dato . . . . . . . . . . . .'
     A            C2PDAT         6Y 0O  9 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 11  2'Dekningsgrad . . . . . . . .'
     A            C2DEKN         5Y 2O 11 31COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 13  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        O 13 31REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Kostpris . . . . . . . . . .'
     A            C2KOPR    R        O 14 31REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Innkjøpspris . . . . . . . .'
     A            C2INPR    R        O 15 31REFFLD(VPINPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Butikkpris . . . . . . . . .'
     A            C2BUPR    R        O 16 31REFFLD(VPBUPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
