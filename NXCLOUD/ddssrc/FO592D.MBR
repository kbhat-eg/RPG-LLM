     A*%%TS  SD  20080131  095943  BOF560      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO592D
     A* Beskrivelse . . : Ordre som plukkes
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.32 A*  050517 BSA Lagt inn valg for utskrift av etiketter
7.xx A* 7.xx  300316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
8.01 A*K0000  130122  bsa  Skriver om til bredt skjermbilde.
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01 A**                                    DSPSIZ(24 80 *DS3)
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080103  152305  BOF560      REL-V5R4M0  5722-WDS
     A            B1KUND    R        H      REFFLD(FOKUND)
8.01 A            B1LKUN    R        H      REFFLD(FOKUND)
     A            B1KPRO    R        H      REFFLD(FOKPRO)
     A            B1ITYP    R        H      REFFLD(FOOTYP)
     A            B1BDAT    R        H      REFFLD(FOBDAT)
     A            B1ADR1    R        H      REFFLD(FOADR1)
     A            B1KODE         2A  H
     A            B1KOD2         1A  H
7.xx A            B1VALG         2Y 0B  9  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.32 A                                      VALUES(0 2 5 6 7 8 9 10 14 16 60)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A            B1LDAT         6Y 0O  9  5EDTWRD('  .  .  ')
7.xx A            B1OTYP    R        O  9 14REFFLD(FOOTYP)
7.xx A            B1KJOR    R        O  9 17REFFLD(FOKJOR)
7.xx A            B1KJNR    R        O  9 20REFFLD(FOKJNR)
7.fb A  45                                  COLOR(RED)
7.xx A            B1NUMM    R        O  9 26REFFLD(FONUMM)
7.fb A  45                                  COLOR(RED)
7.xx A            B1SUFF    R        O  9 35REFFLD(FOSUFF)
8.01 A            B1KNAV        20   O  9 38
8.01 A            B1LNAV        20   O  9 60
8.01 A            B1PLSE    R        O  9 88REFFLD(FOPLSE)
8.01 A            B1PLDA         6Y 0O  9 93EDTWRD('  .  .  ')
8.01 A            B1PLTI         4Y 0O  9102EDTWRD('  :  ')
8.01 A            B1ALLE         5A  O  9108
     A  43                                  COLOR(BLU)
     A  44                                  COLOR(RED)
     A  41                                  COLOR(WHT)
     A  42                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080103  152421  BOF560      REL-V5R4M0  5722-WDS
8.01 A                                      SFLSIZ(0017)
8.01 A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
8.01 A**                                    CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Ordrer som plukkes'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO592B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'KR'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 26'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 56'Sn'
7.xx A                                      COLOR(WHT)
6.32 A                                  6  2'2=Endre  5=Vis ordre  6=Utskrift-
6.32 A                                       7=Beh ordre  10=Skriv kollietikett-
6.32 A                                      er  60=Logg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'8=Hvem behandler ordre  9=Avbryt p-
     A                                      lukking  14=Merk ferdig plukket  16-
     A                                      =Utplukk'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'KR'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 21'KRnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 26'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 35'S'
7.xx A                                      COLOR(WHT)
8.01 A*N51                              8 38'Kunde'
8.01 A                                  8 38'Kunde'
7.xx A                                      COLOR(WHT)
8.01 A* 51                              8 38'Lev.adr'
8.01 A                                  8 60'Lev.adr'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 88'Sn'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 94' Dato '
7.xx A                                      COLOR(WHT)
8.01 A                                  8102' Kl. '
7.xx A                                      COLOR(WHT)
7.xx A*N51                              8  2'V  Lev.dato Ot KR KR-nr Ordre    S-
7.xx A*                                       Kunde             Sn     Dato    -
7.xx A*                                     Kl.'
7.xx A*                                     COLOR(WHT)
7.xx A* 51                              6  2'V  Lev.dato Ot KR KR-nr Ordre    S-
7.xx A*                                       Lev.Adr.          Sn     Dato    -
7.xx A*                                     Kl.'
7.xx A*                                     COLOR(WHT)
7.xx A            B2LDAT         6Y 0B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A            B2OTYP    R        B  5 14REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2KJOR    R        B  5 17REFFLD(FOKJOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NUMM    R        B  5 26REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 54'+'
7.xx A            B2PLSE    R        B  5 56REFFLD(FOPLSE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20080131  095943  BOF560      REL-V5R4M0  5722-WDS
8.01 A                                 26  2'F1=Bla-alt   F3=Gå tilbake   F4=Sp-
     A                                      ørring   F5=Forny'
     A                                      COLOR(BLU)
8.01 A**                               26 55'F23=Bytt Info'
8.01 A**                                    COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20070402  180735  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Avbryt plukking av ordre '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenr . . . .'
     A            C2NUMM    R        O  4 18REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.NU A            C2SUFF    R        O  4 27REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Kunde . . . . .'
     A            C2KNAV        30A  O  5 18OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Kundeprosjekt .'
     A            C2PNAV        30A  O  6 18OVRDTA
     A                                      COLOR(YLW)
     A                                  8 20'Dato'
     A                                      COLOR(WHT)
     A                                  8 30'Kl'
     A                                      COLOR(WHT)
     A                                  8 36'Bruker'
     A                                      COLOR(WHT)
     A                                  8 47'Skjerm'
     A                                      COLOR(WHT)
     A                                  9  2'Under Plukking'
     A            C2UPDA         6Y 0O  9 18EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A            C2UPTI         6Y 0O  9 27EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A            C2UPUS        10A  O  9 36COLOR(YLW)
     A            C2UPWS        10A  O  9 47COLOR(YLW)
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20070914  141518  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 10 50)
     A                                      CA03
6.30 A                                      CF04
     A                                      CA12
6.30 A                                      RTNCSRLOC(&W_ARC1 &W_AFL1)
     A                                      PUTOVR
6.30 A            W_ARC1        10A  H
6.30 A            W_AFL1        10A  H
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
     A                                  4  2'Droppe kode 4, under plukking . . -
     A                                      .'
     A            F1KO04    R        B  4 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 40'1=Ja,0=Nei'
     A                                  5  2'Droppe kode 6, ferdig plukket . . -
     A                                      .'
     A            F1KO06    R        B  5 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  5 40'1=Ja,0=Nei'
     A                                  6  2'Droppe kode 7, til restbehandling -
     A                                      .'
     A            F1KO07    R        B  6 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  6 40'1=Ja,0=Nei'
7.xx A                                  7  2'Oppgi ønsket lager. . . . . . . . -
7.xx A                                      +'
     A            F1LAGE    R        B  7 38REFFLD(FOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.30 A                                  8  2'Forsendelsesmåte. . . . . . . . . -
7.xx A                                      +'
6.30 A            F1LMA1    R        B  8 38REFFLD(FOLMAT)
6.30 A  35                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 35)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  8 41'+'
7.xx A            F1LMA2    R        B  8 43REFFLD(FOLMAT)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  8 46'+'
7.xx A            F1LMA3    R        B  8 48REFFLD(FOLMAT)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
