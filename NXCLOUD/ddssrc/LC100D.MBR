     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LC100D
     A* Beskrivelse . . : Vedlikehold av batch-register
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A*--------------------------------------------------------------------
6.20 A* 021214 NHO Sperrekode skal ikke med ved kopiering
6.20 A*            datofelt må fylles ut på nytt ved kopiering
6.20 A*            Batch nr må venstrejusteres
6.30 A*        bsa Legger inn lager for filtrering
     A*
7.xx A* 190516 bsa Justeringer basert på ny GUI og regler
     A*
8.01 A* 080323 bsa Feilmelding hvis lager ikke er utfylt og switch er på
8.03 A* 141024 ask Lagt inn filter funksjonalitet
8.04 A* 151024 ask Lagt inn valg for å kunne kjøre telleprosess fra et bilde
8.08 A* 211124 ask Viser bunter på tellebatc
     A*
     A*
     A* -------------------------------------------------------------------
     A*%%TS  SD  20080520  123555  ASK560      REL-V5R4M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      HELP
     A          R A1WIN
     A*%%TS  SD  20020924  114602  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 40)
     A                                      CA03
     A                                      KEEP
     A                                  2  2' Feilmelding '
     A                                      DSPATR(RI)
     A                                  5  2'Bruker kan ikke utføre dette punkt-
     A                                      et'
     A                                      COLOR(GRN)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080520  123555  ASK560      REL-V5R4M0  5722-WDS
     A            B1VALG         2Y 0B  8  2
8.04 A                                      VALUES(0 2 3 4 5 6 7 8 9 10 11 12)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
8.04 A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A            B1BATC    R        O  8  5REFFLD(LCBATC)
     A            B1ANSV        28A  O  8 16
     A            B1BDAT         6Y 0O  8 45EDTWRD('  .  .  ')
     A            B1EDAT         6Y 0O  8 54EDTWRD('  .  .  ')
     A            B1STEL    R        O  8 63REFFLD(LCSTEL)
     A            B1SPER    R        O  8 67REFFLD(LCSPER)
     A            B1NUMM    R        O  8 69REFFLD(LCNUMM)
8.04 A            B1ANTB        15A  O  8 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080520  122425  ASK560      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
8.03 A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Lagertelling'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LC100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Batch-nr.'
7.xx A                                      COLOR(WHT)
8.04 A                                  5  2'2=Endre 3=Kopier 4=Slett 5=Vis 6=U-
8.04 A                                      tskrift tellelister 7=Arbeide med t-
8.04 A                                      ellebunter'
     A                                      COLOR(BLU)
8.04 A                                  6  2'8=vedlikehold telleliste 9=Lagerav-
8.04 A                                      vik 10=Lagerverdi 11=Diversevarer 1-
8.04 A                                      2=Overføre til lager'
8.04 A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Batch-nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Ansvarlig            '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 47'Bdato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 56'Sdato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'Syk'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 66'Sp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Liste'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 78'Overførte bunter'
     A                                      COLOR(WHT)
7.xx A            B2BATC    R        B  4 15REFFLD(LCBATC)
     A                                      DLTCHK
     A  31                                  ERRMSGID(LMC0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMC0020 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LMC0030 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19931028  142557  AMD         REL-V2R2M0  5738-PW1
     A                                 23 05'F1=Filter'
     A                                      COLOR(BLU)
     A                                 23 16'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 41'F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1WIN
     A*%%TS  SD  20020924  104833  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Registrere ny batch '
     A                                      DSPATR(RI)
     A                                  4  2'Batchnummer. . . .'
     A            C1BATC    R        B  4 21REFFLD(LCBATC)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(LMC0010 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LMC0030 *LIBL/QMFLAGR 33)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080520  123555  ASK560      REL-V5R4M0  5722-WDS
     A                                      CA03
6.30 A                                      CF04
     A                                      CA12
     A                                      CA24
     A                                      PUTOVR
6.30 A                                      RTNCSRLOC(&WACTRE &WACTFE)
6.30 A            WACTRE        10A  H
6.30 A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Batch-register'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LC100C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Batchnummer. . . . . . . . .'
     A            C2BATC    R        O  5 31REFFLD(LCBATC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2NYEN    R        O  5 69REFFLD(LÅNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Ansvarlig. . . . . . . . . .'
     A            C2ANSV    R        B  7 31REFFLD(LCANSV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  40                                  ERRMSGID(LMC0100 *LIBL/QMFLAGR 40)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8  2'Begynt-dato. . . . . . . . .'
     A            C2BDAT         6Y 0B  8 31CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(LMC0040 *LIBL/QMFLAGR 34)
     A  38                                  ERRMSGID(LMC0080 *LIBL/QMFLAGR 38)
     A                                  9  2'Begynt-tidspunkt . . . . . .'
     A            C2BTIM         4Y 0B  9 31COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A  36                                  ERRMSGID(LMC0060 *LIBL/QMFLAGR 36)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  ')
     A                                 10  2'Slutt-dato . . . . . . . . .'
     A            C2EDAT         6Y 0B 10 31COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  .  ')
     A  35                                  ERRMSGID(LMC0050 *LIBL/QMFLAGR 35)
     A                                 11  2'Slutt-tidspunkt. . . . . . .'
     A            C2ETIM         4Y 0B 11 31COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A  37                                  ERRMSGID(LMC0070 *LIBL/QMFLAGR 37)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  ')
     A                                 12  2'Diverse Varer, Nummerserie .'
     A            C2LOPE    R        B 12 31REFFLD(LCLOPE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  41                                  ERRMSGID(LMC0540 *LIBL/QMFLAGR 41)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13  2'Antall linjer i handterminal'
     A            C2AHTE    R        B 13 31REFFLD(LFRFR/LCAHTE *LIBL/LFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
6.30 A                                 14  2'Lager for filtrering . . . +'
6.30 A            C2LAGE    R        B 14 31REFFLD(LFRFR/LÅLAGE *LIBL/LFRF)
6.30 A                                      COLOR(YLW)
6.30 A  30                                  DSPATR(PR)
6.30 A  42                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 42)
8.01 A  43                                  ERRMSG('Lager må fylles ut, skriv -
8.01 A                                      inn eller velg med F4' 43)
6.30 A                                      OVRDTA
6.30 A                                      OVRATR
     A                                 15  2'Syklisk telling  . . . . . .'
     A            C2STEL    R        B 15 31REFFLD(LCSTEL)
     A                                      COLOR(YLW)
7.xx A                                 15 33'1/0'
     A                                 16  2'Sperret. . . . . . . . . . .'
     A            C2SPER    R        B 16 31REFFLD(LCSPER)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A  39                                  ERRMSGID(LMC0090 *LIBL/QMFLAGR 39)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 16 33'1/0'
     A                                 17  2'Tellelistenr . . . . . . . .'
     A            C2NUMM    R        O 17 31REFFLD(LCNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 19  2'Antall bunter  . . . . . . .'
     A            C2ANTB    R        O 19 31REFFLD(LCANTB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.30 A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020924  104355  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette batch '
     A                                      DSPATR(RI)
     A                                  2 19'Inkl. lister og bunter'
     A                                      COLOR(RED)
     A                                  4  2'Batchnummer. . .'
     A            D1BATC    R        O  4 19REFFLD(LCBATC)
     A                                  5  2'Ansvarlig. . . .'
     A            D1ANSV    R        O  5 19REFFLD(LCANSV)
     A                                  6  2'Begynt-dato. . .'
     A            D1BDAT         6Y 0O  6 19EDTWRD('  .  .  ')
     A                                  7  2'Begynt-tidspunkt'
     A            D1BTIM         4Y 0O  7 19EDTWRD('  .  ')
     A                                  8  2'Slutt-dato . . .'
     A            D1EDAT         6Y 0O  8 19EDTWRD('  .  .  ')
     A                                  9  2'Slutt-tidspunkt.'
     A            D1ETIM         4Y 0O  9 19EDTWRD('  .  ')
     A                                 10  2'Sperret. . . . .'
     A            D1SPER    R        O 10 19REFFLD(LCSPER)
     A                                 12  2'Tellelistenr . .'
     A            D1NUMM    R        O 12 19REFFLD(LCNUMM)
     A                                 13  2'Antall bunter. .'
     A            D1ANTB    R        O 13 19REFFLD(LCANTB)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020924  104355  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Batchnummer. . .'
     A            K1BATC    R        B  4 19REFFLD(LCBATC)
     A  31                                  ERRMSGID(LMC0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMC0030 *LIBL/QMFLAGR 32)
6.20 A****                                  CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Batchnummer. . .'
     A            K1BATF    R        O  8 19REFFLD(LCBATC)
     A                                  9  2'Ansvarlig. . . .'
     A            K1ANSV    R        O  9 19REFFLD(LCANSV)
     A                                 10  2'Begynt-dato. . .'
6.20 A*           K1BDAT         6Y 0O 10 19EDTWRD('  .  .  ')
6.20 A            K1BDAT         6Y 0B 10 19COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A*                                     OVRATR
6.20 A                                      EDTWRD('  .  .  ')
6.20 A  34                                  ERRMSGID(LMC0040 *LIBL/QMFLAGR 34)
6.20 A  38                                  ERRMSGID(LMC0080 *LIBL/QMFLAGR 38)
     A                                 11  2'Begynt-tidspunkt'
     A            K1BTIM         4Y 0O 11 19EDTWRD('  .  ')
     A                                 12  2'Slutt-dato . . .'
6.20 A*           K1EDAT         6Y 0O 12 19EDTWRD('  .  .  ')
6.20 A            K1EDAT         6Y 0B 12 19COLOR(YLW)
6.20 A                                      OVRDTA
7.xx A*                                     OVRATR
6.20 A                                      EDTWRD('  .  .  ')
6.20 A  35                                  ERRMSGID(LMC0050 *LIBL/QMFLAGR 35)
     A                                 13  2'Slutt-tidspunkt.'
     A            K1ETIM         4Y 0O 13 19EDTWRD('  .  ')
6.20 A**                               14  2'Sperret. . . . .'
6.20 A**          K1SPER    R        O 14 19REFFLD(LCSPER)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.03 A          R H1WIN
8.03 A*%%TS  SD  20050915  092234  GRO540      REL-V5R3M0  5722-WDS
8.03 A                                      WINDOW(*DFT 10 40)
8.03 A                                      CA03
8.03 A                                      CA12
8.03 A                                      PUTOVR
8.03 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.03 A            W_ARCD        10A  H
8.03 A            W_AFLD        10A  H
8.03 A                                  2  2'Utvalgskriterier :'
8.03 A                                      COLOR(WHT)
8.03 A                                      DSPATR(UL)
8.03 A                                  4  2'Lager'
8.03 A            H1LAGE         2Y 0B  4 23CHECK(RB)
8.03 A                                      EDTCDE(Z)
8.03 A                                      COLOR(YLW)
8.03 A                                      OVRDTA
8.03 A                                  6  2'Bare syklisk telling'
8.03 A            H1SYKL         1Y 0B  6 23COLOR(YLW)
8.03 A                                      OVRDTA
8.03 A                                  5  2'Bare åpne tellinger'
8.03 A            H1OPEN         1Y 0B  5 23COLOR(YLW)
8.03 A                                      OVRDTA
8.03  *
8.03 A                                  9  1'F3=Gå tilbake'
8.03 A                                      COLOR(BLU)
8.05 A          R A2WIN
8.05 A*%%TS  SD  20020924  114602  HKO         REL-V5R1M0  5722-WDS
8.05 A                                      WINDOW(*DFT 7 50)
8.05 A                                      CA03
8.05 A                                      KEEP
8.05 A                                  2  2'Informasjon'
8.05 A                                      DSPATR(RI)
8.05 A                                  4  2'Det finnes tellebunter som ikke er-
8.05 A                                       overført'
8.05 A                                      COLOR(GRN)
      *
