     A*%%TS  SD  20220427  151915  ASP_J23     REL-V7R3M0  5770-WDS
     A*
8.01 A* 180615 bof Utvidet med prisgruppe (kommer fra spes MG)
7.xx A* 040316 bsa Endringer relatert til ny GUI
8.02 A* 130622 bof utvidet prisgruppe fra 1 til 2 pos.
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140710  104709  BKV630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
8.01 A            B1KAMP    R        O  8  4REFFLD(JMKAMP)
8.02 A            B1PRGR    R        O  8 10REFFLD(JMPRGR)
     A**          B1BESK    R        O  8 11REFFLD(JMBESK)
8.01 A            B1BESK        27A  O  8 13
8.01 A            B1FDAT         6Y 0O  8 41EDTWRD('  .  .  ')
8.01 A            B1TDAT         6Y 0O  8 50EDTWRD('  .  .  ')
8.01 A            B1ANSV    R        O  8 59REFFLD(JMANSV)
     A            B1FODA         1A  O  8 70
     A            B1ODAT         6Y 0O  8 72EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140710  104709  BKV630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
8.01 A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JM100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Kamp.'
7.xx A                                      COLOR(WHT)
8.01 A                                  4 10'PG'
8.01 A                                      COLOR(WHT)
7.xx A                                  4 41'Fra-dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 59'Ansvarlig'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Behandle kampanjepriser'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
8.01 A                                  7  4'Kamp.'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 10'Pg'
8.01 A                                      COLOR(WHT)
8.01 A                                  7 13'Beskrivelse             '
7.xx A                                      COLOR(WHT)
8.01 A                                  7 41'Fra-dato'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 50'Til-dato'
7.xx A                                      COLOR(WHT)
8.01 A                                  7 59'Ansvarlig'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 72'Ovf.dato'
     A                                      COLOR(WHT)
7.xx A            B2KAMP    R        B  5  4REFFLD(JMKAMP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.01 A            B2PRGR    R        B  5 10REFFLD(JMPRGR)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
7.xx A                                  5 13'(kode)'
7.xx A                                      COLOR(WHT)
7.xx A            B2FDAT         6Y 0B  5 41COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
7.xx A            B2ANSV    R        B  5 59REFFLD(JMANSV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020819  131825  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20001204  170624  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
8.01 A                                      CF04
     A                                      CA12
     A                                      PUTOVR
8.01 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.01 A            W_ARCD        10A  H
8.01 A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kampanje . . . . . . . . . .'
     A            C1KAMP    R        B  5 31REFFLD(JMKAMP)
     A  31                                  ERRMSGID(JMM0010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6  2'Prisgruppe . . . . . . . . .'
8.01 A            C1PRGR    R        B  6 31REFFLD(JMPRGR)
8.01 A  32                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 32)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
9.02 A                                  6 37'(kode)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.02 A                                 23 17'F4=Spørring'
8.02 A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19970927  170405  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kampanje finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20001204  170624  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kampanjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kampanje . . . . . . . . . .'
     A            C1KAMP    R        O  5 31REFFLD(JMKAMP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.01 A                                  6  2'Prisgruppe . . . . . . . . .'
8.01 A            C1PRGR    R        O  6 31REFFLD(JMPRGR)
8.01 A                                      COLOR(YLW)
8.01 A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2BESK    R        B  7 31REFFLD(JMBESK)
     A                                      CHGINPDFT(LC)
     A  31                                  ERRMSGID(JMM0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Kampanje fra-dato. . . . . .'
     A            C2FDAT         6Y 0B  9 31
     A  32                                  ERRMSGID(JMM0110 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JM00250 *LIBL/QMFVADM 33)
     A  36                                  ERRMSGID(JMM0130 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Kampanje til-dato. . . . . .'
     A            C2TDAT         6Y 0B 10 31
     A  34                                  ERRMSGID(JMM0120 *LIBL/QMFVADM 34)
     A  35                                  ERRMSGID(JM00250 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12  2'Kampanje-ansvarlig . . . . .'
     A            C2ANSV    R        B 12 31REFFLD(JMANSV)
     A  37                                  ERRMSGID(JMM0140 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Overført EVR-dato. . . . . .'
     A            C2ODAT         6Y 0O 14 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.30 A                                 16  2'Først overført EVR-dato. . .'
6.30 A            C2FODA         6Y 0O 16 31COLOR(YLW)
6.30 A                                      EDTWRD('  .  .  ')
6.30 A                                      OVRDTA
6.30 A                                 19  2'Opprettet dato . . . . . . .'
6.30 A            C2OOAT         6Y 0O 19 31COLOR(YLW)
6.30 A                                      EDTWRD('  .  .  ')
6.30 A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(JMEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020822  101128  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kampanje '
     A                                      DSPATR(RI)
     A                                  4  2'Kampanje . . . . .'
     A            D1KAMP    R        O  4 21REFFLD(JMKAMP)
8.01 A                                  5  2'Prisgruppe . . . .'
8.01 A            D1PRGR    R        O  5 21REFFLD(JMPRGR)
     A                                  6  2'Beskrivelse. . . .'
     A            JMBESK    R        O  6 21
     A                                  7  2'Ansvarlig. . . . .'
     A            JMANSV    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20220427  151915  ASP_J23     REL-V7R3M0  5770-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kampanje . . . . .'
     A            K1KAMP    R        B  4 21REFFLD(JMKAMP)
     A  31                                  ERRMSGID(JMM0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMM0030 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prisgruppe . . . .'
     A            K1PRGR    R        B  5 21REFFLD(JMPRGR)
     A  33                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A  29                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kampanje . . . . .'
     A            JMKAMP    R        O  8 21OVRDTA
     A                                  9  2'Prisgruppe . . . .'
     A            JMPRGR    R        O  9 21OVRDTA
     A                                 10  2'Beskrivelse. . . .'
     A            JMBESK    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5 24'(k)'
8.01 A          R F1WIN
8.01 A*%%TS  SD  20150618  142556  BOF630      REL-V7R1M0  5770-WDS
8.01 A                                      WINDOW(*DFT 10 58)
8.01 A                                      CA03
8.01 A                                      CF04
8.01 A                                      CA12
8.01 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.01 A            W_ARCD        10A  H
8.01 A            W_AFLD        10A  H
8.01 A                                  2  2' Filter '
8.01 A                                      DSPATR(RI)
8.01 A                                  4  2'Prisgruppe . . . .'
8.01 A            F1PRGR    R        B  4 21REFFLD(JMPRGR)
8.01 A  33                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 33)
8.01 A                                      COLOR(YLW)
8.01 A  29                                  DSPATR(PR)
8.01 A            F1PTXT        20   O  4 28
8.01 A                                      COLOR(YLW)
8.01 A                                  6  2'Kun egen prisgr. .'
8.01 A            F1PRVA    R        B  6 21REFFLD(JÅVALG)
8.01 A                                      COLOR(YLW)
8.01 A                                  6 28'1=Ja'
8.01 A                                  8  2'F3=Gå tilbake   F4=Spørring'
8.01 A                                      COLOR(BLU)
