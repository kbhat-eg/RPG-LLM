     A*%%TS  SD  20050621  092907  ASK530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO552D
     A* Beskrivelse . . : Finne butikkordre via bongnummer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       210605 ASK  Nytt program
6.10 A*       150210 BSA  Endret feltlengde på referansefelter
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050621  092907  ASK530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  6  2REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1AARR    R        O  6  4REFFLD(BOAARR)
     A            B1ORNR    R        O  6  9REFFLD(BOORNR)
6.NU A            B1WSID    R        O  6 18REFFLD(BOWSID)
6.NU A            B1BDAT         6Y 0O  6 29EDTCDE(Y)
6.NU A            B1VREF        20A  O  6 38
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050621  092907  ASK530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Finne ordre via bongnummer '
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
6nu  A*                                 5  2'V  År  Ordre  Kasse        Dato   -
6nu  A*                                     Vår ref.            '
6nu  A                                  5  2'V  År  Ordre    Kasse      Dato   -
6nu  A                                        Vår ref.          '
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050621  092907  ASK530      REL-V5R2M0  5722-WDS
6.NU A                                      WINDOW(*DFT 18 58)
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
