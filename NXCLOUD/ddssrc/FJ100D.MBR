     A*%%TS  SD  20020312  135915  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  140316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19931109  135243  AMD         REL-V2R2M0  5738-PW1
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1SL01    R        H      REFFLD(FJSL01)
     A            B1SL02    R        H      REFFLD(FJSL02)
     A            B1SL03    R        H      REFFLD(FJSL03)
     A            B1SL04    R        H      REFFLD(FJSL04)
     A            B1SL05    R        H      REFFLD(FJSL05)
     A            B1NUMM    R        O  8  5REFFLD(FJNUMM)
6.NU A            B1REFX    R        O  8 15REFFLD(FJREFX)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020311  101012  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste ordre-tekster'
     A                                      COLOR(WHT)
     A                                  2 38'FJ100B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  5REFFLD(FJNUMM)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19931028  142557  AMD         REL-V2R2M0  5738-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020311  101205  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste ordre-tekster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tekst-kode . . . . . . . . .'
     A            C1NUMM    R        B  5 31REFFLD(FJNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020312  135915  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Tekst-kode finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020312  135915  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste ordre-tekster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kode . .'
     A            C1NUMM    R        O  4 11REFFLD(FJNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskr. .'
     A            C2REFX    R        B  5 11REFFLD(FJREFX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7  2'Linje-1.'
     A            C2SL01    R        B  7 11REFFLD(FJSL01)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Linje-2.'
     A            C2SL02    R        B  8 11REFFLD(FJSL02)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  9  2'Linje-3.'
     A            C2SL03    R        B  9 11REFFLD(FJSL03)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Linje-4.'
     A            C2SL04    R        B 10 11REFFLD(FJSL04)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 11  2'Linje-5.'
     A            C2SL05    R        B 11 11REFFLD(FJSL05)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2'Linje-6.'
     A            C2SL06    R        B 12 11REFFLD(FJSL06)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 13  2'Linje-7.'
     A            C2SL07    R        B 13 11REFFLD(FJSL07)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 14  2'Linje-8.'
     A            C2SL08    R        B 14 11REFFLD(FJSL08)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 15  2'Linje-9.'
     A            C2SL09    R        B 15 11REFFLD(FJSL09)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 16  2'Linje-10'
     A            C2SL10    R        B 16 11REFFLD(FJSL10)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 17  2'Linje-11'
     A            C2SL11    R        B 17 11REFFLD(FJSL11)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 18  2'Linje-12'
     A            C2SL12    R        B 18 11REFFLD(FJSL12)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 19  2'Linje-13'
     A            C2SL13    R        B 19 11REFFLD(FJSL13)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 20  2'Linje-14'
     A            C2SL14    R        B 20 11REFFLD(FJSL14)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 21  2'Linje-15'
     A            C2SL15    R        B 21 11REFFLD(FJSL15)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 22  2'Linje-16'
     A            C2SL16    R        B 22 11REFFLD(FJSL16)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020312  134544  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette fast ordre-tekst '
     A                                      DSPATR(RI)
     A                                  4  2'Tekst-kode . . . .'
     A            D1NUMM    R        O  4 21REFFLD(FJNUMM)
     A                                  5  2'Beskrivelse. . . .'
     A            FJREFX    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020312  134544  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Tekst-kode . . . .'
     A            K1NUMM    R        B  4 21REFFLD(FJNUMM)
     A  31                                  ERRMSGID(FMJ0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMJ0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Tekst-kode . . . .'
     A            FJNUMM    R        O  9 21OVRDTA
     A                                 10  2'Beskrivelse. . . .'
     A            FJREFX    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
