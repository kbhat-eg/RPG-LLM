     A*%%TS  SD  20081113  135906  BOF570      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FP510D
     A* Beskrivelse . . : Administrering av plukking
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300316  bsa  Justeringer basert på ny GUI og regler
8.01 A*       100622  BOF  Utvidet prisgr. fra 1 til 2 posisjoner
8.02 A*       080124  bsa  Justeret pga overlapp i prisgrupper
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081113  135906  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B 12  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1PRGR    R        O 12  4REFFLD(FPPRGR)
     A            B1RKAT    R        O 12  7REFFLD(FPRKAT)
     A            B1KKAT    R        O 12 11REFFLD(FPKKAT)
     A            B1KUND    R        O 12 16REFFLD(FPKUND)
     A            B1KPRO    R        O 12 23REFFLD(FPKPRO)
     A            B1VARE    R   -7   O 12 30REFFLD(FPVARE)
6.NU A            B1TEK1         2A  O 12 39
6.NU A            B1ENHE    R        O 12 42REFFLD(FPENHE)
6.NU A            B1LETY    R        O 12 46REFFLD(FPLETY)
6.NU A            B1NUMM    R        O 12 48REFFLD(FPNUMM)
     A            B1SAPR         8Y 2O 12 57EDTCDE(4)
     A            B1RAB1         4Y 1O 12 67EDTCDE(4)
     A            B1KOPR         7Y 2O 12 73EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081113  135906  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise spesialpriser for kunde/kunde-
     A                                      prosjekt'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rabattkategori. . .'
     A                                      COLOR(BLU)
     A            B2RKA1    R        O  4 22REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2RTXT        30A  O  4 30COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kundekategori . . .'
     A                                      COLOR(BLU)
     A            B2KKA1    R        O  5 22REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KTXT        30A  O  5 30COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kunde . . . . . . .'
     A                                      COLOR(BLU)
     A            B2KUN1    R        O  6 22REFFLD(FPKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV        30A  O  6 30COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kundeprosjekt . . .'
     A                                      COLOR(BLU)
     A            B2KPR1    R        O  7 22REFFLD(FPKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PNAV    R        O  7 30REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  4'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  8'Rka'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Kka'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 21'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 30'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 39'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 50'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 60'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  2'5=Vis  7=Avtalenotat'
     A                                      COLOR(BLU)
7.xx A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  4'P'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  7'Rka'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 11'Kka'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 16'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 23'Prosj.'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 31'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 42'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 46'L'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 48'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 57'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 67'Rab-1'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 73'Kostpris'
7.xx A                                      COLOR(WHT)
7.xx A**                                          Enh L Ordre    Salgspris Rab--
7.xx A**                                    1 Kostpris'
7.xx A**                                    COLOR(WHT)
7.xx A                                  9  2'+'
7.xx A            B2PRGR    R        B  9  4REFFLD(FPPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
8.02 A                                  9  7'+'
8.02 A            B2RKAT    R        B  9  9REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  9 13'+'
8.02 A            B2KKAT    R        B  9 15REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  9 20'+'
8.02 A            B2KUND    R        B  9 22REFFLD(FPKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  9 29'+'
8.02 A            B2KPRO    R        B  9 31REFFLD(FPKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.02 A                                  9 38'+'
8.02 A            B2VARE    R   -7   B  9 40REFFLD(FPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                  9 49'+'
8.02 A            B2ENHE    R        B  9 51REFFLD(FPENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                  9 55'+'
8.02 A            B2LETY    R        B  9 57REFFLD(FPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A                                  9 59'+'
8.02 A            B2NUMM    R        B  9 61REFFLD(FPNUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990519  102652  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030519  145632  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise spesialpris'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  4 31REFFLD(FPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Rabattkategori . . . . . . .'
     A            C2RKAT    R        O  5 31REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2RTXT        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundekategori. . . . . . . .'
     A            C2KKAT    R        O  6 31REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KTXT        30A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Kunde. . . . . . . . . . . .'
     A            C2KUND    R        O  7 31REFFLD(FPKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KNAV        30A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kundeprosjekt. . . . . . . .'
     A            C2KPRO    R        O  8 31REFFLD(FPKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PNAV    R        O  8 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O  9 31REFFLD(FPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2VTXT    R        O  9 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O 10 31REFFLD(FPENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ETXT    R        O 10 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O 11 31REFFLD(FPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LTXT    R        O 11 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Ordrenummer. . . . . . . . .'
     A            C2NUMM    R        O 13 31REFFLD(FPNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Priskode . . . . . . . . . .'
     A            C2PKOD         1A  O 14 31COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Dekningsgrad . . . . . . . .'
     A            C2DEKN         5Y 2O 15 31COLOR(YLW)
     A                                      EDTCDE(K)
     A                                      OVRDTA
     A                                 17  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        O 17 31REFFLD(FPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Rabatt-1 . . . . . . . . . .'
     A            C2RAB1    R        O 18 31REFFLD(FPRAB1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Rabatt-2 . . . . . . . . . .'
     A            C2RAB2    R        O 19 31REFFLD(FPRAB2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Kostpris . . . . . . . . . .'
     A            C2KOPR    R        O 20 31REFFLD(FPKOPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'Periode. . . . . . . . . . .'
     A            C2FDAT         6Y 0O 22 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 40'-'
     A            C2TDAT         6Y 0O 22 42COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(FPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
