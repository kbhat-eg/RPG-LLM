     A*%%TS  SD  20101227  125704  SST61P      REL-V5R4M0  5722-WDS
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP602D
     A* Beskrivelse . . : Arb.ordre,spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       181110 NHO  Nytt program
7.01 A* K00   060520 bsa  Takler brede skjermer.
7.xx A* K00   080520 bsa  Skrevet om til cloud.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101227  125704  SST61P      REL-V5R4M0  5722-WDS
     A***         B1ALFA    R        H      REFFLD(RPALFA)
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B14ORD    R        O  7  4REFFLD(RP4ORD)
     A  41                                  COLOR(RED)
     A            B14TXT        23A  O  7 34
     A            B14PRO    R        O  7 11REFFLD(RP4PRO)
     A  42                                  COLOR(RED)
     A            B14UPR    R        O  7 18REFFLD(RP4UPR)
     A  43                                  COLOR(RED)
     A            B14AKT    R        O  7 25REFFLD(RP4AKT)
     A  44                                  COLOR(RED)
     A            B14STS    R        O  7 59REFFLD(RP4STS)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101217  191302  SST61P      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Spørring arb.ordre'
     A                                      DSPATR(RI)
7.xx A                                  3  4'Arb.o'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 11'Prosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 18'Uprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 26'Akt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 34'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 58'St.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Arb.o'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Prosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 18'Uprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 26'Akt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 34'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 58'St.'
     A                                      COLOR(WHT)
7.xx A            B24ORD    R        B  4  4REFFLD(RP4ORD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2ALFA        23A  B  4 34COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A            B24PRO    R        B  4 11REFFLD(RP4PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B24UPR    R        B  4 18REFFLD(RP4UPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B24AKT    R        B  4 25REFFLD(RP4AKT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B24STS    R        B  4 59REFFLD(RP4STS)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020106  234033  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 60)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
