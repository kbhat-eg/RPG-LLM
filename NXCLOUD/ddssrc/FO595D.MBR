     A*%%TS  SD  20101203  125550  ASP_X50     REL-V6R1M0  5761-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO595D
     A* Beskrivelse . . : Behandle kjøreruter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.32 A* R6.30 100516 BSA  Innfører F4 for spørring på kjørerute.
7.xx A* 7.xx  310316  bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*K0000  130122  bsa  Skriver om til bredt skjermbilde.
8.04 A* 8.xx  140222  bsa  Justert lengde på felt
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01 A**                                    DSPSIZ(24 80 *DS3)
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101203  125550  ASP_X50     REL-V6R1M0  5761-WDS
     A            B1KUND    R        H      REFFLD(FOKUND)
     A            B1KPRO    R        H      REFFLD(FOKPRO)
     A            B1ITYP    R        H      REFFLD(FOOTYP)
     A            B1BDAT    R        H      REFFLD(FOBDAT)
     A            B1OTYP    R        H      REFFLD(FOOTYP)
     A*           B1PLDA    R        H      REFFLD(FOPLDA)
     A*           B1PLTI    R        H      REFFLD(FOPLTI)
     A            B1ADR1    R        H      REFFLD(FOADR1)
     A            B1KUNA    R        H      REFFLD(FOKUNA)
     A            B1KJOR    R        H      REFFLD(FOKJOR)
     A            B1KJNR    R        H      REFFLD(FOKJNR)
8.01 A*           B1MOBI    R        H      REFFLD(FOMOBI)
     A            B1KODE         2A  H
     A            B1KOD2         1A  H
     A            B1VALG         2Y 0B  8  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 2 5 6 8 60)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1KNAV        14A  O  8  5
     A            B1LDAT         6Y 0O  8 20EDTWRD('  .  .  ')
     A            B1NUMM    R        O  8 29REFFLD(FONUMM)
6.NU A            B1SUFF    R        O  8 38REFFLD(FOSUFF)
8.01 A            B1LNAV        20A  O  8 41
8.01 A            B1MOBI    R        O  8 63REFFLD(FOMOBI)
8.01 A            B1KREF        20A  O  8 80
8.01 A            B1SELG    R        O  8102REFFLD(FOSELG)
8.01 A            B1BYT2         2A  O  8108
8.04 A            B1BYT3         5A  O  8111
8.04 A            B1BYT4         4A  O  8117
     A  50                                  COLOR(RED)
8.04 A            B1MERK         1A  O  8122
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101203  125550  ASP_X50     REL-V6R1M0  5761-WDS
8.01 A                                      SFLSIZ(0018)
8.01 A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
8.01 A**                                    CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandle kjøreruter'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO595B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 20'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 29'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A N51                              4 41'KR'
7.xx A                                      COLOR(WHT)
7.xx A  51                              4 41'Lk'
7.xx A                                      COLOR(WHT)
     A                                  4 72'Filter'
     A                                      COLOR(BLU)
     A            B2LAGE         2Y 0O  4 79COLOR(BLU)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            B2KO02         1A  O  5 72OVRDTA
     A                                      COLOR(BLU)
     A            B2KO03         1A  O  5 74OVRDTA
     A                                      COLOR(BLU)
     A            B2KO04         1A  O  5 76OVRDTA
     A                                      COLOR(BLU)
     A            B2KO05         1A  O  5 78OVRDTA
     A                                      COLOR(BLU)
     A            B2KO06         1A  O  5 80OVRDTA
     A                                      COLOR(BLU)
7.xx A                                  6  2'2=Endre  5=Vis ordre   6=Utskrift -
     A                                       8=Hvem behandler ordre  60=Logg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kunde        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 29'Ordre '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 41'Lev.adr'
7.xx A                                      COLOR(WHT)
8.01 A*N51                              7 54'Kunde.Ref'
8.01 A                                  7 80'Kunde.Ref'
7.xx A                                      COLOR(WHT)
8.01 A* 51                              7 54'Mobil nr.'
8.01 A                                  7 63'Mobil nr.'
7.xx A                                      COLOR(WHT)
8.01 A*                                 7 66'Selg'
8.01 A                                  7102'Selg'
7.xx A                                      COLOR(WHT)
8.01 A*N51                              7 71'KR'
8.01 A                                  7108'KR'
7.xx A                                      COLOR(WHT)
8.01 A* 51                              7 71'Lk'
8.01 A                                  7111'Lk'
7.xx A                                      COLOR(WHT)
8.01 A*                                 7 74'O.T'
8.01 A                                  7114'O.T'
7.xx A                                      COLOR(WHT)
8.01 A*                                 7 80'M'
8.01 A                                  7119'M'
7.xx A                                      COLOR(WHT)
7.xx A*N51                              6  2'V  Kunde          Lev.dato Ordre  -
7.xx A*                                          Lev.adr.     Kunde.Ref   Selg -
7.xx A*                                     KR R.nr  M'
7.xx A*                                     COLOR(WHT)
7.xx A* 51                              6  2'V  Kunde          Lev.dato Ordre  -
7.xx A*                                          Lev.adr      Mobil nr.   Selg -
7.xx A*                                     Lk R.nr  M'
7.xx A*                                     COLOR(WHT)
7.xx A            B2LDAT         6Y 0B  5 20COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                  5 39'+'
7.xx A            B2KJOR    R        B  5 41REFFLD(FOKJOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  3'+'
7.xx A            B2KUNA        14A  B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2NUMM    R        B  5 29REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20101122  091829  BOF610      REL-V5R4M0  5722-WDS
8.01 A                                 26  2'F1=Filter  F3=Gå tilbake  F4=Spør-
8.01 A                                      ring  F5=Forny  F6=Utskrift'
8.01 A**                                    ring  F5=Forny  F6=Utskrift  F23=S-
8.01 A**                                    kift info'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20101123  140342  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Endring kjørerute        '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenr . . . .'
     A            C2NUMM    R        O  4 18REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.NU A            C2SUFF    R        O  4 27REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Kunde . . . . .'
     A            C2KUNA        30A  O  5 18OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Leveringsadr.  '
     A            C2LNAV        30A  O  6 18OVRDTA
     A                                      COLOR(YLW)
     A                                  9  2'Leveringsdato '
     A            C2LDAT         6Y 0B  9 18EDTWRD('  .  .  ')
     A                                      OVRDTA
     A  31                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 10  2'Kjørerute . . .'
     A            C2KJOR    R        B 10 18REFFLD(FOKJOR)
     A                                      OVRDTA
     A  32                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C2YTXT        30A  O 10 21OVRDTA
     A                                 11  2'Rekkefølge  . .'
     A            C2KJNR    R        B 11 18REFFLD(FOKJNR)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 12  2'Merk  . . . . .'
     A            C2MERK         1A  B 12 18
     A                                      OVRDTA
     A                                      VALUES(' ' 'X')
     A                                      COLOR(YLW)
     A                                 12 20' /X'
6.32 A                                 14  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20101124  130245  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 11 50)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
     A                                  4  2'Droppe kode 2, Klar for plukking  -
     A                                      .'
     A            F1KO02    R        B  4 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 40'1=Ja,0=Nei'
     A                                  5  2'Droppe kode 3, Plukkliste kjørt . -
     A                                      .'
     A            F1KO03    R        B  5 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  5 40'1=Ja,0=Nei'
     A                                  6  2'Droppe kode 4, Under plukking . . -
     A                                      .'
     A            F1KO04    R        B  6 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  6 40'1=Ja,0=Nei'
     A                                  7  2'Droppe kode 5, Under plukking . . -
     A                                      .'
     A            F1KO05    R        B  7 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  7 40'1=Ja,0=Nei'
     A                                  8  2'Droppe kode 6, Ferdig plukket . . -
     A                                      .'
     A            F1KO06    R        B  8 38REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  8 40'1=Ja,0=Nei'
     A                                  9  2'Oppgi ønsket lager . . . . . . . .-
     A                                      .'
     A            F1LAGE    R        B  9 38REFFLD(FOLAGE)
     A                                      OVRDTA
     A  33                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R L1WIN
     A*%%TS  SD  20101203  113608  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  1'Utskrift av kjøreliste:'
     A                                      COLOR(WHT)
7.xx A                                  4  1'Lager . . . . . . +'
     A            L1LAGE    R        B  4 21REFFLD(FOKJOR)
     A                                      OVRDTA
     A  33                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 33)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  4 30'Blank gir alle'
7.xx A                                  5  1'Kjørerute . . . . +'
     A            L1KJOR    R        B  5 21REFFLD(FOKJOR)
     A                                      OVRDTA
     A  32                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 32)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 30'Blank gir alle'
     A                                  6  1'Leveringsdato   . .'
     A            L1LDAT         6Y 0B  6 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A  31                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  6 30'Blank gir alle'
     A                                  8  1'Detaljert . . . . .'
     A            L1VALG    R        B  8 21REFFLD(FÅVALG)
     A                                      OVRDTA
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                 10  1'Excel . . . . . . .'
     A            L1EXCL    R        B 10 21REFFLD(FÅVALG)
     A                                      OVRDTA
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                 11  1'Mappe . . . . . . .'
     A            L1MAPP        20A  B 11 21
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  34                                  ERRMSGID(FM00605 *LIBL/QMFOFAK 34)
     A                                 13  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
