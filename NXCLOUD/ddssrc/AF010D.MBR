     A*%%TS  SD  20041202  101729  ASP         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AF010D
     A* Beskrivelse . . : Vedlikehold av poststedsregister
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       190504 AMD  Nytt program
7.xx A*       130516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040519  155114  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1PONR    R        O  8  6REFFLD(APPONR)
     A            B1STED    R        O  8 14REFFLD(APSTED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041202  101729  ASP         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Poststeder     '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Postnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Poststed'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'1=Velg  2=Endre  3=Kopier  4=Slett-
     A                                        5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Postnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Poststed              '
     A                                      COLOR(WHT)
7.xx A            B2PONR    R        B  5  6REFFLD(APPONR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2STED    R        B  5 14REFFLD(APSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040519  155114  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av poststeder     '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Postnummer . . . . . . . . .'
     A            C1PONR    R        B  5 31REFFLD(APPONR)
     A  31                                  ERRMSGID(AMP0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040519  155114  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Poststed finnes fra før    '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040521  091010  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av poststeder     '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Postnummer . . . . . . . . .'
     A            C1PONR    R        O  5 31REFFLD(APPONR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Poststed . . . . . . . . . .'
     A            C2STED    R        B  7 31REFFLD(APSTED)
     A  31                                  ERRMSGID(AMP0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Fylke. . . . . . . . . . . .'
     A            C2FYLK    R        B  8 31REFFLD(APFYLK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Kommune. . . . . . . . . . .'
     A            C2KOMM    R        B  9 31REFFLD(APKOMM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Kommunenavn. . . . . . . . .'
     A            C2KNAV    R        B 10 31REFFLD(APKNAV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                 9  2'Tilleggsfelt-1 . . . . . . .'
     A*           C2TFL1         5A  B  9 31COLOR(YLW)
     A* 30                                  DSPATR(PR)
     A*                                     OVRDTA
     A*                                10  2'Tilleggsfelt-2 . . . . . . .'
     A*           C2TFL2         5A  B 10 31COLOR(YLW)
     A* 30                                  DSPATR(PR)
     A*                                     OVRDTA
     A*                                18  2'Opprettet dato . . . . . . .'
     A*           C2ODAT         6Y 0O 18 31COLOR(YLW)
     A*                                     EDTWRD('  .  .  ')
     A*                                     OVRDTA
     A*                                19  2'Sist endret dato . . . . . .'
     A*           C2EDAT         6Y 0O 19 31COLOR(YLW)
     A*                                     EDTWRD('  .  .  ')
     A*                                     OVRDTA
     A*                                20  2'Endret av. . . . . . . . . .'
     A*           C2EUSR    R        O 20 31REFFLD(MREUSR)
     A*                                     COLOR(YLW)
     A*                                     OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040521  090356  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette poststed     '
     A                                      DSPATR(RI)
     A                                  4  2'Postnummer . . . .'
     A            D1PONR    R        O  4 21REFFLD(APPONR)
     A                                  6  2'Poststed . . . . .'
     A            APSTED    R        O  6 21REFFLD(APSTED)
     A                                  7  2'Fylke. . . . . . .'
     A            APFYLK    R        O  7 21REFFLD(APFYLK)
     A                                  8  2'Kommunenr. . . . .'
     A            APKOMM    R        O  8 21REFFLD(APKOMM)
     A                                  9  2'Kommunenavn. . . .'
     A            APKNAV    R        O  9 21REFFLD(APKNAV)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040521  083026  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Postnummer . . . .'
     A            K1PONR    R        B  4 21REFFLD(APPONR)
     A  31                                  ERRMSGID(AMP0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMP0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Postnummer . . . .'
     A            APPONR    R        O  8 21OVRDTA
     A                                  9  2'Poststed . . . . .'
     A            APSTED    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
