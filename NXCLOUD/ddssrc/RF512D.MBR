     A*%%TS  SD  20080303  090416  KOB560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF512D
     A* Beskrivelse . . : Spørring på bilagsnummer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       261003 KOB  Nytt program
6.20 A* R6.20 021210 NHO  RBPROS er byttet ut med RHBPRO
7.xx A* K000  051120 bsa  Skrevet om til cloud
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20080303  090416  KOB560      REL-V5R4M0  5722-WDS
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1RAAR    R        H      REFFLD(RNRAAR)
     A            B1RPER    R        H      REFFLD(RNRPER)
     A            B1BILN    R        H      REFFLD(RNBILN)
7.xx A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1KONT    R        O  8  4REFFLD(RBKON6)
7.xx A            B1AVDE    R        O  8 11REFFLD(RBAVDE)
7.xx A            B1SPES    R        O  8 16REFFLD(RBSPES)
6.20 A**          B1PROS    R        O  9 21REFFLD(RBPROS)
7.xx A            B1PROS         6   O  8 21
7.xx A            B1NAVN    R   -5   O  8 28REFFLD(RBTEXT)
7.xx A            B1TEXT    R   -1   O  8 44REFFLD(RBTEXT)
7.xx A            B1MVAK    R        O  8 64REFFLD(RBMVAK)
7.xx A            B1BELØ        11Y 2O  8 66EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080215  134104  KOBASP      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0015)
7.xx A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.21 A            B2FIRM         3  0O  1 34DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på bilagsnummer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bilag:'
     A                                      COLOR(WHT)
     A            B2BILN    R        O  5  9REFFLD(RNBILN)
6.NU A**                                5 17'Dato:'
6.NU A                                  5 19'Dato:'
     A                                      COLOR(WHT)
6.NU A**          B2BIDA         6Y 0O  5 23EDTCDE(Y)
6.NU A            B2BIDA         6Y 0O  5 25EDTCDE(Y)
6.NU A**                                5 33'Regnskapsperiode:'
6.NU A                                  5 35'Regnskapsperiode:'
     A                                      COLOR(WHT)
6.NU A**          B2RPER    R        O  5 51REFFLD(RBRPER)
6.NU A            B2RPER    R        O  5 53REFFLD(RBRPER)
6.NU A**                                5 54'/'
6.NU A                                  5 56'/'
6.NU A**          B2RAAR    R        O  5 56REFFLD(RBRAAR)
6.NU A            B2RAAR    R        O  5 58REFFLD(RBRAAR)
     A                                  6  2'5=Vis  9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Avd.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Bære'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 21'Prosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 28'Kontonavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 44'Bilagstekst       '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 64'Mk'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 71'Beløp     '
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise kundeposter
     A*
     A          R C2WIN
     A*%%TS  SD  20031027  221315  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 75)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  1'Kunde. . . . . .:'
     A            RKKUND    R        O  2 19REFFLD(RKKUND)
     A            RKNAVN    R        O  2 26REFFLD(RKNAVN)
     A                                  2 61'Kundepostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            RNBILN    R        O  3 19DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            RNBILK    R        O  4 19DSPATR(UL)
     A                                      OVRDTA
     A            RNTEXT    R        O  4 22DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            RNRPER    R        O  5 61DSPATR(UL)
     A                                      OVRDTA
     A            RNRAAR    R        O  5 64DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            RNREKL    R        O  6 19DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            RNJOUR    R        O  6 61DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            RNREFN    R        O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Generalnota-nr .:'
     A            RNSAMF    R        O  7 61DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            RNANTP    R        O  8 19DSPATR(UL)
     A                                      OVRDTA
     A                                  8 24'Dato:'
     A            C2PDAT         6Y 0O  8 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8 43'Beløp. . . . . .:'
     A            RNBELØ    R        O  8 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Renteberegnet. .:'
     A            RNRENK    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                  9 24'Dato:'
     A            C2RDAT         6Y 0O  9 30DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  9 43'Restbeløp. . . .:'
     A            RNREST    R        O  9 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Inkassokode. . .:'
     A            RNINKO    R        O 10 19DSPATR(UL)
     A                                      OVRDTA
     A                                 10 24'Dato:'
     A            C2IDAT         6Y 0O 10 30DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            RNVALK    R        O 10 61DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            RNAVDE    R        O 11 19OVRDTA
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A            C2GTXT    R   18   O 11 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valutabeløp. . .:'
     A            RNVALB    R        O 11 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12  1'Selger . . . . .:'
     A            RNSLGR    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2ITXT    R   18   O 12 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            RNVALR    R        O 12 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C3 - vindu for vise leverandørpost
     A*
     A          R C3WIN
     A*%%TS  SD  20031027  221315  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  1'Leverandør . . .:'
     A            RLLEVR    R        O  2 19REFFLD(RLLEVR)
     A            RLNAVN    R        O  2 26REFFLD(RLNAVN)
     A                                  2 57'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            ROBILN    R        O  3 19DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            ROBILK    R        O  4 19DSPATR(UL)
     A                                      OVRDTA
     A            ROTEXT    R        O  4 22DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C3BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C3FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            RORPER    R        O  5 61DSPATR(UL)
     A                                      OVRDTA
     A            RORAAR    R        O  5 64DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            ROBETB    R        O  6 19DSPATR(UL)
     A                                      OVRDTA
     A            C3CTXT    R        O  6 22REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            ROJOUR    R        O  6 61DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            ROREKL    R        O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlegiro-nr.. .:'
     A            ROSAMB    R        O  7 61DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            ROREFN    R        O  8 19DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            ROBELØ    R        O  8 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            ROAVDE    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A            C3GTXT    R   18   O  9 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            ROREST    R        O  9 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjonskode:'
     A            ROATTS    R        O 10 19DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            RODELB    R        O 10 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            ROBETK    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            ROVALK    R        O 11 61DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            RORRF1    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            ROVALB    R        O 12 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            RORRF2    R        O 13 19DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            ROVALR    R        O 13 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            RODECK    R        O 14 19DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            ROBFNR    R        O 14 61DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            ROKIDI    R        O 15 17DSPATR(UL)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            ROBFLI    R        O 15 61DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 43'Lev.fakturanr. .:'
     A            ROLFAK    R        O 16 61DSPATR(UL)
     A                                      OVRDTA
     A*
     A*
     A*  Bilde C4 - Vindu for vise hovedbokspost
     A*
     A          R C4WIN
     A*%%TS  SD  20050321  135512  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 19 61)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 20'Vis hovedbokspost'
     A                                      COLOR(WHT)
     A                                  3  2'Konto . . . . .:'
     A            RBKONT    R        O  3 19REFFLD(RBKONT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C4TEXT    R        O  3 25REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  4  2'Kostnadsbærer .:'
     A            RBSPES    R        O  4 19REFFLD(RBSPES)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C4BTXT    R        O  4 24REFFLD(REBTXT)
     A                                      OVRDTA
     A                                  5  2'Avdeling. . . .:'
     A            RBAVDE    R        O  5 19REFFLD(RBAVDE)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C4GTXT    R        O  5 24REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                  6  2'Bilagsnr. . . .:'
     A            RBBILN    R        O  6 19REFFLD(RBBILN)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  6 41'Periode. .:'
     A            RBRPER    R        O  6 54REFFLD(RBRPER)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RBRAAR    R        O  6 57REFFLD(RBRAAR)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  7  2'Bilagdato . . .:'
     A            C4BDAT         6Y 0O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  7 41'Journalnummer:'
     A            RBJOUR    R        O  7 56DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Bilagstekst . .:'
     A            RBBILK    R        O  8 19REFFLD(RBBILK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RBTEXT    R        O  8 22REFFLD(RBTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  9  2'Beløp . . . . .:'
     A            RBNETO    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9 41'MVA-kode. . . .:'
     A            RBMVAK    R        O  9 58DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Grunnlag mva. .:'
     A            RBMVAG    R        O 10 19REFFLD(RBMVAG)
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 41'%-andel gr.lag.:'
     A            C4AMVA    R    3 00O 10 58REFFLD(RBAMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2'Beregnet mva. .:'
     A            RBMVAB    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11 41'MVA-% . . . . .:'
     A            C4PMVA    R    2 00O 11 58REFFLD(RBPMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'Invest.avgift .:'
     A            RBINVB    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12 41'MVA-overstyrt .:'
     A            RBMVAO    R        O 12 58DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Prosjekt-nr . .:'
6.20 A**          RBPROS    R        O 13 19DSPATR(UL)
6.20 A            RBHPRO         6   O 13 19DSPATR(UL)
     A                                      OVRDTA
     A                                 13 41'MVA-oppgjør . .:'
     A            RBMMRK    R        O 13 58DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Aktivitet . . .:'
     A            RBAKTI    R        O 14 19DSPATR(UL)
     A                                      OVRDTA
     A                                 14 41'MVA periode . .:'
     A            RBMVAP    R        O 14 58DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Mengde. . . . .:'
     A            RBMNGD    R        O 15 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 15 41'Mengdekode. . .:'
     A            RBMNGK    R        O 15 58DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Valuta-kode . .:'
     A            RBVALK    R        O 16 19DSPATR(UL)
     A                                      OVRDTA
     A                                 16 24'Valutabeløp . .:'
     A            RBVALB    R        O 16 41DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
