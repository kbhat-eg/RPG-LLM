     A*%%TS  SD  20120110  152814  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
6.29 A*          220615 nho  Ikke lov til å registrere manuelt ordrenr
6.29 A*                      dersom FAAMAN = 0
7.xx A*          200116 bsa  Justeringer basert på ny GUI og regler
Q.01 A*     bsa  080219    Quick wins - utskrifter/forhåndsvisning
Q.02 A*     bsa  140219    Quick wins - settes klar for plukking
     A*
7.fb A*     bof  040117  Merke linjer rødt hvis depositum ikke betalt
7.f2 A*     bsa  150420  Endret rekkefølge for å unngå rot i newlook
7.f3 A*     bsa  210920  Blir feil kode i cloud.
7.f4 A*     bsa  300920  Parameterstyring av fargevalg på ordre med
7.f4 A*                  depositumskrav.
     A*
8.01 A*     bsa  280322  Innfører ny ordreanmerkning, NG sperret i NGN
8.06 A*K000 bsa  110523  Justert utlegg av kunde.
8.07 A*K00  SST  120523  Nye bilder vedr kr.grenser
8.18 A*K000 bsa  050525  Legger ut valg for utsendelse av SMS
     A*
     A*-----------------------------------------------------------------
7.2c A                                      DSPSIZ(27 132 *WIDE)
7.2c A*                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070328  124845  AMD550      REL-V5R4M0  5722-WDS
7.xx A            B1DATE        10A  H
7.xx A            B1MOBI    R        H      REFFLD(FOMOBI)
7.xx A            B1KODE         2A  H
7.xx A            B1KOD2         1A  H
7.2c A*            B1LETY    R        H      REFFLD(FOLETY)
7.03 A            B1KRES    R        H      REFFLD(FOLETY)
7.03 A            B1DREF    R        H      REFFLD(FODREF)
     A            B1KUND         6S 0H
     A            B1KUNA    R        H      REFFLD(FOKUNA)
7.2c A            B1BDAT    R        H      REFFLD(FOBDAT)
7.2c A            B1DATO         6Y 0H
     A            B1SALG    R        H      REFFLD(FOTOTS)
7.xx A            B1INKM        12A  H
7.xx A            B1RABT        12A  H
     A            B1ITYP    R        H      REFFLD(FOOTYP)
7.xx A            B1LDAT        10A  H
7.2c A*            B1BENR    R        H      REFFLD(FOBENR)
7.xx A            B1OOUS    R        H      REFFLD(FOOOUS)
7.xx A            B1EUSR    R        H      REFFLD(FOEUSR)
7.2g A            B1VALG         2Y 0B 10  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 2 3 4 5 6 8 9 10 16 18 26 -
8.18 A                                       27 36 38 46 50 60 80 81 82 83 84  -
8.18 A                                      98 99)
7.f4 A  54                                  COLOR(YLW)
7.f4 A  50                                  COLOR(RED)
7.f4 A  51                                  COLOR(PNK)
7.f4 A  52                                  COLOR(BLU)
7.f4 A  53                                  COLOR(TRQ)
     A                                      EDTCDE(Z)
7.xx A N22                                  DSPATR(PC)
7.2g A            B1NUMM    R        O 10  5REFFLD(FONUMM)
7.f4 A* 45                                  COLOR(RED)
7.f4 A  50                                  COLOR(RED)
7.f4 A  51                                  COLOR(PNK)
7.f4 A  52                                  COLOR(BLU)
7.f4 A  53                                  COLOR(TRQ)
7.f4 A* 46                                  COLOR(BLU)
7.2g A            B1SUFF    R        O 10 14REFFLD(FFRFR/FOSUFF *LIBL/FFRF)
7.2g A            B1OTYP    R        O 10 17REFFLD(FOOTYP)
7.2g A            B1KNAV        21A  O 10 47
7.2c A*            B1DATO         6Y 0O  9 20EDTWRD('  .  .  ')
7.2g A            B1BBDA         6Y 0O 10 20EDTWRD('  .  .  ')
7.2ggA            B1LLDA         6Y 0O 10 29EDTWRD('  .  .  ')
7.2g A            B1BENR    R          10 38REFFLD(FOBENR)
7.2g A            B1KPRO    R        O 10 69REFFLD(FOKPRO)
7.2g A            B1NAVN        30A  O 10 77
7.2g A            B1INFO        12A  O 10108
7.2g A            B1ALLE         5A  O 10121
     A  43                                  COLOR(BLU)
     A  44                                  COLOR(RED)
     A  41                                  COLOR(WHT)
     A  42                                  COLOR(YLW)
7.2g A            B1LETY    R          10127REFFLD(FOLETY)
7.2g A            B1LMAT    R          10130REFFLD(FOLMAT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070328  151100  AMD550      REL-V5R4M0  5722-WDS
7.2g A                                      SFLSIZ(0015)
7.2g A                                      SFLPAG(0014)
7.05 A                                      ROLLUP(10)
7.05 A*                                     ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
7.2d A  49                                  CF08
     A                                      CF09
     A                                      CF10
     A                                      CA12
     A                                      CF15
     A                                      CF16
7.05 A                                      CF21
     A                                      CF22
7.2c A*                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
7.2c A                                  1118'Kl. :'
7.2c A                                      COLOR(WHT)
7.2c A                                  1124TIME
7.2c A                                      COLOR(WHT)
7.2c A                                  2  2'Ordrebehandling'
     A                                      COLOR(WHT)
7.2c A                                  2 38'FO500Z2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.2c A                                  2118'Dato:'
7.2c A                                      COLOR(WHT)
7.2c A                                  2124DATE
7.2c A                                      EDTCDE(Y)
7.2c A                                      COLOR(WHT)
7.2c A                                  3  1'-----------------------------------
7.2c A                                      ------------------------------------
7.2c A                                      ------------------------------------
7.2c A                                      ---------------------------'
7.2c A                                      COLOR(WHT)
7.2c A                                  4 83'Ordretype:'
7.xx A                                      COLOR(BLU)
7.2c A            H1OTYP    R        O  4 94REFFLD(FOOTYP)
7.xx A                                      COLOR(BLU)
7.2c A                                  4 97'Anm:'
7.xx A                                      COLOR(BLU)
7.2c A            H1OANM         2A  O  4102COLOR(BLU)
7.2c A                                  5 83'Bruker:'
7.xx A                                      COLOR(BLU)
7.2c A            H1USER    R        O  5 91REFFLD(FOUSER)
7.xx A                                      COLOR(BLU)
7.2c A                                  4105'Kunde:'
7.xx A                                      COLOR(BLU)
7.2c A            H1KUND    R        O  4117REFFLD(FOKUND)
7.xx A                                      COLOR(BLU)
7.2c A                                  5105'Beløp:'
7.xx A                                      COLOR(BLU)
7.2c A            H1VALG    R        O  5112REFFLD(FÅVALA)
7.xx A                                      COLOR(BLU)
7.2c A            H1BELP         9Y 0O  5114COLOR(BLU)
7.xx A                                      EDTCDE(Z)
7.2c A                                  4  3'Frisøk'
7.xx A                                      COLOR(WHT)
7.2c A                                  5  3'Ordre'
7.xx A                                      COLOR(WHT)
7.2c A                                  5 21'S'
7.xx A                                      COLOR(WHT)
7.2c A                                  5 27'Ot'
7.xx A                                      COLOR(WHT)
7.xx A*                                 5 54'Kundealfa'
7.xx A*                                     COLOR(WHT)
7.2c A                                  5 34'Dato'
7.xx A                                      COLOR(WHT)
7.2c A                                  6  2' 2=Endre 3=Kopier 4=Slett 5=Vise 6-
7.xx A                                      =Utskrift  8=Hvem holder ordre 9=Fj-
7.xx A                                      ern kode'
7.xx A                                      COLOR(BLU)
7.2c A                                  6 81'10=Hist 16=Utplukk 18=Plukk til be-
7.2c A                                      stilling'
7.xx A                                      COLOR(BLU)
7.2c A                                  7  3'26=Vis bestilling 36=Reverser 38=T-
7.2c A                                      ilb->spes.pris 46=Vis slette 50=Bes-
7.2c A                                      t.sys. 60=Logg 98=Holde/Frigi 99=In-
7.2c A                                      fo'
     A                                      COLOR(BLU)
7.2g A                                  8  3'80=Forhåndsvisning 81=Email 82=Set-
7.2g A                                      tes klar for plukking'
7.2g A                                      COLOR(BLU)
8.18 A  49                              8 59'83=Ordrelinjer Excel'
7.2g A                                      COLOR(BLU)
8.18 A                                  8 80'84=Send SMS'
8.18 A                                      COLOR(BLU)
7.2g A                                  9  2'V'
     A                                      COLOR(WHT)
7.2g A                                  9  5'Ordre'
     A                                      COLOR(WHT)
7.2g A                                  9 14'S'
     A                                      COLOR(WHT)
7.2g A                                  9 17'Ot'
7.2c A                                      COLOR(WHT)
7.2g A                                  9 20'Ordrdato'
7.2c A                                      COLOR(WHT)
7.2g A                                  9 29'Levdato'
7.2c A                                      COLOR(WHT)
7.2g A                                  9 38'Bestnr'
7.2c A                                      COLOR(WHT)
8.06 A*                                 9 47'Kundealfa'
8.06 A                                  9 47'Kunde    '
     A                                      COLOR(WHT)
7.2g A                                  9 69'Kproj'
7.2c A                                      COLOR(WHT)
7.2c A            B2HEDD         9A  H
7.2g A                                  9 77'Vareadresse'
     A                                      COLOR(WHT)
7.2g A            B2HED2        10A     9108
7.2c A*                                  8108'Ordreverdi'
7.2c A                                      COLOR(WHT)
7.2g A                                  9121'Logg'
     A                                      COLOR(WHT)
7.2g A                                  9127'LT'
7.2c A                                      COLOR(WHT)
7.2g A                                  9130'FM'
7.2c A                                      COLOR(WHT)
7.2c A            B2NAVN        35A  B  4 11COLOR(YLW)
     A                                      OVRDTA
     A  22                                  DSPATR(PC)
7.2c A            B2NAV2        25A  O  4 48COLOR(WHT)
     A                                      OVRDTA
7.2c A                                  4 75'(F21)'
     A                                      COLOR(WHT)
7.2c A            B2NUMM    R        B  5 11REFFLD(FONUMM)
     A                                      DLTCHK
     A  31                                  ERRMSGID(FMO0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.2c A            B2SUFF    R        B  5 23REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.2c A            B2OTYP    R        B  5 30REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A*           B2KUNA        15A  B  6 54COLOR(YLW)
     A*                                     OVRDTA
7.2c A            B2DATO         6Y 0B  5 39COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
Q.01 A*  48        B2FSRT         1  0B  6 75EDTCDE(Z)
Q.01 A*  48        B2FFLT         1  0B  6 77EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20050621  150935  ASK530      REL-V5R2M0  5722-WDS
7.2c A                                 25  2'F1=Bla-alt'
     A                                      COLOR(BLU)
7.2c A                                 25 14'F2=Velg'
     A                                      COLOR(BLU)
7.2c A                                 25 23'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.2c A                                 25 38'F5=Forny'
     A                                      COLOR(BLU)
7.2c A                                 25 48'F15=Start utskrift'
     A                                      COLOR(BLU)
7.2c A                                 25 68'F22=Inkl.mva'
     A                                      COLOR(BLU)
7.2c A                                 26  2'F6=Opprett'
     A                                      COLOR(BLU)
7.2c A                                 26 14'F7=Kunde/ordre'
     A                                      COLOR(BLU)
7.2c A                                 26 30'F9=Vare/ordre'
     A                                      COLOR(BLU)
7.2c A                                 26 55'F16=Utplukk'
     A                                      COLOR(BLU)
7.2d A  49                             26 67'F8=Excel'
7.2d A                                      COLOR(BLU)
7.2c A*                                 26 68'F23=Bytt info'
7.2c A*                                      COLOR(BLU)
7.2c A                                 26 45'F10=Hist'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
7.XX A                                      WINDOW(02 02 21 55)
     A                                      CA03
     A                                      CF04
     A                                      CF09
     A                                      CA12
7.xx A                                      RTNCSRLOC(&W_ARCDH1 &W_AFLDH1)
     A                                      PUTOVR
7.xx A            W_ARCDH1      10A  H
7.xx A            W_AFLDH1      10A  H
7.xx A                                  2 49'FO500H1'
7.xx A                                      COLOR(WHT)
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  3 29'- Utelater'
7.xx A                                  4  2'Velg utelate    '
     A            H1UTLA    R        B  4 19REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '-')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  4 22'(k)'
7.xx A                                  4 33'ordretyper'
7.xx A                                  5  2'Velg ordretype +'
7.xx A                                  5 22'+'
7.xx A                                  5 27'+'
     A            H1OTYP    R        B  5 19REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A            H1OTY2    R        B  5 24REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            H1OTY3    R        B  5 29REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5 33'Blank gir alle'
7.xx A                                  6  2'Velg info-type +'
     A            H1ITYP    R        B  6 19REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  7  2'Velg bruker    +'
     A            H1USER    R        B  7 19REFFLD(FOUSER)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  8  2'Velg kundenr   +'
     A            H1KUND    R        B  8 19REFFLD(FOKUND)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  9  2'Velg best.nr.   '
     A            H1BENR    R        B  9 19REFFLD(FDBENR)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.fb A                                  9 31'Korttype      +'
7.fb A            H1KORT    R        B  9 47REFFLD(FOPSUF)
7.fb A                                      OVRDTA
7.fb A                                      COLOR(YLW)
7.xx A                                 10  2'Velg selger    +'
     A            H1SELG    R        B 10 19REFFLD(FOSELG)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.fb A                                 10 31'Velg Lev.type  '
7.fb A            H1LETY    R        B 10 47REFFLD(FOLETY)
7.03 A                                      DLTCHK
7.03 A                                      CHECK(RB)
7.03 A                                      OVRDTA
7.03 A                                      COLOR(YLW)
7.03 A                                 10 50'(kode)'
7.xx A                                 11  2'Velg avdeling  +'
     A            H1AVDE    R        B 11 19REFFLD(FOAVDE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.fb A                                 11 31'Velg Logg.kode.'
7.fb A            H1LOGG         1A  B 11 47
7.03 A                                      OVRDTA
7.03 A                                      COLOR(YLW)
7.03 A                                 11 50'(kode)'
7.xx A                                 12  2'Velg lager      '
     A            H1LAGE    R        B 12 19REFFLD(FOLAGE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.fb A                                 12 31'Forsend.-måte +'
7.fb A            H1LMAT    R        B 12 47REFFLD(FOLMAT)
7.fb A                                      OVRDTA
7.fb A                                      COLOR(YLW)
7.fb A                                 12 50'(kode)'
7.f2  *
7.f2 A                                 13  2'100% Forhåndbetaling:'
7.f2 A                                 13 24'Ubetalt:'
7.f3 A            H1100K    R        B 13 33REFFLD(FÅJANE)
7.f2 A                                      OVRDTA
7.f2 A                                      COLOR(YLW)
7.f3 A                                 13 35'1/0'
7.f3 A                                 13 39'Innbet:'
7.f3 A            H1100I    R        B 13 47REFFLD(FÅJANE)
7.f2 A                                      OVRDTA
7.f2 A                                      COLOR(YLW)
7.f3 A                                 13 49'1/0'
7.f2 A**                               13 50'1=Ja'
7.f2 A                                 14  2'Delbetaling Krav....:'
7.f2 A                                 14 24'Ubetalt:'
7.f3 A            H1DELK    R        B 14 33REFFLD(FÅJANE)
7.f2 A                                      OVRDTA
7.f2 A                                      COLOR(YLW)
7.f3 A                                 14 35'1/0'
7.f3 A                                 14 39'Innbet:'
7.f3 A            H1DELI    R        B 14 47REFFLD(FÅJANE)
7.f2 A                                      OVRDTA
7.f2 A                                      COLOR(YLW)
7.f3 A                                 14 49'1/0'
7.f2  *
7.f2 A                                 15  2'Rekvisisjon     '
7.f2 A            H1REKV    R        B 15 19REFFLD(FOREKV)
7.f2 A                                      DLTCHK
7.f2 A                                      OVRDTA
7.f2 A                                      COLOR(YLW)
7.f2 A                                 16  2'Mobil'
7.f2 A            H1MOBI    R        B 16 19REFFLD(FOMOBI)
7.07 A                                      DLTCHK
7.07 A                                      OVRDTA
7.07 A                                      COLOR(YLW)
7.f2 A                                 17  2'Beløpsgrense    '
7.f2 A            H1BELP         9Y 0B 17 19CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.f2 A                                 18  2'Over / under    '
7.f2 A            H1VALG    R        B 18 19REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.f2 A                                 18 24'O=Over, U=Under'
7.f2 A                                 19  2'Ordreanmerkn    '
     A            H1OANM         2A  B 19 19OVRDTA
     A                                      COLOR(YLW)
8.01 A**                               19 24'**, OK, UT, VE BU'
8.01 A                                 19 24'**, OK, UT, VE, BU, NG'
     A                                 20  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 20 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 20 32'F9=Lagre'
     A                                      COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20051020  151154  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 65)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
7.xx A                                      RTNCSRLOC(&W_ARCDB3 &W_AFLDB3)
7.xx A            W_ARCDB3      10A  H
7.xx A            W_AFLDB3      10A  H
     A                                  2  2'Ordrenummer . . . . . .'
     A                                      COLOR(WHT)
     A            B3NUMM    R        O  2 26REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            B3NAVN    R        O  2 35REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  4  2'Nytt ordre-nr . . . . .'
     A            B3NONR    R        B  4 26REFFLD(FONUMM)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(PC)
6.29 A  39                                  ERRMSGID(FMO0100 *LIBL/QMFOFAK 39)
     A  31                                  ERRMSGID(FMO0030 *LIBL/QMFOFAK 31)
     A                                      COMP(GE 0)
     A                                      COLOR(YLW)
     A                                  4 35'Blank gir automatisk tildeling'
7.xx A                                  5  2'Ny ordre-type . . . . +'
     A            B3OTYP    R        B  5 26REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A  32                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 32)
9.xx A  68                                  ERRMSGID(FMO0662 *LIBL/QMFOFAK 68)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Nytt kunde-nr . . . . +'
     A            B3KUND    R        B  6 26REFFLD(FOKUND)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(FRK0300 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FRK0310 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(FRK0320 *LIBL/QMFOFAK 37)
     A  40                                  ERRMSGID(FRK0340 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                  8  2'Rabatt. . . . . . . . .'
7.04 A*           B3KRAB    R        B  8 26REFFLD(FÅSVA2)
7.04 A            B3KRAB         1A  B  8 26
     A                                      OVRDTA
7.04 A*                                     VALUES(0 1 2)
7.04 A*                                     CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
7.xx A                                  8 30'0=Som ordren,'
7.xx A                                  9 30'1=Fra ny kunde,'
7.xx A                                 10 30'2=Uten rabatt'
7.xx A                                 12  2'Slette kundeprosjekt. .'
7.xx A            B3XPRO    R        B 12 26REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
7.xx A                                 12 35'1=Ja,0=Nei'
7.xx A                                 12 47'(Gjelder kopiering'
7.xx A                                 13  2'Slettes etter kopiering'
7.xx A            B3KCOP    R        B 13 26REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.28 A  38                                  ERRMSGID(FM01220 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
9.xx A  47                                  DSPATR(PR)
7.xx A                                 13 35'1=Ja,0=Nei'
7.xx A                                 13 48'til samme kundenr)'
7.xx A                                 14  2'Ta med topp/bunn-tekst.'
7.xx A            B3KTXT    R        B 14 26REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 14 35'1=Ja,0=Nei'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R B3WINT
     A*%%TS  SD  20051004  150312  HKO530      REL-V5R3M0  5722-WDS
7.24 A                                      WINDOW(2 2 18 39)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Tilbud med slettede varer '
7.24 A                                      COLOR(WHT)
     A                                      DSPATR(RI)
7.24 A                                  2 32'FO500B3'
7.24 A                                      COLOR(WHT)
7.24  *
7.24  * info under er fjernet fordi Newlook fikk problemer
7.24  *
7.24 A*                                  4  2'Ordrenummer . . . . .'
7.24 A*                                      COLOR(WHT)
7.24 A*            B3TNUMM   R        O  4 24REFFLD(FONUMM)
7.24 A*                                      OVRDTA
7.24 A*                                      COLOR(WHT)
7.24 A*            B3TNAVN   R        O  5  2REFFLD(FONAVN)
7.24 A*                                      OVRDTA
7.24 A*                                      COLOR(WHT)
     A                                  7  2'Tilbudet inneholder slettede'
     A                                  8  2'varer og kan ikke kopieres.'
     A                                 10  2'For å vedlikeholde tilbudet,'
     A                                 11  2'trykk ENTER / OK'
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20020312  190907  HKO         REL-V5R1M0  5722-WDS
7.11 A                                      WINDOW(02 02 18 45)
     A                                      CA03
9.xx A                                      CF04
     A                                      CA12
     A                                      PUTOVR
9.xx A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
9.xx A            W_ARCD        10A  H
9.xx A            W_AFLD        10A  H
9.xx A N41                              2  2' Slette ordre '
9.xx A                                      COLOR(WHT)
     A                                      DSPATR(RI)
9.xx A  41                              2  2' Slette tilbud '
9.xx A                                      COLOR(WHT)
9.xx A                                      DSPATR(RI)
7.xx A                                  2 25'FO500B4'
7.xx A                                      COLOR(WHT)
6.32 A                                  4  2'Ordrenummer . . . . .'
     A                                      COLOR(WHT)
6.32 A            B4NUMM    R        O  4 24REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            B4NAVN    R        O  5  2REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
9.xx A  41                              7  2'Slettekode'
9.xx A  41        B4SLKO         2Y 0B  7 13COLOR(YLW)
9.xx A  41 33                               ERRMSGID(FMY0204 *LIBL/QMFOFAK 33)
9.xx A                                      OVRDTA
9.xx A  41                                  DSPATR(PC)
9.xx A                                      CHECK(RB)
9.xx A  41        B4SLTX        30A  O  7 16OVRDTA
9.xx A  41                              8  2'Konkurrent'
9.xx A  41        B4KKKO         2Y 0B  8 13COLOR(YLW)
9.xx A  41 34                               ERRMSGID(FMY0205 *LIBL/QMFOFAK 34)
9.xx A  41 35                               ERRMSGID(FMY0206 *LIBL/QMFOFAK 35)
9.xx A                                      OVRDTA
9.xx A                                      CHECK(RZ)
9.xx A  41        B4KKTX        30A  O  8 16OVRDTA
     A  41                              9  2'Konkurrent pris'
     A  41        B4KOPR        11Y 2B  9 19COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                 11  2'Kommentar til slettingen'
     A            B4KOM1    R        B 12  2REFFLD(FYKOM1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
9.xx A N41                                  DSPATR(PC)
     A  31                                  ERRMSGID(FMY0200 *LIBL/QMFOFAK 31)
     A            B4KOM2    R        B 13  2REFFLD(FYKOM2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  32                                  ERRMSGID(FM00500 *LIBL/QMFOFAK 32)
     A                                 15  2'For å slette, trykk ENTER / OK'
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
9.xx A  41                             17 17'F4=Spørring'
9.xx A  41                                  COLOR(BLU)
     A          R B4WINT
     A*%%TS  SD  20080225  105248  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette ordre '
     A                                      DSPATR(RI)
     A                                  4  2'Det er registrert depositum'
     A                                      COLOR(WHT)
     A                                  5  2'på ordre, kan ikke slettes!'
     A                                      COLOR(WHT)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20020506  154235  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 15 40)
6.32 A                                  2  2'Ordrenummer . . . . .'
     A                                      COLOR(WHT)
6.32 A            B9NUMM    R        O  2 24REFFLD(FONUMM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A  31                                  ERRMSGID(FM00500 *LIBL/QMFOFAK 31)
     A            B9NAVN    R        O  4  2REFFLD(FONAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(RED)
     A            B9ADRE    R        O  5  2REFFLD(FONAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(RED)
     A            B9STED    R        O  6  2REFFLD(FONAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(RED)
     A                                  8  2'Fjern kode for:'
     A                                      COLOR(WHT)
     A                                 10  2'Låsing av ordre . . . .'
     A            B9ORDR    R        B 10 27REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 10 30'1=Ja,0=Nei'
     A                                 11  2'Skrevet pakkseddel. . .'
     A            B9PAKS    R        B 11 27REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 11 30'1=Ja,0=Nei'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B18MSG
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Plukk til bestilling '
     A                                      DSPATR(RI)
     A                                  4  2'Det er krav til depositum,   '
     A                                      COLOR(WHT)
     A                                  5  2'men det er ikke innbetalt.   '
     A                                      COLOR(WHT)
     A                                  6  2'Kan ikke bestille !          '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B18MSG2
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Du kan ikke stå i bestilling '
     A                                      COLOR(WHT)
     A                                  4  2'og gå over til ordre for å   '
     A                                      COLOR(WHT)
     A                                  5  2'lage bestilling.  Du må logge'
     A                                      COLOR(WHT)
     A                                  6  2'på ordrebehandling.          '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
9.xx A          R B18MSG3
9.xx A*%%TS  SD  20170201  135621  G0XEGASSKO  REL-V7R1M0  5770-WDS
9.xx A                                      WINDOW(*DFT 8 35)
9.xx A                                      CA03
9.xx A                                      CA12
9.xx A                                      PUTOVR
9.xx A                                  3  2'Du kan ikke plukke til       '
9.xx A                                      COLOR(WHT)
9.xx A                                  4  2'bestilling fra hovedordre!   '
9.xx A                                      COLOR(WHT)
9.xx A                                  5  2'                             '
9.xx A                                      COLOR(WHT)
9.xx A                                  6  2'                             '
9.xx A                                      COLOR(WHT)
9.xx A                                  7  2'F3=Gå tilbake'
9.xx A                                      COLOR(BLU)
     A          R B16MSG
     A*%%TS  SD  20120110  152814  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Utplukk/Utskrift'
     A                                      DSPATR(RI)
     A                                  4  2'Leveringsdato er ikke utfylt,'
     A                                      COLOR(WHT)
     A                                  5  2'utplukk/utskrift kan ikke kjøres!'
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.07 A          R G1WIN
8.07 A*%%TS  SD  20230512  130627  ASP_EGS     REL-V7R4M0  5770-WDS
8.07 A                                      WINDOW(*DFT 11 60)
8.07 A                                      CA03
8.07 A                                      CA12
8.07 A                                      CF06
8.07 A                                      CA18
8.07 A                                      PUTOVR
8.07 A                                  2  2' Kredittgrense er overskredet '
8.07 A                                      DSPATR(RI)
8.07 A                                  5  2'Ordren er overskredet med . .'
8.07 A            G1TOTL    R        O  5 32REFFLD(FOTOTS)
8.07 A                                      EDTWRD('       0 ,  ')
8.07 A                                 10  2'F3=Gå tilbake                   F1-
8.07 A                                      8=Godkjenn overskridelse'
8.07 A                                      COLOR(BLU)
8.07 A          R G3WIN
8.07 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
8.07 A                                      WINDOW(*DFT 11 60)
8.07 A                                      CA03
8.07 A                                      CA12
8.07 A                                      CF06
8.07 A                                      CA18
8.07 A                                      PUTOVR
8.07 A                                  2  2' Kredittgrense er låst '
8.07 A                                      DSPATR(RI)
8.07 A                                  5  2'Du kan ikke godkjenne overskridel-
8.07 A                                      se'
8.07 A                                 10  2'F3=Gå tilbake'
8.07 A                                      COLOR(BLU)
     A          R M1WIN
     A*%%TS  SD  20140202  133706  BOFBMP      REL-V7R1M0  5770-WDS
     A                                      WINDOW(&W_ACLI &W_ACPO 8 32)
     A                                      CF08
     A                                      KEEP
     A            W_ACLI         2S 0P
     A            W_ACPO         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A            M1MLD1        30A  O  4  2COLOR(WHT)
     A                                      TEXT('Meldingsfelt')
     A            M1MLD2        30A  O  5  2COLOR(WHT)
     A                                      TEXT('Meldingsfelt')
     A                                  7  2'F08=Tilbud  '
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
