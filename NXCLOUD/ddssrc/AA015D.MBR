     A*%%TS  SD  20050524  122033  AMD530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA015D
     A* Beskrivelse . . : Rutineregister, PDF vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A* 5.60  260310 BSA  Nytt program
6.20 A* 6.20  070611 BSA  Aksepterer lower case i input
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C2BLD
     A*%%TS  SD  20050524  122033  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rutine-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rutine . . . . . . . . . . .'
     A            C1RUTI    R        O  5 31REFFLD(AFRUTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Aktiver informasjon. . . . .'
     A                                  7 33'0=Nei,1=Ja,2=For avdelinger'
     A            C2UPDF         1N 0B  7 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSG('Kun verdier 0-2')
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8  2'Sendes på mail . . . . . . .'
     A                                  8 33'0=Nei,1=Ja'
     A            C2MAIL         1N 0B  8 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8 46'Excel . . . . . .'
     A                                  8 66'0=Nei,1=Ja'
     A            C2EXLP         1N 0B  8 64COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  2'Arkiveres. . . . . . . . . .'
     A                                  9 33'0=Nei,1=Ja'
     A            C2ARKI         1N 0B  9 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  2'Forhåndsvisning. . . . . . .'
     A                                 10 33'0=Nei,1=Ja'
     A            C2SKJE         1N 0B 10 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11  2'Liggende utskrift. . . . . .'
     A                                 11 33'0=Nei,1=Ja'
     A            C2LAND         1N 0B 11 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15  2'OBS !- malene må finnes ved -
     A                                      bruk av Excel eller JASPER'
     A                                 16  2'XML mal. . . . . . . . . . .'
     A            C2XMLM        75A  B 17  2COLOR(YLW)
     A  30                                  DSPATR(PR)
6.20 A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18  2'JASPER mal . . . . . . . . .'
     A            C2JASM        75A  B 19  2COLOR(YLW)
     A  30                                  DSPATR(PR)
6.20 A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 20  2'Logo/grafikk . . . . . . . .'
     A            C2LOGO        75A  B 21  2COLOR(YLW)
     A  30                                  DSPATR(PR)
6.20 A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F6=A-
     A                                      vdelingsinformasjon'
     A                                      COLOR(BLU)
