     A*%%TS  SD  20090911  110440  AMD610      REL-V5R4M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  140316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1MKOD    R        O  9  5REFFLD(VAMKOD)
7.xx A            B1MTXT    R        O  9  9REFFLD(VAMTXT)
7.xx A            B1MKOF    R        O  9 49REFFLD(VAMKOF)
7.xx A            B1MKLA    R        O  9 56REFFLD(VAMKLA)
7.xx A            B1MKUO    R        O  9 68REFFLD(VAMKUO)
7.xx A            B1MKUI    R        O  9 76REFFLD(VAMKUI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koblingstabell mva -
     A                                         '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7 48'Mva.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 55'Mva.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 65'Mva exp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 73'Mva exp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Beskrivelse                '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 48'ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 55'innkjøp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 66'ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 74'innk'
7.xx A                                      COLOR(WHT)
7.xx A            B2MKOD    R        B  5  5REFFLD(VAMKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2MTXT    R        B  5  9REFFLD(VAMTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  084800  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koblingstabell mva -
     A                                        '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Avgiftskode vareregister . .'
     A            C1MKOD    R        B  5 31REFFLD(VAMKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Avg.kode finnes fra før '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090911  110440  AMD610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koblingstabell mva -
     A                                        '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Avgiftskode vareregister . .'
     A            C1MKOD    R        O  5 31REFFLD(VAMKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Beskrivelse. . . . . . . . .'
     A            C2MTXT    R        B  6 31REFFLD(VAMTXT)
     A  31                                  ERRMSGID(VMA8100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  8  2'Mva kode i ordre . . . . . .'
     A            C2MKOF    R        B  8 31REFFLD(VAMKOF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(PC)
     A  81                                  ERRMSGID(VMA9150 *LIBL/QMFOFAK 81)
     A                                      OVRDTA
     A                                 10  2'Mva kode i innkjøp . . . . .'
     A            C2MKLA    R        B 10 31REFFLD(VAMKLA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A  82                                  ERRMSGID(VMA9150 *LIBL/QMFOFAK 82)
     A                                      OVRDTA
     A            C2MKUO    R        B 12 31REFFLD(VAMKLA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  83                                  ERRMSGID(VMA9150 *LIBL/QMFOFAK 83)
     A                                      OVRDTA
     A                                 14  2'Mva kode eksport innkjøp . .'
     A            C2MKUI    R        B 14 31REFFLD(VAMKLA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  84                                  ERRMSGID(VMA9150 *LIBL/QMFOFAK 84)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A            C2MEDA         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2MEUS    R        O 23 70REFFLD(VAMEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 13 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette avgiftskode      '
     A                                      DSPATR(RI)
     A                                  4  2'Avgiftskode. . . .'
     A            D1MKOD    R        O  4 21REFFLD(VAMKOD)
     A                                  5  2'Beskrivelse. . . .'
     A            VAMTXT    R        O  5 21
     A                                  7  2'Mva. kode ordre. .'
     A            VAMKOF    R        O  7 21
     A                                  8  2'Mva. kode innkjøp.'
     A            VAMKLA    R        O  8 21
     A                                  9  2'Mva. k. exp. ordre'
     A            VAMKUO    R        O  9 21
     A                                 10  2'Mva. k. exp. ordre'
     A            VAMKUI    R        O 10 21
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20090910  141535  AMD610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Avgiftskode. . . .'
     A            K1MKOD    R        B  4 21REFFLD(VAMKOD)
     A  31                                  ERRMSGID(VMA9110 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA9130 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Avgiftskode. . . .'
     A            VAMKOD    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            VAMTXT    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
