     A*%%TS  SD  20030120  111230  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL107D
     A* Beskrivelse . . : Overføring fra samlefil
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A*
     A          R R1WSF                     SFL
     A*%%TS  SD  19941125  085805  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Subfile; Forslagslinjer for v-
     A                                      indu')
     A            R1BFLI    R        O  6  3REFFLD(ROBFLI)
     A            R1BDAT         8Y 0O  6  9EDTWRD('  .  .    ')
     A            R1BELØ    R        O  6 20REFFLD(ROBELØ)
     A                                      EDTCDE(K)
     A            R1RABB         1A  O  6 36
     A            R1TEXT        15A  O  6 38
     A            R1LEVR    R        O  6 54REFFLD(ROLEVR)
     A                                      TEXT('Leverandør')
     A            R1KIDS         1A  O  6 62TEXT('KID-status')
     A            R1VALG         1A  B  6  1DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '5')
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      DSPATR(PC)
     A          R R2WCT                     SFLCTL(R1WSF)
     A*%%TS  SD  19941125  085805  RH          REL-V2R3M0  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      WINDOW(R2CMD)
     A                                      TEXT('Forslagslinjer for vindu')
     A                                      ROLLUP(20)
     A                                      ROLLDOWN(21)
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A  22                                  SFLDSP
     A  23                                  SFLDSPCTL
     A  24                                  SFLCLR
     A  25                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1 16'F O R S L A G S N U M M E R:'
     A                                      COLOR(WHT)
     A            R2BFNR    R        O  1 45REFFLD(ROBFNR)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  2  1'Velg og trykk Enter'
     A                                      COLOR(BLU)
     A                                  3  1'5=Vise  Blank=Vise fra'
     A                                      COLOR(BLU)
     A                                  4  3'Linje Bet.dato             Beløp R-
     A                                       Bilagstekst     Lev.nr KID'
     A                                      COLOR(WHT)
     A            R2BFLI    R        B  5  3REFFLD(ROBFLI)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R R2CMD
     A*%%TS  SD  19950809  085956  RH1         REL-V3R1M0  5763-PW1
     A                                      WINDOW(&W_AROW &W_ACOL 17 63)
     A                                      TEXT('Kommandotaster for vindu')
     A                                      OVERLAY
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                 16  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030120  111230  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Vise-/Endre post')
     A                                      CA03
     A            C2STAT    R        H      REFFLD(RTSTAT)
     A            C2TIST    R        H      REFFLD(ROTIST)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Betalingstransaksjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN         4A  O  4 77DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      TEXT('Ny/Endr')
     A                                  6  2'Leverandørnummer . .'
     A            C2LEVR    R        O  6 23REFFLD(ROLEVR)
     A                                      DSPATR(UL)
     A                                  7  2'Bilagsdato . . . . .'
     A            C2BDAT         8Y 0O  7 23EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                  7 42'Valutakode. . . . . . .'
     A            C2VALK    R        O  7 66REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                  8  2'Betalingsdato. . . .'
     A            C2FDMÅ         8Y 0B  8 23
     A  33                                  DSPATR(PR)
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A N33                                  COLOR(YLW)
     A                                      CHECK(RB)
     A                                  8 42'Opprinnelig betalingsd.'
     A            C3OPFO         8Y 0O  8 66TEXT('Opprinnelig forfallsdato')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                  9  2'Beløp. . . . . . . .'
     A            C2BELØ    R        B  9 23REFFLD(ROBELØ)
     A  33                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A N33                                  COLOR(YLW)
     A                                      CHECK(RB)
     A                                  9 42'Beløp før rabatt NOK. .'
     A            C3OPBN    R     Y  O  9 66REFFLD(ROBELØ)
     A                                      TEXT('Trukket rabatt NOK')
     A                                      EDTCDE(K)
     A                                      DSPATR(UL)
     A                                 10  2'Valutabeløp. . . . .'
     A            C2VALB    R        B 10 23REFFLD(ROVALB)
     A  33                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A N33                                  COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 42'Beløp før rabatt Valuta'
     A            C3OPBV    R        O 10 66REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Trukket rabatt NOK')
     A                                 12  2'Restbeløp NOK. . . .'
     A            C2REST    R        O 12 23REFFLD(ROREST)
     A                                      DSPATR(UL)
     A                                 12 42'Rabattdato-1. . . . . .'
     A            C3RDA1         8Y 0O 12 66TEXT('Rabattdato-1')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                 13  2'Restbeløp Valuta . .'
     A            C2VALR    R        O 13 23REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                 13 42'Rabattbeløp-1 NOK . . .'
     A            C3RBN1    R        O 13 66REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-1 NOK')
     A                                 14  2'Delbetalt. . . . . .'
     A            C2DELB    R        O 14 23REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                 14 42'Rabattbeløp-2 Valuta. .'
     A            C3RBV1    R        O 14 66REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-1 valuta')
     A                                 16  2'Rabattdato-2 . . . .'
     A            C3RDA2         8Y 0O 16 23TEXT('Rabattdato-2')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                 16 42'Rabattdato-3. . . . . .'
     A            C3RDA3         8Y 0O 16 66TEXT('Rabattdato-3')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                 17  2'Rabattbeløp-2 NOK. .'
     A            C3RBN2    R        O 17 23REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-2 NOK')
     A                                 17 42'Rabattbeløp-3 NOK . . .'
     A            C3RBN3    R        O 17 66REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-3 NOK')
     A                                 18  2'Rabattbeløp-2 Valuta'
     A            C3RBV2    R        O 18 23REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-2 valuta')
     A                                 18 42'Rabattbeløp-3 Valuta. .'
     A            C3RBV3    R        O 18 66REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      TEXT('Rabattbeløp-3 valuta')
     A                                 20  2'Bilagstekst. . . . .'
     A            C2TEXT    R        B 20 23REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A  33                                  DSPATR(PR)
     A N33                                  COLOR(YLW)
     A                                 20 44'Deklarasjonskode. . .'
     A            C2DECK    R     Y  B 20 66REFFLD(RODECK)
     A                                      TEXT('Deklarasjonskodee')
     A  33                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A N33                                  COLOR(YLW)
     A                                 21  2'Kundeindentifikasjon'
     A            C2KIDI    R        B 21 23REFFLD(ROKIDI)
     A  33                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A N33                                  COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20030120  111230  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Vise-/Endre post')
     A                                      CA03
     A            C2STAT    R        H      REFFLD(RTSTAT)
     A            C2TIST    R        H      REFFLD(ROTIST)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Betalingstransaksjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN         4A  O  4 77DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      TEXT('Ny/Endr')
     A                                  6  2'Leverandørnr . . . . . . .'
     A            C2LEVR    R        O  6 29REFFLD(ROLEVR)
     A                                      DSPATR(UL)
     A                                  6 47'Valutakode . . . . . . . .'
     A            C2VALK    R        O  6 74REFFLD(ROVALK)
     A                                      DSPATR(UL)
6.NU A                                  7 47'Ref.nr.  . . . . . . . '
6.nu A            C2REFN    R        O  7 71REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                  8  2'Att.kode . . . . . . . . .'
     A            C2ATTS    R        O  8 29REFFLD(ROATTS)
     A                                      DSPATR(UL)
     A                                  8 47'Journallistenr . . . . . .'
     A            C2JOUR    R        O  8 74REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                 10  2'Bilagskode . . . . . . . .'
     A            C2BILK    R        O 10 29REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                 10 47'Virksomhetskode. . . . . .'
     A            C2VIRK    R        O 10 74REFFLD(ROVIRK)
     A                                      DSPATR(UL)
     A                                 11  2'Bilagsnr . . . . . . . . .'
     A            C2BILN    R        O 11 29REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                 11 47'Betalingskode. . . . . . .'
     A            C2BETK    R        O 11 74REFFLD(ROBETK)
     A                                      DSPATR(UL)
     A                                 12  2'Betalingsreferanse-1 . . .'
     A            C2RRF1    R        O 12 29REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                 12 47'Betalingsreferanse-2 . . .'
     A            C2RRF2    R        O 12 74REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                 14  2'Remitteringskode . . . . .'
     A            C2REMK    R        O 14 29REFFLD(RLREMK)
     A                                      TEXT('Betalingskode')
     A                                 14 47'Betalingsforslagsnr. . . .'
     A            C2BFNR    R        O 14 74REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                 15  2'Reklamasjonskode . . . . .'
     A            C2REKL    R        O 15 29REFFLD(ROREKL)
     A                                      DSPATR(UL)
     A                                 15 47'Betalingsforslagsli. . . .'
     A            C2BFLI    R        O 15 74REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                 16  2'Regnskapsår. . . . . . . .'
     A            C2RAAR    R        O 16 29REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                 16 47'Periode. . . . . . . . . .'
     A            C2RPER    R        O 16 74REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                 18  2'Samlebilagnr . . . . . . .'
     A            C2SAMB    R        O 18 29REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                 18 47'Avdeling . . . . . . . . .'
     A            C2AVDE    R        O 18 74REFFLD(ROAVDE)
     A                                      DSPATR(UL)
     A                                 19  2'Betalingsbetingelse. . . .'
     A            C2BETB    R        O 19 29REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1WIN
     A*%%TS  SD  19950207  103851  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Antall slettede poster')
     A                                      KEEP
     A                                  2  2'Sletting pågår...'
     A                                  4  2'Antall slettede transaksjoner:'
     A            A1SLPO         4Y 0O  4 33DSPATR(UL)
     A                                      TEXT('Antall slettede poster')
     A                                      EDTCDE(Z)
     A          R C2WIN
     A*%%TS  SD  19950207  103851  RH          REL-V2R3M0  5738-PW1
     A                                      TEXT('Antall overførte poster')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 6 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Overføring pågår...'
     A                                  4  2'Antall overførte poster:'
     A            A1OVPO         4Y 0O  4 27DSPATR(UL)
     A                                      TEXT('Antall overførte poster')
     A                                      EDTCDE(Z)
     A          R ASUME
     A*%%TS  SD  19941223  115412  RH          REL-V2R3M0  5738-PW1
     A                                      KEEP
     A                                      ASSUME
     A                                 24 79' '
