     A*%%TS  SD  20111110  100540  BOF620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* Beskrivelse . . : Vise varer som er skaffevare dersom lager
     A*                   endres
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*      180311  NHO  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A*           B1VALG    R        B  7  2REFFLD(FÅVALG)
     A*                                     COLOR(YLW)
     A*                                     VALUES(0 1)
     A*                                     CHKMSGID(FM00010 *LIBL/QMFUMAL)
     A            B1VALG         1S 0H
     A            B1VARE    R        B  7  5REFFLD(VVVARE)
     A            B1TEK1    R        B  7 23REFFLD(VVTEK1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20111110  100540  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Følgende er S-vare på angitt lager'
     A  50                              3  2'Lageret har skaffevarer, kan ikke -
     A                                      registrere videre!'
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  5  5'Varenummer'
     A                                      COLOR(WHT)
     A                                  5 23'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2VARE        15A  H
     A            B2TEK1        35A  H
     A          R B2CMD
     A*%%TS  SD  20051110  104501  SST540S     REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 58)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
