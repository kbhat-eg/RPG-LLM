     A*%%TS  SD  20020221  074253  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Program . . . . : FO11VD
     A* Beskrivelse . . : Registrering av tilbud/ordre, varelinjer
     A*
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*
3.31 A* R3.31 010301 AMD  Dersom det ble lagt inn høye kostpriser, sammen
3.31 A*                   med et stort antall, kunne programmet gå i heng.
3.31 A*                   Totaler i D1BLD er utvidet for å unngå dette.
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R D1BLD
     A*%%TS  SD  20000503  132224  AMD         REL-V4R4M0  5769-PW1
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF21
     A                                      CF23
     A                                      CF24
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      KEEP
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Registrering av varelinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre:'
     A                                      COLOR(WHT)
     A            D1ONUM    R        O  4  9REFFLD(FONUMM)
     A            D1OTYP    R        O  4 16REFFLD(FOOTYP)
     A            D1NAVN    R        O  4 19REFFLD(FONAVN)
     A            D2NAVN    R        O  4 50REFFLD(FONAVN)
     A                                  5  2'Kunde:'
     A                                      COLOR(WHT)
     A            D1KUND    R        O  5  9REFFLD(FOKUND)
     A            D1ADR1    R        O  5 19REFFLD(FOADR1)
     A            D2ADR1    R        O  5 50REFFLD(FOADR1)
     A                                  6  2'L.kun:'
     A                                      COLOR(WHT)
     A            D1LKUN    R        O  6  9REFFLD(FOLKUN)
     A            D1STED    R        O  6 19REFFLD(FOSTED)
     A            D2STED    R        O  6 50REFFLD(FOSTED)
     A                                  7  2'Leveringstype:'
     A                                      COLOR(WHT)
     A            D2LETY    R        O  7 17REFFLD(FOLETY)
     A                                      EDTCDE(Z)
     A            D1BESK    R        O  7 19REFFLD(VYBESK)
     A                                  7 64'Tlf:'
     A            D1TLFN    R        O  7 69REFFLD(RKTLFN *LIBL/RFRF)
     A                                  8 64'Mob:'
     A            D1MOBN    R        O  8 69REFFLD(RKMOBN *LIBL/RFRF)
     A                                  9  2'Valg : 1=Legg til  2=Endre  4=Slet-
     A                                      t  5=Vis'
     A                                      COLOR(BLU)
     A            D1OTXT    R        O  9 50REFFLD(VAOTXT)
     A                                      COLOR(TRQ)
     A                                 10  2'V L Varenr   Varetekst            -
     A                                               Antall'
     A                                      COLOR(WHT)
     A N93                             10 55'Pris     Rabatter    Kode'
     A                                      COLOR(WHT)
     A  93                             10 56'SUM      KOST      DB %'
     A                                      COLOR(WHT)
     A            D1K001         1A  B 11  2
     A  41                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 41)
     A                                      COLOR(YLW)
     A            D1L001         1Y 0O 11  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V001         8A  O 11  6
     A  71                                  DSPATR(RI)
     A  93        D1H001        35A  O 11  6
     A  71                                  DSPATR(RI)
     A N93        D1T001        28A  O 11 15
     A  71                                  DSPATR(RI)
     A  80        D10001         8Y 0O 11 44EDTCDE(4)
     A  71                                  DSPATR(RI)
     A  81        D11001         7Y 1O 11 44EDTCDE(4)
     A  71                                  DSPATR(RI)
     A  82        D12001         7Y 2O 11 44EDTCDE(4)
     A  71                                  DSPATR(RI)
     A  83        D13001         7Y 3O 11 44EDTCDE(4)
     A  71                                  DSPATR(RI)
     A N93        D1S001         8Y 2O 11 53EDTWRD('      ,  ')
     A  71                                  DSPATR(RI)
     A  93        D1U001         7Y 2O 11 54EDTWRD('     ,  ')
     A  71                                  DSPATR(RI)
     A N93        DR1001         5Y 2O 11 63EDTWRD('   ,  ')
     A  71                                  DSPATR(RI)
     A  93        D1G001         7Y 2O 11 64EDTWRD('     ,  ')
     A  71                                  DSPATR(RI)
     A N93        DR2001         5Y 2O 11 70EDTWRD('   ,  ')
     A  71                                  DSPATR(RI)
     A  93        D1F001         5Y 2O 11 73EDTCDE(M)
     A  71                                  DSPATR(RI)
     A N93        D1D001         3A  O 11 77
     A  71                                  DSPATR(RI)
     A            D1K002         1A  B 12  2
     A  42                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 42)
     A                                      COLOR(YLW)
     A            D1L002         1Y 0O 12  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V002         8A  O 12  6
     A  72                                  DSPATR(RI)
     A  93        D1H002        35A  O 12  6
     A  71                                  DSPATR(RI)
     A N93        D1T002        28A  O 12 15
     A  72                                  DSPATR(RI)
     A  80        D10002         8Y 0O 12 44EDTCDE(4)
     A  72                                  DSPATR(RI)
     A  81        D11002         7Y 1O 12 44EDTCDE(4)
     A  72                                  DSPATR(RI)
     A  82        D12002         7Y 2O 12 44EDTCDE(4)
     A  72                                  DSPATR(RI)
     A  83        D13002         7Y 3O 12 44EDTCDE(4)
     A  72                                  DSPATR(RI)
     A N93        D1S002         8Y 2O 12 53EDTWRD('      ,  ')
     A  72                                  DSPATR(RI)
     A  93        D1U002         7Y 2O 12 54EDTWRD('     ,  ')
     A  72                                  DSPATR(RI)
     A N93        DR1002         5Y 2O 12 63EDTWRD('   ,  ')
     A  72                                  DSPATR(RI)
     A  93        D1G002         7Y 2O 12 64EDTWRD('     ,  ')
     A  72                                  DSPATR(RI)
     A N93        DR2002         5Y 2O 12 70EDTWRD('   ,  ')
     A  72                                  DSPATR(RI)
     A  93        D1F002         5Y 2O 12 73EDTCDE(M)
     A  72                                  DSPATR(RI)
     A N93        D1D002         3A  O 12 77
     A  72                                  DSPATR(RI)
     A            D1K003         1A  B 13  2
     A  43                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 43)
     A                                      COLOR(YLW)
     A            D1L003         1Y 0O 13  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V003         8A  O 13  6
     A  73                                  DSPATR(RI)
     A  93        D1H003        35A  O 13  6
     A  71                                  DSPATR(RI)
     A N93        D1T003        28A  O 13 15
     A  73                                  DSPATR(RI)
     A  80        D10003         8Y 0O 13 44EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  81        D11003         7Y 1O 13 44EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  82        D12003         7Y 2O 13 44EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  83        D13003         7Y 3O 13 44EDTCDE(4)
     A  73                                  DSPATR(RI)
     A N93        D1S003         8Y 2O 13 53EDTWRD('      ,  ')
     A  73                                  DSPATR(RI)
     A  93        D1U003         7Y 2O 13 54EDTWRD('     ,  ')
     A  73                                  DSPATR(RI)
     A N93        DR1003         5Y 2O 13 63EDTWRD('   ,  ')
     A  73                                  DSPATR(RI)
     A  93        D1G003         7Y 2O 13 64EDTWRD('     ,  ')
     A  73                                  DSPATR(RI)
     A N93        DR2003         5Y 2O 13 70EDTWRD('   ,  ')
     A  73                                  DSPATR(RI)
     A  93        D1F003         5Y 2O 13 73EDTCDE(M)
     A  73                                  DSPATR(RI)
     A N93        D1D003         3A  O 13 77
     A  73                                  DSPATR(RI)
     A            D1K004         1A  B 14  2
     A  44                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 44)
     A                                      COLOR(YLW)
     A            D1L004         1Y 0O 14  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V004         8A  O 14  6
     A  74                                  DSPATR(RI)
     A  93        D1H004        35A  O 14  6
     A  71                                  DSPATR(RI)
     A N93        D1T004        28A  O 14 15
     A  74                                  DSPATR(RI)
     A  80        D10004         8Y 0O 14 44EDTCDE(4)
     A  74                                  DSPATR(RI)
     A  81        D11004         7Y 1O 14 44EDTCDE(4)
     A  74                                  DSPATR(RI)
     A  82        D12004         7Y 2O 14 44EDTCDE(4)
     A  74                                  DSPATR(RI)
     A  83        D13004         7Y 3O 14 44EDTCDE(4)
     A  74                                  DSPATR(RI)
     A N93        D1S004         8Y 2O 14 53EDTWRD('      ,  ')
     A  74                                  DSPATR(RI)
     A  93        D1U004         7Y 2O 14 54EDTWRD('     ,  ')
     A  74                                  DSPATR(RI)
     A N93        DR1004         5Y 2O 14 63EDTWRD('   ,  ')
     A  74                                  DSPATR(RI)
     A  93        D1G004         7Y 2O 14 64EDTWRD('     ,  ')
     A  74                                  DSPATR(RI)
     A N93        DR2004         5Y 2O 14 70EDTWRD('   ,  ')
     A  74                                  DSPATR(RI)
     A  93        D1F004         5Y 2O 14 73EDTCDE(M)
     A  74                                  DSPATR(RI)
     A N93        D1D004         3A  O 14 77
     A  74                                  DSPATR(RI)
     A            D1K005         1A  B 15  2
     A  45                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 45)
     A                                      COLOR(YLW)
     A            D1L005         1Y 0O 15  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V005         8A  O 15  6
     A  75                                  DSPATR(RI)
     A  93        D1H005        35A  O 15  6
     A  71                                  DSPATR(RI)
     A N93        D1T005        28A  O 15 15
     A  75                                  DSPATR(RI)
     A  80        D10005         8Y 0O 15 44EDTCDE(4)
     A  75                                  DSPATR(RI)
     A  81        D11005         7Y 1O 15 44EDTCDE(4)
     A  75                                  DSPATR(RI)
     A  82        D12005         7Y 2O 15 44EDTCDE(4)
     A  75                                  DSPATR(RI)
     A  83        D13005         7Y 3O 15 44EDTCDE(4)
     A  75                                  DSPATR(RI)
     A N93        D1S005         8Y 2O 15 53EDTWRD('      ,  ')
     A  75                                  DSPATR(RI)
     A  93        D1U005         7Y 2O 15 54EDTWRD('     ,  ')
     A  75                                  DSPATR(RI)
     A N93        DR1005         5Y 2O 15 63EDTWRD('   ,  ')
     A  75                                  DSPATR(RI)
     A  93        D1G005         7Y 2O 15 64EDTWRD('     ,  ')
     A  75                                  DSPATR(RI)
     A N93        DR2005         5Y 2O 15 70EDTWRD('   ,  ')
     A  75                                  DSPATR(RI)
     A  93        D1F005         5Y 2O 15 73EDTCDE(M)
     A  75                                  DSPATR(RI)
     A N93        D1D005         3A  O 15 77
     A  75                                  DSPATR(RI)
     A            D1K006         1A  B 16  2
     A  46                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 46)
     A                                      COLOR(YLW)
     A            D1L006         1Y 0O 16  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V006         8A  O 16  6
     A  76                                  DSPATR(RI)
     A  93        D1H006        35A  O 16  6
     A  71                                  DSPATR(RI)
     A N93        D1T006        28A  O 16 15
     A  76                                  DSPATR(RI)
     A  80        D10006         8Y 0O 16 44EDTCDE(4)
     A  76                                  DSPATR(RI)
     A  81        D11006         7Y 1O 16 44EDTCDE(4)
     A  76                                  DSPATR(RI)
     A  82        D12006         7Y 2O 16 44EDTCDE(4)
     A  76                                  DSPATR(RI)
     A  83        D13006         7Y 3O 16 44EDTCDE(4)
     A  76                                  DSPATR(RI)
     A N93        D1S006         8Y 2O 16 53EDTWRD('      ,  ')
     A  76                                  DSPATR(RI)
     A  93        D1U006         7Y 2O 16 54EDTWRD('     ,  ')
     A  76                                  DSPATR(RI)
     A N93        DR1006         5Y 2O 16 63EDTWRD('   ,  ')
     A  76                                  DSPATR(RI)
     A  93        D1G006         7Y 2O 16 64EDTWRD('     ,  ')
     A  76                                  DSPATR(RI)
     A N93        DR2006         5Y 2O 16 70EDTWRD('   ,  ')
     A  76                                  DSPATR(RI)
     A  93        D1F006         5Y 2O 16 73EDTCDE(M)
     A  76                                  DSPATR(RI)
     A N93        D1D006         3A  O 16 77
     A  76                                  DSPATR(RI)
     A            D1K007         1A  B 17  2
     A  47                                  ERRMSGID(FM09990 *LIBL/QMFOFAX 47)
     A                                      COLOR(YLW)
     A            D1L007         1Y 0O 17  4EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A N93        D1V007         8A  O 17  6
     A  77                                  DSPATR(RI)
     A  93        D1H007        35A  O 17  6
     A  71                                  DSPATR(RI)
     A N93        D1T007        28A  O 17 15
     A  77                                  DSPATR(RI)
     A  80        D10007         8Y 0O 17 44EDTCDE(4)
     A  77                                  DSPATR(RI)
     A  81        D11007         7Y 1O 17 44EDTCDE(4)
     A  77                                  DSPATR(RI)
     A  82        D12007         7Y 2O 17 44EDTCDE(4)
     A  77                                  DSPATR(RI)
     A  83        D13007         7Y 3O 17 44EDTCDE(4)
     A  77                                  DSPATR(RI)
     A N93        D1S007         8Y 2O 17 53EDTWRD('      ,  ')
     A  77                                  DSPATR(RI)
     A  93        D1U007         7Y 2O 17 54EDTWRD('     ,  ')
     A  77                                  DSPATR(RI)
     A N93        DR1007         5Y 2O 17 63EDTWRD('   ,  ')
     A  77                                  DSPATR(RI)
     A  93        D1G007         7Y 2O 17 64EDTWRD('     ,  ')
     A  77                                  DSPATR(RI)
     A N93        DR2007         5Y 2O 17 70EDTWRD('   ,  ')
     A  77                                  DSPATR(RI)
     A  93        D1F007         5Y 2O 17 73EDTCDE(M)
     A  77                                  DSPATR(RI)
     A N93        D1D007         3A  O 17 77
     A  77                                  DSPATR(RI)
     A                                 18  1'LT L Varenr                       -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
     A                                      COLOR(TRQ)
     A            D1LTYP    R        B 19  1REFFLD(FDLTYP)
     A  36                                  ERRMSGID(FMA0230 *LIBL/QMFOFAX 36)
     A  37                                  ERRMSGID(FMA0240 *LIBL/QMFOFAX 37)
     A                                      COLOR(YLW)
     A            D1LETY    R        B 19  4REFFLD(FDLETY)
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  87        D1VAR0         8A  B 19  6
     A  38                                  ERRMSGID(FMD0140 *LIBL/QMFOFAX 38)
     A  39                                  ERRMSGID(FMV0010 *LIBL/QMFOFAX 39)
     A  21                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A  88        D1EANN        13A  B 19  6
     A  38                                  ERRMSGID(FMD0140 *LIBL/QMFOFAX 38)
     A  39                                  ERRMSGID(FMV0010 *LIBL/QMFOFAX 39)
     A  21                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A  89        D1E128        26A  B 19  6
     A  38                                  ERRMSGID(FMD0140 *LIBL/QMFOFAX 38)
     A  39                                  ERRMSGID(FMV0010 *LIBL/QMFOFAX 39)
     A  50                                  ERRMSGID(FM00620 *LIBL/QMFOFAK 50)
     A  21                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A  87        D1VAR1         7A  B 19 14DSPATR(ND)
     A  87                                  DSPATR(PR)
3.31 A                                 19 45'Sum'
     A                                      COLOR(WHT)
3.31 A            D1NETT        11Y 2O 19 50EDTWRD('         ,  -')
3.31 A  93        D1DEKN        11Y 2O 19 67EDTWRD('         ,  -')
3.31 A  93        D1KOST        11Y 2O 20 58EDTWRD('         ,  -')
3.31 A  93                             22 45'Dekningsgrad :'
     A                                      COLOR(WHT)
     A  93        D1GRAD         5Y 2O 22 64EDTWRD('   ,  -')
     A            B1CMD1        78A  O 23  2COLOR(BLU)
     A            B1CMD2        78A  O 24  2COLOR(BLU)
     A*
     A          R C1WIN
     A*%%TS  SD  20000323  155251  AMD         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 11 70)
     A                                      CF02
     A                                      CF03
     A                                      CF08
     A                                      KEEP
     A                                      OVERLAY
     A                                  1  2'Behandling av varenummer :'
     A                                      COLOR(WHT)
     A            C1VARE    R        O  1 29REFFLD(FDVARE)
     A                                      COLOR(WHT)
     A                                  2  2'-----------------------------------
     A                                      ----------------------------------'
     A                                      COLOR(WHT)
     A                                  3  2'Antall'
     A                                  3 17'Enh'
     A                                  3 23'Salgspris'
     A                                  3 36'Kostpris'
     A                                  3 46'Rabatt'
     A                                  3 61'Kode'
     A            C1ANTA    R        B  4  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A            C1ENH1    R        B  4 17REFFLD(FDENH1)
     A  31                                  ERRMSGID(FM00080 *LIBL/QMFOFAX 31)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            C1SAPR         9Y 2B  4 22EDTWRD('     0 ,  ')
     A  32                                  ERRMSGID(FM00090 *LIBL/QMFOFAX 32)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1KOPR    R        B  4 34REFFLD(FDKOPR)
     A                                      EDTWRD('       ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A N49                                  DSPATR(ND)
     A N49                                  DSPATR(PR)
     A            C1RAB1    R        B  4 46REFFLD(FDRAB1)
     A                                      EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1RAB2    R        B  4 54REFFLD(FDRAB2)
     A                                      EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C1KPRI    R        B  4 63REFFLD(FDKPRI)
     A                                      COLOR(YLW)
     A  51                                  ERRMSGID(FMD0700 *LIBL/QMFOFAX 51)
     A            C1ANT1    R        O  6  2REFFLD(FDANTA)
     A                                      EDTCDE(M)
     A            C1ENH4    R        O  6 17REFFLD(FDENH1)
     A            C1TEK1    R        B  6 22REFFLD(FDTEK1)
     A  33                                  ERRMSGID(FM00100 *LIBL/QMFOFAX 33)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                  6 63'N=Netto'
     A            C1ANT2    R        O  7  2REFFLD(FDANTA)
     A                                      EDTCDE(M)
     A            C1ENH5    R        O  7 17REFFLD(FDENH1)
     A                                  7 63'B=Bunn'
     A            C1ANT3    R        O  8  2REFFLD(FDANTA)
     A                                      EDTCDE(M)
     A            C1ENH6    R        O  8 17REFFLD(FDENH1)
     A                                  8 63'F=Fast'
     A                                 10  2'F2=Hjelp til kostpris   F3=Gå tilb-
     A                                      ake   F8=Produktinfo.'
     A                                      COLOR(BLU)
     A          R E0BLD
     A*%%TS  SD  19990528  130916  AMD         REL-V4R2M0  5769-PW1
     A                                      CF03
     A                                      SLNO(18)
     A                                      CLRL(07)
     A                                  1  1'LT Varenummer         Antall     E-
     A                                      nh  Tekst                          -
     A                                                '
     A                                      DSPATR(UL)
     A                                      COLOR(TRQ)
     A            E0LTYP    R        O  2  1REFFLD(FDLTYP)
     A            E0VARE    R        O  2  4REFFLD(FDVARE)
     A            E0ANT1    R        B  2 20REFFLD(FDANTA)
     A  31                                  ERRMSGID(FMA0230 *LIBL/QMFOFAX 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      CHECK(ER)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            E0ENH1    R        O  2 34REFFLD(FDENH1)
     A            E0TEK1    R        O  2 39REFFLD(FDTEK1)
     A            E0ANT2    R        B  3 20REFFLD(FDANTA)
     A                                      CHECK(ER)
     A                                      CHECK(RB)
     A  52                                  DSPATR(ND)
     A  52                                  DSPATR(PR)
     A            E0ENH2    R        O  3 34REFFLD(FDENH1)
     A  52                                  DSPATR(ND)
     A            E0ANT3    R        B  4 20REFFLD(FDANTA)
     A                                      CHECK(ER)
     A                                      CHECK(RB)
     A  53                                  DSPATR(ND)
     A  53                                  DSPATR(PR)
     A            E0ENH3    R        O  4 34REFFLD(FDENH1)
     A  53                                  DSPATR(ND)
     A                                  7  1'     F3=Gå tilbake                -
     A                                                                         -
     A                                           '
     A                                      COLOR(BLU)
     A          R E1BLD
     A*%%TS  SD  20000323  155851  AMD         REL-V4R4M0  5769-PW1
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      SLNO(18)
     A                                      CLRL(07)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  1'LT L Varenummer           Antall  -
     A                                       Enh     Pris    N  Rabatt Faktor  -
     A                                       Kostpris '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            E1LTYP    R        B  2  1REFFLD(FDLTYP)
     A  31                                  ERRMSGID(FMA0230 *LIBL/QMFOFAX 31)
     A  32                                  ERRMSGID(FMA0240 *LIBL/QMFOFAX 32)
     A                                      COLOR(YLW)
     A            E1VARE    R        B  2  6REFFLD(FDVARE)
     A  33                                  ERRMSGID(FMD0140 *LIBL/QMFOFAX 33)
     A  21                                  DSPATR(PC)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A            E1LETY    R        B  2  4REFFLD(FDLETY)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            E1ANTA    R        B  2 23REFFLD(FDANTA)
     A  34                                  ERRMSGID(FMD0150 *LIBL/QMFOFAX 34)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A            E1ENH1    R        B  2 36REFFLD(FDENH1)
     A  35                                  ERRMSGID(FMD0160 *LIBL/QMFOFAX 35)
     A  23                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            E1SAPR         9Y 2B  2 40EDTWRD('     0 ,  ')
     A  36                                  ERRMSGID(FMD0170 *LIBL/QMFOFAX 36)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            E1KPRI    R        B  2 52REFFLD(FDKPRI)
     A                                      COLOR(YLW)
     A  51                                  ERRMSGID(FMD0700 *LIBL/QMFOFAX 51)
     A            E1RAB1    R        B  2 55REFFLD(FDRAB1)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A                                      COLOR(YLW)
     A            E1KOPR    R        B  2 69REFFLD(FDKOPR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('       ,  ')
     A                                      COLOR(YLW)
     A N49                                  DSPATR(ND)
     A N49                                  DSPATR(PR)
     A            E1TEK1    R        B  3  4REFFLD(FDTEK1)
     A  27                                  DSPATR(PC)
     A  37                                  ERRMSGID(FMD0180 *LIBL/QMFOFAX 37)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A            E1SPNX         9Y 2B  3 40EDTWRD('       ,  ')
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            E1RAB2    R        B  3 55REFFLD(FDRAB2)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A                                      COLOR(YLW)
     A            E1PASL    R        B  3 62REFFLD(FDPASL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A                                      COLOR(YLW)
     A                                  3 71'Lev.k'
     A            E1LASS    R        B  3 77REFFLD(FDLASS)
     A                                      COLOR(YLW)
     A                                  4  1'Lev.dato:'
     A            E1LDAT         6Y 0B  4 11COLOR(YLW)
     A  38                                  ERRMSGID(FMD0190 *LIBL/QMFOFAX 38)
     A  39                                  ERRMSGID(FMD0200 *LIBL/QMFOFAX 39)
     A                                      EDTCDE(Y)
     A                                  4 30'Best.nr..:'
     A  25                                  DSPATR(ND)
     A            E1BENR    R        B  4 44REFFLD(FDBENR)
     A  25                                  DSPATR(ND)
     A  25                                  DSPATR(PR)
     A                                  5  1'Prosjekt:'
     A            E1PROJ    R        B  5 13REFFLD(FDPROJ)
     A  40                                  ERRMSGID(FRH0120 *LIBL/QMFOFAX 40)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A                                  5 20'Akt.'
     A            E1AKTI    R        B  5 25REFFLD(FDAKTI)
     A  41                                  ERRMSGID(FMA0220 *LIBL/QMFOFAX 41)
     A                                      COLOR(YLW)
     A                                  5 30'Leverandør'
     A  26                                  DSPATR(ND)
     A            E1LDOR    R        B  5 44REFFLD(FDLDOR)
     A  42                                  ERRMSGID(FRL0020 *LIBL/QMFOFAX 42)
     A  26                                  DSPATR(ND)
     A  26                                  DSPATR(PR)
     A                                  5 52'Lagerbeholdn.:'
     A  28                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            E1BEHL    R        O  5 67REFFLD(VLBEHL)
     A  28                                  DSPATR(ND)
     A                                  6 35'F8=Prod.info.   '
     A                                      COLOR(BLU)
     A                                  6 52'Ordre/Pakks .:'
     A  28                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            E1PAKK    R        O  6 67REFFLD(VLBEHL)
     A  28                                  DSPATR(ND)
     A                                  7  1'F2=Kostpris-hjelp  F3=Gå tilbake  -
     A                                      F9=Vare/Lager   '
     A                                      COLOR(BLU)
     A                                  7 52'Disp.beholdn.:'
     A  28                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            E1DISP    R        O  7 67REFFLD(VLBEHL)
     A  28                                  DSPATR(ND)
     A          R E2BLD
     A*%%TS  SD  19991105  142357  AMD         REL-V4R4M0  5769-PW1
     A                                      CF03
     A                                      SLNO(18)
     A                                      CLRL(07)
     A                                  1  1' Tekstlinje                       -
     A                                                                  LT  L.k-
     A                                      . Lev.dato'
     A                                      DSPATR(UL)
     A                                      COLOR(TRQ)
     A            E2LTYP    R    2A  B  2 63REFFLD(FDLTYP)
     A  31                                  ERRMSGID(FMA0230 *LIBL/QMFOFAX 31)
     A  32                                  ERRMSGID(FMA0250 *LIBL/QMFOFAX 32)
     A                                      COLOR(YLW)
     A            E2LASS    R    2D  B  2 67REFFLD(FDLASS)
     A                                      COLOR(YLW)
     A            E2LDAT         6Y 0B  2 72COLOR(YLW)
     A  33                                  ERRMSGID(FMD0190 *LIBL/QMFOFAX 33)
     A  34                                  ERRMSGID(FMD0200 *LIBL/QMFOFAX 34)
     A                                      EDTCDE(Y)
     A            E2TEK1        70A  B  4  2CHECK(LC)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                  7  1'     F3=Gå tilbake                -
     A                                                                         -
     A                                           '
     A                                      COLOR(BLU)
     A*
     A          R E5BLD
     A*%%TS  SD  19990429  150210  AMD         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 20 50)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Vise informasjon om varelinjen'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  3  2'Varenr. . .'
     A            E5VARE    R        O  3 14REFFLD(FDVARE)
     A                                  4  2'Tekst . . .'
     A            E5TEK1    R        O  4 14REFFLD(FDTEK1)
     A            E5TEK2    R        O  5 14REFFLD(FDTEK2)
     A                                  6  2'S-pris. . .'
     A            E5SAPR         9Y 2O  6 14EDTWRD('     0 ,  ')
     A            E5OSPR         9Y 2O  6 34EDTWRD('     0 ,  ')
     A                                      DSPATR(RI)
     A                                  7  2'K-pris. . .'
     A            E5KOPR    R        O  7 14REFFLD(FDKOPR)
     A                                      EDTWRD('     0 ,  ')
     A            E5OKPR    R        O  7 34REFFLD(FDKOPR)
     A                                      DSPATR(RI)
     A                                      EDTWRD('     0 ,  ')
     A                                  8  2'%-rab1. . .'
     A            E5RAB1    R        O  8 14REFFLD(FDRAB1)
     A                                  9  2'%-rab2. . .'
     A            E5RAB2    R        O  9 14REFFLD(FDRAB2)
     A                                 11  2'O/H/U '
     A                                 11 10'.'
     A                                 11 12'.'
     A            E5OGRP    R        O 11 14REFFLD(FÅOGRP)
     A            E5HGRP    R        O 11 18REFFLD(FÅHGRP)
     A            E5UGRP    R        O 11 23REFFLD(FÅUGRP)
     A                                 12  2'Lev.dør . .'
     A            E5LDOR    R        O 12 14REFFLD(VVLDOR)
     A                                 13  2'Lev.varenr.'
     A            E5LVAR    R        O 13 14REFFLD(FDLVAR)
     A                                 15  2'Antall. . . . . . . .'
     A            E5ANTA    R        O 15 24REFFLD(FDANTA)
     A            E5ENH1    R        O 15 39REFFLD(FDENH1)
     A                                 16  2'Antall i lager. . . .'
     A            E5BEHL    R        O 16 24REFFLD(VLBEHL)
     A                                 17  2'Antall reservert. . .'
     A            E5PAKK    R        O 17 24REFFLD(VLPAKK)
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            T2VALG    R        B 19 43REFFLD(FÅVALA)
     A*
     A          R K1BLD
     A*%%TS  SD  19960819  140326  AMD         REL-V3R6M0  5716-PW1
     A                                      WINDOW(*DFT 12 33)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Hjelpeberegninger for kostpris'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Innkjøpspris . :'
     A            K1IPNY    R        B  4 19REFFLD(FDKOPR)
     A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  4 31'Kr'
     A                                  5  2' - Rabatt  . . :'
     A            K1RABB         5Y 0B  5 19EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 31'%'
     A                                  6  2' - Frakt   . . :'
     A            K1FRAK         5Y 0B  6 19EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  6 31'%'
     A                                  7  2' + Påslag  . . :'
     A            K1PASL         5Y 0B  7 19EDTWRD('   ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7 31'%'
     A                                  9  2' = Kostpris. . :'
     A            K1KOPR    R        B  9 19REFFLD(FDKOPR)
     A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9 31'Kr'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R F1BLD
     A*%%TS  SD  19950920  151850  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(*DFT 8 35)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Utkjøring/Utplukk'
     A                                      COLOR(WHT)
     A                                  4  2'Velg Behandling'
     A            F1SVAR    R        B  4 18REFFLD(FÅSVAR)
     A  31                                  ERRMSGID(FMO0050 *LIBL/QMFOFAX 31)
     A                                      COLOR(YLW)
     A                                  4 22'0=Oppdatering'
     A                                  5 22'1=Utskrift'
     A                                  6 22'2=Utplukk'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R G1BLD
     A*%%TS  SD  20020221  074253  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 40)
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'KREDITTGRENSE ER OVERSKREDET'
     A                                      COLOR(WHT)
     A                                  4  2'Skal ordren legges på venting'
     A            G1SVAR    R        B  4 32REFFLD(FÅVALA)
     A                                      COLOR(YLW)
     A                                  4 35'1=Ja'
     A                                  6  2'Ordren er overskredet med'
     A            G1TOTL    R        O  6 28REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 17'F7=Slett ordren'
     A                                      COLOR(BLU)
     A                                  9 34'F8=OK'
     A                                      COLOR(BLU)
     A          R G4BLD
     A*%%TS  SD  19950920  154156  ASP         REL-V2R3M0  5738-PW1
     A                                      CF03
     A                                      PUTOVR
     A                                      WINDOW(*DFT 11 32)
     A                                  1  2'Sletter ordrenummer :'
     A                                      COLOR(WHT)
     A            G4NUMM    R        O  1 24REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            G4NAVN    R        O  2  2REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  4  2'For å slette, Trykk ENTER'
     A                                  6  2'Kommentar til slettingen'
     A            G4KOM1    R        B  7  2REFFLD(FYKOM1)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(FMY0200 *LIBL/QMFOFAX 31)
     A            G4KOM2    R        B  8  2REFFLD(FYKOM2)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(FM00500 *LIBL/QMFOFAX 32)
     A                                 10  2'F3=Annuller'
     A                                      COLOR(BLU)
     A          R O1BLD
     A*%%TS  SD  19950920  151850  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(*DFT 9 41)
     A                                      CF03
     A                                      CF08
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'ORDRETOTAL ER UNDER GRENSEN :'
     A                                      COLOR(WHT)
     A            O1RGRN    R        O  2 32REFFLD(FAAGRN)
     A                                      COLOR(WHT)
     A                                  4  2'Ordreverdien er på kun kr'
     A            O1TOTL    R        O  4 29REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                  6  2'Skal rabatten nullstilles'
     A            O1SVAR    R        B  6 28REFFLD(FÅVALA)
     A                                      COLOR(YLW)
     A                                  6 35'1=Ja'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R T1BLD
     A*%%TS  SD  19950920  151850  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(*DFT 6 18)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Foran/Etter'
     A            T1KODE    R        B  2 16REFFLD(FÅVALA)
     A                                      VALUES('E' 'F')
     A                                      CHKMSGID(FM00150 *LIBL/QMFOFAX)
     A                                      COLOR(YLW)
     A                                  3  2'Linjetype'
     A            T1LTYP    R        B  3 16REFFLD(FDLTYP)
     A                                      COLOR(YLW)
     A                                  5  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
