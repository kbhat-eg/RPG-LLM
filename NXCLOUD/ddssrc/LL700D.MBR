     A*%%TS  SD  20030627  100400  ASK         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL700D
     A* Beskrivelse . . : Innkjøpsplanlegging
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  170316  bsa  Justeringer basert på ny GUI og regler
7.02 A* 6.30  240118 BSA  Legger inn info om erstatningsvare
7.03 A* 6.30  301018 BSA  Endrer ledeteksten på bestillingspunkt og mengde
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20030627  100400  ASK         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA04
7.02 A                                      CA06
     A                                      CA12
     A                                      CA13
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innkjøpsplanlegging'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL700A1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varenr+'
     A            A1VARE    R   -7   B  4 10REFFLD(FFRFR/VVVARE *LIBL/FFRF)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(LM00180 *LIBL/QMFLAGR 31)
     A                                      CHECK(RZ)
     A            A1TEK1    R        O  4 19REFFLD(FFRFR/VVTEK1 *LIBL/FFRF)
7.01 A                                  4 55'(o)'
     A                                  4 59'Lagerenhet   :'
     A            A1ENHL    R        O  4 74REFFLD(FFRFR/VVENHL *LIBL/FFRF)
7.xx A                                  5  2'Lager +'
     A            A1LAGE    R        B  5 10REFFLD(FFRFR/VLLAGE *LIBL/FFRF)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 32)
     A            A1LTEK        20A  O  5 19
7.01 A                                  5 55'(o)'
     A                                  5 59'Innkjøpsenhet:'
     A            A1ENHI    R        O  5 74REFFLD(FFRFR/VVENHL *LIBL/FFRF)
7.02 A                                  6  2'Erstatter vare:'
7.02 A                                      COLOR(RED)
7.02 A            A1EVAR    R   -7   O  6 18REFFLD(FFRFR/VVVARE *LIBL/FFRF)
7.02 A                                      COLOR(RED)
7.02 A            A1ETEK    R        O  6 27REFFLD(FFRFR/VVTEK1 *LIBL/FFRF)
7.02 A                                      COLOR(RED)
     A                                  7  2'Lagerbeholdning:'
     A            A1BEHL    R        O  7 19REFFLD(FFRFR/VLBEHL *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   -')
     A  41                                  COLOR(RED)
7.02 A            A1EBEH    R        O  7 33REFFLD(FFRFR/VLBEHL *LIBL/FFRF)
7.02 A                                      EDTWRD('      0 ,   -')
7.02 A                                      COLOR(RED)
7.02 A                                  7 47'Minimumslager    :'
7.02 A            A1MINI    R        O  7 66REFFLD(FFRFR/VLMINI *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   ')
7.02 A                                  8 47'Maksimumslager   :'
7.02 A            A1MAKS    R        O  8 66REFFLD(FFRFR/VLMAKS *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   ')
     A                                  9  2'For utlevering :'
     A            A1PAKK    R        O  9 19REFFLD(FFRFR/VLPAKK *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   -')
7.02 A            A1EPAK    R        O  9 33REFFLD(FFRFR/VLPAKK *LIBL/FFRF)
7.02 A                                      EDTWRD('      0 ,   -')
7.02 A                                      COLOR(RED)
7.02 A                                  9 47'Sikkerhetslager  :'
7.02 A            A1SIKL    R        O  9 66REFFLD(FFRFR/VLSIKL *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   ')
     A                                 10  2'I ordre        :'
     A            A1OORD    R        O 10 19REFFLD(FFRFR/VLOORD *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   -')
7.02 A            A1EORD    R        O 10 33REFFLD(FFRFR/VLOORD *LIBL/FFRF)
7.02 A                                      EDTWRD('      0 ,   -')
7.02 A                                      COLOR(RED)
     A                                 11  2'I bestilling   :'
     A            A1BBES    R        O 11 19REFFLD(FFRFR/VLBBES *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   -')
7.02 A            A1EBES    R        O 11 33REFFLD(FFRFR/VLBBES *LIBL/FFRF)
7.02 A                                      EDTWRD('      0 ,   -')
7.02 A                                      COLOR(RED)
7.03 A**                               11 47'Bestillingspunkt :'
7.03 A**                               11 47'Hyllemin         :'
7.03 A            A1BPTX        18     11 47
7.02 A            A1BPUN    R        O 11 66REFFLD(FFRFR/VLBPUN *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   ')
     A                                 12  2'-----------------------------'
7.03 A**                               12 47'Bestillingsmengde:'
7.03 A**                               12 47'Hyllemax         :'
7.03 A            A1BMTX        18     12 47
7.02 A            A1BMEN    R        O 12 66REFFLD(FFRFR/VLBMEN *LIBL/FFRF)
     A                                      EDTWRD('      0 ,   ')
     A                                 13  2'Disp. lager    :'
     A            A1DISP        11Y 3O 13 19EDTWRD('      0 ,   -')
     A  42                                  COLOR(RED)
7.02 A            A1EDIS        11Y 3O 13 33EDTWRD('      0 ,   -')
7.02 A                                      COLOR(RED)
7.02 A                                 13 47'Leveringstid     :'
7.02 A            A1LTID    R        O 13 66REFFLD(FFRFR/VLLTID *LIBL/FFRF)
     A                                      EDTCDE(Z)
     A                                 15  2'Periode:'
     A            A1HE01         8A  O 15 17
     A            A1HE02         8A  O 15 28
     A            A1HE03         8A  O 15 39
     A            A1HE04         8A  O 15 50
     A            A1HE05         8A  O 15 61
     A            A1HE06         8A  O 15 72
     A                                 16  2'Salg:'
     A            A1SA01         8Y 1O 16 16EDTWRD('     0 , -')
     A            A1SA02         8Y 1O 16 27EDTWRD('     0 , -')
     A            A1SA03         8Y 1O 16 38EDTWRD('     0 , -')
     A            A1SA04         8Y 1O 16 49EDTWRD('     0 , -')
     A            A1SA05         8Y 1O 16 60EDTWRD('     0 , -')
     A            A1SA06         8Y 1O 16 71EDTWRD('     0 , -')
     A                                 17  2'Kjøp:'
     A            A1KJ01         8Y 1O 17 16EDTWRD('     0 , -')
     A            A1KJ02         8Y 1O 17 27EDTWRD('     0 , -')
     A            A1KJ03         8Y 1O 17 38EDTWRD('     0 , -')
     A            A1KJ04         8Y 1O 17 49EDTWRD('     0 , -')
     A            A1KJ05         8Y 1O 17 60EDTWRD('     0 , -')
     A            A1KJ06         8Y 1O 17 71EDTWRD('     0 , -')
     A                                 19  2'P:'
     A            A1HE07         8A  O 19  6
     A            A1HE08         8A  O 19 17
     A            A1HE09         8A  O 19 28
     A            A1HE10         8A  O 19 39
     A            A1HE11         8A  O 19 50
     A            A1HE12         8A  O 19 61
     A            A1HE13         8A  O 19 72
     A                                 20  2'S:'
     A            A1SA07         8Y 1O 20  5EDTWRD('     0 , -')
     A            A1SA08         8Y 1O 20 16EDTWRD('     0 , -')
     A            A1SA09         8Y 1O 20 27EDTWRD('     0 , -')
     A            A1SA10         8Y 1O 20 38EDTWRD('     0 , -')
     A            A1SA11         8Y 1O 20 49EDTWRD('     0 , -')
     A            A1SA12         8Y 1O 20 60EDTWRD('     0 , -')
     A            A1SA13         8Y 1O 20 71EDTWRD('     0 , -')
     A                                 21  2'K:'
     A            A1KJ07         8Y 1O 21  5EDTWRD('     0 , -')
     A            A1KJ08         8Y 1O 21 16EDTWRD('     0 , -')
     A            A1KJ09         8Y 1O 21 27EDTWRD('     0 , -')
     A            A1KJ10         8Y 1O 21 38EDTWRD('     0 , -')
     A            A1KJ11         8Y 1O 21 49EDTWRD('     0 , -')
     A            A1KJ12         8Y 1O 21 60EDTWRD('     0 , -')
     A            A1KJ13         8Y 1O 21 71EDTWRD('     0 , -')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
7.02 A                                 23 32'F6=Innkj.planl gml vare'
7.02 A                                      COLOR(BLU)
     A                                 23 68'F13=Scanning'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  19990428  224045  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      PUTOVR
     A                                      CA03
     A                                      CA12
     A                                  2  2' Scanning '
     A                                      DSPATR(RI)
     A                                  4  2'GTIN-nummer .'
     A            E1EANN        14A  B  4 16COLOR(YLW)
     A  31                                  ERRMSGID(VMX0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A                                      CHECK(ER)
     A                                      OVRDTA
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
