     A*%%TS  SD  20110426  153900  AMD620      REL-V7R1M0  5770-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31 A*       110515 BOF  Test på mobil, styres fra RAKFAX (ra11pf)
7.xx A*       160216 bsa  Justeringer basert på ny GUI og regler
7.fb A*       090720 bsa  Forskuddsbetaling
7.01 A*       260320 ask  Oppslag mot 1881
7.03 A*       101220 ask  Oppslag mot Bisnode
8.01 A*       291121 ask  Endring/opprydding i skjermbilde
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20110426  153900  AMD620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA05
7.fb A  50                                  CA06
     A                                      CF07
7.01 A                                      CA08
7.03 A                                      CA09
7.fb A  51                                  CF10
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      OVERLAY
     A                                      PUTOVR
7.fb A                                      WINDOW(&WACTLI &WACTPO 21 70)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Oppretting av ny kunde '
     A                                      DSPATR(RI)
     A                                  2 33'Alfa.:'
     A                                  3  2'Kunde. . . . . . .'
     A            C2KUND    R        O  3 21REFFLD(RKKUND)
     A  30                                  ERRMSGID(RMK3900 *LIBL/QMFOKON 30)
     A                                      OVRDTA
     A            C2ALFA    R        B  3 33REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4  2'Navn . . . . . . .'
     A            C2NAVN    R        B  4 21REFFLD(RKNAVN)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(FRK0040 *LIBL/QMFOFAK 31)
     A                                      DSPATR(PC)
     A                                      OVRDTA
7.03 A                                  5  2'Fornavn. . . . . .'
7.03 A            C2FNAV    R        B  5 21REFFLD(RKNAVN)
7.03 A                                      CHECK(LC)
7.03 A                                      COLOR(YLW)
7.03 A                                      OVRDTA
     A                                  6  2'Adresse. . . . . .'
     A            C2GATE    R        B  6 21REFFLD(RKGATE)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  65 81                               ERRMSGID(RMK0530 *LIBL/QMFOKON 81)
     A                                  7  2'Adresse2 . . . . .'
     A            C2ADR2    R        B  7 21REFFLD(RKADR2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  8  2'Postnummer . . . .'
     A            C2PONR         4Y 0B  8 21COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A  32                                  ERRMSGID(FRK0170 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FRK0180 *LIBL/QMFOFAK 33)
     A  66 81                               ERRMSGID(RMK0540 *LIBL/QMFOKON 81)
     A                                  9  2'Postadresse. . . .'
     A            C2STED    R        B  9 21REFFLD(RKSTED)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Telefon. . . . . .'
     A            C2TLFN    R        B 10 21REFFLD(RKTLFN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  68 81                               ERRMSGID(RMK0550 *LIBL/QMFOKON 81)
     A                                 10 34'Mob:'
     A            C2MOBN    R        B 10 40REFFLD(RKMOBN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  69 81                               ERRMSGID(RMK0590 *LIBL/QMFOKON 81)
8.01 A*                                11  2'Bankgiro . . . . .'
8.01 A*           C2BGIR    R        B 11 21REFFLD(RKBGIR)
8.01 A*                                     DSPATR(UL)
8.01 A*                                     COLOR(YLW)
8.01 A*                                     CHECK(RB)
8.01 A*                                     OVRDTA
8.01 A*                                11 34'Fax:'
8.01 A*           C2TLFX    R        B 11 40REFFLD(RKTLFX)
8.01 A*                                     COLOR(YLW)
8.01 A*                                     OVRDTA
     A                                 11  2'Organisasjonsnr. .'
     A            C2FTNR    R        B 11 21REFFLD(RKFTNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  49 81                               ERRMSGID(RMK0500 *LIBL/QMFOKON 81)
     A                                 11 34'Kred:'
     A            C2KRES    R        B 11 40REFFLD(RKKRES)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
8.01 A*                                11 42'(Kred ,1,2,3)'
8.01 A            C2KGRE         8Y 0O 11 42OVRDTA
8.01 A                                      EDTCDE(Z)
7.xx A                                 12  2'Avdeling . . . . +'
     A            C2AVDE    R        B 12 21REFFLD(RKAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A  36                                  ERRMSGID(FRK0150 *LIBL/QMFOFAK 36)
     A                                      OVRDTA
     A            C2GTXT    R        O 12 26REFFLD(RAGTXT)
     A                                      OVRDTA
7.xx A                                 13  2'Distrikt . . . . +'
     A            C2DIST    R        B 13 21REFFLD(RKDIST)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A  34                                  ERRMSGID(FRK0130 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
6.30 A            C2HTXT        20A  O 13 26OVRDTA
7.xx A                                 14  2'Kundekategori. . +'
     A            C2KATG    R        B 14 21REFFLD(RKKATG)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A  35                                  ERRMSGID(FRK0140 *LIBL/QMFOFAK 35)
     A  71 81                               ERRMSGID(RMK0580 *LIBL/QMFOKON 81)
6.30 A            C2KTXT        20A  O 14 26OVRDTA
     A                                 15  2'E-mail-adresse . .'
     A            C2EMAL    R        B 16  2REFFLD(RKEMAL)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  70 81                               ERRMSGID(RMK0510 *LIBL/QMFOKON 81)
8.01 A*                                18  2'Mobiloperatør. . +'
8.01 A*           C2MOBO    R        B 18 21REFFLD(RFRFR/RECKOD *LIBL/RFRF)
8.01 A*                                     DSPATR(UL)
8.01 A*                                     COLOR(YLW)
8.01 A*                                     OVRDTA
8.01 A*           C2MTXT    R   29   O 18 24REFFLD(RECTXT)
8.01 A*                                     OVRDTA
     A                                 19  2'F3=Gå tilbake   F4=Spørring   F5=B-
     A                                      esøksadresse'
     A                                      COLOR(BLU)
7.fb A**                               20  2'F6=Kort  F7=Kontaktpersoner'
7.fb A                                 20  2'F6=Kort'
7.fb A N50                                  DSPATR(ND)
7.01 A                                      COLOR(BLU)
7.fb A                                 20 11'F7=Kontaktpersoner'
7.fb A                                      COLOR(BLU)
7.01 A  61                             20 31'F8=Søk 1881'
7.01 A                                      COLOR(BLU)
7.01 A  62                             20 44'F9=Bisnode'
7.01 A                                      COLOR(BLU)
7.fb A                                 20 56'F10=Spes.avt'
7.fb A N51                                  DSPATR(ND)
7.fb A                                      COLOR(BLU)
     A*
     A          R E2WIN
     A*%%TS  SD  20060205  191338  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 56)
     A                                      PUTOVR
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Besøksadresse '
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr.. . .'
     A            E2KUND    R        O  4 16REFFLD(RKKUND)
     A                                  5  2'Navn. . . . .'
     A            E2NAVN    R        O  5 16REFFLD(RKNAVN)
     A                                  7  2'Adresse . . .'
     A            E2BGAT    R        B  7 16REFFLD(RKBGAT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  8  2'Adresse 2 . .'
     A            E2BADR    R        B  8 16REFFLD(RKBADR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'Poststed. . .'
     A            E2BPNR    R        B  9 16REFFLD(RKBPNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            E2BSTE    R   25   O  9 21REFFLD(RKSTED)
     A                                      TEXT('Poststed')
     A                                 11  2'Web . . . . .'
     A            E2WEBA    R        B 11 16REFFLD(RKWEBA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R F2WIN
     A*%%TS  SD  20051119  203545  KOB550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 10 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  1'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  4  1'Utskrift av. . .:'
     A            F2VALG         1A  B  4 19TEXT('Bare åpne poster')
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES(' ' '1' '2' '3')
     A                                  4 22'1=Kundeinformasjon'
     A                                  7  1'Sendes som mail :'
     A            F2MAIL         1A  B  7 19CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES('0' '1' ' ')
     A                                  7 22'1=Ja, 0=Nei'
     A                                  9  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.01 A*
7.01 A*  Bilde G2 - vindu for oppslag mot 1881
7.01 A*
7.01 A          R G2WIN
7.01 A*%%TS  SD  20200327  131714  ASK700      REL-V7R3M0  5770-WDS
7.01 A                                      CA03
7.01 A                                      CA06
7.01 A                                      CA12
7.01 A                                      PUTOVR
7.01 A                                      WINDOW(&W_ALIN &W_ACOL 7 39)
7.01 A            W_ALIN         2S 0P
7.01 A            W_ACOL         3S 0P
7.01 A                                  2  3'Søk på 1881'
7.01 A                                  4  3'Telefonnr'
7.01 A            G2TLFN        10A  B  4 14COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A                                  6  1'F3=Gå tilbake'
7.01 A                                      COLOR(BLU)
7.03 A*
7.03 A*  Bilde G3 - vindu for oppslag mot Bisnode
7.03 A*
7.03 A          R G3WIN
7.03 A*%%TS  SD  20170521  121339  G0XEGASSKO  REL-V7R1M0  5770-WDS
7.03 A                                      WINDOW(&WACTLI &WACTPO 13 49)
7.03 A                                      CA03
7.03 A                                      CA12
7.03 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
7.03 A                                      OVERLAY
7.03 A                                      PUTOVR
7.03 A            WACTLI         2S 0P
7.03 A            WACTPO         3S 0P
7.03 A            W_ARCD        10A  H
7.03 A            W_AFLD        10A  H
7.03 A                                  2  2' Kredittsjekk mot Bisnode '
7.03 A                                      DSPATR(RI)
7.03 A                                  4  3'Foretaks nummer. .'
7.03 A            G3FNUM         9   B  4 22COLOR(YLW)
8.02 A N63                              6  5'eller'
8.02 A N63                              8  3'Person nummer. . .'
8.02 A N63        G3PNUM        11A  B  8 22DSPATR(UL)
8.02 A N63                                  COLOR(YLW)
8.02 A N63                                  OVRDTA
8.02 A N63                              9  4'Personnummer benyttes kun til kred-
8.02 A                                      ittsjekk.'
8.02 A N63                                  COLOR(RED)
7.03 A N63                             10  4'Dette vil ikke bli lagret.'
7.03 A N63                                  COLOR(RED)
7.03 A                                 12  4'F3=Tilbake'
7.03 A                                      COLOR(BLU)
     A          R G3MSG1
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Kredittgrenser for Bisnode '
     A                                  3  2'er ikke satt opp!     '
     A                                  5  2'Må settes opp før bruk.'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R S2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme mobiltelefonnummer er'
     A                                  3  2'registrert på kundenr.'
     A            S2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            S2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R T2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme ean-kundenummer er'
     A                                  3  2'registrert på kundenr.'
     A            T2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            T2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R V2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme foretaksnummer er '
     A                                  3  2'registrert på kundenr.'
     A            V2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            V2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R W2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme alfakode og postnr. er'
     A                                  3  2'registrert på kundenr.'
     A            V2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            V2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R X2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme bankgironr. er    '
     A                                  3  2'registrert på kundenr.'
     A            X2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            X2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Y2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme telefonnummer er'
     A                                  3  2'registrert på kundenr.'
     A            Y2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Y2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Z2MSG
     A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme e-mail adresse er'
     A                                  3  2'registrert på kundenr.'
     A            Z2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Z2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.fb A          R J2WIN
7.fb A*%%TS  SD  20050920  155801  AMD540      REL-V5R3M0  5722-WDS
7.fb A                                      WINDOW(&W_ALIN &W_ACOL 7 52)
7.fb A                                      CA03
7.fb A                                      CF04
7.fb A                                      PUTOVR
7.fb A                                      HELP
7.fb A            W_ALIN         2S 0P
7.fb A            W_ACOL         3S 0P
7.fb A                                  2  2'Spesialavtale:'
7.fb A            J2SAT1    R        B  2 18REFFLD(RKSAT1)
7.fb A            J2SAT2    R        B  3 18REFFLD(RKSAT2)
7.fb A            J2SAT3    R        B  4 18REFFLD(RKSAT3)
7.fb A                                  6  2'F3=Gå tilbake'
7.fb A                                      COLOR(BLU)
      *
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
