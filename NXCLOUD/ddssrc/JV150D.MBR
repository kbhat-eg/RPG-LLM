     A*%%TS  SD  20031015  185952  HKO         REL-V5R2M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*        020816 bsa Endringer relatert til ny GUI
7.01 A*        250521 bsa Justert kode for å unngå sjekkboks
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20031015  185952  HKO         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(*DFT 15 67)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Bytting av priskode på varer '
     A                                      DSPATR(RI)
     A                                  5  2'Priskode . . . . .'
     A            B1PKOD    R        B  5 21REFFLD(JVDPKO)
     A  31                                  ERRMSGID(JMK0020 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
7.01 A                                  5 23'(k)'
     A                                  7  2'Utplukk:'
     A                                      COLOR(WHT)
7.xx A                                  8  2'Leverandør . . . +'
     A            B1LDOR    R        B  8 21REFFLD(JVLDOR)
     A  32                                  ERRMSGID(JMV0400 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JML0020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1NAVN    R   -5   O  8 35REFFLD(JLNAVN)
     A                                      COLOR(YLW)
7.xx A                                  9  2'Varegruppe . . . +'
     A            B1OGRP    R        B  9 21REFFLD(JVOGRP)
     A  34                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
7.xx A                                  9 24'+'
7.xx A            B1HGRP    R        B  9 26REFFLD(JVHGRP)
     A  35                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
7.xx A                                  9 29'+'
7.xx A            B1UGRP    R        B  9 31REFFLD(JVUGRP)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1UTXT        30A  O  9 35COLOR(YLW)
7.xx A                                 10  2'Modul. . . . . . +'
     A            B1MODN    R        B 10 21REFFLD(JVMODN)
     A  37                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1MTE1        30A  O 10 35COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
