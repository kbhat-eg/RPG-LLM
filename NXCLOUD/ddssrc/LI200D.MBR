     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
7.xx A*      220819  bsa  Nytt program basert på 132 posisjoners bilde
7.02 A*      281019  bsa  Fjerner muligheten for filtrering på lager
7.04 A*      131219  bsa  F-tast for overgang til scanne SSCC nummer
7.06 A*      090320  bsa  Viser bestillingsnummer i subfile.
7.09 A* K000 061020  bsa  Utvider funksjonaliteten rundt filter. Betyr
7.09 A* K000              tilbakeføring av endring 7.02.
     A*
     A*----------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A            B1MNUM    R        H      REFFLD(LONUMM)
     A            B1LDOR    R        H      REFFLD(LILDOR)
     A            B1AARR         4S 0H
     A            B1PVER         3S 0H
     A            B1VALG         2Y 0B  7  2
     A                                      VALUES(0 4 5 6 7 8 9 10)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            B1PAID        17A  O  7  5
     A            B1PSDT         6Y 0O  7 23EDTWRD('  .  .  ')
     A            B1LNAV        28A  O  7 32
7.06 A            B1BNUM    R        O  7 62REFFLD(LONUMM)
7.06 A            B1BANT         2Y 0O  7 73EDTCDE(Z)
7.06 A            B1AKOL         3Y 0O  7 76EDTCDE(Z)
7.06 A            B1SKOL         3Y 0O  7 80EDTCDE(Z)
     A            B1TMAT        25A  O  7 84
     A            B1LMAT         3A  O  7110
     A            B1VOLU         5  3O  7114EDTCDE(M)
     A            B1VEKT         6  3O  7122EDTCDE(M)
     A            B1STAT         2A  O  7131
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  33                                  COLOR(GRN)
     A  34                                  COLOR(WHT)
      *
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA04
     A                                      CF05
     A                                      CA09
     A                                      CA10
7.04 A                                      CA11
     A                                      CA12
     A                                      CA13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW     W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av EDI pakksedler'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'5=Se på'
     A                                      COLOR(BLU)
     A                                  5 11'4=Slett'
     A                                      COLOR(BLU)
     A                                  5 20'6=Skriv ut'
     A                                      COLOR(BLU)
     A                                  5 32'7=Se kolli'
     A                                      COLOR(BLU)
     A                                  5 44'8=Se varelinjer'
     A                                      COLOR(BLU)
     A                                  5 61'9=Vise bestillinger'
     A                                      COLOR(BLU)
     A                                  5 82'10=Godkjenn pakkseddel'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  5'PakkseddelID'
     A                                      COLOR(WHT)
     A                                  6 23'Lev.dato'
     A                                      COLOR(WHT)
     A                                  6 32'Leverandør'
     A                                      COLOR(WHT)
7.06 A                                  6 64'Bestnr'
     A                                      COLOR(WHT)
7.06 A                                  6 72'Abe'
     A                                      COLOR(WHT)
7.06 A                                  6 76'Ako'
     A                                      COLOR(WHT)
7.06 A                                  6 80'Asc'
     A                                      COLOR(WHT)
     A                                  6 84'Tr.måte '
     A                                      COLOR(WHT)
     A                                  6110'Lmt'
     A                                      COLOR(WHT)
     A                                  6114'Volum'
     A                                      COLOR(WHT)
     A                                  6124'Vekt'
     A                                      COLOR(WHT)
     A                                  6130'St'
     A                                      COLOR(WHT)
      *
     A          R B2CMD
     A*%%TS  SD  20041125  110702  GRO530      REL-V5R2M0  5722-WDS
     A                                 26  2'F1=Filter'
     A                                      COLOR(BLU)
     A                                 26 13'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 26 28'F5=Forny'
     A                                      COLOR(BLU)
     A                                 26 38'F9=Hent godkjente pakksedler'
     A                                      COLOR(BLU)
     A                                 26 69'F10=Bestilling'
     A                                      COLOR(BLU)
7.04 A                                 26 85'F11=Scanne kolli'
7.04 A                                      COLOR(BLU)
7.04 A                                 26103'F13=Statuskoder'
     A                                      COLOR(BLU)
     A          R C5BLD
     A*%%TS  SD  20040217  123227  BSA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av EDI pakksedler'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Pakkseddel . . . . . .'
     A            C1PAID        17A  O  5 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PVER         3Y 0O  5 45
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Status . . . . . . . .'
     A            C1STAT         2A  O  7 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SBES        30A  O  7 28
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Pakkseddeldato . . . .'
     A            C1PADT         6Y 0O  8 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  9  2'Leveringsperiode . . .'
     A            C1PSDT         6Y 0O  9 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C1PEDT         6Y 0O  9 34COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Bestillingsnummer. . .'
     A            C1BNUM        17A  O 10 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                               11  2'NX Bestillingsnr . . .'
     A**          C1NUMM         8Y 0O 11 25
     A**                                    EDTCDE(Z)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A                                 12  2'Leverandørens ordrenr.'
     A            C1LONR        35A  O 12 25COLOR(YLW)
     A                                      OVRDTA
     A                                 12 62'Prosjektnummer . . . .'
     A            C1PRNR        35A  O 12 85COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Transportmåte. . . . .'
     A            C1TMAT        25A  O 13 25COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Transportinfo. . . . .'
     A            C1TINF        55A  O 14 25COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Totalt volum . . . . .'
     A            C1TVOL         8  4O 15 25
     A                                      EDTWRD('  0 ,    ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 40'Totalt vekt. . . . . .'
     A            C1TVEK        10  4O 15 63
     A                                      EDTWRD('    0 ,    ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Leveringsmåte. . . . .'
     A            C1LMAT         3A  O 16 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'Leveringsbetingelse. .'
     A            C1LBET         3A  O 17 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R B6WIN
     A*%%TS  SD  20050523  130713  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(2 10 9 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Skriv ut pakkseddel '
     A                                      DSPATR(RI)
     A                                  5  2'Skriv dokumentkopi . .'
     A            B6DKOP    R        B  5 25REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                  5 28'1/0'
     A          R B9WIN3
     A*%%TS  SD  20081127  125855  ASP_MSX     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 13 45)
     A                                  2  2' Feil i faktura innlesning!!!!!'
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A                                  5  2'En eller flere fakturaer feilet'
     A                                  6  2'ved innlesing. Gå til loggen for'
     A                                  7  2'å finne ut mer (F8)'
     A                                 10  2'**** **** **** FEIL !!! **** **** -
     A                                       ****'
     A                                      COLOR(RED)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B13WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Statuskoder '
     A                                      DSPATR(RI)
     A*                                 3  2'E = Feil ved innlesing.'
     A*                                     COLOR(RED)
     A*                                 4  2'1 = Feil i bestillingsnummer.'
     A*                                     COLOR(RED)
     A*                                 5  2'2 = Sjekk leverandør'
     A*                                     COLOR(RED)
     A*                                 6  2'3 = Avvik fra bestilling'
     A*                                     COLOR(RED)
     A*                                 7  2'4 = Feil ordretype'
     A*                                     COLOR(RED)
     A*                                 8  2'5 = Avvik totalsum'
     A*                                     COLOR(RED)
     A*                                10  2'7 = Slettemarkert'
     A*                                     COLOR(BLU)
     A*                                11  2'8 = Manuelt godkjent'
     A*                                     COLOR(YLW)
     A*                                12  2'9 = Oppdatert'
     A*                                     COLOR(WHT)
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 9 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette pakkseddel'
     A                                      DSPATR(RI)
     A                                  4  2'Tidspunkt :'
     A            B4MDAT         6Y 0O  4 15EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  5  2'Pakkseddel:'
     A            B4PAID        17A  O  5 15
     A                                      COLOR(YLW)
     A                                  5 34'/'
     A            B4PVER         6Y 0O  5 36EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  6  2'Leverandør:'
     A            B4LEVR        30A  O  6 15COLOR(YLW)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050915  092234  GRO540      REL-V5R3M0  5722-WDS
7.xx A                                      WINDOW(*DFT 17 72)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Leverandørnr. +'
     A            H1SLDO         6Y 0B  4 18CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            H1LNAV        30A  O  4 26OVRDTA
     A                                  5  2'Leverandørnavn.'
     A            H1SNAV        20A  B  5 18COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Pakkseddeldato.'
     A            H1PADT         6Y 0B  6 18COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'PakkseddelID. .'
     A            H1PAID        17A  B  7 18COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Bestillingsnr..'
     A            H1SBES        20A  B  8 18COLOR(YLW)
     A                                      OVRDTA
     A*                                 9  2'Selger/innkj. .'
     A*           H1INNK    R        B  9 18REFFLD(LOINNK)
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
     A*                                     CHECK(RB)
     A*           H1INAV        20A  O  9 26OVRDTA
      *
7.09 A                                 10  2'Lager . . . . +'
7.09 A            H1LAGE    R        B 10 18REFFLD(LOLAGE)
7.09 A  32                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 32)
7.09 A                                      OVRDTA
7.09 A                                      COLOR(YLW)
7.09 A                                      CHECK(RB)
7.09 A                                 10 23'+'
7.09 A            H2LAGE    R        B 10 25REFFLD(LOLAGE)
7.09 A  34                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 34)
7.09 A                                      OVRDTA
7.09 A                                      COLOR(YLW)
7.09 A                                      CHECK(RB)
7.09 A                                 10 30'+'
7.09 A            H3LAGE    R        B 10 32REFFLD(LOLAGE)
7.09 A  35                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 35)
7.09 A                                      OVRDTA
7.09 A                                      COLOR(YLW)
7.09 A                                      CHECK(RB)
7.09 A                                 10 37'+'
7.09 A            H4LAGE    R        B 10 39REFFLD(LOLAGE)
7.09 A  36                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 36)
7.09 A                                      OVRDTA
7.09 A                                      COLOR(YLW)
7.09 A                                      CHECK(RB)
7.09 A                                 10 44'+'
7.09 A            H5LAGE    R        B 10 46REFFLD(LOLAGE)
7.09 A  37                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 37)
7.09 A                                      OVRDTA
7.09 A                                      COLOR(YLW)
7.09 A                                      CHECK(RB)
6.30 A*                                11  2'Varemotatt. . .'
6.30 A*           H1VMOT         1A  B 11 18
6.30 A*                                     OVRDTA
6.30 A*                                     COLOR(YLW)
6.30 A* 33                                  ERRMSG('Kun blank, 1,2 og 3 er gy+
6.30 A*                                             ldige valg')
7.xx A*                                11 21'0=Ikke valgt,1=Mottatt,2=Ikke mott+
7.xx A*                                     att, 3=Uten best.'
      *
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 18'F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 27132' '
