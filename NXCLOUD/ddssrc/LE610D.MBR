     A*%%TS  SD  20060912  152202  ASK540      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LE610D
     A* Beskrivelse . . : Utskrift av telleliste - avviks liste
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R B1BLD
     A*%%TS  SD  20060912  152202  ASK540      REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CF04
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av avvik-liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Seleksjonskriterier:'
     A                                      COLOR(WHT)
     A                                  6 31'Fra'
     A                                  6 49'Til'
7.xx A                                  7  2'Overgruppe . . . . . . . . +'
     A            B1FOGR    R        B  7 31REFFLD(LWOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
7.xx A                                  7 47'+'
     A            B1TOGR    R        B  7 49REFFLD(LWOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
7.xx A                                  8  2'Hovedgruppe. . . . . . . . +'
     A            B1FHGR    R        B  8 31REFFLD(LWHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
7.xx A                                  8 47'+'
     A            B1THGR    R        B  8 49REFFLD(LWHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
7.xx A                                  9  2'Undergruppe. . . . . . . . +'
     A            B1FUGR    R        B  9 31REFFLD(LWUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  9 47'+'
     A            B1TUGR    R        B  9 49REFFLD(LWUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 10  2'Varenummer . . . . . . . . +'
     A            B1FVAR    R        B 10 31REFFLD(LWVARE)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
7.xx A                                 10 47'+'
     A            B1TVAR    R        B 10 49REFFLD(LWVARE)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
7.xx A                                 12  2'Lagernummer. . . . . . . . +'
     A            B1FLAG    R        B 12 31REFFLD(LWHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
     A            B1TLAG    R        B 12 49REFFLD(LWHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(LSO0050 *LIBL/QMFLAGR 37)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020924  115502  HKO         REL-V5R1M0  5722-WDS
     A                                      CF04
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av avvik-liste'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Sorteringsrekkefølge . . . .'
     A            C1VAL1         1Y 0B  5 31CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3)
     A                                  5 35'1 = Varegruppe/varenummer'
     A                                  6 35'3 = Varenummer'
     A                                  9  2'Hvilke varer skal skrives ut'
     A            C1VAL2         1Y 0B  9 31CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
6.30 A                                      VALUES(0 1 2 3 4 5)
     A                                  9 35'0 = Alle varer'
     A                                 10 35'1 = Kun varer med differanse'
     A                                 11 35'2 = Varer med 0 i antall tellet'
6.30 A                                 12 35'3 = Varer m/negativ differanse'
6.30 A                                 13 35'4 = Varer m/positiv differanse'
6.30 A                                 14 35'5 = Varer u/differanse'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
