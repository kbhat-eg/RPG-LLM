     A*%%TS  SD  20110606  204019  ASK610      REL-V7R1M0  5770-WDS
     A*%%EC
     A*
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051030  124625  HKO550      REL-V5R3M0  5722-WDS
     A            B1ALFA        18A  H
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
6.11 A                                      VALUES(3 6 7 8 9 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1NAVN        30A  O  9  5
7.xx A            B1KUND    R        O  9 37REFFLD(EKBKUN)
7.xx A            B1BONU         1Y 0O  9 47EDTCDE(Z)
7.xx A            B1SBON    R        O  9 52REFFLD(EKSBON)
     A                                      EDTCDE(2)
7.xx A            B1BDAT         6Y 0O  9 65EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090118  170046  ASK560      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
6.11 A**                                    CA06
     A                                      CA08
     A                                      CA09
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundebonus'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 37'Nummer'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'3=Kopier  6=Kundebrev  7=Bonustype-
6.11 A**                                5  2'3=Kopier               7=Bonustype-
6.11 A                                      r  8=Tid  9=Kalkuler bonus'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Navn                  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 37'Nummer'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'Bonus'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'bonus'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 67'Dato'
7.xx A                                      COLOR(WHT)
7.xx A            B2ALFA        18A  B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2KUND    R        B  5 37REFFLD(EKBKUN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 53'Kalkulert'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20110603  151859  ASK610      REL-V7R1M0  5770-WDS
6.11 A                                 23  2'F3=Gå tilbake  F5=Forny  F8=Tid  F-
6.11 A                                      9=Beregn'
6.11 A                                      COLOR(BLU)
6.11 A                                 23 62'F23=Alle/med bonus'
6.11 A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051020  153510  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Kunde. . . . . . +'
     A            K1KUND    R        B  4 21REFFLD(EKBKUN)
     A  31                                  ERRMSGID(FRK0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(EMK0090 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Kunde. . . . . . .'
     A            EKBKUN    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
6.11 A          R O1WIN
6.11 A*%%TS  SD  20110603  152353  ASK610      REL-V7R1M0  5770-WDS
6.11 A                                      WINDOW(*DFT 15 58)
6.11 A                                      CA03
6.11 A                                      CA12
6.11 A                                      PUTOVR
6.11 A                                  2  2' Kalkuler bonus for kunde '
6.11 A                                      DSPATR(RI)
6.11 A                                  5  2'Kunde. . . . . . .'
6.11 A            O1KUND    R        O  5 21REFFLD(EKBKUN)
6.11 A                                      OVRDTA
6.11 A            O1KNAV        30A  O  5 28OVRDTA
6.11 A                                  7  2'År . . . . . . . .'
6.11 A            O1PAAR         4Y 0B  7 21COLOR(YLW)
6.11 A  31                                  ERRMSGID(EMK0300 *LIBL/QMFOFAK 31)
6.11 A                                      DSPATR(PC)
6.11 A                                      CHECK(RB)
6.11 A                                      OVRDTA
6.21 A                                  7 26'Periode (fra-til)'
6.21 A            O0PERI         2A  B  7 44COLOR(YLW)
6.21 A                                      CHECK(RB)
6.21 A                                      OVRDTA
7.xx A                                  7 47'-'
7.xx A            O1PERI         2A  B  7 49COLOR(YLW)
6.21 A  32                                  ERRMSGID(EMK0300 *LIBL/QMFOFAK 31)
6.21 A  33                                  ERRMSG('Feil i periode')
6.20 A                                      DSPATR(PC)
6.20 A                                      CHECK(RB)
6.20 A                                      OVRDTA
6.20 A                                  8  2'Bonustype. . . . .'
6.20 A            O1TBOT         2   B  8 21COLOR(YLW)
6.20 A                                      DSPATR(UL)
6.11 A                                 10  2'Jobben startes som bakgrunnsjobb.'
6.11 A                                 14  2'F3=Gå tilbake'
6.11 A                                      COLOR(BLU)
6.11 A          R P1WIN
6.11 A*%%TS  SD  20110603  152353  ASK610      REL-V7R1M0  5770-WDS
6.11 A                                      WINDOW(*DFT 11 40)
6.11 A                                      CA03
6.11 A                                      CA12
6.11 A                                      PUTOVR
6.11 A                                  2  2' Beregn bonus for alle '
6.11 A                                      DSPATR(RI)
6.11 A                                  4  2'Bonusår:'
6.11 A            P1PAAR         4A  B  4 11COLOR(YLW)
6.11 A  31                                  ERRMSGID(EMK0300 *LIBL/QMFOFAK 31)
6.11 A                                  4 18'Bonustype:'
6.11 A            P1TBOT         2   B  4 29DSPATR(UL)
6.11 A                                      COLOR(YLW)
6.11 A                                  6  2'Beregning '
6.11 A            P1ALT1         1A  B  6 13COLOR(YLW)
6.11 A  32                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 32)
6.11 A                                  6 15'(0=Nei,1=Ja)'
6.11 A                                  7  2'Sumskjema '
6.11 A            P1ALT2         1A  B  7 13COLOR(YLW)
6.11 A  33                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 33)
6.11 A                                  7 15'(0=Nei,1=Ja)'
6.11 A                                  8  2'Kreditnota'
6.11 A            P1ALT3         1A  B  8 13COLOR(YLW)
6.11 A  34                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 34)
6.11 A                                  8 15'(0=Nei,1=Ja)'
6.11 A                                 10  3'F3=Gå tilbake'
6.11 A                                      COLOR(BLU)
6.12 A          R P2WIN
6.12 A*%%TS  SD  20110606  204019  ASK610      REL-V7R1M0  5770-WDS
6.22 A                                      WINDOW(03 03 11 40)
6.12 A                                      CA03
6.12 A                                      CA12
6.12 A                                      PUTOVR
6.12 A                                  2  2' Faste data til kundebrev '
6.12 A                                      DSPATR(RI)
6.12 A            P2KUND    R        O  4  2REFFLD(EKBKUN)
6.12 A                                      EDTCDE(Z)
6.12 A                                      OVRDTA
6.12 A            P2KNAV        30A  O  4  9OVRDTA
6.12 A                                  6  2'År. . . .:'
6.12 A            P2PAAR    R        B  6 13REFFLD(SOAARR)
6.12 A                                      COLOR(YLW)
6.12 A  31                                  ERRMSGID(EMK0300 *LIBL/QMFOFAK 31)
6.12 A  33                                  ERRMSG('Kunde har ikke bonusavtale')
6.12 A                                  7  2'Detaljert:'
6.12 A            P2ALT1    R        B  7 13REFFLD(EKEKTB)
6.12 A                                      COLOR(YLW)
6.12 A  32                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 32)
6.12 A                                  7 15'0=Nei,1=Ja'
6.12 A                                 10  3'F3=Gå tilbake'
6.12 A                                      COLOR(BLU)
6.12 A*
     A          R L1WIN
     A*%%TS  SD  20110603  152353  ASK610      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 11 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Endre bonus tidsrom '
     A                                      DSPATR(RI)
6.11 A                                  4  2'Bonustype:'
6.11 A            L1TBOT         2   B  4 13DSPATR(UL)
6.11 A                                      COLOR(YLW)
     A                                  5  2'Endrer:'
     A            L1KUND        30A  O  5 10
     A                                  7  2'Start bonusperiode:'
     A            L1STDA         6Y 0B  7 22COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                  7 30'(ddmmåå)'
     A                                  8  2'Slutt bonusperiode:'
     A            L1SLDA         6Y 0B  8 22COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                  8 30'(ddmmåå)'
     A                                 10  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
