     A*%%TS  SD  20040104  213001  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RM120D
     A* Beskrivelse . . : Eiendomsknytning, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160303 KOB  Nytt program
7.xx A* 7.xx  130516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030316  220029  KOB         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
     A            B1XGNR    R        O  8  5REFFLD(RMXGNR)
     A            B1XBNR    R        O  8 11REFFLD(RMXBNR)
     A            B1ENAV    R        O  8 17REFFLD(RMENAV)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030318  010734  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendomsknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'G.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'B.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'G.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'B.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'Navn               '
     A                                      COLOR(WHT)
7.xx A            B2XGNR    R        B  5  5REFFLD(RMXGNR)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B2XBNR    R        B  5 11REFFLD(RMXBNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A            B2ENAV    R        B  5 17REFFLD(RMENAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030318  011433  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendomsknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gårdsnummer/Bruksnummer. . .'
     A            C1XGNR    R        B  5 31REFFLD(RMXGNR)
     A  31                                  ERRMSGID(RMM0070 *LIBL/QMFOKON 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1XBNR    R        B  5 37REFFLD(RMXBNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20030316  220029  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Samme eiendom finnes på denne'
     A                                  3  2'kunden fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030318  005407  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Almenning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eiendomsknytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Gårdsnummer/Bruksnummer. . .'
     A            C1XGNR    R        O  5 31REFFLD(RMXGNR)
     A  31                                  ERRMSGID(RMM0070 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1XBNR    R        O  5 37REFFLD(RMXBNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2'Navn . . . . . . . . . . . .'
     A            C2ENAV    R        O  7 31REFFLD(RMENAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2XEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2XEUS    R        O 20 31REFFLD(RMXEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030318  115607  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette eiendomsknytning '
     A                                      DSPATR(RI)
     A                                  4  2'Gårdsnummer/Bruksnummer'
     A            D1XGNR    R        O  4 26REFFLD(RMXGNR)
     A            D1XBNR    R        O  4 32REFFLD(RMXBNR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  6  2'Navn . . . . . . . . . '
     A            D1ENAV    R        O  6 26REFFLD(RMENAV)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20030318  115742  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Gårdsnummer/Bruksnummer:'
     A            K1XGNR    R        B  4 27REFFLD(RMXGNR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(RMM0070 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMM0090 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A            K1XBNR    R        B  4 33REFFLD(RMXBNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Gårdsnummer/Bruksnummer:'
     A            RMXGNR    R        O  8 27OVRDTA
     A            RMXBNR    R        O  8 33REFFLD(RMXBNR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  9  2'Navn . . . . . . . . . :'
     A            K1ENAV    R        O  9 27REFFLD(RMENAV)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
