     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EL600D
     A* Beskrivelse . . : Leverandører, utskrift av bonusgrunnlag, parametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050224 BSA  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20060423  091137  KOB         REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Bonus  '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av bonusgrunnlag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 11'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5 11'Bonusår .:'
     A            A2BRAR    R        B  5 22REFFLD(RKPRÅR)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  6 11'Utvalg:'
     A                                      COLOR(WHT)
     A                                  6 34'Fra - Til'
     A                                      COLOR(WHT)
     A                                  7 13'Bonustype . . . . .+'
     A                                  7 45'Blank gir alle'
     A            A2BONU         2A  B  7 34
     A  32                                  ERRMSG('Bonustype finnes ikke')
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 13'Leverandørnummer. .+'
     A            A2PLNF    R        B  8 34REFFLD(RKPKNR)
     A  57                                  DSPATR(PC)
     A  40                                  ERRMSGID(RMH0140 *LIBL/QMFOKON 40)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  8 41'-'
     A            A2PLNT    R        B  8 43REFFLD(RKPKNR)
     A  58                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 11 13'Type . . . . . . . .'
     A            A2ALT1         1A  B 11 34COLOR(YLW)
     A  34                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 32)
     A                                 11 36'0=Summert,1=Detaljert'
      *
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
