     A*%%TS  SD  20080227  164315  TSV         REL-V5R4M0  5722-WDS
     A*
6.3d A*     bsa  20180803  Legger til leverandørens ordrenummer.
7.xx A* 7.00 150119  BKV  Endringer pga ny GUI
7.05 A* 7.00 140819  BSA  Utvidet skjermbilde og legger ut leveringsdato
7.06 A*K0000 080920  bsa  Utvidet felt for GTIN nummer
7.08 A*K0000 220621  bsa  Utvidet meldingsnummer
     A*
8.01 A*K0000 231221  bsa  Viser akseptansekode fra leverandør
8.03 A*800   290923  ask  Utvidet felt for leverandørens ordrebekreftelsesnummer
     A*
     A*
     A*%%EC
7.05 A                                      DSPSIZ(27 132 *WIDE)
7.05 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
      *
     A          R B1SFL                     SFL
     A*%%TS  SD  20050915  125621  GRO540      REL-V5R3M0  5722-WDS
     A            B1LINE    R        H      REFFLD(LIMLIN)
     A            B1VALG    R        B 11  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
6.34 A                                      VALUES(0 4 5 6 7 8)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1TEK1        25A  O 11  4
6.12 A            B1OKVA         8Y 2O 11 30EDTWRD('      ,  ')
     A            B1BKVA         8Y 2O 11 40EDTWRD('      ,  ')
     A            B1BENH         3A  O 11 50
     A            B1LKVA         8Y 2O 11 54EDTWRD('      ,  ')
     A            B1LENH         3A  O 11 64
7.05 A            B1EDAT         6Y 0O 11 68EDTWRD('  .  .  ')
7.05 A            B1BDAT         6Y 0O 11 77EDTWRD('  .  .  ')
7.05 A            B1STAT        12A  O 11 86
8.01 A            B1AKSP        12A  O 11100
7.05  *
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080122  120124  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      CA10
     A                                      CF11
     A                                      CA12
     A                                      CF14
6.35 A                                      CF15
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.05 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.05 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Detaljer EDI ordrebekreftelse'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LI102B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.05 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.05 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.05 A                                      ------------------------------------
7.05 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Meldingsdato'
     A            B2MDAT         6Y 0O  4 15EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  4 24'Tid'
     A            B2MTID         6Y 0O  4 28EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
6.18 A                                  4 37'Be.tot:'
6.26 A**          B2BBEL         7Y 2O  4 45EDTWRD('   0 ,  ')
6.26 A            B2BBEL         7Y 0O  4 45EDTCDE(Z)
6.18 A                                      COLOR(YLW)
     A                                  4 54'Status'
     A            B2STAT        15A  O  4 65COLOR(YLW)
7.03 A                                  5  2'Meld.nr.'
7.08 A            B2MLNR         8S 0O  5 11COLOR(YLW)
7.03 A                                  6  2'Best . .'
7.03 A            B2NUMM    R        O  6 11REFFLD(LINUMM)
     A                                      COLOR(YLW)
7.03 A  30                              6 20'/'
7.03 A  30        B2SUFF    R        O  6 22REFFLD(LISUFF)
     A                                      COLOR(YLW)
6.18 A                                  5 37'Bk.tot:'
6.26 A**          B2KBEL         7Y 2O  5 45EDTWRD('   0 ,  ')
6.26 A            B2KBEL         7Y 0O  5 45EDTCDE(Z)
6.18 A                                      COLOR(YLW)
     A                                  5 54'Ønsket lev'
     A            B2ODAT         6Y 0O  5 65EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
7.03 A                                  6 37'Ks.tot:'
7.03 A            B2BKBL         7Y 0O  6 45EDTCDE(Z)
7.03 A                                      COLOR(YLW)
7.03 A                                  7  2'Lev. . .'
7.03 A            B2LDOR    R        O  7 11REFFLD(LILDOR)
     A                                      COLOR(YLW)
7.03 A            B2LNAV        30A  O  7 18COLOR(YLW)
     A                                  6 54'Lovet lev.'
     A            B2LDAT         6Y 0O  6 65EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
7.03 A                                  8  2'Lev ref.'
7.03 A            B2VREF        30A  O  8 11COLOR(YLW)
     A                                  7 54'Obkr.'
8.03 A            B2KREF        30A  O  7 65COLOR(YLW)
6.19 A**31                                  ERRMSG('Linje kan ikke oppdateres')
6.34 A                                  9  2'4=Slett fra bes  5=Vis detaljer  6-
6.34 A                                      =Oppd ant  7=Oppd pris  8=Oppd avvi-
6.34 A                                      k'
6.34 A**                                    ik'
     A                                      COLOR(BLU)
7.xx A*                                10  2'V Vare                       Ord.a-
7.xx A*                                     nt  Best.ant  Enh Bekr.ant  Enh Sta-
7.xx A*                                     tus'
7.xx A*                                     COLOR(WHT)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'Vare'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 30'Ord.ant'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 40'Best.ant'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 50'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 54'Bekr.ant'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 64'Enh'
7.xx A                                      COLOR(WHT)
7.05 A                                 10 68'EDI dato'
7.05 A                                      COLOR(WHT)
7.05 A                                 10 77'Bes.lin'
7.05 A                                      COLOR(WHT)
7.05 A                                 10 86'Status'
7.xx A                                      COLOR(WHT)
8.01 A                                 10100'Aks.kode lev'
8.01 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20080122  120124  ASP_NOR     REL-V5R4M0  5722-WDS
7.05 A                                 25  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.05 A                                 25 18'F7=Partsinformasjon'
     A                                      COLOR(BLU)
7.05 A                                 25 40'F8=Tillegg/fradrag'
     A                                      COLOR(BLU)
7.05 A                                 25 62'F9=Totaler'
     A                                      COLOR(BLU)
7.05 A                                 25 75'F10=Bestilling'
     A                                      COLOR(BLU)
7.05 A                                 25 92'F11=Referanseinformasjon'
     A                                      COLOR(BLU)
7.05 A                                 26  2'F14=kundeordre'
     A                                      COLOR(BLU)
7.05 A                                 26 19'F15=Ordretekst'
6.35 A                                      COLOR(BLU)
6.35 A  32                                  DSPATR(RI)
     A          R B5WIN
     A*%%TS  SD  20061017  123545  GRO540      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 21 65)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Linjedetaljer '
     A                                      DSPATR(RI)
     A                                  2 30'Status . . .'
     A            B5STAT        12A  O  2 43
     A                                  4  2'Linjenr. .'
     A            B5LINN         6Y 0O  4 13EDTCDE(Z)
     A                                  4 30'Lev.linjenr.'
     A            B5LLIN         6Y 0O  4 43EDTCDE(Z)
     A                                  5  2'GTIN-nr. .'
7.06 A            B5EANN        14Y 0O  5 13EDTCDE(Z)
     A                                  5 30'Nobbnr . . .'
     A            B5NOBB         8Y 0O  5 43EDTCDE(Z)
     A                                  6  2'Varenr . .'
     A            B5EVNR         8Y 0O  6 13
     A                                  6 30'Lev.varenr..'
     A            B5LVNR        15A  O  6 43
     A            B5TEK1        35A  O  8  2
     A            B5TEK2        35A  O  9  2
     A                                 10  2'Best:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
6.12 A            B5BNUM         6A  O 10  9
6.12 A                                      COLOR(WHT)
     A                                 10 30'Bekr:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2'Ant.:'
     A            B5BKVA        14Y 3O 11  8EDTWRD('         0 ,   ')
     A            B5BENH         3A  O 11 24
     A                                 11 30'Ant. . . . :'
     A            B5LKVA        14Y 3O 11 43EDTWRD('         0 ,   ')
     A            B5LENH         3A  O 11 59
     A                                 12 30'Ant.omr. . :'
     A            B5OANT        14Y 3O 12 43EDTWRD('           ,   ')
     A            B5OAEN         3A  O 12 59
     A                                 13  2'Pris:'
     A            B5BPRI        14Y 3O 13  8EDTWRD('         0 ,   ')
     A            B5BPEN         3A  O 13 24
     A                                 13 30'Pris . . . :'
     A            B5LPRI        14Y 3O 13 43EDTWRD('         0 ,   ')
     A            B5PENH         3A  O 13 59
     A                                 14  2'Kost:'
     A            B5BKOS        14Y 3O 14  8EDTWRD('         0 ,   ')
     A            B5BKEN         3A  O 14 24
     A                                 14 30'Pris omr . :'
     A            B5OPRI        14Y 3O 14 43EDTWRD('           ,   ')
     A            B5OENH         3A  O 14 59
6.12 A                                 15  2'Ordre:'
6.12 A                                      DSPATR(UL)
6.12 A                                      COLOR(WHT)
6.12 A            B5ONUM         6A  O 15  9
6.12 A                                      COLOR(WHT)
6.12 A                                 16  2'Ant.:'
6.12 A            B5FKVA        14Y 3O 16  8EDTWRD('         0 ,   ')
6.12 A            B5FENH         3A  O 16 24
     A                                 16 30'Nobb pris. :'
     A            B5NPRI        14Y 3O 16 43EDTWRD('         0 ,   ')
     A            B5NENH         3A  O 16 59
6.12 A                                 17  2'Kost:'
6.12 A            B5FKOS        14Y 3O 17  8EDTWRD('         0 ,   ')
     A                                 17 30'Rabatt . . :'
     A            B5RABB        13Y 2O 17 43EDTWRD('         0 ,  ')
     A                                 18  2'Ønsket lev.dat:'
     A            B5ODAT         6Y 0O 18 18EDTWRD('  .  .  ')
     A                                 18 30'Bekr.levdat:'
     A            B5LDAT         6Y 0O 18 43EDTWRD('  .  .  ')
     A                                 19 30'Aksept . . :'
     A            B5AKSP         6A  O 19 43
     A                                 20  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20080227  164315  TSV         REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 15 65)
     A                                  1  2' Partsinformasjon '
     A                                      DSPATR(RI)
     A*
     A            B7HDG01       18A  O  3  2
     A            B7NUM01       13Y 0O  3 21EDTCDE(Z)
     A            B7TXT01       30A  O  3 35
     A*
     A            B7HDG02       18A  O  4  2
     A            B7NUM02       13Y 0O  4 21EDTCDE(Z)
     A            B7TXT02       30A  O  4 35
     A*
     A            B7HDG03       18A  O  5  2
     A            B7NUM03       13Y 0O  5 21EDTCDE(Z)
     A            B7TXT03       30A  O  5 35
     A*
     A            B7HDG04       18A  O  6  2
     A            B7NUM04       13Y 0O  6 21EDTCDE(Z)
     A            B7TXT04       30A  O  6 35
     A*
     A            B7HDG05       18A  O  7  2
     A            B7NUM05       13Y 0O  7 21EDTCDE(Z)
     A            B7TXT05       30A  O  7 35
     A*
     A            B7HDG06       18A  O  8  2
     A            B7NUM06       13Y 0O  8 21EDTCDE(Z)
     A            B7TXT06       30A  O  8 35
     A*
     A            B7HDG07       18A  O  9  2
     A            B7NUM07       13Y 0O  9 21EDTCDE(Z)
     A            B7TXT07       30A  O  9 35
     A*
     A            B7HDG08       18A  O 10  2
     A            B7NUM08       13Y 0O 10 21EDTCDE(Z)
     A            B7TXT08       30A  O 10 35
     A*
     A            B7HDG09       18A  O 11  2
     A            B7NUM09       13Y 0O 11 21EDTCDE(Z)
     A            B7TXT09       30A  O 11 35
     A*
     A            B7HDG10       18A  O 12  2
     A            B7NUM10       13Y 0O 12 21EDTCDE(Z)
     A            B7TXT10       30A  O 12 35
     A*
     A            B7HDG11       18A  O 13  2
     A            B7NUM11       13Y 0O 13 21EDTCDE(Z)
     A            B7TXT11       30A  O 13 35
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R B8WIN
     A*%%TS  SD  20020924  124732  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Tillegg/fradrag '
     A                                      DSPATR(RI)
     A            B8TEK0        30A  O  4  2
     A            B8BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B8TEK1        30A  O  5  2
     A            B8BEL1        12Y 2O  5 33EDTWRD('          , 0-')
     A            B8TEK2        30A  O  6  2
     A            B8BEL2        12Y 2O  6 33EDTWRD('          , 0-')
     A            B8TEK3        30A  O  7  2
     A            B8BEL3        12Y 2O  7 33EDTWRD('          , 0-')
     A            B8TEK4        30A  O  8  2
     A            B8BEL4        12Y 2O  8 33EDTWRD('          , 0-')
     A            B8TEK5        30A  O  9  2
     A            B8BEL5        12Y 2O  9 33EDTWRD('          , 0-')
     A            B8TEK6        30A  O 10  2
     A            B8BEL6        12Y 2O 10 33EDTWRD('          , 0-')
     A            B8TEK7        30A  O 11  2
     A            B8BEL7        12Y 2O 11 33EDTWRD('          , 0-')
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20020924  124732  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Ordretotaler '
     A                                      DSPATR(RI)
     A            B9TEK0        30A  O  4  2
     A            B9BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B9TEK1        30A  O  5  2
     A            B9BEL1        12Y 2O  5 33EDTWRD('          , 0-')
     A            B9TEK2        30A  O  6  2
     A            B9BEL2        12Y 2O  6 33EDTWRD('          , 0-')
     A            B9TEK3        30A  O  7  2
     A            B9BEL3        12Y 2O  7 33EDTWRD('          , 0-')
     A            B9TEK4        30A  O  8  2
     A            B9BEL4        12Y 2O  8 33EDTWRD('          , 0-')
     A            B9TEK5        30A  O  9  2
     A            B9BEL5        12Y 2O  9 33EDTWRD('          , 0-')
     A            B9TEK6        30A  O 10  2
     A            B9BEL6        12Y 2O 10 33EDTWRD('          , 0-')
     A            B9TEK7        30A  O 11  2
     A            B9BEL7        12Y 2O 11 33EDTWRD('          , 0-')
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.12 A          R C1MSG
6.12 A*%%TS  SD  20020912  150837  ASK         REL-V5R1M0  5722-WDS
6.12 A                                      WINDOW(*DFT 7 30)
6.12 A                                      CA03
6.12 A                                      CA12
6.12 A                                  3  2'Bestillingen er i ugyldig '
6.12 A                                      COLOR(WHT)
6.12 A                                  4  2'status. Kan ikke endres !'
6.12 A                                      COLOR(WHT)
6.12 A                                  6  2'F3=Gå tilbake'
6.12 A                                      COLOR(BLU)
     A          R B11WIN
     A*%%TS  SD  20050311  130040  GRO530      REL-V5R2M0  5722-WDS
6.3d A                                      WINDOW(*DFT 11 48)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Referanseinformasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Bestillingsnr./kjøper .'
     A            B11BST        20A  O  4 26
     A                                  5  2'Kundereferansenr. . . .'
     A            B11KUN        20A  O  5 26
     A                                  6  2'Prosjektnr. . . . . . .'
     A            B11PRO        20A  O  6 26
     A                                  7  2'Kontraktsnr.. . . . . .'
     A            B11KON        20A  O  7 26
6.3d A                                  8  2'Leverandørens ordrenr .'
6.3d A            B11LONR       20A  O  8 26
6.3d A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.34 A          R B4WIN
6.34 A*%%TS  SD  20050311  130040  GRO530      REL-V5R2M0  5722-WDS
6.34 A                                      WINDOW(*DFT 10 48)
6.34 A                                      CA03
6.34 A                                      CA12
6.34 A                                  2  2' Bekreft sletting av varelinje'
6.34 A                                      DSPATR(RI)
6.34 A                                  4  2'Nobbnummer .'
6.34 A            B4NOBB         8Y 0O  4 15EDTCDE(Z)
6.34 A                                  5  2'Tekst. . . .'
6.34 A            B4TEK1        30A  O  5 15
6.34 A                                  6  2'Bestilt ant.'
6.34 A            B4BKVA        14Y 3O  6 15EDTWRD('         0 ,   ')
6.34 A                                  7  2'Status. . .'
6.34 A            B4STAT        12A  O  7 15
6.34 A                                  9  2'F3=Gå tilbake'
6.34 A          R C4MSG
6.34 A*%%TS  SD  20020912  150837  ASK         REL-V5R1M0  5722-WDS
6.34 A                                      WINDOW(*DFT 7 30)
6.34 A                                      CA03
6.34 A                                      CA12
6.34 A                                  2  2'Bare linjer på bestillingen'
6.34 A                                      COLOR(WHT)
6.34 A                                  3  2'som ikke er bekreftet kan'
6.34 A                                      COLOR(WHT)
6.34 A                                  4  2'slettes !!'
6.34 A                                      COLOR(WHT)
6.34 A                                  6  2'F3=Gå tilbake'
6.34 A                                      COLOR(BLU)
6.35 A          R B15WIN
6.35 A*%%TS  SD  20050311  130040  GRO530      REL-V5R2M0  5722-WDS
6.35 A                                      WINDOW(*DFT 11 72)
6.35 A                                      CA03
6.35 A                                      CA12
6.35 A                                  2  2'Tekst '
6.35 A                                      DSPATR(RI)
6.35 A            B15TX1        70      4  2
6.35 A            B15TX2        70      5  2
6.35 A            B15TX3        70      6  2
6.35 A            B15TX4        70      7  2
6.35 A            B15TX5        70      8  2
6.35 A                                 10  2'F3=Gå tilbake'
6.35 A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
