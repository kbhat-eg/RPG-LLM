     A*%%TS  SD  20140716  101010  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JX100D
     A* Beskrivelse . . : Priser, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       310511 bof  Utvidet med skrivebeskyttelse på felter
7.xx A*       060616 bsa  Endringer relatert til ny GUI
7.01 A*       011119 BOF  Linje blir rød hvis prisgiver er utgått
     A*
     A* -------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140714  144840  BOF630      REL-V7R1M0  5770-WDS
     A            B1RSTS    R        H      REFFLD(JXRSTS)
     A            B1VALG    R        B 12  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1LDOR    R        O 12  4REFFLD(JXLDOR)
7.01 A  50                                  COLOR(RED)
     A            B1LNAV    R   -7   O 12 11REFFLD(JLNAVN)
7.01 A  50                                  COLOR(RED)
     A            B1PRIS    R        O 12 40REFFLD(JXPRIS)
     A                                      EDTCDE(M)
7.01 A  50                                  COLOR(RED)
     A            B1GDAT         6Y 0O 12 52EDTWRD('  .  .  ')
7.01 A  50                                  COLOR(RED)
     A            B1TDAT         6Y 0O 12 62EDTWRD('  .  .  ')
7.01 A  50                                  COLOR(RED)
     A            B1CRDO    R        O 12 77REFFLD(JXCRDO)
7.01 A  50                                  COLOR(RED)
     A            B1RKOD         1A  O 12 79
7.01 A  50                                  COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140714  144840  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av priser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Varenummer .'
     A                                      COLOR(BLU)
7.xx A            B2VARE    R   -7   O  5 15REFFLD(JXVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2TEK1    R        O  5 24REFFLD(JVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  6  2'Leverandør .'
     A                                      COLOR(BLU)
7.xx A            B2NUMM    R        O  6 15REFFLD(JXLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2LNAV    R        O  6 24REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  7  2'Prisenhet. .'
     A                                      COLOR(BLU)
7.xx A            B2ENHE    R        O  7 15REFFLD(JWENHE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  8  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 11'Navn                     '
7.xx A                                      COLOR(WHT)
7.xx A                                 11 41'Nobb-pris'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 52'Fra Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 62'Til Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 77'C'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 79'R'
7.xx A                                      COLOR(WHT)
7.xx A**                                8  2'V Lev.nr Navn                     -
7.xx A**                                         Nobb-pris  Fra Dato  Til Dato -
7.xx A**                                          C R'
7.xx A**                                    COLOR(WHT)
7.xx A                                  9  2'+'
7.xx A            B2LDOR    R        B  9  4REFFLD(JXLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020819  142849  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake   F4=Spør-
     A                                      ring   F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020820  090053  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1RSTS    R        H      REFFLD(JXRSTS)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av priser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare . . . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(JXVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  6 31REFFLD(JXLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2'Gyldig fra-dato. . . . . . .'
     A            C1GDAT         6Y 0B  7 31
     A  32                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20000918  135509  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Nobb-pris finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140715  085750  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            C1RSTS    R        H      REFFLD(JXRSTS)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av priser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare . . . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(JXVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  6 31REFFLD(JXLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Gyldig fra-dato. . . . . . .'
     A            C1GDAT         6Y 0O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  9  2'Nobb-pris. . . . . . . . . .'
     A            C2PRIS    R        B  9 31REFFLD(JXPRIS)
     A  31                                  ERRMSGID(JMX0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  60
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(K)
     A                                 11  2'Gyldig til-dato. . . . . . .'
     A            C2TDAT         6Y 0B 11 31COLOR(YLW)
     A  33                                  ERRMSGID(JM00250 *LIBL/QMFVADM 33)
     A                                      CHECK(RB)
     A  62
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 13  2'Leverandørens varenr . . . .'
6.34 A            C2LVAR    R        B 13 31REFFLD(JVLLVA)
     A                                      COLOR(YLW)
6.34 A  68
6.34 AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Frakt. . . . . . . . . . . .'
     A            C2FBEL    R        B 16 31REFFLD(JXFBEL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  65
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(K)
     A                                 18  2'Valutakode . . . . . . . . .'
     A            C2VALU    R        B 18 31REFFLD(JXVALU)
     A                                      COLOR(YLW)
     A  66
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Cross-docking. . . . . . . .'
     A            C2CRDO    R        B 20 31REFFLD(JXCRDO)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      COLOR(YLW)
     A  67
     AO 30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20 36'1=Ja,0=Nei'
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(JMEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20140716  101010  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A            D1RSTS    R        H      REFFLD(JXRSTS)
     A                                  2  2' Slette Nobb-pris '
     A                                      DSPATR(RI)
     A                                  4  2'Vare. . . . . .'
     A            D1VARE    R        O  4 18REFFLD(JXVARE)
     A                                  5  2'Leverandør. . .'
     A            D1LDOR    R        O  5 18REFFLD(JXLDOR)
     A                                  6  2'Gyldig fra-dato'
     A            D1GDAT         6Y 0O  6 18
     A                                  8  2'Nobb-pris . . .'
     A            JXPRIS    R        O  8 18EDTCDE(K)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20140715  142908  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            K1RSTS    R        H      REFFLD(JXRSTS)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Vare. . . . . .'
     A            K1VARE    R        O  4 18REFFLD(JXVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Leverandør. . +'
     A            K1LDOR    R        B  5 18REFFLD(JXLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMX0030 *LIBL/QMFVADM 32)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Pris Dato . . .'
     A            K1GDAT         6Y 0B  6 18
     A  36                                  ERRMSGID(JML0010 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Varenummer. . .'
     A            JXVARE    R        O 10 18OVRDTA
     A                                 11  2'Leverandør. . .'
     A            JXLDOR    R        O 11 18OVRDTA
     A                                 12  2'Pris Dato . . .'
     A            K2GDAT         6Y 0O 12 18OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20140714  150213  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 13 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Filter '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgiver  . . . . +'
     A            F1LDOR    R        B  4 23REFFLD(JXLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            F1LNAV        28A  O  4 30COLOR(YLW)
     A                                  5  2'Vise utgåtte priser '
     A            F1UTGP         1Y 0B  5 23COLOR(YLW)
     A                                  5 31'1=Ja,0=Nei'
     A                                 12  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
