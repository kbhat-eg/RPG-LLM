     A*%%TS  SD  20070723  140638  ASP_MGR     REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A* 7.00 030316  BSA  Endringer pga ny GUI
     A*------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A            B1LEAN        13  0H
7.xx A            B1VALG    R        B 10  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1REFN        30     10  4
7.xx A            B1TYPE         4     10 35
7.xx A            B1MNUM    R        O 10 40REFFLD(LÅLDOR)
7.xx A            B1MDA1         6Y 0O 10 48EDTCDE(Y)
7.xx A**          B1MDA2         6Y 0O 10 58EDTCDE(Y)
7.xx A            B1STAT         2   O 10 58
7.xx A            B1EROR         2   O 10 61
7.xx A            B1LEAT        13   O 10 68
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på EDI logg'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør'
     A                                      COLOR(BLU)
     A            B2LEVN    R        O  4 13REFFLD(LÅLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV        30   O  4 20
     A                                      COLOR(BLU)
     A                                  4 66'År. . . .'
     A                                      COLOR(BLU)
     A            B2AARR    R        O  4 76REFFLD(SLAARR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LADR        30   O  5 20
     A                                      COLOR(BLU)
7.xx A                                  6  4'Referanse nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 35'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 40'EDI-løp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'5=Vis logg'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Referanse nr'
     A                                      COLOR(WHT)
7.xx A                                  9 35'Type'
     A                                      COLOR(WHT)
7.xx A                                  9 40'EDI-løp'
     A                                      COLOR(WHT)
7.xx A                                  9 48'Mot1.dato'
     A                                      COLOR(WHT)
7.xx A                                  9 58'St'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 62'Ek'
     A                                      COLOR(WHT)
7.xx A                                  9 68'EAN-leverand.'
     A                                      COLOR(WHT)
7.xx A            B2REFN        30   B  7  4
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2TYPE         4   B  7 35
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2MNUM    R        B  7 40REFFLD(LÅLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A                                 23  2'F1=Filter  F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R B3WIN
     A                                      WINDOW(*DFT 17 65)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2 19'Viser EDI logg'
     A                                      COLOR(WHT)
     A                                  3  2'Leverandør'
     A                                      COLOR(BLU)
     A            B2LEVN    R        O  3 13REFFLD(LÅLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV        30   O  3 20
     A                                      COLOR(BLU)
     A                                  3 56'År:'
     A                                      COLOR(BLU)
     A            B2AARR    R        O  3 60REFFLD(SLAARR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LADR        30   O  4 20
     A                                      COLOR(BLU)
     A                                  5  2'Referansenr . . :'
     A            B3REFN        35      5 20
     A                                  6  2'EDI-løpenummer. :'
     A            B3MNUM    R        O  6 20REFFLD(LÅLDOR)
     A                                  7  2'Mot1.dato/tid . :'
     A            B3MDA1         6Y 0O  7 20EDTCDE(Y)
     A            B3MTI1         8   O  7 30
     A                                  8  2'Mot2.dato/tid . :'
     A            B3MDA2         6Y 0O  8 20EDTCDE(Y)
     A            B3MTI2         8   O  8 30
     A**                                9  2'Mot3.dato/tid . :'
     A**          B3MDA3         6Y 0O  9 20EDTCDE(Y)
     A**          B3MTI3         8   O  9 30
     A                                  9  2'Godk. dato/tid. :'
     A            B3GODD         6Y 0O  9 20EDTCDE(Y)
     A            B3GODT         8   O  9 30
     A            B3GODU        10   O  9 40
     A                                 10  2'Utsk. dato/tid. :'
     A            B3SKRD         6Y 0O 10 20EDTCDE(Y)
     A            B3SKRT         8   O 10 30
     A            B3SKRU        10   O 10 40
     A                                 11  2'Avsl. dato/tid. :'
     A            B3AVSD         6Y 0O 11 20EDTCDE(Y)
     A            B3AVST         8   O 11 30
     A            B3AVSU        10   O 11 40
     A                                 12  2'EAN-leverandør. :'
     A            B3LEAN        13  0O 12 20
     A            B3LDOT        20   O 12 35
     A                               O 13  2'Status. . . . . :'
     A            B3STAT         2   O 13 20
     A                               O 13 30'Errorkode . . . :'
     A            B3EROR         2   O 13 48
     A                               O 14  2'Feilbeskrivelse :'
     A            B3MRK1        30   O 14 20
     A            B3MRK2        50   O 15  2
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20051016  223621  ASK530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 13 55)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5  2'Meldingsdato. .'
     A            F1SMDA         6Y 0B  5 18COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  6  2'Kun feil. . . .'
     A            F1EROR    R        B  6 18REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      OVRDTA
     A                                  6 20'1=Ja'
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
