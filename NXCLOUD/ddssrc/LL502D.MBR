     A*%%TS  SD  20031107  140655  ASK         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL502D
     A* Beskrivelse . . : Lagerspørring, vise alle lagre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*        170117 bsa Justeringer basert på ny GUI og regler
8.01 A*        190623 BOF skal vise F8 knapp, det er switch som styrer hva
     A*                   som skal vises.
8.03  * 800   211124 ask lagt inn mulighet for å vise
8.03  *                  disponibel beholdning med bekr. NXS-20463
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.00 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R D1SFL                     SFL
     A*%%TS  SD  20001215  092459  ASK         REL-V4R4M0  5769-PW1
     A            D1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      VALUES(0 2 5 6 7 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            D1LAGE    R        O  8  4REFFLD(VLLAGE)
     A                                      EDTWRD('0 ')
     A            D1LTEK        13A  O  8  7
     A            D1DISP         9Y 2O  8 23EDTWRD('     0 ,  -')
     A  41                                  COLOR(RED)
     A            D1BEHL         9Y 2O  8 37EDTWRD('     0 ,  -')
8.03 A            D1BBEK         9Y 2O  8 51EDTWRD('     0 ,  -')
8.03 A            D1BBES         9Y 2O  8 65EDTWRD('     0 ,  -')
8.03 A N31                                  DSPATR(ND)
8.03 A            D1OORD         9Y 2O  8 80EDTWRD('     0 ,  -')
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20031107  140655  ASK         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CF08
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise lagerinformasjon, alle lager'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL502D2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare:'
     A                                      COLOR(BLU)
     A            D2VARE    R   -7   O  4  8REFFLD(VVVARE)
     A                                      COLOR(BLU)
     A            D2TEK1    R        O  4 17REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                  4 58'Lagerenhet:'
     A                                      COLOR(BLU)
     A            D2ENHL         3   O  4 70COLOR(BLU)
     A                                  6  2'5=Detaljer  6=Disp.dato  7=Histori-
     A                                      kk  9=Innkjøpsplanlegging'
     A                                      COLOR(BLU)
8.03 A                                  7  2'V'
8.03 A                                      COLOR(WHT)
8.03 A                                  7  4'Lager'
8.03 A                                      COLOR(WHT)
8.03 A                                  7 23'Disp.lager'
8.03 A                                      COLOR(WHT)
8.03 A                                  7 40'I lager'
8.03 A                                      COLOR(WHT)
8.03 A  31                              7 52'Bekr best'
8.03 A  31                                  COLOR(WHT)
8.03 A N31                              7 52'   I best'
8.03 A N31                                  COLOR(WHT)
8.03 A  31                              7 69'Ubekr best'
8.03 A  31                                  COLOR(WHT)
8.03 A N31                              7 66'          '
8.03 A N31                                  COLOR(WHT)
8.03 A                                  7 83'I ordre'
8.03 A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  20020924  125918  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
8.01 A                                 23 27'F8=Andre lager'
     A                                      COLOR(BLU)
     A          R D8WIN
     A*%%TS  SD  20030516  102030  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 8 32)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'Dette er en kundetilpasset'
     A                                  4  2'funksjonalitet og må lages'
     A                                  5  2'i hvert enkelt tilfelle.'
     A                                  1  2'Informasjonsmelding'
     A                                      DSPATR(RI)
     A          R ASUME                     ASSUME
     A                                 24 79' '
