     A*%%TS  SD  20050720  145634  ASP         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : SP123D
     A* Beskrivelse . . : Vedlikehold av parameterfil-distrikt
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050720  145634  ASP         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B 12  2REFFLD(SÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('4' ' ')
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A            B1HKOD    R        O 12  5REFFLD(RFRFR/RAHKOD *LIBL/RFRF)
     A            B1TEXT        20A  O 12 10
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030731  090604  MHA         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'AS Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av parameterfil'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Type:'
     A            B2TYPE    R        O  4  8REFFLD(SFRFR/SPTYPE *LIBL/SFRF)
     A            B2TEXT        30A  O  4 19
     A                                  4 65'Distrikt'
     A                                      COLOR(WHT)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Distrikt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  9  2'4=Slette'
     A                                      COLOR(BLU)
7.xx A                                 11  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 11  5'Dist'
7.xx A                                      COLOR(WHT)
7.xx A                                 11 10'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2VALG         1A  B  7  2COLOR(YLW)
     A                                      DSPATR(PC)
7.xx A            B2HKOD    R        B  7  5REFFLD(RFRFR/RAHKOD *LIBL/RFRF)
     A  31                                  ERRMSGID(SKD1110 *LIBL/QMFSTAT 31)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20030731  084339  MHA         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 27'F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20010613  150729  KRKRI       REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      CLRL(*ALL)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'AS Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av parameterfil'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Distriktkategori . . . . . +'
     A            C1HKOD    R        B  5 32REFFLD(RFRFR/RAHKOD *LIBL/RFRF)
     A  32                                  ERRMSGID(SRA0820 *LIBL/QMFSTAT 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990309  091603  MWE         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Distrikt er valgt'
     A                                  6  2'F3=Gå tilbake   F12=Annuler'
     A                                      COLOR(BLU)
     A                                  4  2'tidligere'
     A          R C2BLD
     A*%%TS  SD  19980622  125233  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'AS Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av parameterfil'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Distrikt . . . . . . . . . :'
     A            C2HKOD    R        B  5 31REFFLD(RFRFR/RAHKOD *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                  7  2'Tekst. . . . . . . . . . . :'
     A            C2TEXT    R        O  7 31REFFLD(RFRFR/RAHTXT *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20010911  114756  KRKRI       REL-V4R4M0  5769-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette post '
     A                                      DSPATR(RI)
     A                                  4  2'Distrikt. . . :'
     A            D1HKOD    R        O  4 18REFFLD(RFRFR/RAHKOD *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                  5  2'Tekst . . . . :'
     A            D1TEXT    R        O  5 18REFFLD(RFRFR/RAHTXT *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                  9  2'F3=Gå tilbake   F12=Annuler'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 10 55)
