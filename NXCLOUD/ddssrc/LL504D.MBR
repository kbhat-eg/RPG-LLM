     A*%%TS  SD  20120829  135828  ASP_N50     REL-V6R1M0  5761-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL504D
     A* Beskrivelse . . : Lagerspørring, detaljinformasjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.01 A* 7.01  290812 AMD  Ønske fra Gipling om at lagerplass alternativ
7.01 A*                   1 og 2 må komme fram i skjermbildet.
8.01 A*       271124 ask  Utvidet skjermbredde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20120829  135828  ASP_N50     REL-V6R1M0  5761-WDS
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise lagerdetaljer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . .'
     A            B1VARE         8Y 0B  5 19
     A  31                                  ERRMSGID(LMD0140 *LIBL/QMFLAGR 31)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A            B1TEK1    R        O  5 30REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                  6  2'Lagernummer. . .'
     A            B1LAGE    R        B  6 19REFFLD(VLLAGE)
     A                                      CHECK(ER)
     A                                      CHECK(RB)
     A                                      EDTWRD('0 ')
     A                                      COLOR(YLW)
     A            B1LATX        20A  O  6 30COLOR(YLW)
     A                                  8  2'Lagerplass . . .'
     A            B1PLAS    R        O  8 19REFFLD(VLPLAS)
     A                                      COLOR(YLW)
     A                                  8 30'Bestillingspkt.'
     A            B1BPUN    R        O  8 46REFFLD(VLBPUN)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A                                  8 60'Pris pr..'
     A            B1ENH1    R        O  8 70REFFLD(FÅENHE)
     A                                      COLOR(YLW)
     A                                  9  2'Leverandør . . .'
     A            B1LDOR    R        O  9 19REFFLD(VVLDOR)
     A                                      COLOR(YLW)
     A                                  9 30'Best. kvantum .'
     A            B1BMEN    R        O  9 46REFFLD(VLBMEN)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A                                  9 60'Kostpris'
     A N49                                  DSPATR(ND)
     A            B1KPNY    R        O  9 70REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      EDTWRD('     0 ,  ')
     A N49                                  DSPATR(ND)
     A                                 10  2'Varegruppe . . .'
     A            B1OGRP    R        O 10 19REFFLD(VVOGRP)
     A                                      COLOR(YLW)
     A            B1HGRP    R        O 10 22REFFLD(VVHGRP)
     A                                      COLOR(YLW)
     A            B1UGRP    R        O 10 25REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                 10 30'Sikkerhetslager'
     A            B1SIKL    R        O 10 46REFFLD(VLSIKL)
     A                                      EDTWRD('      0 ,   ')
     A                                      COLOR(YLW)
     A                                 10 60'Salgspris'
     A            B1SPNY    R        O 10 70REFFLD(VPSAPR)
     A                                      EDTWRD('     0 ,  ')
     A                                      COLOR(YLW)
     A                                 11  2'Siste trans.dato'
     A            B1TRDA         6Y 0O 11 19EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
7.01 A                                 11 30'Lagerpl. alt1 .'
7.01 A            B1PLA1    R        O 11 46REFFLD(VLPLAS)
7.01 A                                      COLOR(YLW)
     A                                 12  2'Siste opptelling'
     A            B1OPDA         6Y 0O 12 19EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
7.01 A                                 12 30'Lagerpl. alt2 .'
7.01 A            B1PLA2    R        O 12 46REFFLD(VLPLAS)
7.01 A                                      COLOR(YLW)
     A                                 12 60'Lagerenhet'
     A            B1ENHL    R        O 12 71REFFLD(VVENHL)
     A                                      COLOR(YLW)
     A                                 14  2'Inngang i år'
     A            B1INNG    R        O 14 19REFFLD(VLINNG)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 14 44'  Lagerbeholdning'
     A            B1BEHL    R        O 14 63REFFLD(VLBEHL)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 15  2'Uttak i år'
     A            B1UTTA    R        O 15 19REFFLD(VLUTTA)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 15 44'- For utlevering'
     A            B1PAKK    R        O 15 63REFFLD(VLPAKK)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 16  2'Justering i år'
     A            B1JUST    R        O 16 19REFFLD(VLJUST)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 16 63'-------------'
     A                                 17  2'Salg i år'
     A            B1SALG    R        O 17 19REFFLD(VLSALG)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 17 44'='
     A            B1TOTL    R        O 17 63REFFLD(VLPAKK)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 18  2'Telle-avvik i år'
     A            B1TELL    R        O 18 19REFFLD(VLTELL)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 18 44'- I ordre'
     A            B1OORD    R        O 18 63REFFLD(VLOORD)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 19  2'Saldo pr. 1/1'
     A            B1B101    R        O 19 19REFFLD(VLB101)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 19 44'+ I bestilling'
     A            B1BBES    R        O 19 63REFFLD(VLBBES)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 20 63'-------------'
     A                                 21  2'Ant. bevegelser'
     A            B1BEVE    R    8 00O 21 19REFFLD(VLBEVE)
     A                                      EDTWRD('      0 ')
     A                                      COLOR(YLW)
     A                                 21 44'= Disp. lager'
     A            B1DISP    R        O 21 63REFFLD(VLBRES)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
