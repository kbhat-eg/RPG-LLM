     A*%%TS  SD  20140821  104212  BOF630      REL-V7R1M0  5770-WDS
     A* 210814 BOF : Utvidet med overføring av fraktbet.
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140821  104212  BOF630      REL-V7R1M0  5770-WDS
     A            B1ERST    R        H      REFFLD(JPERST)
     A            B1ELIN    R        H      REFFLD(JPELIN)
     A            B1VALG         2Y 0B 15  2COLOR(YLW)
     A                                      VALUES(2 3 4 5 9 16 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
     A            B1ERAE    R        O 15  5REFFLD(JPERAE)
     A            B1EREG    R        O 15 43REFFLD(JPEREG)
     A            B1EVER    R        O 15 47REFFLD(JPEVER)
     A            B1EFDA         6Y 0O 15 59EDTWRD('  .  .  ')
     A            B1ETDA         6Y 0O 15 68EDTWRD('  .  .  ')
     A            B1AKTI         1A  O 15 77
     A            B1RKOD         1A  O 15 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140821  104212  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rabattelement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2ELDO    R        O  4 31REFFLD(JPELDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LNAV    R        O  4 41REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Prisgruppe . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2EPRG    R        O  5 31REFFLD(JPEPRG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Varegruppe . . ... . . . . .'
     A                                      COLOR(BLU)
     A            B2EOGR    R        O  6 31REFFLD(JPEOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2EHGR    R        O  6 34REFFLD(JPEHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2EUGR    R        O  6 37REFFLD(JPEUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Modulnummer. . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2EMOD    R        O  7 31REFFLD(JPEMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2MTE1    R        O  7 41REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Varenummer . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2EVAR    R   -7   O  8 31REFFLD(JPEVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TEK1    R        O  8 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Nobbnummer . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2NOBB    R        O  9 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varetype . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2EVTY    R        O 10 31REFFLD(JPEVTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Leveringstype. . . . . . . .'
     A                                      COLOR(BLU)
     A            B2ELET    R        O 11 31REFFLD(JPELET)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Betingelse . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2BETI    R        O 12 31REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       9=Aktiviser/passiviser'
     A                                      COLOR(BLU)
     A                                 13 61'16=Overfør Frakt'
     A                                      COLOR(BLU)
     A                                 14  2'V  Rabattelement                  -
     A                                           Regel     Verdi   Fra-dato Til-
     A                                      -dato   R'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020819  134103  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19990812  155627  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            C1ERST    R        H      REFFLD(JPERST)
     A            C1ELIN    R        H      REFFLD(JPELIN)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rabattelement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rabattelement. . . . . . . .'
     A            C1ERAE    R        B  5 31REFFLD(JPERAE)
     A  31                                  ERRMSGID(JMP1010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19980515  155214  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Rabattelement finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020826  103434  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1ERST    R        H      REFFLD(JPERST)
     A            C1ELIN    R        H      REFFLD(JPELIN)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rabattelement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rabattelement. . . . . . . .'
     A            C1ERAE    R        B  5 31REFFLD(JPERAE)
     A  31                                  ERRMSGID(JMP1010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7  2'Regel. . . . . . . . . . . .'
     A            C2EREG    R        B  7 31REFFLD(JPEREG)
     A  32                                  ERRMSGID(JMP1100 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JMP1110 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 44'1=Rabatt/Følgerabatt'
     A                                  8 44'2=Tilleggsrabatt'
     A                                  9 44'N=Nettopris'
     A                                 10 44'K=Kronetillegg'
     A                                 12  2'Verdi. . . . . . . . . . . .'
     A            C2EVER    R        B 12 31REFFLD(JPEVER)
     A  34                                  ERRMSGID(JMP1120 *LIBL/QMFVADM 34)
     A  35                                  ERRMSGID(JMP1130 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12 44'(Rabatt-prosent eller beløp)'
     A                                 14  2'Fra-dato . . . . . . . . . .'
     A            C2EFDA         6Y 0B 14 31COLOR(YLW)
     A  36                                  ERRMSGID(JM00250 *LIBL/QMFVADM 36)
     A  38                                  ERRMSGID(JM00260 *LIBL/QMFVADM 38)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 15  2'Til-dato . . . . . . . . . .'
     A            C2ETDA         6Y 0B 15 31COLOR(YLW)
     A  37                                  ERRMSGID(JM00250 *LIBL/QMFVADM 37)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Opprettet dato . . . . . . .'
     A            C2EODA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EEDA         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EEUS    R        O 21 31REFFLD(JPEEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C4WIN
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du overfører til Fraktbetingelse'
     A                                  3  2'Hvis OK, tast <Enter>           '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C4WINA
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du kan kun overføre på varenivå'
     A                                  3  2'sjekk betingelse <enter>        '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C4WINB
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du kan kun overføre kronetillegg'
     A                                  3  2'sjekk betingelse <enter>        '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990812  155627  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A            D1ERST    R        H      REFFLD(JPERST)
     A            D1ELIN    R        H      REFFLD(JPELIN)
     A                                  2  2' Slette rabattelement '
     A                                      DSPATR(RI)
     A                                  4  2'Rabattelement .'
     A            JPERAE    R        O  4 18REFFLD(JPERAE)
     A                                  6  2'Regel . . . . .'
     A            JPEREG    R        O  6 18
     A                                  7  2'Verdi . . . . .'
     A            JPEVER    R        O  7 18
     A                                  9  2'Fra-dato. . . .'
     A            D1EFDA         6Y 0O  9 18EDTWRD('  .  .  ')
     A                                 10  2'Til-dato. . . .'
     A            D1ETDA         6Y 0O 10 18EDTWRD('  .  .  ')
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19980515  181344  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            K1ERST    R        H      REFFLD(JPERST)
     A            K1ELIN    R        H      REFFLD(JPELIN)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Rabattelement .'
     A            K1ERAE    R        B  4 18REFFLD(JPERAE)
     A  31                                  ERRMSGID(JMP1010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JMP1030 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Rabattelement .'
     A            JPERAE    R        O  9 18OVRDTA
     A                                 10  2'Regel . . . . .'
     A            JPEREG    R        O 10 18OVRDTA
     A                                 11  2'Verdi . . . . .'
     A            JPEVER    R        O 11 18OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
