     A*%%TS  SD  20081210  090129  ASP_MGR     REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JX800D
     A* Beskrivelse . . : Eksport av XML fil
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       030117 bsa  Endringer relatert til ny GUI
     A*
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20081210  090129  ASP_MGR     REL-V5R4M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadminstrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Danne XML-fil priser             '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 33'Fra'
7.xx A                                  5 52'Til'
7.xx A                                  7  2'Varegruppe . . . . . . . . +'
     A            A1FOGR         2D 0B  7 31CHECK(RZ)
     A  32                                  ERRMSGID(JMO0020 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JMO0010 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
7.xx A                                  7 34'+'
7.xx A            A1FHGR         2D 0B  7 36CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 39'+'
     A            A1FUGR         3D 0B  7 41CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 50'+'
7.xx A            A1TOGR         2D 0B  7 52CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 55'+'
7.xx A            A1THGR         2D 0B  7 57CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 60'+'
7.xx A            A1TUGR         3D 0B  7 62CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Leverandørnummer . . . . . +'
     A            A1LDOR         6D 0B  8 31CHECK(RZ)
     A  35                                  ERRMSGID(JML0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
7.xx A                                  9  2'Varesortiment  . . . . . . +'
     A            A1VSOR    R        B  9 31REFFLD(VCVSOR)
     A  36                                  ERRMSGID(JMT0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                 10  2'Mappe for xml-filen  . . . .'
     A            A1MAPP        10A  B 10 31
     A  37                                  ERRMSG('Mappe må fylles ut' 37)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                  8 44'Lev. i VA'
     A                                  9 44'Sortiment i EVR'
