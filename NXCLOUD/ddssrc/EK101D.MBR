     A*%%TS  SD  20100305  135445  ASP_A04     REL-V6R1M0  5761-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EK101
     A* Beskrivelse . . : BONUS
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.23 A*       111113 BSA  Reaktivert bruk av leveringstype
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A*--------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051021  082418  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1RKAT    R        O 10  4REFFLD(EKRKAT)
     A            B1KUND    R        O 10  8REFFLD(EKKUND)
     A            B1KPRO    R        O 10 15REFFLD(EKKPRO)
     A            B1OGRP    R        O 10 22REFFLD(EKOGRP)
     A            B1HGRP    R        O 10 25REFFLD(EKHGRP)
     A            B1UGRP    R        O 10 28REFFLD(EKUGRP)
     A            B1LDOR    R        O 10 32REFFLD(EKLDOR)
     A            B1MODN    R        O 10 39REFFLD(EKMODN)
     A            B1VARE    R   -7   O 10 48REFFLD(EKVARE)
     A**          B1ENHE    R        O 10 57REFFLD(EKENHE)
6.23 A            B1LETY    R        O 10 57REFFLD(EKLETY)
     A**          B1BOTY    R        O 10 63REFFLD(EKBOTY)
6.23 A            B1TEXT         4A  O 10 59
     A            B1SPER    R        O 10 64REFFLD(EKLETY)
     A            B1FDAT         6Y 0O 10 66EDTCDE(Z)
     A            B1TDAT         6  0O 10 73EDTCDE(Z)
     A            B1MERK         1A  O 10 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051021  131856  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundebonus'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde . .'
     A**                                    COLOR(BLU)
     A            B2BKUN    R        O  4 12REFFLD(EKBKUN)
     A            B2BKNA        30A  O  4 19
     A                                  5  2'Bonustype'
     A**                                    COLOR(BLU)
     A            B2TBOT    R        O  5 12REFFLD(VATBOT)
     A            B2TBES    R        O  5 15REFFLD(VATBES)
7.xx A                                  6  4'Rkat'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 19'Kprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 28'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 33'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 38'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 44'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 53'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 64'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 75'Lt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'  2=Endre  3=Kopier  4=Slett  5=Vi-
     A                                      s  7=Bonusdetaljer'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Rkat'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  9'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 15'Kprosj'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 22'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 25'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 28'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 32'Levnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 39'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 48'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 57'Lt'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 64'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 67'FDato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 74'TDato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 80'M'
7.xx A                                      COLOR(WHT)
7.xx A**                                8  4'kat        prosj.                 -
7.xx A**                                                                 dato  -
7.xx A**                                     dato'
7.xx A**                                    COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A            B2RKAT    R        B  7  4REFFLD(EKRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  7  8'+'
7.xx A            B2KUND    R        B  7 10REFFLD(EKKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 17'+'
7.xx A            B2KPRO    R        B  7 19REFFLD(EKKPRO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 26'+'
7.xx A            B2OGRP    R        B  7 28REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 31'+'
7.xx A            B2HGRP    R        B  7 33REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 36'+'
7.xx A            B2UGRP    R        B  7 38REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 42'+'
7.xx A            B2LDOR    R        B  7 44REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 51'+'
7.xx A            B2MODN    R        B  7 53REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 62'+'
7.xx A            B2VARE    R   -7   B  7 64REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A**          B2ENHE    R        B  9 57REFFLD(EKENHE)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
7.xx A                                  7 73'+'
7.xx A            B2LETY    R        B  7 75REFFLD(EKLETY)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
     A**          B2BOTY    R        B  9 63REFFLD(EKBOTY)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A          R B2CMD
     A*%%TS  SD  20051026  223816  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051028  085705  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprette bonusnivå for kunde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Rabattkategori . . . . +'
7.xx A            C1RKAT    R        B  4 27REFFLD(EKRKAT)
     A  33                                  ERRMSGID(EMK0060 *LIBL/QMFOFAK 33)
     A  36                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1RTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Kunde. . . . . . . . . +'
7.xx A            C1KUND    R        B  5 27REFFLD(EKKUND)
     A  38                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Kundeprosjekt. . . . . +'
7.xx A            C1KPRO    R        B  6 27REFFLD(EKKPRO)
     A  39                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1PNAV    R        O  6 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varegruppe . . . . . . +'
     A            C1OGRP    R        B  7 27REFFLD(EKOGRP)
     A  41                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 30'+'
7.xx A            C1HGRP    R        B  7 32REFFLD(EKHGRP)
     A  42                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 35'+'
     A            C1UGRP    R        B  7 37REFFLD(EKUGRP)
     A  43                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1GTXT    R        O  7 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Leverandør . . . . . . +'
7.xx A            C1LDOR    R        B  8 27REFFLD(EKLDOR)
     A  44                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV        30A  O  8 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Modulnr. . . . . . . . +'
7.xx A            C1MODN    R        B  9 27REFFLD(EKMODN)
     A  34                                  ERRMSGID(EMK0070 *LIBL/QMFOFAK 34)
     A  45                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTXT        35A  O  9 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Varenr . . . . . . . . +'
7.xx A            C1VARE    R   -7   B 10 27REFFLD(EKVARE)
     A  35                                  ERRMSGID(EMK0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                               11  2'Enhet. . . . . . . . . . . .'
     A**          C1ENHE    R        B 11 31REFFLD(EKENHE)
     A**47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A**          C1ETXT    R        O 11 41REFFLD(VAETXT)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
7.xx A                                 11  2'Leveringstype. . . . . +'
7.xx A            C1LETY    R        B 11 27REFFLD(EKLETY)
6.23 A  48                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 48)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
6.23 A            C1LTXT    R        O 11 41REFFLD(VYBESK)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
7.xx A                                 12  2'Bonustype. . . . . . . .'
7.xx A            C1BOTY    R        O 12 27REFFLD(EKBOTY)
     A  31                                  ERRMSGID(EMK0050 *LIBL/QMFOFAK 31)
     A  49                                  ERRMSGID(VMA6520 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.23 A            C1BTXT    R        O 12 41REFFLD(VATBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Fra-dato . . . . . . . .'
7.xx A            C1FDAT         6Y 0B 13 27COLOR(YLW)
     A  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A  52                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 52)
     A  53                                  ERRMSGID(EMK0100 *LIBL/QMFOFAK 53)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                 14  2'Til-dato . . . . . . . .'
7.xx A            C1TDAT         6Y 0B 14 27COLOR(YLW)
     A  51                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 51)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 18  2'Dersom det ikke velges rabattkateg-
     A                                      ori, kunde eller kundeprosjekt regn-
     A                                      es bonus'
     A                                 19  2'mot salg som er fakturert bonuskun-
     A                                      den.'
     A                                 20  2'Dersom Kunde oppgis regnes bonus m-
     A                                      ot alt salg til denne kunden uavhen-
     A                                      gig av'
     A                                 21  2'fakturakunde/bonuskunde.'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20051021  083406  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bonus-nivå finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051026  142713  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF20
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonus-nivå'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rabattkategori . . . . . . .'
     A            C1RKAT    R        O  4 31REFFLD(EKRKAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1RTXT        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kunde. . . . . . . . . . . .'
     A            C1KUND    R        O  5 31REFFLD(EKKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KNAV        30A  O  5 41COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundeprosjekt. . . . . . . .'
     A            C1KPRO    R        O  6 31REFFLD(EKKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PNAV    R        O  6 41REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . . . . . . . .'
     A            C1OGRP    R        O  7 31REFFLD(EKOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGRP    R        O  7 34REFFLD(EKHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGRP    R        O  7 37REFFLD(EKUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1GTXT    R        O  7 41REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(EKLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  8 41COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O  9 31REFFLD(EKMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTXT        35A  O  9 41COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenr . . . . . . . . . . .'
     A            C1VARE    R   -7   O 10 31REFFLD(EKVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VTXT    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                               11  2'Enhet. . . . . . . . . . . .'
     A**          C1ENHE    R        O 11 31REFFLD(EKENHE)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A**          C1ETXT    R        O 11 41REFFLD(VAETXT)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
6.23 A                                 11  2'Leveringstype. . . . . . . .'
6.23 A            C1LETY    R        O 11 31REFFLD(EKLETY)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
6.23 A            C1LTXT    R        O 11 41REFFLD(VYBESK)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
6.23 A                                 12  2'Bonustype. . . . . . . . . .'
6.23 A            C1BOTY    R        O 12 31REFFLD(EKBOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.23 A            C1BTXT    R        O 12 41REFFLD(VATBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.23 A                                 13  2'Fra-dato . . . . . . . . . .'
6.23 A            C1FDAT         6Y 0O 13 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.23 A                                 14  2'Til-dato . . . . . . . . . .'
6.23 A            C1TDAT         6Y 0O 14 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.23 A                                 15  2'Sperrekode . . . . . . . . .'
6.23 A            C2SPER    R        B 15 31REFFLD(EKLETY)
     A  31                                  ERRMSG('Gyldi verdi er 0 og 1')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.23 A                                 16  2'Bonusbyggende. . . . . . . .'
6.23 A                                 16 33'1=Nei'
6.23 A            C2EBYG    R        B 16 31REFFLD(EKLETY)
6.25 A  32                                  ERRMSG('Gyldi verdi er 0 og 1')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.23 A                                 17  2'Merknad. . . . . . . . . . .'
6.23 A            C2MERK    R        B 17 31REFFLD(EKMERK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Beregnet bonusgrunnlag . . .'
     A            C2BGRU    R        O 19 31REFFLD(EKBGRU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Beregnet bonus . . . . . . .'
     A            C2SBON    R        O 20 31REFFLD(EKSBON)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 21  2'Beregnet dato. . . . . . . .'
     A            C2BDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Beregnet tidspunkt . . . . .'
     A            C2BTIM         6Y 0O 22 31COLOR(YLW)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(FREUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051027  213608  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bonus-nivå '
     A                                      DSPATR(RI)
     A                                  4  2'Rabattkategori . .'
     A            D1RKAT    R        O  4 21REFFLD(EKRKAT)
     A                                  5  2'Kunde. . . . . . .'
     A            D1KUND    R        O  5 21REFFLD(EKKUND)
     A                                  6  2'Kundeprosjekt. . .'
     A            D1KPRO    R        O  6 21REFFLD(EKKPRO)
     A                                  7  2'Varegruppe . . . .'
     A            D1OGRP    R        O  7 21REFFLD(EKOGRP)
     A            D1HGRP    R        O  7 24REFFLD(EKHGRP)
     A            D1UGRP    R        O  7 27REFFLD(EKUGRP)
     A                                  8  2'Leverandør . . . .'
     A            D1LDOR    R        O  8 21REFFLD(EKLDOR)
     A                                  9  2'Modulnr. . . . . .'
     A            D1MODN    R        O  9 21REFFLD(EKMODN)
     A                                 10  2'Varanr . . . . . .'
     A            D1VARE    R        O 10 21REFFLD(EKVARE)
     A**                               11  2'Enhet. . . . . . .'
     A**          D1ENHE    R        O 11 21REFFLD(EKENHE)
6.23 A                                 11  2'Leveringstype. . .'
6.23 A            D1LETY    R        O 11 21REFFLD(EKLETY)
6.23 A                                 12  2'Bonustype. . . . .'
6.23 A            D1BOTY    R        O 12 21REFFLD(EKBOTY)
6.23 A                                 13  2'Fra-dato . . . . .'
6.23 A            D1FDAT         6Y 0O 13 21EDTWRD('  .  .  ')
6.23 A                                 14  2'Til-dato . . . . .'
6.23 A            D1TDAT         6Y 0O 14 21EDTWRD('  .  .  ')
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051021  122950  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 22 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  2  2'Rabattkategori . +'
     A            K1RKAT    R        B  2 21REFFLD(EKRKAT)
     A  32                                  ERRMSGID(EMK0030 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(EMK0060 *LIBL/QMFOFAK 33)
     A  36                                  ERRMSGID(FRA0620 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  3  2'Kunde. . . . . . +'
     A            K1KUND    R        B  3 21REFFLD(EKKUND)
     A  38                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  4  2'Kundeprosjekt. . +'
     A            K1KPRO    R        B  4 21REFFLD(EKKPRO)
     A  39                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 40)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Varegruppe . . . +'
     A            K1OGRP    R        B  5 21REFFLD(EKOGRP)
     A  41                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 24'+'
7.xx A            K1HGRP    R        B  5 26REFFLD(EKHGRP)
     A  42                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 29'+'
7.xx A            K1UGRP    R        B  5 31REFFLD(EKUGRP)
     A  43                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 43)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Leverandør . . . +'
     A            K1LDOR    R        B  6 21REFFLD(EKLDOR)
     A  44                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 44)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Modulnr. . . . . +'
     A            K1MODN    R        B  7 21REFFLD(EKMODN)
     A  34                                  ERRMSGID(EMK0070 *LIBL/QMFOFAK 34)
     A  45                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Varenr . . . . . +'
     A            K1VARE    R   -7   B  8 21REFFLD(EKVARE)
     A  35                                  ERRMSGID(EMK0080 *LIBL/QMFOFAK 35)
     A  46                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A**                                9  2'Enhet. . . . . . .'
     A**          K1ENHE    R        B  9 21REFFLD(EKENHE)
     A**47                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 47)
     A**                                    COLOR(YLW)
     A**                                    CHECK(RB)
     A**                                    OVRDTA
7.xx A                                  9  2'Leveringstype. . +'
6.23 A            K1LETY    R        B  9 21REFFLD(EKLETY)
6.23 A  48                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 48)
6.23 A                                      COLOR(YLW)
6.23 A                                      CHECK(RB)
6.23 A                                      OVRDTA
7.xx A                                 10  2'Bonustype. . . . .'
6.23 A            K1BOTY    R        O 10 21REFFLD(EKBOTY)
     A  31                                  ERRMSGID(EMK0050 *LIBL/QMFOFAK 31)
     A  49                                  ERRMSGID(VMA6520 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A**                                    CHECK(RB)
     A                                      OVRDTA
6.23 A                                 11  2'Fra-dato . . . . .'
6.23 A            K1FDAT         6Y 0B 11 21COLOR(YLW)
     A  50                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 50)
     A  52                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 52)
     A  53                                  ERRMSGID(EMK0100 *LIBL/QMFOFAK 53)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.23 A                                 12  2'Til-dato . . . . .'
6.23 A            K1TDAT         6Y 0B 12 21COLOR(YLW)
     A  51                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 51)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 15  2'Kat./kunde/prosj..'
     A            EKRKAT    R        O 15 21OVRDTA
     A                                 15 25'/'
     A            EKKUND    R        O 15 27OVRDTA
     A                                 15 34'/'
     A            EKKPRO    R        O 15 36OVRDTA
     A                                 16  2'Varegrp/Lev./Modul'
     A            EKOGRP    R        O 16 21OVRDTA
     A            EKHGRP    R        O 16 24OVRDTA
     A            EKUGRP    R        O 16 27OVRDTA
     A                                 16 31'/'
     A            EKLDOR    R        O 16 33OVRDTA
     A                                 16 40'/'
     A            EKMODN    R        O 16 42OVRDTA
     A**                               17  2'Varenr/Enhet . . .'
     A                                 17  2'Varenr . . . . . .'
     A            EKVARE    R        O 17 21OVRDTA
     A**                               17 37'/'
     A**          EKENHE    R        O 17 39OVRDTA
6.23 A                                 18  2'Leveringstype. . .'
6.23 A            EKLETY    R        O 18 21OVRDTA
6.23 A                                 19  2'Bonustype. . . . .'
6.23 A            EKBOTY    R        O 19 21OVRDTA
6.23 A                                 20  2'Fra-dato/Til-dato.'
6.23 A            K1FDA2         6Y 0O 20 21EDTWRD('  .  .  ')
     A                                      OVRDTA
6.23 A                                 20 30'/'
6.23 A            K1TDA2         6Y 0O 20 32EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R M1WIN
     A*%%TS  SD  20100305  135445  ASP_A04     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopier bonus-nivå til kunder'
     A                                      COLOR(WHT)
     A                                  3  2' i Kundekategori :'
     A                                      COLOR(WHT)
     A            M1KKAT    R        B  3 22REFFLD(FÅKKAT)
     A  31                                  ERRMSGID(FRA1110 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRA1120 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                  4  2'Rabattkategori . .'
     A            M1RKAT    R        O  4 22REFFLD(EKRKAT)
     A                                  5  2'Kunde. . . . . . .'
     A            M1KUND    R        O  5 22REFFLD(EKKUND)
     A                                  6  2'Kundeprosjekt. . .'
     A            M1KPRO    R        O  6 22REFFLD(EKKPRO)
     A                                  7  2'Varegruppe . . . .'
     A            M1OGRP    R        O  7 22REFFLD(EKOGRP)
     A            M1HGRP    R        O  7 25REFFLD(EKHGRP)
     A            M1UGRP    R        O  7 28REFFLD(EKUGRP)
     A                                  8  2'Leverandør . . . .'
     A            M1LDOR    R        O  8 22REFFLD(EKLDOR)
     A                                  9  2'Modulnr. . . . . .'
     A            M1MODN    R        O  9 22REFFLD(EKMODN)
     A                                 10  2'Varanr . . . . . .'
     A            M1VARE    R        O 10 22REFFLD(EKVARE)
     A**                               11  2'Enhet. . . . . . .'
     A**          M1ENHE    R        O 11 22REFFLD(EKENHE)
6.23 A                                 11  2'Leveringstype. . .'
6.23 A            M1LETY    R        O 11 22REFFLD(EKLETY)
6.23 A                                 12  2'Bonustype. . . . .'
6.23 A            M1BOTY    R        O 12 22REFFLD(EKBOTY)
6.23 A                                 13  2'Fra-dato . . . . .'
6.23 A            M1FDAT         6Y 0O 13 22EDTWRD('  .  .  ')
6.23 A                                 14  2'Til-dato . . . . .'
6.23 A            M1TDAT         6Y 0O 14 22EDTWRD('  .  .  ')
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
