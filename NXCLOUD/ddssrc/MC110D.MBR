     A*%%TS  SD  20050426  103809  BOF530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MC110D
     A* Beskrivelse . . : Erstatningsvarer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       100205 bø   Nytt program
6.30 A* 6.30  050214 BSA  Utvidet med felter ifbm BM pakke
6.32 A* 6.30  270314 BSA  Flere feilmeldinger ved opprettelse
6.33 A* 6.30  170614 BKV  Siste innkjøpsdato må være større enn Første innkjøpsd
6.34 A* 6.30  290515 BSA  Utelater felter hvis det ikke er BMP pakke.
7.xx A* 7.xx  220316 bsa  Justeringer basert på ny GUI og regler
7.03 A* 6.30  110917 BSA  Legger inn mulighet til erstatningsvarer for kun
7.03 A*                   et lager.
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050210  130624  BOF530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1ENVR    R   -7   O  8  5REFFLD(VVENVR)
     A            B1TXT1        25A  O  8 14
     A            B1EGVR    R   -7   O  8 40REFFLD(VVEGVR)
     A            B1EGTX    R   -5   O  8 49REFFLD(VVEGTX)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050210  130915  BOF530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av erstatningsvarer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'N.varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 40'G.varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'N.varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Varetekst            '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'G.varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Tekst                '
     A                                      COLOR(WHT)
7.xx A            B2ENVR         8Y 0B  5  5
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2EGVR         8Y 0B  5 40
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2EGTX    R   -5   B  5 49REFFLD(VVEGTX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050224  135412  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av erstatningsvarer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Nytt varenr. . . . . . . . +'
     A            C1ENVR         8Y 0B  5 31COLOR(YLW)
     A  31                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 31)
6.32 A  32                                  ERRMSGID(VMV0200 *LIBL/QMFOFAK 32)
6.32 A  33                                  ERRMSGID(VMV0210 *LIBL/QMFOFAK 33)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1NVNA        35A  O  5 43
7.xx A                                  6  2'Gammelt varenr.  . . . . . +'
     A            C1EGVR         8Y 0B  6 31COLOR(YLW)
6.32 A  34                                  ERRMSGID(VMV0220 *LIBL/QMFOFAK 34)
6.32 A  35                                  ERRMSGID(VMV0230 *LIBL/QMFOFAK 35)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                5 40'F4'
7.xx A**                                6 40'F4'
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Erstatningsvare finnes'
     A                                  3  2'fra før               '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050210  145859  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av erstatningsvarer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Nytt varenr. . . . . . . . .'
     A            C1ENVR         8Y 0O  5 31COLOR(YLW)
     A                                      OVRDTA
     A            C1NVNA        35A  O  5 43
     A                                  6  2'Gammelt varenr.  . . . . . .'
     A            C1EGVR         8Y 0O  6 31COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenavn (gammelt varenr)  .'
     A            C2EGTX    R        B  7 31REFFLD(VVEGTX)
     A  31                                  ERRMSGID(VMV0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.30 A                                  9  2'Tidligste innkjøpsdato . . .'
6.34 A  40                                  DSPATR(ND)
6.30 A            C2FIDT         6Y 0B  9 31
6.30 A  32                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 32)
6.30 A                                      COLOR(YLW)
6.30 A                                      EDTWRD('  .  .  ')
6.30 A  30                                  DSPATR(PR)
6.34 A  40                                  DSPATR(ND)
6.30 A                                      OVRDTA
6.30 A                                 10  2'Siste innkjøpsdato . . . . .'
6.34 A  40                                  DSPATR(ND)
6.30 A            C2SIDT         6Y 0B 10 31
6.30 A  33                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 33)
6.33 A  36                                  ERRMSGID(FM01160 *LIBL/QMFOFAK 36)
6.30 A                                      COLOR(YLW)
6.30 A                                      EDTWRD('  .  .  ')
6.30 A  30                                  DSPATR(PR)
6.34 A  40                                  DSPATR(ND)
6.30 A                                      OVRDTA
6.30 A                                 11  2'Omregningsfaktor lagerenhet.'
6.34 A  40                                  DSPATR(ND)
6.30 A            C2OMRE    R        B 11 31REFFLD(VVOMRE)
6.30 A  34                                  ERRMSG('Omregn.fakt. må legges inn')
6.30 A  30                                  DSPATR(PR)
6.34 A  40                                  DSPATR(ND)
6.30 A                                      CHECK(RB)
6.30 A                                      COLOR(YLW)
6.30 A                                      OVRDTA
6.30 A                                 12  2'Antall bestillingsforslag. .'
6.34 A  40                                  DSPATR(ND)
6.30 A            C2ANTB    R        O 12 31REFFLD(VVANTB)
6.34 A  40                                  DSPATR(ND)
6.30 A                                      OVRDTA
      *
7.03 A                                 13  2'Lagernummer. . . . . . . . .'
7.03 A            C2ELAG         2  0B 13 31
7.03 A                                      COLOR(YLW)
7.03 A  41                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 41)
7.03 A  30                                  DSPATR(PR)
7.03 A                                      CHECK(RB)
7.03 A                                      OVRDTA
      *
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2EODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EEUS    R        O 20 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Erstatningsvare har   '
     A                                  3  2'arvet ABC-koder       '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette erstatningsvarer'
     A                                      DSPATR(RI)
     A                                  4  2'Nytt varenr. . . .'
     A            D1ENVR    R        O  4 21REFFLD(VVENVR)
     A                                  5  2'Varenavn . . . . .'
     A            D1NAVN        35A  O  5 21
     A                                  6  2'Nytt varenr. . . .'
     A            D1EGVR    R        O  6 21REFFLD(VVEGVR)
     A                                  7  2'Gammelt varenavn .'
     A            D1EGTX    R        O  7 21REFFLD(VVEGTX)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20050210  145859  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Nytt varenr. . . +'
     A            K1ENVR         8Y 0B  4 21COLOR(YLW)
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Gammelt varenr . +'
     A            K1EGVR         8Y 0B  5 21COLOR(YLW)
     A  33                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(VMV0030 *LIBL/QMFOFAK 34)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Gammelt varenr.  .'
     A            K2EGVR         8Y 0O  9 21COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Gammelt varenavn .'
     A            VVEGTX    R        O 10 21OVRDTA
     A                                 11  2'Nytt varenr. . . .'
     A            K2ENVR         8Y 0O 11 21COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                4 30'F4'
7.xx A**                                5 30'F4'
     A          R M1WIN
     A*%%TS  SD  20050426  103809  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 10 53)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Gamle erstatningsvarer '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer '
     A            M1EGVR         8Y 0O  4 14
     A                                  4 24'har tilknytninger fra før.'
     A                                  6  2'Skal disse knytningene'
     A                                  7  2'overføres til :'
     A            M1ENVR         8Y 0O  7 19
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  8  2'Tast OK, hvis OK ellers F3        -
     A                                             '
