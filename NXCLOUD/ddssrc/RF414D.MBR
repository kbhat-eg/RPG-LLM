     A*%%TS  SD  20040121  115401  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RR100D
     A* Beskrivelse . . : Resultatrapport, vedlikehold rapporthode
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160902 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040121  113425  KOB         REL-V5R2M0  5722-WDS
     A            B1VALG    R    1   B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
     A            B10RAP    R        O 10  5REFFLD(RR0RAP)
     A                                      EDTCDE(Z)
     A            B10TXT    R        O 10  9REFFLD(RR0TXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040121  115401  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kopiering av resultatrapporter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Fra firma .....:'
     A            A1FFIR    R        O  5 19REFFLD(AFRFR/AAFFIR *LIBL/AFRF)
     A                                      DSPATR(UL)
     A            A1FNAV    R        O  5 23REFFLD(AFRFR/AAFNAV *LIBL/AFRF)
     A                                      DSPATR(UL)
     A                                  6  2'Til firma .....:'
     A            A1TFIR    R        O  6 19REFFLD(AFRFR/AAFFIR *LIBL/AFRF)
     A                                      DSPATR(UL)
     A            A1TNAV    R        O  6 23REFFLD(AFRFR/AAFNAV *LIBL/AFRF)
     A                                      DSPATR(UL)
     A                                  7  2'3=Kopier'
     A                                      COLOR(BLU)
     A                                  8  2'V  Rap Rapportnavn'
     A                                      COLOR(WHT)
     A            B20RAP    R        B  9  5REFFLD(RR0RAP)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A            B20TXT    R        B  9  9REFFLD(RR0TXT)
     A          R B2CMD
     A*%%TS  SD  20030203  143229  KOB         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A                                 23 42'F8=Generering'
     A                                      COLOR(BLU)
