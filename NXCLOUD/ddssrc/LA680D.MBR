     A*%%TS  SD  20020923  150151  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LA680D
     A* Beskrivelse . . : Kontroll av beholdning mot lagerbok
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       090402 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1BLD
     A*%%TS  SD  20020923  150151  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kontroll lagerbeholdning  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager . . . . . . . . . . . .'
     A            B1LAGE         2Y 0B  5 32
     A  31                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            B1LATX        20A  O  5 37
     A                                  7  2'Kontrollere mot pr. 01/01 . .'
     A            B1VAL1         1Y 0B  7 32COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  7 37'1=Ja, 0=Nei'
     A                                  8  2'Kontrollere mot siste telling'
     A            B1VAL2         1Y 0B  8 32COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  8 37'1=Ja, 0=Nei'
     A                                  9  2'Kontrollere mot angitt dato .'
     A            B1VAL3         1Y 0B  9 32COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  9 37'1=Ja, 0=Nei'
     A                                  9 51'Dato . '
     A            B1DATO         6Y 0B  9 59
     A  32                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
