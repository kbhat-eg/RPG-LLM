     A*%%TS  SD  20090925  125241  ASK610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MB100D
     A* Beskrivelse . . : ABC - parameter og summer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       180804 bø   Nytt program
     A*       040614 BOF  sjekket mtp. nummerutvidelse
6.30 A*       230914 BSA  Utvider prosentsatsene til 100 %
7.xx A* 7.xx  170316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080513  120327  ASP_NOR     REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VSOR    R        O  8  4REFFLD(MAVSOR)
     A            B1OGRP    R        O  8 15REFFLD(MAOGRP)
     A            B1HGRP    R        O  8 19REFFLD(MAHGRP)
     A            B1UGRP    R        O  8 22REFFLD(MAUGRP)
6.30 A            B1TEXT    R  -12   O  8 26REFFLD(MATEXT)
6.30 A            B1ABAP    R        O  8 49REFFLD(MAABAP)
6.30 A            B1ABBP    R        O  8 53REFFLD(MAABBP)
6.30 A            B1ABCP    R        O  8 57REFFLD(MAABCP)
6.30 A            B1XYXP    R        O  8 61REFFLD(MAXYXP)
6.30 A            B1XYYP    R        O  8 65REFFLD(MAXYYP)
6.30 A            B1LAGK    R        O  8 46REFFLD(MALAGK)
     A            B1VDAT    R        O  8 73REFFLD(LÅDATR)
     A                                      EDTWRD('  .  .  ')
6.30 A            B1XYZP    R        O  8 69REFFLD(MAXYZP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050429  120301  BOF530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ABC parameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  6 11'3=Kopier'
     A                                      COLOR(BLU)
7.xx A                                  6 21'4=Slett'
     A                                      COLOR(BLU)
7.xx A                                  6 30'6=Utskrift'
     A                                      COLOR(BLU)
7.xx A                                  6 42'7=Kjør analyse'
     A                                      COLOR(BLU)
7.xx A                                  6 58'8=Resultat'
     A                                      COLOR(BLU)
7.xx A                                  6 70'9=Oppdater'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 23'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 27'Tekst          '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 45'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 50'A%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 54'B%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 58'C%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'X%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 66'Y%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 70'Z%'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 73'Opd.dat'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2VSOR    R        B  5  4REFFLD(MAVSOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 15'+'
7.xx A            B2OGRP    R        B  5 17REFFLD(MAOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050429  110338  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ABC parameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varesortiment  . . . . . . +'
     A            C1VSOR    R        B  4 31REFFLD(MAVSOR)
     A  34                                  ERRMSGID(MMA0040 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 45
     A                                      OVRDTA
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(MAOGRP)
     A  31                                  ERRMSGID(MMA0050 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 45
     A                                      OVRDTA
7.xx A                                  6  2'Hovedgruppe  . . . . . . . +'
     A            C1HGRP    R        B  6 31REFFLD(MAHGRP)
     A  32                                  ERRMSGID(MMA0060 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 45
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe  . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(MAUGRP)
     A  33                                  ERRMSGID(MMA0070 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 45
     A                                      OVRDTA
7.xx A                                  8  2'Lager som oppdateres . . . +'
     A            C1LAGK         2A  B  8 31
     A  34                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                  8 37'blank gir respektive'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A*                                 5 34'F4'
7.xx A*                                 6 34'F4'
7.xx A*                                 7 35'F4'
7.xx A*                                 4 42'F4'
7.xx A*                                 8 34'F4'
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'ABC-parameter finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080513  120327  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ABC-parameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varesortiment  . . . . . . .'
     A            C1VSOR    R        O  4 31REFFLD(MAVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 43OVRDTA
     A                                  5  2'Overgruppe   . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(MAOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 43OVRDTA
     A                                  6  2'Hovedgruppe  . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(MAHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 43OVRDTA
     A                                  7  2'Undergruppe  . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(MAUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 43OVRDTA
     A                                  8  2'Lager som oppdateres . . . .'
     A            C1LAGK         2A  O  8 31
     A                                  9  2'Beskrivelse  . . . . . . . .'
     A            C2TEXT    R        B  9 31REFFLD(MATEXT)
     A  31                                  ERRMSGID(MMB0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'A prosent  . . . . . . . . .'
     A            C2ABAP    R        B 10 31REFFLD(MAABAP)
     A  31                                  ERRMSGID(MMB0010 *LIBL/QMFLAGR 31)
6.30 A  33                                  ERRMSGID(MMM0060 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'B prosent  . . . . . . . . .'
     A            C2ABBP    R        B 11 31REFFLD(MAABBP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'C prosent  . . . . . . . . .'
     A            C2ABCP    R        B 12 31REFFLD(MAABCP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'X prosent  . . . . . . . . .'
     A            C2XYXP    R        B 14 31REFFLD(MAXYXP)
     A  32                                  ERRMSGID(MMB0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Y prosent  . . . . . . . . .'
     A            C2XYYP    R        B 15 31REFFLD(MAXYYP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Z prosent  . . . . . . . . .'
     A            C2XYZP    R        B 16 31REFFLD(MAXYZP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 17  2'Lager  . . . . . . . . . . +'
     A            C2LAGE    R        B 17 32REFFLD(MBLAGE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  35                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 35)
     A                                      OVRDTA
     A            C2LAGR        30A  O 17 39COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 18  2'Leveringstype  . . . . . . +'
     A            C2LETY    R        B 18 32REFFLD(LÅLETY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  36                                  ERRMSGID(MMB0060 *LIBL/QMFLAGR 36)
     A                                      OVRDTA
     A            C2LTYP        20A  O 18 39COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Fra - til mnd./år '
     A            C2FPER         6Y 0B 19 21COLOR(YLW)
     A                                      OVRDTA
     A  37                                  ERRMSGID(MMA0010 *LIBL/QMFLAGR 37)
     A  38                                  ERRMSGID(MMB0030 *LIBL/QMFLAGR 38)
     A                                      CHECK(RB)
     A            C2TPER         6Y 0B 19 30OVRDTA
     A  39                                  ERRMSGID(MMB0030 *LIBL/QMFLAGR 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 19 38'Eks. 012004'
     A                                 20  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 22 31REFFLD(MAUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
7.xx A**                               17 35'F4'
7.xx A**                               18 35'F4'
     A          R C3BLD
     A*%%TS  SD  20041215  125318  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'ABC  Analyseresultat'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varesortiment  .'
     A            C1VSOR    R        O  4 20REFFLD(MAVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 31
     A                                  5  2'Overgruppe . . .'
     A            C1OGRP    R        O  5 20REFFLD(MAOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 31
     A                                  6  2'Hovedgruppe  . .'
     A            C1HGRP    R        O  6 20REFFLD(MAHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 31
     A                                  7  2'Undergruppe  . .'
     A            C1UGRP    R        O  7 20REFFLD(MAUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 31
     A                                  8  2'Lager som oppdateres . . . .'
     A            C1LAGK         2A  O  8 31
     A                                  9  2'Prosentfordeling'
     A                                      COLOR(WHT)
     A                                  9 20'ABC'
     A                                  9 27'XYZ'
     A                                 10 13'A / X'
     A            C3ABAP    R        O 10 20REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3XYXP    R        O 10 27REFFLD(MAXYXP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 13'B / Y'
     A            C3ABBP    R        O 11 20REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3XYYP    R        O 11 27REFFLD(MAXYYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 13'C / Z'
     A            C3ABCP    R        O 12 20REFFLD(MAABAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3XYZP    R        O 12 27REFFLD(MAXYZP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Resultat '
     A                                      COLOR(WHT)
     A                                 15  3'(antall'
     A                                      COLOR(WHT)
     A                                 16  3'forekomster)'
     A                                      COLOR(WHT)
     A                                 14 26'X'
     A                                 14 41'Y'
     A                                 14 56'Z'
     A                                 15 17'A'
     A            C3ANAX    R   -2   O 15 20REFFLD(MAANAX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 15 30'+'
7.xx A            C3AXF4         1A  B 15 32COLOR(YLW)
     A  50                                  DSPATR(PC)
     A                                      OVRDTA
     A            C3ANAY    R   -2   O 15 35REFFLD(MAANAY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 15 45'+'
7.xx A            C3AYF4         1A  B 15 47COLOR(YLW)
     A                                      OVRDTA
     A            C3ANAZ    R   -2   O 15 50REFFLD(MAANAZ)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 15 60'+'
7.xx A            C3AZF4         1A  B 15 62COLOR(YLW)
     A                                      OVRDTA
     A                                 16 17'B'
     A            C3ANBX    R   -2   O 16 20REFFLD(MAANBX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 16 30'+'
7.xx A            C3BXF4         1A  B 16 32COLOR(YLW)
     A                                      OVRDTA
     A            C3ANBY    R   -2   O 16 35REFFLD(MAANBY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 16 45'+'
7.xx A            C3BYF4         1A  B 16 47COLOR(YLW)
     A                                      OVRDTA
     A            C3ANBZ    R   -2   O 16 50REFFLD(MAANBZ)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 16 60'+'
7.xx A            C3BZF4         1A  B 16 62COLOR(YLW)
     A                                      OVRDTA
     A                                 17 17'C'
     A            C3ANCX    R   -2   O 17 20REFFLD(MAANCX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 17 30'+'
7.xx A            C3CXF4         1A  B 17 32COLOR(YLW)
     A                                      OVRDTA
     A            C3ANCY    R   -2   O 17 35REFFLD(MAANCY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 17 45'+'
7.xx A            C3CYF4         1A  B 17 47COLOR(YLW)
     A                                      OVRDTA
     A            C3ANCZ    R   -2   O 17 50REFFLD(MAANCZ)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                 17 60'+'
7.xx A            C3CZF4         1A  B 17 62COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Opprettet dato . . . . . . .'
     A            C3ODAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Sist endret dato . . . . . .'
     A            C3EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C3EUSR    R        O 21 31REFFLD(MAUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F7=T-
     A                                      otalresultat  F8=Omsetning   F9=Ord-
     A                                      relinjer'
     A                                      COLOR(BLU)
7.xx A*                                15 32'F4'
7.xx A*                                16 32'F4'
7.xx A*                                17 32'F4'
7.xx A*                                15 47'F4'
7.xx A*                                16 47'F4'
7.xx A*                                17 47'F4'
7.xx A*                                15 62'F4'
7.xx A*                                16 62'F4'
7.xx A*                                17 62'F4'
     A          R D1WIN
     A*%%TS  SD  20041215  121958  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette ABC-parameter'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            D1VSOR    R        O  4 21REFFLD(MAVSOR)
     A                                  5  2'Overgruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(MAOGRP)
     A                                  6  2'Hovedgruppe  . . .'
     A            D1HGRP    R        O  6 21REFFLD(MAHGRP)
     A                                  7  2'Undergruppe  . . .'
     A            D1UGRP    R        O  7 21REFFLD(MAUGRP)
     A                                  8  2'Lager som oppdat. '
     A            D1LAGK    R        O  8 21REFFLD(MALAGK)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  20041215  125318  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 65)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kjøre analyse'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            E1VSOR    R        O  4 21REFFLD(MAVSOR)
     A            E1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            E1OGRP    R        O  5 21REFFLD(MAOGRP)
     A            E1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            E1HGRP    R        O  6 21REFFLD(MAHGRP)
     A            E1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            E1UGRP    R        O  7 21REFFLD(MAUGRP)
     A            E1UGNA        25A  O  7 32
     A                                  8  2'Lager som oppdat. '
     A            E1LAGK    R        O  8 21REFFLD(MALAGK)
     A                                 10  2'Fra - til mnd./år '
     A            E1FPER         6Y 0B 10 21COLOR(YLW)
     A                                      OVRDTA
     A  31                                  ERRMSGID(MMA0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(MMB0030 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A            E1TPER         6Y 0B 10 30OVRDTA
     A  33                                  ERRMSGID(MMB0030 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 10 38'Eks. 012004'
     A                                 11  2'Lager  . . . . . .'
     A            E1LAGR        30   O 11 21COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Leveringstype  . .'
     A            E1LTYP        20   O 12 21COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Varetype'
     A            E1VTYP         1A  B 13 21COLOR(YLW)
     A  34                                  ERRMSGID(MMB0040 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                 13 24'L=Lagervare S=Skaffevare blank=beg-
     A                                      ge'
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 28'-'
     A          R F1WIN
     A*%%TS  SD  20090924  144441  ASK610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Oppdater varer med ABC-koder'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            F1VSOR    R        O  4 21REFFLD(MAVSOR)
     A            F1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            F1OGRP    R        O  5 21REFFLD(MAOGRP)
     A            F1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            F1HGRP    R        O  6 21REFFLD(MAHGRP)
     A            F1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            F1UGRP    R        O  7 21REFFLD(MAUGRP)
     A            F1UGNA        25A  O  7 32
     A                                  8  2'Lager som oppdat. '
     A            F1LAGK    R        O  8 21REFFLD(MALAGK)
     A                                 12  2'NB! Vareregister blir oppdatert pe-
     A                                      rmanent !!'
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R P1WIN
     A*%%TS  SD  20041215  130427  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 58)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Utskrift av ABC analyse'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            P1VSOR    R        O  4 21REFFLD(MAVSOR)
     A            P1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            P1OGRP    R        O  5 21REFFLD(MAOGRP)
     A            P1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            P1HGRP    R        O  6 21REFFLD(MAHGRP)
     A            P1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            P1UGRP    R        O  7 21REFFLD(MAUGRP)
     A            P1UGNA        25A  O  7 32
     A                                  8  2'Lager som oppdat. '
     A            P1LAGK    R        O  8 21REFFLD(MALAGK)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20041215  130427  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varesortiment  . +'
     A            K1VSOR    R        B  4 21REFFLD(MAVSOR)
     A  31                                  ERRMSGID(MMA0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Overgruppe . . . +'
     A            K1OGRP    R        B  5 21REFFLD(MAOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Hovedgruppe  . . +'
     A            K1HGRP    R        B  6 21REFFLD(MAHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe  . . +'
     A            K1UGRP    R        B  7 21REFFLD(MAUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2'Lager som oppdat. '
     A            K1LAGK    R        B  8 21REFFLD(MALAGK)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 10  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  2'Varesortiment  . .'
     A            MAVSOR    R        O 11 21OVRDTA
     A                                 12  2'Overgruppe . . . .'
     A            MAOGRP    R        O 12 21OVRDTA
     A                                 13  2'Hovedgruppe  . . .'
     A            MAHGRP    R        O 13 21OVRDTA
     A                                 14  2'Undergruppe  . . .'
     A            MAUGRP    R        O 14 21OVRDTA
     A                                 15  2'Lager som oppdat. '
     A            MALAGK    R        O 15 21OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A*                                 5 24'F4'
7.xx A*                                 6 24'F4'
7.xx A*                                 7 25'F4'
7.xx A*                                 4 32'F4'
     A* 5.51 210508 TSV G1WIN - OPPDATER LAGER MED ABC-KODE.
     A          R G1WIN
     A*%%TS  SD  20090925  125241  ASK610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Oppdater lager med ABC-koder '
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            G1VSOR    R        O  4 21REFFLD(MAVSOR)
     A            G1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            G1OGRP    R        O  5 21REFFLD(MAOGRP)
     A            G1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            G1HGRP    R        O  6 21REFFLD(MAHGRP)
     A            G1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            G1UGRP    R        O  7 21REFFLD(MAUGRP)
     A            G1UGNA        25A  O  7 32
     A                                  8  2'Lager som oppdat. '
     A            G1LAGK    R        O  8 21REFFLD(MALAGK)
     A                                 10  2'Oppdatere for prognose eller sykli-
     A                                      sk telling?'
     A            G1OPKO         1A  B 10 48COLOR(YLW)
     A  31                                  ERRMSGID(MMA0080 *LIBL/QMFLAGR 31)
     A                                 10 50'(P,S)'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12  2'NB! Lageregister blir oppdatert pe-
     A                                      rmanent !!'
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
