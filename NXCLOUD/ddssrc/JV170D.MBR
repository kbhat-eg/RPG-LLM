     A*%%TS  SD  20050914  200004  HKO530      REL-V5R3M0  5722-WDS
7.xx A*      BSA  260116    Endringer relatert til ny GUI
7.02 A* K000 030720 bsa Skrevet om tiol 27x132 bilde
7.03 A* K000 030720 bsa Utvidet funksjonalitet rundt søk.
7.04 A* K000 050723 mst Kompilert pga endring i src
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
7.02 A                                      DSPSIZ(27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20000921  144752  HKO         REL-V4R4M0  5769-PW1
7.02 A            B1NYPO        15A  H
7.02 A            B1VALG         1Y 0B 10  2COLOR(YLW)
     A                                      VALUES(5 8 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
7.02 A            B1TEK1    R        O 10  4REFFLD(JVTEK1)
6.34 A  51                                  COLOR(BLU)
6.34 A  52                                  COLOR(YLW)
7.02 A            B1VARE    R        O 10 40REFFLD(JVVARE)
6.34 A  51                                  COLOR(BLU)
6.34 A  52                                  COLOR(YLW)
7.02 A            B1NOBB    R        O 10 56REFFLD(JVNOBB)
7.02 A  51                                  COLOR(BLU)
7.02 A  52                                  COLOR(YLW)
7.02 A            B1LVAR    R        O 10 65REFFLD(JVLVAR)
7.02 A  51                                  COLOR(BLU)
7.02 A  52                                  COLOR(YLW)
7.02 A            B1LNAV        24A  O 10 86
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050914  200004  HKO530      REL-V5R3M0  5722-WDS
7.02 A                                      SFLSIZ(0016)
7.02 A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF14
     A                                      CF15
     A                                      CF16
7.02 A**                                    CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A  60                                  SFLMSGID(JM90001 *LIBL/QMFVADM 60)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
7.02 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.02 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på vare/pris fra vareadm.'
     A                                      COLOR(WHT)
     A                                  2 38'JV170B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.02 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.02 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
7.02 A                                      ------------------------------------
7.02 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varegrp+'
     A                                      COLOR(BLU)
7.02 A            B2OGRP    R        B  4 12REFFLD(JVOGRP)
     A  31                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.02 A                                  4 15'+'
7.xx A                                      COLOR(BLU)
7.02 A            B2HGRP    R        B  4 17REFFLD(JVHGRP)
     A  32                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.02 A                                  4 20'+'
7.xx A                                      COLOR(BLU)
7.02 A            B2UGRP    R        B  4 22REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.03 A                                  4 27'Leverandør+'
7.03 A                                      COLOR(BLU)
7.03 A            B2LDOR    R        B  4 40REFFLD(JVLDOR)
7.03 A                                      COLOR(YLW)
7.03 A                                      OVRDTA
7.03 A            B2LNVN        30A  O  4 47COLOR(YLW)
7.03 A                                      OVRDTA
7.02 A                                  4 80'Kunde. .'
     A                                      COLOR(BLU)
7.02 A            B2KUND         6Y 0O  4 89OVRDTA
     A                                      EDTCDE(Z)
7.02 A            B2KNAV        30A  O  4 96OVRDTA
     A                                  5  2'Søk. . .'
     A                                      COLOR(BLU)
7.02 A            B2STEK        35A  B  5 12COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.03 A                                  5 49'Nullstill filter'
7.03 A                                      COLOR(BLU)
7.03 A            B2NULL         1Y 0B  5 67
7.03 A                                      COLOR(YLW)
7.03 A                                      OVRDTA
7.03 A                                  5 69'1/0'
7.03 A                                      COLOR(BLU)
7.02 A                                  5 80'Kun.pro.'
     A                                      COLOR(BLU)
7.02 A            B2KPRO         6Y 0O  5 89OVRDTA
     A                                      EDTCDE(Z)
7.02 A            B2PNAV        30A  O  5 96OVRDTA
     A                                  6  2'Frisøk .'
     A                                      COLOR(BLU)
7.02 A            B2FTEK        35A  B  6 12COLOR(YLW)
     A                                      OVRDTA
7.02 A                                  6 80'Levtype.'
     A                                      COLOR(BLU)
7.02 A            B2LTXT        24A  O  6 89
7.02 A                                  7  2'Varetekst'
7.02 A                                      COLOR(WHT)
7.02 A            B2TEK1    R        B  7 12REFFLD(JVTEK1)
7.02 A                                      COLOR(YLW)
7.02 A                                      CHECK(LC)
7.02 A                                      OVRDTA
7.02 A                                  7 56'Varenr'
7.02 A                                      COLOR(WHT)
7.02 A            B2VARE         8Y 0B  7 64COLOR(YLW)
7.02 A                                      CHECK(RB)
7.02 A                                      EDTCDE(Z)
7.02 A                                      OVRDTA
7.02 A                                  7 80'Nobbnr'
7.02 A                                      COLOR(WHT)
7.02 A            B2NOBB    R        B  7 88REFFLD(JVNOBB)
7.02 A                                      COLOR(YLW)
7.02 A                                      CHECK(RB)
7.02 A                                      OVRDTA
7.02 A                                  7100'Lev.varenr'
7.02 A                                      COLOR(WHT)
7.02 A            B2LVAR    R        B  7112REFFLD(JVLVAR)
7.02 A                                      COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A                                  8  2'5=Vis/kalkuler  8=Produktinformasj-
     A                                      on'
     A                                      COLOR(BLU)
7.xx A**                                8  2'V Varetekst                       -
7.02 A**                                8  4'Varetekst                       -
7.02 A**                                        Varenr   Nobbnr   Lev. varenr'
7.02 A**                                    COLOR(WHT)
7.02 A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Varetekst                       '
7.xx A                                      COLOR(WHT)
7.02 A                                  9 40'Varenr       '
     A                                      COLOR(WHT)
7.02 A                                  9 56'Nobbnr  '
     A                                      COLOR(WHT)
7.02 A                                  9 65'Lev. vnr         '
     A                                      COLOR(WHT)
7.02 A                                  9 86'Leverandør'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020916  141838  HKO         REL-V5R1M0  5722-WDS
7.02 A                                 26  2'F3=Gå tilbake  F4=Spørring  F9=Gje-
     A                                      nta søk  F14=Kundeinformasjon  F15=-
     A                                      Rabattmatrise  F16=Spesialpriser'
     A                                      COLOR(BLU)
7.02 A**                               26  2'F14=Kundeinformasjon  F15=Rabattma-
7.02 A**                                    trise   F16=Spesialpriser     F23=B-
7.02 A**                                    ytt info.'
7.02 A**                                    COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 27131' '
