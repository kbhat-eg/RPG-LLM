     A*%%TS  SD  20140530  130610  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO570D
     A* Beskrivelse . . : Viser bestilling på lev.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* - - - - - -  - -  - - - - - - - - - - - - - - - - - - - - - - - - -
     A*       080905 GRo  Nytt program
6.nu A*       300514 bof  Utvidet mtp. nummerutvidelse
7.xx A*       180516 BSA  Endringer relatert til ny GUI
7.01 A*  7.00 270819 BSA  Takler bredere skjermbilder
7.03 A*       310817 bsa  Legger inn lager fra valgt EDI melding som default
7.03 A*                   lager i filter.
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  130610  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1SUFF    R        O  8 13REFFLD(LOSUFF)
     A            B1OTYP    R        O  8 16REFFLD(LOOTYP)
     A            B1DATO         6Y 0O  8 19EDTCDE(Y)
     A            B1NAVN    R   -6   O  8 28REFFLD(LONAVN)
     A            B1TOTI    R        O  8 62REFFLD(LOTOTI)
     A            B1ORNR    R        O  8 53REFFLD(LOORNR)
     A            B1NUMM    R        O  8  4REFFLD(LONUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050909  132207  GRO540      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Søking på bestilling via lev. '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør:'
     A            B2LDOR         6Y 0O  4 14OVRDTA
     A            B2LNAV        20A  O  4 22OVRDTA
     A                                  6  2'1=Velg  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V Bestnr         Best.dat Vareadre-
     A                                      sse              Ordre    Best.tot'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050912  152124  GRO540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 20 75)
     A                                 19  2'F1=Filter'
     A                                      COLOR(BLU)
     A                                 19 13'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 19 28'F5=Forny'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050915  095820  GRO540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 13 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Bestillingsdato. . .'
     A            H1BDAT         6Y 0B  4 23COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  5  2'Bestillingsnr. . . .'
     A            H1BEST        20A  B  5 23COLOR(YLW)
     A                                      OVRDTA
7.03 A                                  6  2'Lager. . . . . . . .'
7.03 A            H1LAGE    R        B  6 23REFFLD(LÅLAGE)
7.03 A                                      COLOR(YLW)
7.03 A                                      OVRDTA
7.xx A                                  7  2'Ordretype. . . . . +'
     A            H1OTYP         2A  B  7 23COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Best.total fra-til .'
     A            H1FBEL         8Y 0B  8 23CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8 32'-'
     A            H1TBEL         8Y 0B  8 34CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varenummer . . . . +'
     A            H1VARE        15A  B  9 23COLOR(YLW)
     A                                      OVRDTA
     A            H1VTXT        20A  O  9 40OVRDTA
7.xx A                                 10  2'Selger/innkjøper . +'
     A            H1INNK    R        B 10 23REFFLD(LOINNK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            H1INAV        20A  O 10 29OVRDTA
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
