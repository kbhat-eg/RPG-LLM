     A*%%TS  SD  20160929  200415  ASP_G0XA    REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : EHANDEL
     A* Format. . . . . : CLN02D
     A* Beskrivelse . . : VEDLIKEHOLD AV NOBBKATEGORI - VAREGRUPPE
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060916 ASK  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20160906  105907  ASP_G0XA    REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1NNKA         4Y 0O  8  5
     A            B1NNTX        30A  O  8 10
     A            B1NNOG         2Y 0O  8 41
     A            B1NNHG         2Y 0O  8 44
     A            B1NNUG         3Y 0O  8 47
     A            B1VGTX        28A  O  8 51
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160922  122915  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Nobbkategori mot varegrupper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Kode Nobbklasifisering         -
     A                                           Og Hg Ugr Varegruppetekst'
     A                                      COLOR(WHT)
     A            B2NNKA         4Y 0B  7  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20160906  143143  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny   F6=Oppre-
     A                                      tt   F8=Nobbkategorier'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20160906  152122  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nobbkategori'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Nobb kategori. . .'
     A            C1NNKA         4Y 0B  5 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1NNTX        30   O  5 27COLOR(GRN)
     A                                      OVRDTA
     A                                  6  2'Overgruppe . . . .'
     A            C1NNOG         2Y 0B  6 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OGTX        35A  O  6 27
     A                                      OVRDTA
     A                                  7  2'Hovedgruppe. . . .'
     A            C1NNHG         2Y 0B  7 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HGTX        35A  O  7 27
     A                                      OVRDTA
     A                                  8  2'Undergruppe. . . .'
     A            C1NNUG         3Y 0B  8 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UGTX        35A  O  8 27
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Søk'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20160929  200415  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandels løsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nobbkategori'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kjedekategori. . .'
     A            C2NNKA         4  0O  5 22OVRDTA
     A            C2NNTX        30   O  5 27OVRDTA
     A                                  7  2'Overgruppe . . . .'
     A            C2NNOG         2Y 0O  7 22EDTCDE(4)
     A                                      OVRDTA
     A            C2OGTX        35A  O  7 27
     A                                  8  2'Hovedgruppe. . . .'
     A            C2NNHG         2Y 0O  8 22EDTCDE(4)
     A                                      OVRDTA
     A            C2HGTX        35A  O  8 27
     A                                  9  2'Undergruppe. . . .'
     A            C2NNUG         3Y 0O  9 22EDTCDE(4)
     A                                      OVRDTA
     A            C2UGTX        35A  O  9 27
     A*
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2EODA         6  0O 18 31EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EEDA         6  0O 19 31EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EEUS    R        O 20 31REFFLD(VVEUSR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Søking'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20160906  143143  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kombinasjon av varegrupper'
     A                                  3  2'finnes fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20160906  143143  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Minimum overgruppe må '
     A                                  3  2'fylles ut!     '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'finnes fra før!'
     A          R D1WIN
     A*%%TS  SD  20160906  145722  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kategorikobling '
     A                                      DSPATR(RI)
     A                                  4  2'Nobbkategori '
     A            D1NNKA         4Y 0O  4 17
     A            D1NNTX        30A  O  4 22
     A                                  6  2'Overgruppe'
     A            D1NNOG         2Y 0O  6 17EDTCDE(4)
     A            D1OGTX        35A  O  6 22
     A                                  7  2'Hovedgruppe'
     A            D1NNHG         2Y 0O  7 17EDTCDE(4)
     A            D1HGTX        35A  O  7 22
     A                                  8  2'Undergruppe'
     A            D1NNUG         3Y 0O  8 17EDTCDE(4)
     A            D1UGTX        35A  O  8 22
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
