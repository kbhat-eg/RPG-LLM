     A*%%TS  SD  20070131  231556  KOB         REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL502D
     A* Beskrivelse . . : Leverandør, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       300702 KOB  Nytt program
     A* V5.40 250505 KOB  Lagt inn vising av besøkadresse
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      HOME(21)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20050720  123948  KOB540      REL-V5R2M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      CA10
     A                                      CA11
     A                                      CA24
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lev.nr.. :'
     A            C2LEVR    R        O  5 13REFFLD(RLLEVR)
     A                                      DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  94        C2NOTB         8A  O  5 44DSPATR(RI)
     A                                  6  2'Navn.  . :'
     A            C2NAVN    R        O  6 13REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 44'Alfa . . . :'
     A            C2ALFA    R        O  6 57REFFLD(RLALFA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A**                                    OVRATR
     A                                  7  2'Adresse. :'
     A            C2GATE    R        O  7 13REFFLD(RLGATE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Adresse2 :'
     A            C2ADR2    R        O  8 13REFFLD(RLADR2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 44'Postnr.. . :'
     A            C2PONR    R        O  8 57REFFLD(RLPONR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Postadr. :'
     A            C2STED    R        O  9 13REFFLD(RLSTED)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 44'Land . . . :'
     A            C2LAND    R        O  9 57REFFLD(RLLAND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2LTXT    R        O  9 60REFFLD(RALTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A*                                10  2'Kont.pers:'
     A*           C2PERS    R        O 10 13REFFLD(RLPERS)
     A*                                     DSPATR(UL)
     A*                                     OVRDTA
     A                                 10 44'Sperret. . :'
     A            C2SPRK    R        O 10 57REFFLD(RLSPRK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 60'Passiv.:'
     A            C2PASS    R        O 10 69REFFLD(RFRFR/RLPASS *LIBL/RFRF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Telefon  :'
     A            C2TLFN    R        O 11 13REFFLD(RLTLFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 26'Mobil'
     A            C2MOBN    R        O 11 32REFFLD(RLMOBN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 44'Vårt kundnr:'
6.20 A            C2KUNR    R        O 11 57REFFLD(RLKUNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  2'Bankgiro :'
     A            C2BGIR    R        O 12 13REFFLD(RLBGIR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 26'Fax'
     A            C2TLFX    R        O 12 32REFFLD(RLTLFX)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 44'Avgiftsfri :'
     A            C2MKOD    R        O 12 57REFFLD(RLMKOD)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Foretaksnr'
     A            C2FTNR    R        O 13 13REFFLD(RLFTNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 44'Saksbehandl:'
     A            C2SAKS    R        O 13 57REFFLD(RLSAKS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2ZTXT    R   18   O 13 62REFFLD(RAZTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Bet.bet. :'
     A            C2BETB    R        O 14 13REFFLD(RLBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2CTXT    R        O 14 16REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 44'Distrikt . :'
     A            C2DIST    R        O 14 57REFFLD(RLDIST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2HTXT    R   18   O 14 62REFFLD(RAHTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Valuta . :'
     A            C2VALK    R        O 15 13REFFLD(RLVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2PMNA    R        O 15 17REFFLD(RAPMNA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15 44'Lager. . . :'
     A            C2LAGR    R        O 15 57REFFLD(RLLAGR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2JTXT    R        O 15 60REFFLD(RAJTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Avdeling :'
     A            C2AVDE    R        O 16 13REFFLD(RLAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A**                                    OVRATR
     A            C2GTXT    R   25   O 16 18REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16 44'Ordrerabatt:'
     A            C2ORAB    R        O 16 57REFFLD(RLORAB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                 17  2'Rabattkat:'
     A            C2RABK    R        O 17 13REFFLD(RLRABK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2FTXT    R        O 17 17REFFLD(RAFTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 17 44'Fors. måte.:'
     A            C2FORM    R        O 17 57REFFLD(RLFORM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2QTXT    R        O 17 60REFFLD(RAQTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18  2'Bet. måte:'
     A            C2BETM    R        O 18 13REFFLD(RLBETM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2BTXT    R        O 18 15REFFLD(RABTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18 44'Frie koder :'
     A            C2FRIE    R        O 18 57REFFLD(RLFRIE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 19  2'Lev.kat..:'
     A            C2KATG    R        O 19 13REFFLD(RLKATG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2NTXT    R   25   O 19 18REFFLD(RANTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 19 44'Lev.bet. :'
     A            C2LBET    R        O 19 55REFFLD(RLLBET)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RTX1    R   -3   O 19 58REFFLD(RARTX1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 20  2'E-mail . :'
     A            C2EMAL    R        O 20 13REFFLD(RLEMAL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 21  2'Sist endret dato:'
     A            C2EDAT         6Y 0O 21 20EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A**                                    OVRATR
     A                                 21 44'Endret av. :'
     A            C2ESIG    R        O 21 57REFFLD(RLESIG)
     A                                      DSPATR(UL)
     A**                                    OVRATR
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20050525  143343  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 22  2'F2=Notatark  F3=Gå tilbake  F5=Pos-
     A                                      ter  F6=Flere koder  F7=Fact./EDI'
     A                                      COLOR(BLU)
     A                                 23  2'F8=Utenl.bank  F10=Kontaktperson  -
     A                                      F11=Besøkadresse'
     A                                      COLOR(BLU)
     A*
     A          R E2WIN
     A*%%TS  SD  20050525  143343  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      PUTOVR
     A                                      CA03
     A                                      CA12
     A                                  2  2' Besøkadresse '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandørnr. .'
     A            E2LEVR    R        O  4 18REFFLD(RLLEVR)
     A                                  5  2'Navn. . . . . .'
     A            E2NAVN    R        O  5 18REFFLD(RLNAVN)
     A                                  6  2'Adresse . . . .'
     A            E2BGAT    R        O  6 18REFFLD(RLBGAT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'Adresse 2 . . .'
     A            E2BADR    R        O  7 18REFFLD(RLBADR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Poststed. . . .'
     A            E2BPNR    R        O  8 18REFFLD(RLBPNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            E2BSTE    R   25   O  8 23REFFLD(RLSTED)
     A                                      TEXT('Poststed')
     A                                 10  2'Web . .:'
     A            E2WEBA    R        O 10 11REFFLD(RLWEBA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R F2WIN
     A*%%TS  SD  20070131  231556  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 17 55)
     A                                      CA03
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Koder AS Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  4 33'Purrekode ..........:'
     A            F2PURR    R        O  4 55REFFLD(RLPURR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  1'Ordrebekreftelse ...:'
     A            F2ORSK    R        O  5 23REFFLD(RLORSK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5 33'Prioritetskode .....:'
     A            F2PRIO    R        O  5 55REFFLD(RLPRIO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Pakkseddel .........:'
     A            F2PAKK    R        O  6 23REFFLD(RLPAKK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 33'Restordre ..........:'
     A            F2REST    R        O  6 55REFFLD(RLREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Spesialavtale ......:'
     A            F2SPES    R        O  7 23REFFLD(RLSPES)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 33'Bonuskode ..........:'
     A            F2BONU    R        O  7 55REFFLD(RLBONU)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Avtalespesifikasjon.:'
     A            F2AVTS    R        O  8 23REFFLD(RLAVTS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Kostpriskontering ..:'
     A            F2KPRI    R        O  9 23REFFLD(RLKPRI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 33'Slettekode .........:'
     A            F2SLET    R        O  9 55REFFLD(RLSLET)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Påslagsprosent .....:'
     A            F2PPRO    R        O 10 23REFFLD(RLPPRO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                 10 33'Type hylle-etikett .:'
     A            F2HYLE    R        O 10 55REFFLD(RLHYLE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Remmiteringskode ...:'
     A            F2REMK    R        O 11 23REFFLD(RLREMK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 33'Skrive i div. felt .:'
     A            F2LVKO    R        O 11 55REFFLD(RLLVKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Frie statistikk-kode:'
     A            F2STKO    R        O 12 23REFFLD(RLSTKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13  1'Opprettet av . . . .:'
     A            F2NSIG    R        O 13 23REFFLD(RLNSIG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  1'Opprettet dato . . .:'
     A            F2NDAT         6Y 0O 14 23EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R G2WIN
     A*%%TS  SD  20020106  212315  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 18'Factoring og EDI-koder'
     A                                      COLOR(WHT)
     A                                  4  2'EDI-Kode ...........:'
     A            G2EDIK    R        O  4 24REFFLD(RLEDIK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  2'EAN-Leverandør......:'
     A            G2EANL    R        O  5 24REFFLD(RLEANL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'Factoring L.nr. ....:'
     A            G2FACT    R        O  7 24REFFLD(RLFACT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            G2NAVN    R        O  7 31REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      TEXT('Leverandørnavn......')
     A                                  8  2'Factoring-nummer ...:'
     A            G2FACN    R        O  8 24REFFLD(RLFACN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  19950509  185030  KB          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Utenlandsk bank'
     A                                      COLOR(WHT)
     A                                  4  1'Bankforbindelse(SW).:'
     A            H2HSWA    R        O  4 23REFFLD(RVHSWA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  1'Bankadresse-1 ......:'
     A            H2HBA1    R        O  5 23REFFLD(RVHBA1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Bankadresse-2 ......:'
     A            H2HBA2    R        O  6 23REFFLD(RVHBA2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Bankadresse-3 ......:'
     A            H2HBA3    R        O  7 23REFFLD(RVHBA3)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Bankadresse-4 ......:'
     A            H2HBA4    R        O  8 23REFFLD(RVHBA4)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Bankkonto-nr. ......:'
     A            H2HBKO    R        O  9 23REFFLD(RVHBKO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Omkostninger i utl. :'
     A            H2HBKU    R        O 10 23REFFLD(RVHBKU)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Omkostninger i No. .:'
     A            H2HBKI    R        O 11 23REFFLD(RVHBKI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Belaste valuta(J/N) :'
     A            H2HBVK    R        O 12 23REFFLD(RVHBVK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
