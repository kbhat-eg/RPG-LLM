     A*%%TS  SD  20020128  145656  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK695D
     A* Beskrivelse . . : Kunder, utskrift generalnota kopi - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       181207 HFL  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20020122  203232  KOB         REL-V5R1M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av generalnota kopi'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  8 32'Fra    - Til'
     A                                  9 10'Generalnota nr. . . :'
     A            A2PGNF    R        B  9 32REFFLD(RKPKNR)
     A  40                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  9 39'-'
     A            A2PGNT    R        B  9 41REFFLD(RKPKNR)
     A  41                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 11 10'Regnskapsår . . . . :'
     A            A2PRÅR    R        B 11 32REFFLD(RKPRÅR)
     A  42                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 13 10'Utskriftsdato . . . :'
     A            A2PKDA    R        B 13 32REFFLD(RKPKDA)
     A  43                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            A2MLD         50   O 22  2
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Meldin10: '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
