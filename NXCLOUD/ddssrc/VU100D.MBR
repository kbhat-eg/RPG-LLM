     A*%%TS  SD  20020530  130450  AMD         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VU100D
     A* Beskrivelse . . : Struktur-vare, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       290502 AMD  Nytt program
6.10 A* R6.10 240909 BSA  Utvidet funksjonalitet for strukturvarer
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020530  100706  AMD         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 8)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VARE    R        O  8  5REFFLD(VUVARE)
6.10 A**          B1VARS    R        O  8 15REFFLD(VUVARE)
     A            B1BESK    R        O  8 22REFFLD(VUBESK)
6.10 A**          B1ANTA    R        O  8 58REFFLD(VUANTA)
6.10 A            B1KSTR    R        O  8 63REFFLD(VUKSTR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020530  100706  AMD         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-varer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Hovedvare'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 22'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
6.10 A                                        8=Struktur'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Hovedvare      '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'Beskrivelse                 '
     A                                      COLOR(WHT)
7.xx A            B2VARE    R        B  5  5REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A*                                     CHECK(RB)
     A                                      OVRDTA
6.10 A**          B2VARS    R        B  7 15REFFLD(VUVARE)
6.10 A*                                     COLOR(YLW)
6.10 A* 22                                  DSPATR(PC)
6.10 A*                                     CHECK(RB)
6.10 A*                                     OVRDTA
7.xx A            B2BESK    R        B  5 22REFFLD(VUBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020530  130450  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Hovedvare. . . . . . . . . +'
     A            C1VARE    R        B  5 31REFFLD(VUVARE)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMU0120 *LIBL/QMFOFAK 32)
     A  41                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A**                                    CHECK(RB)
     A                                      OVRDTA
6.10 A*                                 6  2'Strukturvare . . . . . . . .'
6.10 A*           C1VARS    R        B  6 31REFFLD(VUVARE)
6.10 A* 33                                  ERRMSGID(VMU0130 *LIBL/QMFOFAK 33)
6.10 A* 42                                  DSPATR(PC)
6.10 A*                                     COLOR(YLW)
6.10 A*                                     CHECK(RB)
6.10 A*                                     OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020529  101558  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varenummer finnes fra før  '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020530  123022  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av struktur-vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Hovedvare. . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(VUVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.10 A*                                 6  2'Strukturvare . . . . . . . .'
6.10 A*           C1VARS    R        O  6 31REFFLD(VUVARE)
6.10 A*                                     COLOR(YLW)
6.10 A*                                     OVRDTA
6.10 A                                  6  2'Varetekst. . . . . . . . . .'
6.10 A            C2BESK    R        O  6 31REFFLD(VUBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.10 A                                  8  2'Konstruksjonsvare. . . . . .'
6.10 A                                  8 33'(J=Ja)'
6.10 A            C2KSTR    R        B  8 31REFFLD(VUKSTR)
6.10 A                                      COLOR(YLW)
6.10 A                                      CHECK(RB)
6.10 A**                                    EDTCDE(M)
6.10 A  34                                  ERRMSG('Feil valg - gyldige verdier-
6.10 A                                       er J eller blank')
6.10 A**                                    DSPATR(PR)
6.10 A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VUEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020529  135139  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette struktur-vare '
     A                                      DSPATR(RI)
     A                                  4  2'Hovedvare. . . . .'
     A            D1VARE    R        O  4 21REFFLD(VUVARE)
6.10 A*                                 5  2'Strukturvare . . .'
6.10 A*           D1VARS    R        O  5 21REFFLD(VUVARE)
     A                                  5  2'Varetekst. . . . .'
     A            VUBESK    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020529  135139  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Hovedvare. . . . .'
     A            K1VARE    R        B  4 21REFFLD(VUVARE)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMU0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
6.10 A*                                     CHECK(RB)
     A                                      OVRDTA
6.10 A*                                 5  2'Strukturvare . . .'
6.10 A*           K1VARS    R        B  5 21REFFLD(VUVARE)
6.10 A* 31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
6.10 A* 32                                  ERRMSGID(VMU0030 *LIBL/QMFOFAK 32)
6.10 A*                                     COLOR(YLW)
6.10 A*                                     CHECK(RB)
6.10 A*                                     OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Hovedvare. . . . .'
     A            VUVARE    R        O  9 21OVRDTA
6.10 A*                                10  2'Strukturvare . . .'
6.10 A*           VUVARS    R        O 10 21OVRDTA
     A                                 10  2'Varetekst. . . . .'
     A            VUBESK    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
