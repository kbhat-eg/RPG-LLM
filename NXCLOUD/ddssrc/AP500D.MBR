     A*%%TS  SD  20020312  143851  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx A*          130516 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990319  135552  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFOFAK)
     A            B1PONR         4Y 0O  7  6EDTCDE(Z)
     A            B1STED        25A  O  7 13
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  143851  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge postadresse '
     A                                      DSPATR(RI)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  3  5'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 13'Poststed'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 13'Poststed'
     A                                      COLOR(WHT)
7.xx A            B2PONR         4Y 0B  4  6COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2STED        25A  B  4 13COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 38)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
