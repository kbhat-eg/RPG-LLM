     A*%%TS  SD  20090903  215611  ASK610      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : MD510D
     A* Beskrivelse . . : Lagerlokasjoner, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       030909 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
7.xx A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.xx A            B1LOKA    R        O  8  5REFFLD(MDLOKA)
7.xx A            B1LBES    R        O  8 14REFFLD(MDLBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090903  215611  ASK610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
7.xx A                                  2  2' Velge lagerlokasjon AA'
     A                                      DSPATR(RI)
7.xx A                                  4  5'Lokasjon'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A            B2LOKA    R        B  5  5REFFLD(MDLOKA)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.01 A*                                     CHECK(RB)
7.xx A                                      OVRDTA
7.xx A            B2LBES    R        B  5 14REFFLD(MDLBES)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                  6  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  5'Lokasjon'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Beskrivelse'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
7.xx A                                      WINDOW(*DFT 18 50)
7.xx A                                 17  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
