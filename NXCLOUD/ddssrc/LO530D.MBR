     A*%%TS  SD  20140530  123308  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
6.nu A* 300514 bof : Utvidet mtp. nummerutvidelse
7.xx A* 020216 bsa : Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20051025  090112  GRO550      REL-V5R3M0  5722-WDS
     A*                                     WINDOW(*DFT 11 40)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A*                                     PUTOVR
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Spørring ikke-bekreftede best.'
     A                                      DSPATR(RI)
     A                                  4  2'Bruker . . . . . . . . .'
     A            A1USER        10A  B  4 27
     A                                      COLOR(YLW)
     A                                  5  2'Bestillingsdato t.o.m. .'
     A            A1BDAT         6Y 0B  5 27COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A*                                     OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  6  2'Lager  . . . . . . . . .'
     A            A1LAGE    R        B  6 27REFFLD(LOLAGE)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 32)
     A*                                     OVRDTA
     A                                      CHECK(RB)
7.xx A                                  7  2'Leverandør . . . . . . +'
     A            A1LDOR    R        B  7 27REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 33)
     A*                                      OVRDTA
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  123308  BOF630      REL-V7R1M0  5770-WDS
     A            B1BDAT    R        H      REFFLD(LOBDAT)
     A            B1VALG    R        B  6  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1BEDA         6Y 0O  6  4EDTWRD('  .  .  ')
     A            B1NUMM    R        O  6 13REFFLD(LINUMM)
     A            B1SUFF    R        O  6 22REFFLD(LISUFF)
     A            B1LEVR        22A  O  6 28
     A            B1LADR        15A  O  6 51
     A            B1SALG    R        O  6 67REFFLD(LOTOTI)
     A            B1OTYP    R        O  6 25REFFLD(LOOTYP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140530  123308  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på ikke-bekreftede bestil-
     A                                      linger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'2=Endre  5=Se på'
     A                                      COLOR(BLU)
     A                                  5  2'V Best.dat Bestnr   Su OT Leverand-
     A                                      ør             Vareadresse       Be-
     A                                      st.verdi'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20051107  142056  HKO550      REL-V5R3M0  5722-WDS
     A                                 23  2'F1=Filter   F3=Gå tilbake   -
     A                                       F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
