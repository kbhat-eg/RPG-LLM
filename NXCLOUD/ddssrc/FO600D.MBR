     A*%%TS  SD  20070309  153346  KOB         REL-V5R4M0  5722-WDS
     A*%%EC
7.xx A*          290116 bsa  Justeringer basert på ny GUI og regler
7.01 A*          111119 bsa  Sende EDI ordrebekreftelse
7.03 A*  6.30 230119 bsa  Mulighet for utskrift av ordrebekreftels
7.03 A*                   uten pris.
7.05 A*  6.30 060320 bsa  Tillater valg 5.
     A*
     A*-----------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R E2BLD
     A*%%TS  SD  20070309  153346  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 18 40)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Utskrift av tilbud'
     A                                      COLOR(WHT)
7.xx A                                  2 30'FO600E2'
     A                                  4  2'Alternativ. . .'
7.xx A            E2VALG    R        B  4 18REFFLD(FÅSVA2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 4 5)
7.xx A                                  4 21'0=Standard forslag,'
7.xx A                                  5 21'1=Netto priser,'
7.xx A                                  6 21'2=Kun priser,'
7.xx A                                  7 21'3=Spesial/15,'
     A                                  8 21'4=Kun summer'
     A                                 10  2'Start utskrift.'
7.xx A            E2SVAR    R        B 10 18REFFLD(FÅSVA2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.05 A                                      VALUES(0 1 2 3 5)
6.20 A  36                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
7.xx A                                 10 21'0=Fellesutkjøring'
7.xx A                                 11 21'1=Direkte'
7.xx A                                 12 21'2=Via jobbkø'
PDF  A N35                             13 21'3=Som e-mail'
7.02 A  35
7.02 AA 40                             13 21'5=Til Excel '
7.02 A N35
7.02 AA 40                             14 21'5=Til Excel '
     A                                 15  2'Alt. utkø . . .'
     A            E2OUTQ    R        B 15 18REFFLD(FK1UTQ)
     A  31                                  ERRMSGID(FM01060 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FM01070 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                 15 30'(Skriver)'
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R E3BLD
     A*%%TS  SD  20021018  144235  AMD         REL-V5R1M0  5722-WDS
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
7.xx A                                      WINDOW(*DFT 15 45)
     A                                  2  2'Utskrift av interne ordrepapirer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO600E3'
     A                                  4  2'Alternativ . .'
7.xx A            E3VALG    R        B  4 17REFFLD(FÅSVA2)
7.03 A                                      VALUES(0 1 2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A  34                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A            E3BTX1    R   +2   O  4 20REFFLD(VABTX1)
     A            E3BTX2    R   +2   O  5 20REFFLD(VABTX2)
7.03 A            E3BTX3    R   +2   O  6 20REFFLD(VABTX2)
7.03 A                                  8  2'Start utskrift'
7.03 A            E3SVAR    R        B  8 17REFFLD(FÅSVA2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.01 A                                      VALUES(0 1 2 3 4 5)
6.20 A  36                                  ERRMSGID(FM00011 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
7.03 A                                  8 20'0=Fellesutkjøring,'
7.03 A                                  9 20'1=Direkte,'
7.03 A                                 10 20'2=Jobbkø,'
7.03 A N35                             11 20'3=E-mail'
7.03 A  35                             11 20'4=EDI'
7.02 A  40                             12 20'5=Excel'
7.03 A                                 13  2'Alt. utkø. . :'
7.03 A            E3OUTQ    R        B 13 17REFFLD(FK1UTQ)
     A  31                                  ERRMSGID(FM01060 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FM01070 *LIBL/QMFOFAK 32)
7.01 A  37                                  ERRMSG('Kan ikke sendes som-
7.01 A                                       EDI, sjekk kundeinfo' 37)
7.01 A  38                                  ERRMSG('Bestilling er ikke kommet -
7.01 A                                      som EDI' 38)
     A                                      COLOR(YLW)
7.03 A                                 13 29'(Skriver)'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E4BLD
     A*%%TS  SD  20021018  144235  AMD         REL-V5R1M0  5722-WDS
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(*DFT 12 45)
     A                                  2  2'Utskrift av pakkseddel-papirer'
     A                                      COLOR(WHT)
     A                                  4  2'Start utskrift'
7.xx A            E4SVAR    R        B  4 18REFFLD(FÅSVA2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1 2)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(FMO0700 *LIBL/QMFOFAK 33)
7.xx A                                  4 21'0=Fellesutkjøring,'
7.xx A                                  5 21'1=Direkte,'
7.xx A                                  6 21'2=Jobbkø'
     A                                  8  2'Alt. utkø. . :'
     A            E4OUTQ    R        B  8 18REFFLD(FK1UTQ)
     A  31                                  ERRMSGID(FM01060 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FM01070 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                  8 30'(Skriver)'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R E5BLD
     A*%%TS  SD  19980513  143311  AMD         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 12 45)
     A                                      CF03
     A                                      KEEP
     A                                      OVERLAY
     A                                  2  2'Utskrift av faktura/kreditnota'
     A                                      COLOR(WHT)
     A                                  4  2'Start utskrift'
7.xx A            E5SVAR    R        B  4 18REFFLD(FÅSVA2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1 2)
     A                                      COLOR(YLW)
7.xx A                                  4 21'0=Fellesutkjøring,'
7.xx A                                  5 21'1=Direkte,'
7.xx A                                  6 21'2=Jobbkø'
     A                                  8  2'Alt. utkø. . :'
     A            E5OUTQ    R        B  8 18REFFLD(FK1UTQ)
     A  31                                  ERRMSGID(FM01060 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FM01070 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FM00800 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                  8 30'(Skriver)'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.01 A          R A1BLD
7.01 A*%%TS  SD  20031124  152303  ASK         REL-V5R2M0  5722-WDS
7.01 A                                      WINDOW(*DFT 10 40)
7.01 A                                      CF03
7.01 A                                      CF10
7.01 A                                      KEEP
7.01 A                                  2  2'Overføre ordrebekreftelse'
7.01 A                                      COLOR(WHT)
7.01 A            A1KUND         6S 0O  4  2
7.01 A            A1KTXT        30A  O  4  9
7.01 A            A1STAT        35A  O  6  2COLOR(WHT)
7.01 A                                  9  2'F3=Gå tilbake'
7.01 A                                      COLOR(BLU)
7.01 A                                  9 17'F10=Omkjøring'
7.01 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
