     A*%%TS  SD  20060412  123326  KOB         REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK650D
     A* Beskrivelse . . : Kunder, betalingsvarsel - paramter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150902 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20060412  123326  KOB         REL-V5R3M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring til factoring - forslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  8'Kjøredato. . . . . . . .:'
     A            A2PKDA    R        B  5 34REFFLD(RKPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'Utskrift:'
     A                                      COLOR(WHT)
     A                                  8  8'Skriv kontrolliste . . .:'
     A            A2PSKR         1A  B  8 34TEXT('Skriv kvitteringsliste')
     A  36                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A  36                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 36)
     A                                  8 37'(J=Ja, Blank)'
     A                                 10  2'Sortering:'
     A                                      COLOR(WHT)
     A                                 11  8'Sorteringsrekkefølge . .:'
     A            A25RAF    R        B 11 34REFFLD(RA5RAF)
     A  45                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES('B' 'K' 'A')
     A  45                                  ERRMSGID(RMH0290 *LIBL/QMFOKON 45)
     A                                 11 37'(B=Bilagsnummer)'
     A                                 12 37'(K=Kundenummer)'
     A                                 13 37'(A=Alfabetisk)'
     A                                 14  2'Valg av bilagskode:'
     A                                      COLOR(WHT)
     A                                 15  8'Bilagskod . . . . . . .: '
     A            A2BILK    R        B 15 34REFFLD(RAAKOD)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 15 38'(Kun for overstyring)'
     A                                 23  2'F3=G tilbake '
     A                                      COLOR(BLU)
