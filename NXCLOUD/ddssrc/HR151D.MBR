     A*%%TS  SD  20120704  073640  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      MSGLOC(16)
     A          R B1SFL                     SFL
     A*%%TS  SD  20071005  134722  BOF550      REL-V5R4M0  5722-WDS
     A            B1VARE    R        H      REFFLD(MKVARE)
     A            B1LINE    R        H      REFFLD(MKLINE)
     A            B1ENHP    R        H      REFFLD(MKENHP)
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1TEK1        17A  O  8  4
     A            B1TEK2        17A  O  9  4
     A            B1ANTA         5Y 1O 10  4EDTCDE(M)
     A            B1ENHE    R        O 10 15REFFLD(MKENHE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20060814  150845  BOF550      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0002)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            B2SCA1        19A  B  4  2COLOR(YLW)
     A  31                                  ERRMSGID(HMR0120 *LIBL/QMFLAGR 31)
     A  22                                  DSPATR(PC)
     A*                                     OVRDTA
     A            B2SCA2         8A  B  5  2COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'2=Endre         '
     A                                      COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  20000927  084512  HKO         REL-V4R4M0  5769-PW1
     A                                 15  2'F4=Søk  F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20060822  093932  BOF550      REL-V5R3M0  5722-WDS
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      PUTOVR
     A            C1ENHE    R        H      REFFLD(MKENHE)
     A            C1ENHP    R        H      REFFLD(MKENHP)
     A            C1LINE    R        H      REFFLD(MKLINE)
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Vare:'
     A            C1VARE    R        O  3  8REFFLD(MKVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1        19A  O  4  2COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK2        16A  O  5  2COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Ant.ord.'
     A                                  6 11'Ant.plukk'
     A            C2ANTO         5Y 1O  7  2EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ANTP         5Y 1O  7 13EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Ordretype:'
     A            C2OTYP    R        O  8 13REFFLD(MKOTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ENH1    R        O 10  2REFFLD(MKENHE)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH2    R        O 10  6REFFLD(MKENHE)
     A                                      COLOR(YLW)
     A  81                                  DSPATR(RI)
     A                                      OVRDTA
     A            C2ENH3    R        O 10 10REFFLD(MKENHE)
     A                                      COLOR(YLW)
     A  82                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 12  2'Antall:'
     A            C2ANTA    R   -1   B 12 10REFFLD(MKANTA)
     A  31                                  ERRMSGID(HMR0130 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 15  2'F1=Enhet'
     A                                      COLOR(BLU)
     A          R D1BLD
     A*%%TS  SD  20040323  214143  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Bekrefte sletting'
     A                                  4  2'av registrering'
     A                                  6  2'Vare:'
     A            D1LINE    R        H      REFFLD(MKLINE)
     A            D1VARE    R        O  6  8REFFLD(MKVARE)
     A                                      COLOR(YLW)
     A            D1TEK1        19A  O  7  2COLOR(YLW)
     A            D1TEK2        16A  O  8  2COLOR(YLW)
     A                                 10  2'Enhet:'
     A            D1ENHE    R        O 10 10REFFLD(MKENHE)
     A                                      COLOR(YLW)
     A                                 11  2'Antall:'
     A            D1ANTA    R   -1   O 11 10REFFLD(MKANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A          R E1BLD
     A*%%TS  SD  20040604  155323  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Sluttbehandling'
     A                                  4  2'av plukking  '
     A                                  6  2'Ordre:'
     A            E1NUMM    R        O  6  9REFFLD(MKNUMM)
     A            E1KNAV        19A  O  7  2
     A                                 10  3'Valg:'
     A            E1VALG         1Y 0B 10  9COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      VALUES(0 1 2)
     A                                 11  3'0=Avslutt plukk'
     A                                 12  3'1=Til restbehand.'
     A                                 13  3'2=Pakkseddel'
     A                                 15  3'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R F1BLD
     A*%%TS  SD  20040331  222432  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Varenummer ikke'
     A                                  4  2'funnet!'
     A                                 12  3'F4=Søk'
     A                                      COLOR(BLU)
     A          R G1BLD
     A*%%TS  SD  20060823  135828  BOF550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Vare ikke funnet'
     A                                  4  2'på ordre     '
     A          R G2BLD
     A*%%TS  SD  20060823  135828  BOF550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                  1  2'  PLUKKING  '
     A                                      DSPATR(RI)
     A                                  3  2'Vare er allerede'
     A                                  4  2'plukket         '
     A          R M1MSG
     A*%%TS  SD  20080103  144410  BOF560      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  5  2'Varen du har valgt '
     A                                  6  2'er en skaffevare.  '
     A                                  7  2'Tast <enter> for å '
     A                                  8  2'godkjenne dette.'
     A          R M2MSG
     A*%%TS  SD  20120704  073640  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  5  2'Ordre er innhentet '
     A                                  6  2'i kasse.  Kan ikke '
     A                                  7  2'avslutte med pakk- '
     A                                  8  2'seddel.         '
     A          R M3MSG
     A*%%TS  SD  20120704  073640  BOF620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA05
     A                                  1  2' SKAFFEVARE'
     A                                      DSPATR(RI)
     A                                  5  2'Err: Kontantkunder '
     A                                  6  2'kan ikke få        '
     A                                  7  2'pakkseddel         '
     A                                  8  2'                '
