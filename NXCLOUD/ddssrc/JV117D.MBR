     A*%%TS  SD  20140930  091101  NHO630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV117D
     A* Beskrivelse . . : Vare - består av
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       190809 bof  Nytt program
6.30 A*       211014 BSA  Justert pga jacada
7.xx A*       060616 bsa  Endringer relatert til ny GUI
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140211  121839  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1UBVA    R   -7   O  9  5REFFLD(JVUBVA)
7.xx A            B1NAVN    R   -5   O  9 14REFFLD(JVTEK1)
7.xx A            B1UENH    R        O  9 45REFFLD(JVUENH)
7.xx A            B1UANT    R        O  9 49REFFLD(JVUANT)
7.xx A            B1UGTI    R        O  9 63REFFLD(JVUGTI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140305  101521  MHA620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring Vare består av '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenr.  . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2UVAR    R   -7   O  4 31REFFLD(JVUVAR)
     A                                      COLOR(YLW)
     A            B2VTE2    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  5'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2' 5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'GTIN     '                        -
7.xx A**                                6  2'V  Varenr                         -
7.xx A**                                             Enh Antall        GTIN'
     A                                      COLOR(WHT)
7.xx A            B2UBVA    R   -7   B  6  5REFFLD(JVUBVA)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140930  091101  NHO630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring Vare består av '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Nobb.nr  . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            C1UVAR    R        O  4 31REFFLD(JVUVAR)
     A                                      COLOR(YLW)
     A*           C1VTE2    R        O  4 41REFFLD(JVTEK1)
     A            C1VTE2        33   O  4 47COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Består av varenr . . . . . .'
     A            C2UBVA    R   -7   O  5 31REFFLD(JVUBVA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1    R        O  5 40REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Enhet  . . . . . . . . . . .'
     A            C2UENH    R        O 10 31REFFLD(JVUENH)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Antall . . . . . . . . . . .'
     A            C2UANT    R        O 11 31REFFLD(JVUANT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'GTIN . . . . . . . . . . . .'
     A            C2UGTI    R        O 12 31REFFLD(JVUGTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2NODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2NEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2NUSR    R        O 20 31REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
