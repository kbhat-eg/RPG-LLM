     A*%%TS  SD  20210317  135627  SST_NXC     REL-V7R3M0  5770-WDS
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A*      250216 BSA Linjenummer 24 skal ALDRI benyttes til annet
6.30 A*                 enn meldinger.
     A*-------------------------------------------------------------------
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20180525  093645  G01EGSVSTR  REL-V7R3M0  5770-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 4 5 6 7 8 9)
     A                                      CHKMSGID(JM00010 *LIBL/QMFBYGG)
     A            B1FSD     R        H      REFFLD(RP1FSD)
     A            B1FED     R        H      REFFLD(RP1FED)
     A            B1BUO     R        H      REFFLD(RP1BUO)
     A            B1BUT     R        H      REFFLD(RP1BUT)
     A            B1AVD     R        H      REFFLD(RP1AVD)
     A            B1PRL     R        H      REFFLD(RP1PRL)
     A            B1PROS    R        O  9  4REFFLD(RP1PRO)
     A            B1BESK    R   -8   O  9 11REFFLD(RP1TXT)
     A            B1FLT1        11A  O  9 34
     A            B1FLT2        11A  O  9 46
     A            B1FLT3        11A  O  9 58
     A  41                                  COLOR(RED)
     A            B1FLT4        11A  O  9 70
     A  41                                  COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20191101  151926  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A  *DS3                                SFLSIZ(0013)
     A  *WIDE                               SFLSIZ(0013)
     A  *DS3                                SFLPAG(0012)
     A  *WIDE                               SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CA10
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Prosjekt   '
     A                                      COLOR(WHT)
     A                                  2 38'RP100B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bare aktive . . .'
     A            B2AKTIV        1A  B  4 20COLOR(YLW)
     A                                  4 22'(J/ )'
     A                                  4 29'Avdeling  . . . .'
     A            B2VAVD    R        B  4 47REFFLD(RP1AVD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  4 53'Prosj.leder . . .'
     A            B2VPRL    R        B  4 71REFFLD(RP1PRL)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  5  4'Prosj'
     A                                      COLOR(WHT)
     A                                  5 11'Beskrivelse'
     A                                      COLOR(WHT)
     A                                  5 58'Avd.'
     A                                      COLOR(WHT)
     A                                  5 70'Prosj.leder'
     A                                      COLOR(WHT)
     A            B2PROS    R        B  6  4REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2BESK    R   -5   B  6 11REFFLD(RP1TXT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A  22                                  ERRMSGID(RMS0011 *LIBL/QMFOKON 22)
     A                                      OVRDTA
     A            B2AVD     R        B  6 58REFFLD(RP1AVD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2PRL     R        B  6 70REFFLD(RP1PRL)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  7  2'1=Status 2=Endre  3=Kopier  4=Slet-
     A                                      t  5=Vis 6=Skriv 7=Varer 8=Poster 9-
     A                                      =Ordre/Bst'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  4'Prosj.'
     A                                      COLOR(WHT)
     A                                  8 11'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A*            B2HEAD        47A  O  8 34COLOR(WHT)
     A            B2HEA1        11A  O  8 34COLOR(WHT)
     A            B2HEA2        11A  O  8 46COLOR(WHT)
     A            B2HEA3        11A  O  8 58COLOR(WHT)
     A            B2HEA4        11A  O  8 70COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20191002  154004  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny  F6=Oppret-
     A                                      t  F7=Skriv ut prosjekt  F11=Endre -
     A                                      info.'
     A                                      COLOR(BLU)
     A**                               23 43'F10=Skriv Rapport'
     A          R C1BLD
     A*%%TS  SD  20130408  085738  SST620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprette Prosjekt   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Prosjekt . . . . . . . . . .'
     A            C1PROS    R        B  5 31REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20130215  111700  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Prosjekt finnes fra før.   '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20191002  154902  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prosjekt         '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prosjekt   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  5  2'Prosjekt . . . . . . . . . .'
     A            C1PROS    R        B  5 31REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A            C2BESK    R        B  5 40REFFLD(RP1TXT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  23                                  ERRMSGID(RMP0400 *LIBL/QMFOKON 23)
     A                                      OVRDTA
     A                                  7  2'Forv. start dato . . . . . .'
6.37 A*           C2FSD         10A  B  7 31COLOR(YLW)
6.37 A            C2FSD          6Y 0B  7 31COLOR(YLW)
     A  24                                  ERRMSGID(RMP0410 *LIBL/QMFOKON 24)
     A  25                                  ERRMSGID(RMP0420 *LIBL/QMFOKON 25)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
6.37 A                                      EDTWRD('  .  .  ')
     A                                  8  2'Forv slutt dato  . . . . . .'
6.37 A*           C2FED         10A  B  8 31COLOR(YLW)
6.37 A            C2FED          6Y 0B  8 31COLOR(YLW)
     A  26                                  ERRMSGID(RMP0430 *LIBL/QMFOKON 26)
     A  27                                  ERRMSGID(RMP0440 *LIBL/QMFOKON 27)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
6.37 A                                      EDTWRD('  .  .  ')
     A                                  9  2'Prosjektleder .  . . . . . +'
     A            C2PRL     R        B  9 31REFFLD(RP1PRL)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(RMP0490 *LIBL/QMFOKON 37)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A            C2PRX     R        O  9 40REFFLD(ILRNAV)
     A                                      OVRDTA
     A                                 10  2'Avdeling . . . . . . . . . +'
     A            C2AVD     R        B 10 31REFFLD(RP1AVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A            C2AVX     R        O 10 40REFFLD(ILRNAV)
     A                                      OVRDTA
     A                                 11  2'Kundenummer . .  . . . . . +'
     A            C2KNR     R        B 11 31REFFLD(RP1KNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  28                                  ERRMSGID(RMP0450 *LIBL/QMFOKON 28)
     A  29                                  ERRMSGID(RMP0460 *LIBL/QMFOKON 29)
     A  30                                  DSPATR(PR)
     A            C2KNA     R        O 11 40REFFLD(RKNAVN)
     A                                      OVRDTA
     A                                 12  2'Budsjett inntekter . . . . .'
     A            C2BUO     R        B 12 31REFFLD(RP1BUO)
     A  40                                  ERRMSGID(RMP0520 *LIBL/QMFOKON 40)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(M)
     A                                 13  2'Budsjett kostnader . . . . +'
     A            C2BUT     R        B 13 31REFFLD(RP1BUT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(M)
     A                                 14  2'Prosjekt type  . . . . . . k'
     A            C2PTY     R        B 14 31REFFLD(RP1PTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  35                                  ERRMSGID(RMP0470 *LIBL/QMFOKON 35)
     A  30                                  DSPATR(PR)
     A                                 14 40'(I=Intern, K=Kunde)'
     A                                 15  2'Prosjekt kategori  . . . . +'
     A            C2PRKA         4A  B 15 31COLOR(YLW)
     A  49                                  ERRMSGID(RMP0530 *LIBL/QMFOKON 49)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A                                 16  2'Status . . . . . . . . . . .'
     A            C2STS     R        B 16 31REFFLD(RP1STS)
     A                                      COLOR(YLW)
     A  36                                  ERRMSGID(RMP0480 *LIBL/QMFOKON 36)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 16 40'(A=Avsluttet)             '
     A                                 17  2'Bruke underprosjekt. . . . J'
     A            C2RUP     R        B 17 31REFFLD(RP1RUP)
     A                                      COLOR(YLW)
     A  38                                  ERRMSGID(RMP0500 *LIBL/QMFOKON 38)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 17 40'(J=Ja)'
     A                                 18  2'Bruke Aktivitet. . . . . . J'
     A            C2RAK     R        B 18 31REFFLD(RP1RAK)
     A                                      COLOR(YLW)
     A  39                                  ERRMSGID(RMP0510 *LIBL/QMFOKON 39)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                 18 40'(J=Ja)'
     A                                 20  2'Endret dato. . . . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20 42'Av:'
     A            C2EUSR    R        O 20 46REFFLD(RP1EAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'F3=Gå tilbake  F6=Skriv prosjektra-
     A                                      p.  F7=Intekter/Kostnader  F8=I ord-
     A                                      re/bestil.'
     A                                      COLOR(BLU)
     A                                 13 45'(F4:Hent budsjett fra Kalkyle)'
     A                                 23  2'F9=Varelinjer'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20130215  111700  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette prosjekt   '
     A                                      DSPATR(RI)
     A                                  4  2'Prosjekt . . . . .'
     A            D1PROS    R        O  4 21REFFLD(RP1PRO)
     A                                  6  2'Beskrivelse. . . .'
     A            RP1TXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20130215  111700  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Prosjekt . . . . .'
     A            K1PROS    R        B  4 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Prosjekt . . . . .'
     A            RP1PRO    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            RP1TXT    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R CCMSG
     A*%%TS  SD  20130218  084604  SST620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Feilmelding:               '
     A            CCMSG1        30   O  4  1
     A            CCMSG2        30   O  5  1
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20151123  151832  ASP_C07     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 13 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_AFLD        10A  H
     A            W_ARCD        10A  H
     A                                  2  2'Valg for utskrift.'
     A                                  4  2'Prosjekt(fra). . .'
     A            C3FRAP    R        B  4 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                  4 29'Ev.til'
     A            C3TILP    R        B  4 36REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Denne Periode(fra)'
     A            C3FPER    R        B  5 21REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A            C3TPER    R        B  5 36REFFLD(RP1PRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                  5 29'Ev.til'
     A                                  5 43'(ååååmm)'
     A                                  6  2'Avdeling . . . . .'
     A            C3AVDE    R        B  6 21REFFLD(RP1AVD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C3AVDX        25A  B  6 29COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Prosjektleder. . .'
     A            C3PROL         5Y 0B  7 21COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C3PROX        25A  B  7 29COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Rapport. . . . . .'
     A            C3RAPP    R        B  8 21REFFLD(RPHRAP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C3RAPT        25A  B  8 29COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Hittil i år  . . .'
     A            C3HIT          1A  B  9 21COLOR(YLW)
     A                                      OVRDTA
     A                                  9 29'J dersom bare hittil i år  '
     A                                 10  2'Valg av prosjekt .'
     A            C3AKTIV        1A  B 10 21COLOR(YLW)
     A                                      OVRDTA
     A                                 10 29'blank = Alle, 1 = Aktive'
     A                                 11 29'2 = Avsl. 3 = Aktive m/post.'
