     A*%%TS  SD  20020314  114728  MHA         REL-V5R1M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : FP810D
     A* Beskrivelse . . : Utskrift av tilbudsstatistikk
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.00  040321  bsa  Problemer med visning av datofelt
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20020314  114728  MHA         REL-V5R1M0  5722-WDS
7.XxxA**                                    WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av tilbudsstatistikk '
     A                                      DSPATR(RI)
7.xx A                                  2 42'FP810B1'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  2'Selger. . . . . . +'
     A            B1SELG    R        B  4 23REFFLD(FFRFR/FOSELG)
     A  40                                  ERRMSGID(FRA0920 *LIBL/QMFOFAK 40)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  4 46'Blank = Alle'
7.xx A                                  5  2'Avdeling. . . . . +'
     A            B1AVDE    R        B  5 23REFFLD(FFRFR/FOAVDE)
     A  37                                  ERRMSGID(FRA0720 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  5 46'Blank = Alle'
7.xx A                                  6  2'Årsak . . . . . . +'
     A            B1ITYP    R        B  6 23REFFLD(FFRFR/FOITYP *LIBL/FFRF)
     A  39                                  ERRMSGID(VMA8020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  6 46'Blank = Alle'
7.01 A                                  8  2'Ordretyper Ofak . +'
     A            B1OOT1    R        B  8 23REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  31                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A            B1OOT2    R        B  8 26REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  32                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A            B1OOT3    R        B  8 29REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  33                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A            B1OOT4    R        B  8 32REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  34                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A            B1OOT5    R        B  8 35REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  35                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A            B1OOT6    R        B  8 38REFFLD(FFRFR/FOOTYP)
     A                                      DLTCHK
     A  36                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A            B1OVLG         1A  B  8 43COLOR(YLW)
     A                                  8 46'Blank = Alle'
     A                                  9 46'    1 = Ta med'
     A                                 10 46'    2 = Unnlate'
7.01 A**                               11  2'Fra/Til dato. . . .'
7.01 A                                 11  2'Fra dato'
7.01 A            B1FDAT         6Y 0B 11 13COLOR(YLW)
     A  46                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 46)
     A  47                                  ERRMSGID(FM00055 *LIBL/QMFOFAK 47)
7.01 A                                      EDTWRD('  .  .  ')
7.01 A*                                     EDTCDE(4)
7.01 A                                 11 24'Til dato'
7.01 A*                                11 41'/'
7.01 A            B1TDAT         6Y 0B 11 33COLOR(YLW)
     A  48                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 48)
7.01 A                                      EDTWRD('  .  .  ')
7.01 A*                                     EDTCDE(4)
     A                                 13  2'Sortering . . . . .'
     A            B1SORT         1A  B 13 23COLOR(YLW)
     A  38                                  ERRMSGID(FRA0920 *LIBL/QMFOFAK 38)
     A                                      CHECK(RB)
     A                                 13 46'    1 = Selger'
     A                                 14 46'    2 = Årsak '
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
