     A*%%TS  SD  20111123  100623  AMD620      REL-V7R1M0  5770-WDS
     A*
7.xx A*          190216 bsa  Justeringer basert p책 ny GUI og regler
     A*
8.01 A*K000 8.xx 150322 bsa  Endrer slik at koden i cloud blir riktig.
8.02 A* K0000    140122 bsa  H책ndtering av varekunde/fakturakunde
8.03 A* K0000    210422 bof  Takle bredere skjerm
     A*
     A*-----------------------------------------------------------------
     A*%%EC
8.03 A                                      DSPSIZ(24 80 *DS3   -
8.03 A                                             27 132 *WIDE )
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20111123  100445  AMD620      REL-V7R1M0  5770-WDS
     A            B1KUND    R        H      REFFLD(FOKUND)
8.02 A            B1LKUN    R        H      REFFLD(FOLKUN)
     A            B1KPRO    R        H      REFFLD(FOKPRO)
     A            B1BDAT    R        H      REFFLD(FOBDAT)
7.xx A            B1VALG         2Y 0B  9  2VALUES(0 2 5 6 7 8 9 60)
7.xx A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A                                      COLOR(YLW)
7.xx A                                      EDTCDE(Z)
7.xx A            B1NUMM    R        O  9  5REFFLD(FONUMM)
7.xx A            B1SUFF    R        O  9 14REFFLD(FOSUFF)
7.xx A            B1OTYP    R        O  9 17REFFLD(FOOTYP)
7.xx A            B1KNAV        14A  O  9 20
7.xx A            B1ODAT         6Y 0O  9 35EDTWRD('  .  .  ')
7.xx A            B1VADR        17A  O  9 44
7.xx A            B1OSUM    R        O  9 62REFFLD(FOTOTS)
7.xx A            B1ITYP    R        O  9 76REFFLD(FOOTYP)
     A  43                                  COLOR(BLU)
     A  44                                  COLOR(RED)
     A  41                                  COLOR(WHT)
     A  42                                  COLOR(YLW)
7.xx A            B1KODE         2A  O  9 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20111123  100445  AMD620      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Restede ordre til levering'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO570B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 35'Ord.dato '
     A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  5=Vise ordre  6=Vis kunde-
     A                                      /kundeprosjekt   60=Logg'
     A                                      COLOR(BLU)
7.xx A                                  7  2'7=Behandle ordre       8=Hvem beha-
     A                                      ndler ordre       9=Klar til plukki-
     A                                      ng '
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Ordre  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Su'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Kunde         '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 35'Ord.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 44'Vareadresse   '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Ordreverdi'
     A                                      COLOR(WHT)
7.xx A                                  8 76'Ty'
     A                                      COLOR(WHT)
7.xx A                                  8 79'Ko'
     A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  5REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2DATO         6Y 0B  5 35COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A          R B2CMD
     A*%%TS  SD  20071109  114016  BOF550      REL-V5R4M0  5722-WDS
     A                                 23 16'F3=G책 tilbake'
     A                                      COLOR(BLU)
     A                                 23 33'F5=Forny'
     A                                      COLOR(BLU)
7.xx A                                 23  3'F1=Bla alt'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20071109  114641  BOF550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Vis bare leveringsklare:'
     A            H1LEVK         1A  B  4 27OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
8.01 A**                                4 30'Blank gir alle'
8.01 A                                  4 30'1/0'
     A                                  5  2'Vis bare lager . . . . :'
     A            H1LAGE    R        B  5 27REFFLD(FOLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5 30'Blank gir alle'
     A                                 13  2'F3=G책 tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
