     A*%%TS  SD  20020327  213716  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA535D
     A* Beskrivelse . . : Kundegruppe, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       030718 bsa  Nytt program
7.xx A* 7.xx  060219 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1FKOD    R        O  7  5REFFLD(REFKOD)
     A            B1FBES    R        O  7 17REFFLD(REFBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020327  213716  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge kundegruppe'
     A                                      DSPATR(RI)
7.xx A                                  3  5'Kode '
7.xx A                                      COLOR(WHT)
7.xx A                                  3 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
     A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A**                                5  2'V  Kode        Beskrivelse'
7.xx A**                                    COLOR(WHT)
7.xx A            B2FKOD    R        B  4  5REFFLD(REFKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FBES    R        B  4 17REFFLD(REFBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Kode '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 17'Beskrivelse'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
