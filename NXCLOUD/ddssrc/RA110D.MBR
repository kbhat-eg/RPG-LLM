     A*%%TS  SD  20050506  215645  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA110D
     A* Beskrivelse . . : Lagere, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
7.01 A*  7.01 120615 ASK  Lagt inn kobling til region for Mestergruppen
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020326  210628  KOB         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1JKOD    R        O  8  5REFFLD(RAJKOD)
     A            B1JTXT    R        O  8 11REFFLD(RAJTXT)
     A            B1JAVD    R        O  8 29REFFLD(RAJAVD)
     A            B1JADR    R   28   O  8 34REFFLD(RAJADR)
     A            B1JPNR    R        O  8 63REFFLD(RAJPNR)
     A            B1JSTE    R   12   O  8 68REFFLD(RAJSTE)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020325  205458  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lagere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse        '
     A                                      COLOR(WHT)
7.xx A                                  7 34'Adresse'
     A                                      COLOR(WHT)
7.xx A                                  7 63'Post'
     A                                      COLOR(WHT)
7.xx A                                  7 68'Sted     '
7.xx A                                      COLOR(WHT)
7.xx A            B2JKOD    R        B  5  5REFFLD(RAJKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2JTXT    R        B  5 11REFFLD(RAJTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  205458  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lagere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager. . . . . . . . . . . .'
     A            C1JKOD    R        B  5 31REFFLD(RAJKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020325  205458  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Lager finnes fra før '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050506  215645  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
7.01 A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lagere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Lager. . . . . . . . . . . .'
     A            C1JKOD    R        O  5 31REFFLD(RAJKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2JTXT    R        B  7 31REFFLD(RAJTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'Adresse. . . . . . . . . . .'
     A            C2JADR    R        B  9 31REFFLD(RAJADR)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                 10  2'Mellomadresse. . . . . . . .'
     A            C2JAD2    R        B 10 31REFFLD(RAJAD2)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
7.xx A                                 11  2'Postnr/sted. . . . . . . . +'
     A            C2JPNR    R        B 11 31REFFLD(RAJPNR)
     A  32                                  ERRMSGID(RM01000 *LIBL/QMFOKON 32)
     A  32                                  DSPATR(PC)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A            C2JSTE    R        O 11 36REFFLD(RAJSTE)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Telefonnummer. . . . . . . .'
     A            C2JTLF    R        B 12 31REFFLD(RAJTLF)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'Telefaxnummer. . . . . . . .'
     A            C2JFAX    R        B 13 31REFFLD(RAJFAX)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 15  2'Avdeling . . . . . . . . . +'
     A            C2JAVD    R        B 15 31REFFLD(RAJAVD)
     A  33                                  DSPATR(PC)
     A  33                                  ERRMSGID(RM01070 *LIBL/QMFOKON 33)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A            C2JATX    R        O 15 36REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
7.01 A                                 16  2'Region . . . . . . . . . . .'
7.01 A            C2REGI         2Y 0O 16 31
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A            C2REGT        30   O 16 36
7.01 A                                      OVRDTA
7.01 A                                      OVRATR
7.01 A                                      COLOR(YLW)
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A*                                23  2'F3=Gå tilbake   F4=Spørring'
7.01 A                                 23  2'F3=Gå tilbake   F4=Spørring  F8=Re-
7.01 A                                      gioner'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  205458  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette lager '
     A                                      DSPATR(RI)
     A                                  4  2'Lager. . . . . . .'
     A            D1JKOD    R        O  4 21REFFLD(RAJKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAJTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020325  205458  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Lager. . . . . . .'
     A            K1JKOD    R        B  4 21REFFLD(RAJKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Lager. . . . . . .'
     A            RAJKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAJTXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
