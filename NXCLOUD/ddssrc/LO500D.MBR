     A*%%TS  SD  20140305  103314  MHA620      REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A*  7.00    020316 bsa  Justeringer basert på ny GUI og regler
7.07 A*  7.00    150819 BSA  Takler bredere skjermbilder
7.09 A*  K000    220420 BSA  Utvider til 27x132 skjermbilde
7.11 A*  K000    250620 BSA  Legger ut ordrenummer, forsendelsesmåte
7.11 A*                      og bekreftet lev.dato i skjermbildet.
7.12 A*  K000    120819 SST  Splitte IP i to m/samme nr men nytt suffix
     A*
7.fb A*  K000    160419 bsa  Forskuddsbetaling/depositum
     A*
     A*
     A*-----------------------------------------------------------------
7.09 A                                      DSPSIZ(27 132 *WIDE)
7.09 A*                                            27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990615  084325  ASK         REL-V4R2M0  5769-PW1
     A            B1LDOR         6S 0H
     A            B1SALG    R        H      REFFLD(LOTOTI)
7.05 A            B1LETY    R        H      REFFLD(LÅLETY)
7.05 A            B1KRES    R        H      REFFLD(LÅLETY)
7.09 A            B1VALG         2Y 0B 10  2CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A                                      VALUES(0 2 3 4 5 6 8 9 10 16 18 26 -
7.12 A                                      11 36 46 50 55 60)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.09 A            B1NUMM    R        O 10  5REFFLD(LONUMM)
7.05 A  45                                  COLOR(RED)
7.05 A  46                                  COLOR(BLU)
7.09 A            B1SUFF    R        O 10 14REFFLD(LOSUFF)
7.fb A  45                                  COLOR(RED)
7.09 A            B1OTYP    R        O 10 17REFFLD(LOOTYP)
7.fb A  45                                  COLOR(RED)
7.09 A            B1LENA        15A  O 10 20
7.fb A  45                                  COLOR(RED)
7.09 A            B1DATO         6Y 0O 10 36EDTWRD('  .  .  ')
7.fb A  45                                  COLOR(RED)
7.11 A            B1ORNR    R        O 10 45REFFLD(LONUMM)
7.fb A  45                                  COLOR(RED)
7.11 A            B1NAVN        17A  O 10 55
7.fb A  45                                  COLOR(RED)
7.11 A            B1INFO        12A  O 10 73
7.11 A            B1ITYP    R        O 10 86REFFLD(LOOTYP)
7.11 A            B1KODE         2A  O 10 89
7.11 A            B1VEKT         6Y 0O 10 92EDTCDE(Z)
7.11 A            B1VOLU         8Y 3O 10100EDTCDE(M)
7.11 A            B1LMAT    R        O 10111REFFLD(LOLMAT)
7.11 A            B1LDAT         6Y 0O 10115EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070327  131420  ASK550      REL-V5R4M0  5722-WDS
7.09 A                                      SFLSIZ(0015)
7.09 A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
7.02 A*                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF15
     A                                      CF16
7.02 A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      PUTOVR
     A                                      SFLCSRRRN(&WCURRN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.09 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.09 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Bestillingsbehandling'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LO500B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.09 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.09 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.09 A                                      ------------------------------------
7.09 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Ordretype:'
     A                                      COLOR(BLU)
7.xx A            H1OTYP    R        O  4 13REFFLD(LOOTYP)
     A                                      COLOR(BLU)
7.xx A                                  4 16'Anm:'
     A                                      COLOR(BLU)
7.xx A            H1OANM         2A  O  4 21COLOR(BLU)
7.xx A                                  4 24'Bruker:'
     A                                      COLOR(BLU)
7.xx A            H1USER    R        O  4 32REFFLD(LOUSER)
     A                                      COLOR(BLU)
7.xx A                                  4 43'Leverandør:'
     A                                      COLOR(BLU)
7.xx A            H1LDOR    R        O  4 55REFFLD(LOLDOR)
     A                                      COLOR(BLU)
7.xx A                                  4 62'Beløp:'
     A                                      COLOR(BLU)
7.xx A            H1VALG    R        O  4 69REFFLD(LÅVALA)
     A                                      COLOR(BLU)
7.xx A            H1BELP         9Y 0O  4 71COLOR(BLU)
     A                                      EDTCDE(Z)
7.02 A                                  6  5'Best.'
7.02 A                                      COLOR(WHT)
7.02 A                                  6 21'S'
7.02 A                                      COLOR(WHT)
7.02 A                                  5  5'Frisøk'
7.02 A                                      COLOR(WHT)
7.02 A                                  6 26'Ot'
7.02 A                                      COLOR(WHT)
7.02 A                                  6 32'Lev.alfa'
7.02 A                                      COLOR(WHT)
7.02 A                                  6 57'Dato'
7.xx A                                      COLOR(WHT)
7.02 A*                                  6 51'Vareadresse'
7.02 A*                                      COLOR(WHT)
7.09 A                                  7  2' 2=Endre  3=Kopier  4=Slett  5=Vis-
7.09 A                                       best  6=Utskrift  8=Hvem behandle-
7.12 A                                      r best  9=Fjern kode  10=Hist  11=-
7.12 A                                      Splitt IP 16=Utplukk'
7.xx A                                      COLOR(BLU)
7.12 A                                  8  2'18=Send bestilling  -
7.12 A                                      26=Endre ordre  36=Reverser  46=Sl-
7.09 A                                      ettede  50=Ordresystemet  55=Match -
7.09 A                                      mot ordre  60=Log'
7.xx A                                      COLOR(BLU)
7.xx A**                                4  2'2=Endre'
7.xx A**                                    COLOR(BLU)
7.xx A**                                4 15'8=Hvem behandler'
7.xx A**                                    COLOR(BLU)
7.xx A**                                5  2'3=Kopier'
7.xx A**                                    COLOR(BLU)
7.xx A*                                 7 14'16=Utplukk'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 6  2'4=Slett'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 8 14'18=Send bestilling'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 7  2'5=Vis best'
7.xx A*                                     COLOR(BLU)
7.09 A                                  9  2'V'
     A                                      COLOR(WHT)
7.09 A                                  9  5'Best.   '
     A                                      COLOR(WHT)
7.09 A                                  9 14'S'
     A                                      COLOR(WHT)
7.09 A                                  9 17'Ot'
     A                                      COLOR(WHT)
7.09 A                                  9 20'Lev.alfa'
     A                                      COLOR(WHT)
7.09 A            B2HEDD         9A  O  9 35COLOR(WHT)
7.11 A                                  9 45'Ordrenr.'
7.11 A                                      COLOR(WHT)
7.11 A                                  9 55'Vareadresse'
     A                                      COLOR(WHT)
7.11 A            B2HED2        10A  O  9 74COLOR(WHT)
7.11 A                                  9 87'T'
     A                                      COLOR(WHT)
7.11 A                                  9 89'K'
7.09 A                                      COLOR(WHT)
7.11 A                                  9 94'Vekt'
7.09 A                                      COLOR(WHT)
7.11 A                                  9106'M3'
     A                                      COLOR(WHT)
7.11 A                                  9111'FM'
7.11 A                                      COLOR(WHT)
7.11 A            B2HED3        10A  O  9114COLOR(WHT)
      *
7.02 A            B2FSOK        35A  B  5 12COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A  22                                  DSPATR(PC)
7.02 A            B2FSO2        25A  O  5 48COLOR(WHT)
7.02 A                                      OVRDTA
7.02 A                                  5 75'(F21)'
7.02 A                                      COLOR(WHT)
7.02 A            B2NUMM    R        B  6 12REFFLD(LONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.02 A*  22                                  DSPATR(PC)
7.02 A            B2SUFF    R        B  6 23REFFLD(LOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.02 A            B2OTYP    R        B  6 29REFFLD(LOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.02 A            B2LENA        15A  B  6 41COLOR(YLW)
     A                                      OVRDTA
7.02 A            B2DATO         6Y 0B  6 62COLOR(YLW)
     A  34                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
7.02 A*            B2NAVN        17A  B  6 63OVRDTA
7.02 A*                                      COLOR(YLW)
7.02 A*                                      CHECK(LC)
7.xx A*                                 5 15'9=Fjern kode'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 4 34'26=Endre ordre'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 7 34'50=Ordresystemet'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 5 34'36=Reverser'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 6 14'10=Ordrehistorikk'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 8 34'55=Match mot ordre'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 6 34'46=Slettede'
7.xx A*                                     COLOR(BLU)
7.xx A*                                 8 55'60=Log'
7.xx A*                                     COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  20050623  085634  ASK530      REL-V5R2M0  5722-WDS
7.09 A                                 25  2'F1=Bla-alt'
     A                                      COLOR(BLU)
7.09 A                                 25 14'F2=Velg'
     A                                      COLOR(BLU)
7.09 A                                 25 23'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.09 A                                 25 38'F5=Forny'
     A                                      COLOR(BLU)
7.09 A                                 25 48'F6=Opprett'
     A                                      COLOR(BLU)
7.09 A                                 25 60'F7=Lever.'
     A                                      COLOR(BLU)
7.09 A                                 25 71'F9=Vare'
     A                                      COLOR(BLU)
7.09 A                                 25 80'F10=Hist'
     A                                      COLOR(BLU)
7.09 A                                 25 90'F11=Ordre'
     A                                      COLOR(BLU)
7.09 A                                 25101'F15=Start utskrift'
     A                                      COLOR(BLU)
7.09 A                                 26  2'F16=Utplukk'
     A                                      COLOR(BLU)
7.09 A                                 26 15'F22=Inkl.mva'
     A                                      COLOR(BLU)
7.09 A                                 26 29'F23=Bytt info'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20071206  121428  AMD550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
6.35 A                                      WINDOW(2 2 17 50)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Velg ordretype.+'
6.35 A            H1OTYP    R        B  4 19REFFLD(LOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
6.35 A                                  4 31'Blank gir'
7.xx A                                  5  2'Velg info-type.+'
6.35 A            H1ITYP    R        B  5 19REFFLD(LOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
6.35 A                                  5 31'alle'
7.xx A                                  6  2'Velg bruker.... '
6.35 A            H1USER    R        B  6 19REFFLD(LOUSER)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Velg leverandør+'
6.35 A            H1LDOR    R        B  7 19REFFLD(LOLDOR)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  8  2'Velg ordrenr... '
6.35 A            H1ORNR    R        B  8 19REFFLD(LONUMM)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  9  2'Avdeling ......+'
6.35 A            H1AVDE    R        B  9 19REFFLD(LOAVDE)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 10  2'Beløpsgrense... '
6.35 A            H1BELP         9Y 0B 10 19CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A                                 11  2'Over / under... '
6.35 A            H1VALG    R        B 11 19REFFLD(LÅVALA)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.35 A                                 11 31'U=Under,'
6.35 A                                 11 40'O=Over  '
7.xx A                                 12  2'Best.anmerkn... '
6.35 A            H1OANM         2A  B 12 19OVRDTA
     A                                      COLOR(YLW)
6.35 A                                 12 31'**, OK, UT, VE, BU'
7.xx A                                 13  2'Best. eldre enn '
6.35 A            H1DATR    R        B 13 19REFFLD(LÅDATR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
6.35 A                                 13 31'Uten lev.ordrenr.'
7.xx A                                 14  2'Selger  . . . .+'
6.35 A            H1SELG    R        B 14 19REFFLD(LÅSELG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 15  2'Forsinket lev . '
6.35 A            H1DLAY    R        B 15 19REFFLD(LÅVALA)
6.30 A  31                                  ERRMSG('Ugyldig kode')
6.30 A                                      DSPATR(UL)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.35 A                                 15 31'0=Nei, 1=Ja'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20140305  103314  MHA620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 18 60)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Ordrenummer . .'
     A                                      COLOR(WHT)
     A            B3NUMM    R        O  2 18REFFLD(LONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            B3NAVN    R        O  2 28REFFLD(LONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  4  2'Nytt best.-nr .'
     A            B3NONR    R        B  4 18REFFLD(LONUMM)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(LMO0030 *LIBL/QMFLAGR 31)
     A                                      COMP(GE 0)
     A                                      COLOR(YLW)
     A                                  4 27'Blank gir automatisk tildeling'
7.xx A                                  5  2'Ny best.-type +'
     A            B3OTYP    R        B  5 18REFFLD(LOOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A  32                                  ERRMSGID(LM01110 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Nytt lev.-nr. +'
     A            B3LDOR    R        B  6 18REFFLD(LOLDOR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  33                                  ERRMSGID(LM06000 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                  8  2'Rabatt   .  . .'
     A            B3KRAB    R        B  8 18REFFLD(LÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1 2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A                                      COLOR(YLW)
     A                                  8 27'0=Som ordren, 1=Fra ny levr.'
     A                                  9 27'2=Uten rabatt'
     A                                 10  2'Slettes'
     A                                 10 10'etter'
     A                                 11  2'kopiering.  . .'
     A            B3KCOP    R        B 11 18REFFLD(LÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A                                      COLOR(YLW)
     A                                 11 27'0=Nei, 1=Ja'
     A                                 12  2'Beholde '
     A                                 13  2'ordrereferanse '
     A            B3BEOR    R        B 13 18REFFLD(LÅSVAR)
     A                                      OVRDTA
7.06 A  41                                  DSPATR(PR)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A  34                                  ERRMSGID(LM10020 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                 13 27'0=Nei, 1=Ja'
     A                                 14  2'Ta med'
     A                                 15  2'Topp/Bunn-tekst'
     A            B3KTXT    R        B 15 18REFFLD(LÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAX)
     A                                      COLOR(YLW)
     A                                 15 27'0=Nei, 1=Ja'
     A                                 17  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20021216  142723  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 32)
     A                                      CA03
     A                                      PUTOVR
     A                                  1  2'Sletting av bestilling'
     A                                      COLOR(WHT)
     A            B4NUMM    R        O  3 17REFFLD(LONUMM)
     A                                      OVRDTA
     A            B4NAVN    R        O  4  2REFFLD(LONAVN)
     A                                      OVRDTA
     A                                  6  2'Kommentar til slettingen'
     A            B4KOM1    R        B  7  2REFFLD(LYKOM1)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(LMO0650 *LIBL/QMFLAGR 31)
     A            B4KOM2    R        B  8  2REFFLD(LYKOM2)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LMO0660 *LIBL/QMFLAGR 32)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'Bestillingsnr:'
     A          R B9WIN
     A*%%TS  SD  20021216  142723  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 8 32)
     A                                      CA03
     A                                      PUTOVR
     A                                  1  2'Sletter kode for bestilling'
     A                                      COLOR(WHT)
     A            B9NUMM    R        O  4 19REFFLD(LONUMM)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  31                                  ERRMSGID(LMO0670 *LIBL/QMFLAGR 31)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  4  2'Bestillings nr.'
     A          R B18MSG
     A*%%TS  SD  20120110  152814  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2' Leveringsmåte på ordre er   '
     A                                      COLOR(WHT)
     A                                  4  2'ikke satt, endre ordre før   '
     A                                      COLOR(WHT)
     A                                  5  2'bestilling sendes.'
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
