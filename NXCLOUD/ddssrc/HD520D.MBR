     A*%%TS  SD  20010323  094822  HKO         REL-V4R4M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : HD520D
     A* Beskrivelse . . : Vise varelinjer fra håndterminal
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  200516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20001013  100711  HKO         REL-V4R4M0  5769-PW1
6.20 A            B1VALG    R        B 11  2REFFLD(LÅVALG)
6.20 A                                      COLOR(YLW)
6.20 A                                      VALUES(0 6 7)
6.20 A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
6.20 A            B1VARE    R        O 11  4REFFLD(HDVARE)
6.20 A            B1TEK1        35A  O 11 13
6.20 A            B1ENHE    R        O 11 49REFFLD(HDENHE)
6.20 A            B1ANTA    R        O 11 53REFFLD(HDANTA)
6.20 A            B1DATO         6Y 0O 11 67EDTCDE(Z)
6.20 A            B1TIME         6Y 0O 11 74EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20010323  094822  HKO         REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise varelinjer fra håndterminal'
     A                                      COLOR(WHT)
7.xx A                                  2 38'HD520B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Refnr. .'
     A            B2REFN    R        O  4 11REFFLD(HDREFN)
     A                                  4 51'Status'
     A            B2STAT    R        O  4 58REFFLD(HOSTAT)
     A                                  5  2'Kunde. .'
     A            B2KUND    R        O  5 11REFFLD(HOKUND)
     A            B2KNAV        30A  O  5 18
     A                                  5 51'Dato .'
     A            B2DATO         6Y 0O  5 58EDTWRD('  .  .  ')
     A                                  6  2'Prosjekt'
     A            B2KPRO    R        O  6 11REFFLD(HOKPRO)
     A            B2PNAV        30A  O  6 18
     A                                  6 51'Kl.  .'
     A            B2TIME         6Y 0O  6 58EDTWRD('  :  :  ')
     A                                  7  2'Selgernr'
     A            B2SELG    R        O  7 11REFFLD(HOSELG)
     A                                  7 51'K-ref.'
     A            B2DREF    R        O  7 58REFFLD(HODREF)
     A                                  8  2'Lev.adr.'
     A            B2NAVN    R        O  8 11REFFLD(HONAVN)
     A                                  8 51'Rekv. '
     A            B2REKV    R        O  8 58REFFLD(HOREKV)
6.20 A                                  9  2'6=Vis lagerbok   7=Statistikk'
6.20 A                                      COLOR(BLU)
6.20 A                                 10  2'V Varenr.  Varetekst              -
6.20 A                                                   Enh     Antall     Dat-
6.20 A                                      o   Kl.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20000605  161021  ASK         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
