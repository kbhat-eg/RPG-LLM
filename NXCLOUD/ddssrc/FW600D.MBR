     A*%%TS  SD  19990205  152856  HKO         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FW600D
     A* Beskrivelse . . : Utskrift av varer fra leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  19990205  152856  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av varer fra leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rapporttype . . . . . . . .'
     A            B1RTYP    R        B  5 30REFFLD(FÅVALG)
     A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                  5 40'1 = Endrede varer'
     A                                  6 40'2 = Nye varer'
     A                                  7 40'3 = Alle varer'
7.xx A                                  9  2'Leverandør. . . . . . . . +'
     A            B1LDOR    R        B  9 30REFFLD(FWLDOR)
     A  32                                  ERRMSGID(FRL0010 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1NAVN        30A  O  9 40COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Lev. varenummer fra-til . .'
     A            B1LVAF    R        B 10 30REFFLD(FWLVAR)
     A  34                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 51'-'
     A            B1LVAT    R        B 10 54REFFLD(FWLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Prisdato. . . . . . . . . .'
     A            B1DATO         6Y 0B 12 30COLOR(YLW)
     A  35                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 13  2'Operand . . . . . . . . . .'
     A            B1OPER         2A  B 13 30COLOR(YLW)
     A  36                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 36)
     A                                      OVRDTA
     A                                 13 40'EQ = lik prisdato'
     A                                 14 40'GT = større enn prisdato'
     A                                 15 40'LT = mindre enn prisdato'
     A                                 17  2'Endringsprosent større enn.'
     A            B1ENDR         5Y 2B 17 30COLOR(YLW)
     A  37                                  ERRMSGID(FMW0300 *LIBL/QMFOFAK 37)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 17 40'Absoluttverdien av endringen'
     A                                 19  2'Endringskode (fra lev.) . .'
     A            B1EKOD    R        B 19 30REFFLD(FWEKOD)
     A  38                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19 40'N = Ny vare'
     A                                 20 40'E = Endret vare'
     A                                 21 40'S = Slettet vare'
     A                                 22 40'X = Første gangs nyinnlegging'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
