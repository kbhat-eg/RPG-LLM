     A*%%TS  SD  20050914  205206  HKO530      REL-V5R3M0  5722-WDS
     A*%%EC
6.30  *  6.30  150515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020717  093605  HKO         REL-V5R1M0  5722-WDS
     A            B1LDOR    R        H      REFFLD(JVLDOR)
     A            B1VALG         2Y 0B 11  2COLOR(YLW)
     A                                      VALUES(1 5 7 8 9 10 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
     A            B1TEK1    R        O 11  5REFFLD(JVTEK1)
     A            B1VARE    R   -7   O 11 41REFFLD(JVVARE)
     A            B1NOBB    R        O 11 50REFFLD(JVNOBB)
     A            B1LVAR    R   -5   O 11 59REFFLD(JVLVAR)
     A            B1NAVN         6A  O 11 75
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050914  205206  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Gruppebehandling - velge varer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varegruppe . . .'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  4 19REFFLD(JVOGRP)
     A  31                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2HGRP    R        B  4 22REFFLD(JVHGRP)
     A  32                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UGRP    R        B  4 25REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Søketekst. . . .'
     A                                      COLOR(BLU)
     A            B2STEK    R        B  5 19REFFLD(JSSTEK)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Frisøk . . . . .'
     A                                      COLOR(BLU)
     A            B2FTEK    R        B  6 19REFFLD(JSSTEK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'1=Velg  5=Vis  7=Betingelser  8=Sø-
     A                                      ketekster  9=Priser  10=Enheter'
     A                                      COLOR(BLU)
     A                                  9  2'V  Varetekst                      -
     A                                            Varenr  Nobb-nr  Lev. varenr -
     A                                          Lev.'
     A                                      COLOR(WHT)
     A            B2TEK1    R        B 10  5REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2VARE         8Y 0B 10 41COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2NOBB    R        B 10 50REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2LVAR    R   -5   B 10 59REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020717  093605  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F9=Gjenta søk   F13=Prisbeha-
     A                                      ndling'
     A                                      COLOR(BLU)
