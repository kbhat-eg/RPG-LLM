     A*%%TS  SD  20190819  141215  ASP_A22     REL-V7R3M0  5770-WDS
     A* -X- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RC100D
     A* Beskrivelse . . : Inkasso, vedlikehold av statuskoder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130302 KOB  Nytt program
7.xx A*       160120 BKV  Cloud tilpasninger
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020923  215706  KOB         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1NASK    R        O  7  5REFFLD(RCNASK)
7.xx A            B1NSTA    R        O  7 12REFFLD(RCNSTA)
7.xx A            B1NAVN    R  -10   O  7 14REFFLD(RKNAVN)
7.xx A            B1NSAK    R        O  7 35REFFLD(RCNSAK)
7.xx A            B1NASB    R        O  7 44REFFLD(RCNASB)
7.xx A            B1BDAT         6  0O  7 53EDTCDE(Y)
7.xx A            B1NSTO    R        O  7 63REFFLD(RCNSTO)
     A                                      EDTCDE(J)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20190819  141215  ASP_A22     REL-V7R3M0  5770-WDS
7.xx A                                      SFLSIZ(0016)
7.xx A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA07
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av saksregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'2=Endre  5=Vise   7=Lindorf'
     A                                      COLOR(BLU)
7.xx A*                                 8  2'V  Kunde  St Navn                S-
7.xx A*                                     aksnr.  Bilag    Bil.dato  Hovedsto-
7.xx A*                                     l'
7.xx A*                                     COLOR(WHT)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 12'S'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 14'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 35'Saksnr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 44'Bilag'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 53'Bil.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 63'Hovedstol'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  2'Valg'
7.xx A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  4  7REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A                                  4 10'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A            B2NASK    R        B  4 17REFFLD(RCNASK)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  4 25'Saksnr'
7.xx A                                      COLOR(WHT)
7.xx A            B2NSAK    R        B  4 32REFFLD(RCNSAK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020913  150031  KOB         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 27'F6=Opprett'
     A                                      COLOR(BLU)
     A                                 23 39'F7=Åpne saker/Alle saker'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020923  215706  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av saksregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN    R        O  4 69REFFLD(RWNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  5  2'Kunde. . . . . . .'
     A            C2NASK    R        O  5 23REFFLD(RCNASK)
     A                                      OVRDTA
     A            C2NAVN    R        O  5 30REFFLD(RKNAVN)
     A                                      OVRDTA
     A                                  7  2'Status . . . . . .'
     A            C2NSTA    R        B  7 23REFFLD(RCNSTA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 42'Saksnummer . . . .'
     A            C2NSAK    R        B  7 61REFFLD(RCNSAK)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Bilag. . . . . . .'
     A            C2NASB    R        B  8 23REFFLD(RCNASB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RZ)
     A                                  8 42'Bilagsdato . . . .'
     A            C2BDAT         6Y 0B  8 61EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Kundeid. . . . . .'
     A            C2NCNK    R        B 10 23REFFLD(RCNCNK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 42'Referanse. . . . .'
     A            C2NREF    R        B 10 61REFFLD(RCNREF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Hovedstol. . . . .'
     A            C2NSTO    R        B 12 23REFFLD(RCNSTO)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 12 42'Omkostninger . . .'
     A            C2NOMK    R        B 12 61REFFLD(RCNOMK)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 13  2'Renter . . . . . .'
     A            C2NRNT    R        B 13 23REFFLD(RCNRNT)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 13 42'Provisjon. . . . .'
     A            C2NPRV    R        B 13 61REFFLD(RCNPRV)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 15  2'Innbetalt. . . . .'
     A            C2NINB    R        B 15 23REFFLD(RCNINB)
     A                                      COLOR(YLW)
     A                                 15 42'Dato Innbet/Avsl..'
     A            C2NDAT    R        B 15 61REFFLD(RCNDAT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Avdrag . . . . . .'
     A            C2NAVR    R        B 16 23REFFLD(RCNAVR)
     A                                      COLOR(YLW)
6.NU A**                                    OVRATR
     A                                 16 42'Overvåkning. . . .'
     A            C2NOVÅ    R        B 16 61REFFLD(RCNOVÅ)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Kundens info . . .'
     A            C2NLED    R        B 18 23REFFLD(RCNLED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 20  2'Inkassovarsel nr..'
     A            C2NINR    R        B 20 23REFFLD(RCNINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 22 68' '
     A                                 23  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 15 56)
