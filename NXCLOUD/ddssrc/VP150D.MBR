     A*%%TS  SD  20081105  090606  BOF570      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VP150D
     A* Beskrivelse . . : Prisjustering
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
8.01 A*PRG2   050722  BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20081105  090606  BOF570      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Prisjustering'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Utplukk:'
     A                                      DSPATR(UL)
7.xx A                                  5  2'Leverandør . . . . . . . . . +'
     A            B1LDOR    R        B  5 33REFFLD(VVLDOR)
     A  31                                  ERRMSGID(FM00260 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1NAVN        30A  O  5 47COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Varegruppe . . . . . . . . . +'
     A            B1OGRP    R        B  6 33REFFLD(VVOGRP)
     A  33                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 36'+'
7.xx A            B1HGRP    R        B  6 38REFFLD(VVHGRP)
     A  34                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 41'+'
7.xx A            B1UGRP    R        B  6 43REFFLD(VVUGRP)
     A  35                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1UTXT        30A  O  6 47COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varenummer fra/til . . . . . +'
     A            B1VARF         8Y 0B  7 33COLOR(YLW)
     A  36                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                  7 42'-'
7.xx A                                  7 44'+'
7.xx A            B1VART         8Y 0B  7 46COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                  8  2'Leveringstype. . . . . . . . .'
     A            B1LETY         1A  B  8 33COLOR(YLW)
     A  42                                  ERRMSGID(VMP0160 *LIBL/QMFOFAK 38)
     A                                      OVRDTA
     A                                  8 41'Blank=Alle lev.typer'
7.xx A                                  9  2'Prisgruppe . . . . . . . . . +'
8.01 A            B1PRGR         2A  B  9 33COLOR(YLW)
     A  43                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 43)
     A                                      OVRDTA
     A                                  9 41'Blank=Alle prisgrupper'
     A                                 10  2'Prisdato . . . . . . . . . . .'
     A            B1DATO         6Y 0B 10 33COLOR(YLW)
     A  37                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 37)
     A  38                                  ERRMSGID(VMP0150 *LIBL/QMFOFAK 38)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12  2'Justering av salgspris . . . .'
     A            B1SPRO         5Y 2B 12 33COLOR(YLW)
     A  39                                  ERRMSGID(VMP0500 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(VMP0510 *LIBL/QMFOFAK 40)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 12 41'%'
     A                                 13  2'Justering av kostpris. . . . .'
     A            B1KPRO         5Y 2B 13 33COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 13 41'%'
     A                                 14  2'Justering av innkjøpspris. . .'
     A            B1IPRO         5Y 2B 14 33COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 14 41'%'
     A                                 15  2'Justering av butikkpris. . . .'
     A            B1BPRO         5Y 2B 15 33COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 15 41'%'
     A                                 16  2'Dekningsgrad (DG). . . . . . .'
     A            B1DEKN         4Y 2B 16 33COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A                                 16 41'%'
     A                                 18  2'Beholde nyere priser . . . . .'
     A            B1NYPR         1Y 0B 18 33COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 18 41'1=Ja,0=Nei'
     A                                 19  2'Utelate ordinært prisede varer'
     A            B1ORPV         1Y 0B 19 33COLOR(YLW)
     A  41                                  ERRMSGID(VMP0510 *LIBL/QMFOFAK 41)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 19 41'1=Ja,0=Nei'
     A                                 20  2'Utelate nettoprisede varer . .'
     A            B1NTPV         1Y 0B 20 33COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 20 41'1=Ja,0=Nei'
     A                                 21  2'Utelate bunnprisede varer. . .'
     A            B1BUPV         1Y 0B 21 33COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 21 41'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
