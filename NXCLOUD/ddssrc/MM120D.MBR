     A*%%TS  SD  20070102  100308  BOF550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : MM120D
     A* Beskrivelse . . : prognose - store avvik best.mengde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       020107 BØ   Nytt program
7.xx A* 7.xx  210316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070102  085037  BOF550      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5 8)
     A                                      CHKMSGID(MM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   O  8  5REFFLD(MQVARE)
     A            B1LAGE    R        O  8 14REFFLD(MQLAGE)
     A            B1NAVN    R   -5   O  8 17REFFLD(LÅBESK)
     A            B1BMEG    R        O  8 48REFFLD(MQBMEG)
     A            B1BMEN    R        O  8 62REFFLD(MQBMEN)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070102  100308  BOF550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Prognose'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vis større avvik på best.mengde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'5=Vis  8=Lagervedl.'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 20'Varenavn        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 46'Gml.best.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Ny best.mengde'
7.xx A**                                              Gml.best.mengde  Ny best.-
7.xx A**                                    mengde     '
     A                                      COLOR(WHT)
7.xx A            B2VARE    R   -7   B  5  5REFFLD(MQVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070102  085037  BOF550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Utviklingsmal'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Se større avvik best.mengde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenrummer  . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(MQVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Lager  . . . . . . . . . . .'
     A            C2LAGE    R        O  7 31REFFLD(MQLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Gammel bestillingsmengde   .'
     A            C2BMEG    R        O  8 31REFFLD(MQBMEG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Ny  bestillingsmengde   .  .'
     A            C2BMEN    R        O  9 31REFFLD(MQBMEN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(MQUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
