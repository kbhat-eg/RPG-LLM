     A*%%TS  SD  20101207  141535  ASP_X50     REL-V6R1M0  5761-WDS
     A*%%EC
7.xx A*          200116 bsa  Justeringer basert på ny GUI og regler
     A*----------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101207  141439  ASP_X50     REL-V6R1M0  5761-WDS
     A            B1VARE    R        H      REFFLD(VVVARE)
     A            B1LVAR    R        H      REFFLD(VVLVAR)
     A            B1NOBB    R        H      REFFLD(VVNOBB)
     A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00010 *LIBL/QMFVEDL)
     A            B1TEK1    R   -3   O 10  4REFFLD(VVTEK1)
     A  50                                  COLOR(RED)
     A            B1NYPO        15A  O 10 37
6.31 A  51                                  COLOR(BLU)
6.31 A  52                                  COLOR(YLW)
     A            B1NAVN        14A  O 10 53
6.31 A  51                                  COLOR(BLU)
6.31 A  52                                  COLOR(YLW)
     A            B1SORT    R   -9   O 10 68REFFLD(VVSORT)
     A            B1SENT         1A  H
     A            B1SESS         1A  H
     A            B1STAT         2A  O 10 70
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101207  141535  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF08
     A                                      CA12
     A                                      CF23
     A                                      KEEP
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge vare '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varegruppe . . +'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  4 19REFFLD(VVOGRP)
     A  31                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 22'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  4 24REFFLD(VVHGRP)
     A  32                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 27'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  4 29REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Frisøk . . . . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  5 19COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Varetekst 1'
7.xx A                                      COLOR(WHT)
7.xx A  81                              6 37'Lev. varenr.'
7.xx A  81                                  COLOR(WHT)
7.xx A  80                              6 37'Varenr'
7.xx A  80                                  COLOR(WHT)
7.xx A  82                              6 37'Nobbnr'
7.xx A  82                                  COLOR(WHT)
7.xx A                                  8  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Varetekst 1         '
     A                                      COLOR(WHT)
7.xx A  81                              9 37'Lev. varenr.'
     A  81                                  COLOR(WHT)
7.xx A  80                              9 37'Varenr'
     A  80                                  COLOR(WHT)
7.xx A  82                              9 37'Nobbnr'
     A  82                                  COLOR(WHT)
7.xx A                                  9 53'Leverandør'
     A                                      COLOR(WHT)
7.xx A                                  9 68'S'
     A                                      COLOR(WHT)
7.xx A                                  9 70'CP'
     A                                      COLOR(WHT)
7.xx A            B2TEK1    R   -3   B  7  4REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A  80        B2VARE         8Y 0B  7 37COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A  81        B2LVAR    R   -5   B  7 37REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A  82        B2NOBB    R        B  7 37REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20040514  130013  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 20 71)
     A                                 19  2'F3=Gå tilbake  F4=Spørring  F5=For-
     A                                      ny  F8=Skafferegister   F23=Ny pos.'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
