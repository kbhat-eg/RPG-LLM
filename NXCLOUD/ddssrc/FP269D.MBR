     A*%%TS  SD  20160902  134153  ASP_G0XA    REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : OFAK
     A* Format. . . . . : FP269D
     A* Beskrivelse . . : Vedlikehold av elektroniske hylleforkanter (ADM)
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       060218 bsa  Nytt program
7.01 A*       130818 bsa  Lagt til status
     A*
8.01 A* K000  030921 bsa  Utvidet med Delfi som leverandør
8.02 A* K000  080622 bsa  Mulighet for REST API integrasjon
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20151026  150945  ASP_G0X     REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1FILG         3A  O  8  5
     A            B1FIRM         3Y 0O  8  9
     A            B1LAGE         2Y 0O  8 13
     A            B1FIDE        50A  O  8 16
7.01 A            B1AKTI         1A  O  8 68
      *
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160901  133258  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av elektroniske hylle-
     A                                      forkanter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Flg Fir LG Filbeskrivelse'
7.01 A                                      COLOR(WHT)
7.01 A                                  6 68'A'
     A                                      COLOR(WHT)
     A            B2FILG         3A  B  7  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20160901  130747  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny   F6=Oppre-
     A                                      tt'
     A                                      COLOR(BLU)
     A                                 23 40' '
     A          R C1BLD
     A*%%TS  SD  20160902  133614  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av elektroniske hylle-
     A                                      forkanter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Filgruppe. . . . .'
     A            C1FILG         3A  B  5 22COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Firma. . . . . . .'
     A            C1FIRM         3Y 0B  6 22COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Lager. . . . . . .'
     A            C1LAGE         2Y 0B  7 22COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake                   '
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20160902  134153  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av elektroniske hylle-
     A                                      forkanter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Filgruppe. . . . .'
     A            C1FILG         3A  O  5 22COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Firma. . . . . . .'
     A            C1FIRM         3Y 0O  6 22COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Lager. . . . . . .'
     A            C1LAGE         2Y 0O  7 22COLOR(YLW)
     A                                      OVRDTA
      *
     A                                  9  2'Aktiv kode . . . .'
     A            C2AKTI         1A  B  9 22COLOR(YLW)
     A                                      OVRDTA
     A                                  9 25'0=nei, 1=ja'
     A                                 10  2'Etikettype . . . .'
     A            C2ETIK         1A  B 10 22COLOR(YLW)
     A                                      OVRDTA
8.01 A                                 10 25'E=ESL, P=Pricer, D=Delfi'
     A                                 11  2'Filnavn. . . . . .'
     A            C2FIDE        50A  B 11 22COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2'Overf.anmodning. .'
     A            C2OVFA        10A  B 12 22COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Mappe. . . . . . .'
     A            C2MPTY        10A  B 13 22COLOR(YLW)
     A                                      OVRDTA
8.02 A                                 13 33'(FTP/SFTP/IFS/REST)'
     A                                 16  2'Siste kjøredato. .'
     A            C2SKDA         6Y 0O 16 22COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17  2'Sist overført. . .'
     A            C2SODA         6Y 0O 17 22COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
      *
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20151026  125839  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kobling finnes fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20160901  133258  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kobling       '
     A                                      DSPATR(RI)
     A                                  4  2'Filgruppe    '
     A            D1FILG         3A  O  4 17
     A                                  5  2'Firmakode    '
     A            D1FIRM         3Y 0O  5 17
     A                                  6  2'Lager        '
     A            D1LAGE         2Y 0O  6 17
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
