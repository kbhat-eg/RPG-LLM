     A*%%TS  SD  20210128  094715  ASP_I27     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RP140R
     A* Beskrivelse . . : Prosjekt, Budsjett pr konto
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230819 sst  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20201009  124850  SST700      REL-V7R3M0  5770-WDS
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 4 5 9)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        B  7  5REFFLD(RAGKOD)
     A  41                                  DSPATR(PR)
     A            B1TEXT        30A  O  7 11
     A            B1BUDS         9Y 0B  7 43EDTWRD('   .   .   -')
     A  41                                  DSPATR(PR)
     A            B1REGN         9Y 0O  7 57EDTWRD('   .   .   -')
     A            BHBUDS         9Y 0H
     A            BHKONT    R        H      REFFLD(RAGKOD)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20210128  094715  ASP_I27     REL-V7R3M0  5770-WDS
     A  *DS3                                SFLSIZ(0012)
     A  *WIDE                               SFLSIZ(0012)
     A  *DS3                                SFLPAG(0011)
     A  *WIDE                               SFLPAG(0011)
     A  *DS3                                WINDOW(B2CMD)
     A  *WIDE                               WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA09
     A                                      CA10
     A                                      CA12
     A                                      CA15
     A                                      CA22
     A                                      CA23
     A                                      CA24
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  3  5'Konto Beskrivelse                 -
     A                                                  '
     A                                      COLOR(WHT)
     A                                  5  2'5=Varer  4=Slett  9=Spørre på kont-
     A                                      o'
     A                                      COLOR(BLU)
     A                                  6  2'V  Konto Beskrivelse              -
     A                                                Budsjett      Regnskap   '
     A                                      COLOR(WHT)
     A            B2FKTO    R        B  4  5REFFLD(RAGKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2FTXT    R        B  4 11REFFLD(RAGTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  1  1'Prosjekt:'
     A                                      COLOR(WHT)
     A            B2PTXT        60A  O  2 11COLOR(YLW)
     A                                      OVRDTA
     A            B2PROS        60A  O  1 11COLOR(YLW)
     A                                      OVRDTA
     A                                  2  1'(RP140)'
     A          R B2CMD
     A*%%TS  SD  20190903  120632  G0XEGSVSTR  REL-V7R3M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 20 70)
     A  *WIDE                               WINDOW(*DFT 20 70)
     A                                 19  2'F3=Gå tilbake  F5=Forny  F9=Smartk-
     A                                      alk  F10=Oppdat.Kost  F15=Varer'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 80' '
