     A*%%TS  SD  20050922  093418  GRO540      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : LA107D
     A* Beskrivelse . . : Synkroniseringsparametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  160316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20050922  093418  GRO540      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Synkroniseringsparametre'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  5  5'Type. . . . . . . . . .'
     A            A1FTYP    R        B  5 31REFFLD(LAFTYP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A**                                5 35'N/M/A nei/manuell/automatisk'
7.xx A                                  5 35'N=nei, M=manuell, A=Automatisk'
      *
     A                                  7  5'Varsling. . . . . . . .'
     A            A1FVAR    R        B  7 31REFFLD(LAFVAR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7 35'J=ja NB Kun ved M og A'
     A                                  9  5'*** Automatiske endringer - NB Gje+
     A                                      lder kun for kode A ***'
     A                                      COLOR(WHT)
     A                                 10  5'Rekalkuler kostpris . .'
     A            A1FCHK    R        B 10 31REFFLD(LAFCHK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 35'J=ja'
     A                                 11  5'Oppdatering av antall .'
     A            A1FCHA    R        B 11 31REFFLD(LAFCHA)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11 35'J=ja'
     A                                 12  5'Nye linjer. . . . . . .'
     A            A1FCHL    R        B 12 31REFFLD(LAFCHL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 12 35'J=ja'
     A                                 13  5'Ordreinfokode . . . . .'
     A            A1FITY    R        B 13 31REFFLD(LAFITY)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.20 A                                 14  5'Synkron. gjelder fra. .'
6.20 A            A1FGFR         6Y 0B 14 31
6.20 A  33                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 33)
6.20 A                                      EDTWRD('  .  .  ')
6.20 A                                      OVRDTA
6.20 A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
