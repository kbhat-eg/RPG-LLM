     A*%%TS  SD  20051021  083151  HKO530      REL-V5R3M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A*                                     DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051021  083151  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
6.20 A                                      VALUES(0 2 3 4 5 7 8)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1TBOT    R        O  8  8REFFLD(VATBOT)
     A            B1TBES    R        O  8 16REFFLD(VATBES)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051021  083151  HKO530      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
6.20 A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonustype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Bonustype'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 16'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
6.21 A                                        7=Maler  8=Bonussatser'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  8'BT'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Beskrivelse               '
7.xx A                                      COLOR(WHT)
7.xx A            B2TBOT    R        B  5  8REFFLD(VATBOT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2TBES    R        B  5 16REFFLD(VATBES)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  080848  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett   F9=Bonus/kkat'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051019  120047  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprett bonustype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bonustype. . . . . . . . . .'
     A            C1TBOT    R        B  5 31REFFLD(VATBOT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20051019  114913  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bonustype finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051019  115603  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonustype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bonustype. . . . . . . . . .'
     A            C1TBOT    R        O  5 31REFFLD(VATBOT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2TBES    R        B  7 31REFFLD(VATBES)
     A  31                                  ERRMSGID(VMA6100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Summeringsnivå . . . . . . .'
     A            C2BSUM    R        B  8 31REFFLD(VATSUM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 39'0=pr.elem, 1=totalt'
     A                                  9  2'Fratrekk . . . . . . . . . .'
     A            C2BRED    R        B  9 31REFFLD(VATRED)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 39'Oppgis i prosent'
     A                                 11  2'Undertekst portalvisning'
     A            C2UTX1        78A  B 12  2COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2UTX2        78A  B 13  2COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            C2TODA         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2TEDA         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2TEUS    R        O 22 70REFFLD(VATEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051019  114913  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bonustype '
     A                                      DSPATR(RI)
     A                                  4  2'Bonustype. . . . .'
     A            D1TBOT    R        O  4 21REFFLD(VATBOT)
     A                                  6  2'Beskrivelse. . . .'
     A            VATBES    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051019  114913  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Bonustype. . . . .'
     A            K1TBOT    R        B  4 21REFFLD(VATBOT)
     A  31                                  ERRMSGID(VMA6510 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA6530 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Bonustype. . . . .'
     A            VATBOT    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            VATBES    R        O  9 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
