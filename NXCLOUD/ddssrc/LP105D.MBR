     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LP105D
     A* Beskrivelse . . : Vedlikehold av tilleggsdager pr leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A* 6.30  010914 BSA  Nytt program
7.xx A* 7.xx  160316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010702  094226  ASK         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1LDOR    R        O  8  4REFFLD(LPGLDO)
     A            B1LAGE    R        O  8 11REFFLD(LPLAGE)
     A            B1LNAV        30A  O  8 15
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  084253  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WCURRN         5S 0H
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilleggsdager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Levr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Levr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Navn                '
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  5  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 2 3 4 5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  5  4REFFLD(LPGLDO)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LAGE    R        B  5 11REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
      *
     A          R B2CMD
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny  F6=Opprett'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020924  153446  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CLRL(*ALL)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilleggsdager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  5 31REFFLD(LPGLDO)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LNAV        30A  O  5 39COLOR(GRN)
     A                                  6  2'Lager. . .(Blank gir alle) +'
     A            C1LAGE    R        B  6 31REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LAGN        20A  O  6 39COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1MSG
     A*%%TS  SD  19971028  093526  ASK         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  1  2'Feilmelding!'
     A                                  3  2'Distribusjonsliste finnes'
     A                                  4  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilleggsdager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . .'
     A            C1LDOR    R        O  5 21REFFLD(LPGLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  5 29COLOR(GRN)
     A                                  6  2'Lager. . . . . . .'
     A            C1LAGE    R        O  6 21REFFLD(LPLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LAGN        20A  O  6 29COLOR(GRN)
     A                                      OVRDTA
     A                                 13  2'Antall dager . . .'
     A            C2GDAG         3  0B 13 21
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(LMA0260 *LIBL/QMFLAGR 33)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Sist endret dato .'
     A            C2EDAT         6Y 0O 20 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 21  2'Endret av. . . . .'
     A            C2EUSR    R        O 21 21REFFLD(LPEUSR)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R D1WIN
     A*%%TS  SD  19990527  123440  ASK         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette oppføring '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør.'
     A            D1LDOR    R        O  4 14REFFLD(LPGLDO)
     A            D1LNAV        30A  O  4 21
     A                                  5  2'Lager . . .'
     A            D1LAGE    R        O  5 14REFFLD(LPLAGE)
     A            D1LAGN        20A  O  5 21
     A                                  7  2'Dager . . .'
     A            D1GDAG         3  0O  7 14
     A                                 13  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
     A*
     A          R K1WIN
     A*%%TS  SD  20000228  151422  ASK         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Leverandør +'
7.xx A            K1LDOR    R        B  4 15REFFLD(LPGLDO)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LMP0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LNAV        30A  O  4 21
     A                                      OVRDTA
7.xx A                                  5  2'Lager. . . +'
7.xx A            K1LAGE    R        B  5 15REFFLD(LPLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LAGN        20A  O  5 21
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Leverandør'
     A            K2LDOR    R        O 10 13REFFLD(LPGLDO)
     A                                      OVRDTA
     A            K2LNAV        30A  O 10 21
     A                                      OVRDTA
     A                                 11  2'Lager. . .'
     A            K2LAGE    R        O 11 13REFFLD(LPLAGE)
     A                                      OVRDTA
     A            K2LAGN        20A  O 11 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
