     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AP624D
     A* Beskrivelse . . : Parametre - egenskaper for PROA
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       210710 BSA  Nytt program
6.20 A*       070611 BSA  Utvdet feltvisning
6.30 A*       130814 BSA  Utvdet felt
8.01  *       260525 ASK  Lagt inn mulighet for å legge inn nye poster
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1UIDE    R        O  8  5REFFLD(AFUIDE)
6.20 A            B1UDSS        50A  O  8 17
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
8.01 A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av egenskaper PROA   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre            5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  5'Identitet'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A            B2UIDE    R        B  6  5REFFLD(AFUIDE)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
8.01 A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
8.01 A                                 23  2'F3=Gå tilbake  F5=Forny  F6=Oppret+
     A                                       t'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av egenskaper PROA    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Identitet. . . .'
     A            C1UIDE    R        B  5 20REFFLD(AFUIDE)
8.01 A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040913  082010  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Feil med data    '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040913  090351  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av egenskaper PROA    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Identitet. . . .'
     A            C2UIDE    R        O  5 20REFFLD(AFUIDE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2DSS1        50A  B  7 20
     A                                      COLOR(YLW)
6.10 A                                      CHECK(LC)
     A                                      OVRDTA
     A            C2DSS2        50A  B  8 20
     A                                      COLOR(YLW)
6.10 A                                      CHECK(LC)
     A                                      OVRDTA
6.30 A            C2DSS3        50A  B  9 20
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(LC)
6.30 A                                      OVRDTA
6.30 A            C2DSS4        50A  B 10 20
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(LC)
6.30 A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040913  082010  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette identitet    '
     A                                      DSPATR(RI)
     A                                  4  2'Identitet. . . . .'
     A            D1UIDE    R        O  4 21REFFLD(AFVPRT)
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040913  090035  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Identitet. . . . .'
     A            K1UIDE    R        O  4 21REFFLD(AFVPRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Identitet. . . . .'
     A            K2UIDE    R        O 10 21REFFLD(AFVPRT)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
