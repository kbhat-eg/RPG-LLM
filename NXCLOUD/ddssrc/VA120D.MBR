     A*%%TS  SD  20031014  153620  AMD         REL-V5R2M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20031014  151927  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1KKOD    R        O  8  5REFFLD(VAKKOD)
     A            B1KLTY    R        O  8 12REFFLD(VAKLTY)
     A            B1KOTY    R        O  8 15REFFLD(VAKOTY)
     A            B1KTXT    R        O  8 19REFFLD(VAKTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20031014  145614  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontohenvisning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 19'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  4 51'Leveringstype/Ordretype oppgis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'LT'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 19'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 51'bare om det ønskes avvikende  '
     A                                      COLOR(BLU)
7.xx A            B2KKOD    R        B  5  5REFFLD(VAKKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2KTXT    R        B  5 19REFFLD(VAKTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 51'kontering på en av disse. '
     A                                      COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  19931028  142557  AMD         REL-V2R2M0  5738-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20031014  151927  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontohenvisning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontohenvisning. . . . . . .'
     A            C1KKOD    R        B  5 31REFFLD(VAKKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Ordretype. . . . . . . . . +'
     A            C1KOTY    R        B  7 31REFFLD(VAKOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leveringstype. . . . . . . .'
     A            C1KLTY    R        B  6 31REFFLD(VAKLTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020306  135010  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kontohenvisning finnes'
     A                                  3  2'fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20031014  151927  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontohenvisning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontohenvisning. . . . . . .'
     A            C1KKOD    R        O  5 31REFFLD(VAKKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Leveringstype. . . . . . . .'
     A            C1KLTY    R        O  6 31REFFLD(VAKLTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Ordretype. . . . . . . . . .'
     A            C1KOTY    R        O  7 31REFFLD(VAKOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2KTXT    R        B  8 31REFFLD(VAKTXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10 31'Konto'
     A                                      COLOR(WHT)
     A                                 10 40'Spes'
     A                                      COLOR(WHT)
     A                                 10 47'Avdeling'
     A                                      COLOR(WHT)
     A                                 11  2'Salg, inkl. mva. . . . . . .'
     A            C2KKO1    R        B 11 31REFFLD(VAKKO1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A  31 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 31)
     A  31 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 31)
     A  31 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 31)
     A  31 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 31)
     A  31 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A            C2KSP1    R        B 11 40REFFLD(VAKSP1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV1    R        B 11 49REFFLD(VAKAV1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12  2'Salg, eksl. mva. . . . . . .'
     A            C2KKO2    R        B 12 31REFFLD(VAKKO2)
     A  32 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 32)
     A  32 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 32)
     A  32 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 32)
     A  32 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 32)
     A  32 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP2    R        B 12 40REFFLD(VAKSP2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV2    R        B 12 49REFFLD(VAKAV2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14  2'Kost . . . . . . . . . . . .'
     A            C2KKO3    R        B 14 31REFFLD(VAKKO3)
     A  33 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 33)
     A  33 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 33)
     A  33 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 33)
     A  33 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 33)
     A  33 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP3    R        B 14 40REFFLD(VAKSP3)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV3    R        B 14 49REFFLD(VAKAV3)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 15  2'Innkjøp. . . . . . . . . . .'
     A            C2KKO4    R        B 15 31REFFLD(VAKKO4)
     A  34 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 34)
     A  34 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 34)
     A  34 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 34)
     A  34 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 34)
     A  34 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP4    R        B 15 40REFFLD(VAKSP4)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV4    R        B 15 49REFFLD(VAKAV4)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'Kost, motpost. . . . . . . .'
     A            C2KKO5    R        B 17 31REFFLD(VAKKO5)
     A  35 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 35)
     A  35 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 35)
     A  35 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 35)
     A  35 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 35)
     A  35 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP5    R        B 17 40REFFLD(VAKSP5)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV5    R        B 17 49REFFLD(VAKAV5)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 18  2'Rabatter . . . . . . . . . .'
     A            C2KKO6    R        B 18 31REFFLD(VAKKO6)
     A  36 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 36)
     A  36 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 36)
     A  36 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 36)
     A  36 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 36)
     A  36 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP6    R        B 18 40REFFLD(VAKSP6)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV6    R        B 18 49REFFLD(VAKAV6)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 19  2'Prosjekt uttak . . . . . . .'
     A            C2KKO7    R        B 19 31REFFLD(VAKKO7)
     A  37 41                               ERRMSGID(FRH0020 *LIBL/QMFOFAK 37)
     A  37 42                               ERRMSGID(FRH0100 *LIBL/QMFOFAK 37)
     A  37 43                               ERRMSGID(FRH0110 *LIBL/QMFOFAK 37)
     A  37 44                               ERRMSGID(FRH0120 *LIBL/QMFOFAK 37)
     A  37 45                               ERRMSGID(FRH0130 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KSP7    R        B 19 40REFFLD(VAKSP7)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2KAV7    R        B 19 49REFFLD(VAKAV7)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20031014  151927  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kontohenvisning '
     A                                      DSPATR(RI)
     A                                  4  2'Kontohenvisning'
     A            D1KKOD    R        O  4 21REFFLD(VAKKOD)
     A                                  5  2'Leveringstype .'
     A            D1KLTY    R        O  5 21REFFLD(VAKLTY)
     A                                  6  2'Ordretype . . .'
     A            D1KOTY    R        O  6 21REFFLD(VAKOTY)
     A                                  7  2'Beskrivelse . .'
     A            VAKTXT    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20031014  151927  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 16 51)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kontohenvisning. .'
     A            K1KKOD    R        B  4 21REFFLD(VAKKOD)
     A  31                                  ERRMSGID(VMA2010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA2030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Leveringstype. . .'
     A            K1KLTY    R        B  5 21REFFLD(VAKLTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Ordretype. . . . .'
     A            K1KOTY    R        B  6 21REFFLD(VAKOTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Kontohenvisning. .'
     A            VAKKOD    R        O 10 21OVRDTA
     A                                 11  2'Leveringstype. . .'
     A            VAKLTY    R        O 11 21OVRDTA
     A                                 12  2'Ordretype. . . . .'
     A            VAKOTY    R        O 12 21OVRDTA
     A                                 13  2'Beskrivelse. . . .'
     A            VAKTXT    R        O 13 21OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
