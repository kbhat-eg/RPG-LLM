     A*%%TS  SD  19981019  131605  AMD         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSHOP
     A* Format. . . . . : BO430D
     A* Beskrivelse . . : Vedlikehold av skjerm-kasse-register
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  260516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A                                      HLPTITLE('Hjelpetekst for BO430D')
     A                                      HLPPNLGRP(HELE BO430H)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A          R B1SFL                     SFL
     A*%%TS  SD  19950221  095032  AMD         REL-V2R3M0  5738-PW1
     A            B1VALG    R        B 11  2REFFLD(BÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '3' '4' '5' ' ')
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A            B1WSID    R        O 11  5REFFLD(BWWSID)
     A            B1TXT1    R        O 11 16REFFLD(BWTXT1)
     A            B1TXT2    R        O 11 47REFFLD(BWTXT2)
     A            B1WSGR    R        O 11 68REFFLD(BWWSGR)
     A            B1KTYP    R        O 11 77REFFLD(BWKTYP)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  19960925  155207  AMD         REL-V3R6M0  5716-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A          H                           HLPPNLGRP(B2HELE BO430H)
     A                                      HLPARA(01 001 24 080)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
7.xx A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 38'BO430B1'
     A                                      COLOR(WHT)
7.xx A                                  2 66'Dato:'
     A                                      COLOR(WHT)
7.xx A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
7.xx A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
7.xx A                                  2 54USER
     A                                      COLOR(WHT)
7.xx A                                  2  2'Vedlikehold av skjerm-kasse-regist-
     A                                      er'
     A                                      COLOR(WHT)
7.xx A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
7.xx A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A**                                5  2'V'
7.xx A**                                    COLOR(WHT)
7.xx A                                  5  4'Skjerm/kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  9  2'1=Opprette'
     A                                      COLOR(BLU)
7.xx A                                  9 14'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  9 23'3=Kopiere'
     A                                      COLOR(BLU)
7.xx A                                  9 35'4=Slette'
     A                                      COLOR(BLU)
7.xx A                                  9 46'5=Vise'
     A                                      COLOR(BLU)
7.xx A                                  9 55'Blank=Vise fra'
7.xx A                                      COLOR(BLU)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'Skjerm/kasse'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 17'Tekst                '
7.xx A                                      COLOR(WHT)
7.xx A                                 10 47'Plassering'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 67'Gruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 75'Type'
7.xx A**                                9  2'V Skjerm/kasse  Tekst             -
7.xx A**                                               Plassering          Grup-
7.xx A**                                    pe  Type '
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  6  2REFFLD(BÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('1' '2' '3' '4' '5' ' ')
     A                                      CHKMSGID(BM00010 *LIBL/QMFSHOP)
     A                                      OVRDTA
7.xx A            B2WSID    R        B  6  5REFFLD(BWWSID)
     A                                      DLTCHK
     A  31                                  ERRMSGID(BMW0010 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BMW0020 *LIBL/QMFSHOP 32)
     A  33                                  ERRMSGID(BMW0030 *LIBL/QMFSHOP 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19950221  101050  AMD         REL-V2R3M0  5738-PW1
     A                                 23  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Forny'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19960925  155207  AMD         REL-V3R6M0  5716-PW1
     A                                      CA03
     A                                      CA12
     A                                      CLRL(*ALL)
     A          H                           HLPPNLGRP(C1HELE BO430H)
     A                                      HLPARA(01 001 23 080)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
     A                                  1 31'***  AS Butikk  ***'
     A                                      COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 10USER
     A                                      COLOR(WHT)
     A                                  2 25'Vedlikehold av skjerm-kasse-regist-
     A                                      er'
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skjerm/kasse . . . . . . . .'
     A            C1WSID    R        B  5 31REFFLD(BWWSID)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Annuller'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  19981019  131605  AMD         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A          H                           HLPPNLGRP(C2HELE BO430H)
     A                                      HLPARA(01 001 23 080)
     A                                  1  2'System:'
     A                                      COLOR(WHT)
     A                                  1 10SYSNAME
     A                                      COLOR(WHT)
     A                                  1 31'***  AS Butikk  ***'
     A                                      COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 10USER
     A                                      COLOR(WHT)
     A                                  2 25'Vedlikehold av skjerm-kasse-regist-
     A                                      er'
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Skjerm/kasse . . . . . . . .'
     A            C1WSID    R        O  5 31REFFLD(BWWSID)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2NYEN    R        O  5 69REFFLD(BÅNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Gruppekode . . . . . . . . .'
     A            C2WSGR    R        B  7 31REFFLD(BWWSGR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8  2'Tekst. . . . . . . . . . . .'
     A            C2TXT1    R        B  8 31REFFLD(BWTXT1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  9  2'Plassering . . . . . . . . .'
     A            C2TXT2    R        B  9 31REFFLD(BWTXT2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 10  2'Bongnummer . . . . . . . . .'
     A            C2BONG    R        B 10 31REFFLD(BWBONG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSGID(BMW0040 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BMW0050 *LIBL/QMFSHOP 32)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11  2'Avdeling . . . . . . . . . .'
     A            C2AVDE    R        B 11 31REFFLD(BWAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12  2'Spesifikasjon. . . . . . . .'
     A            C2SPES    R        B 12 31REFFLD(BWSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13  2'Lager. . . . . . . . . . . .'
     A            C2LAGE    R        B 13 31REFFLD(BWLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A  34                                  ERRMSGID(BMW0060 *LIBL/QMFSHOP 34)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2LATX        20A  O 13 34OVRDTA
     A                                 15  2'Skriver/utkø Kontant . . . .'
     A            C2KONQ    R        B 15 31REFFLD(AFRFR/AFOUTQ *LIBL/AFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15 47'Kode for kassetype . . . . .'
     A            C2KTYP    R        B 15 76REFFLD(BWKTYP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  2'Skriver/utkø Kreditt . . . .'
     A            C2KRNQ    R        B 16 31REFFLD(AFRFR/AFOUTQ *LIBL/AFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16 47'Kode for antall. . . . . . .'
     A            C2KANT    R        B 16 76REFFLD(BWKANT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 17  2'Skriver/utkø Faktura . . . .'
     A            C2FANQ    R        B 17 31REFFLD(AFRFR/AFOUTQ *LIBL/AFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 17 47'Kode for varenummer. . . . .'
     A            C2KVAR    R        B 17 76REFFLD(BWKVAR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18  2'Skriver/utkø Inn/Utbet . . .'
     A            C2IUTQ    R        B 18 31REFFLD(AFRFR/AFOUTQ *LIBL/AFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18 47'Kode for type bong . . . . .'
     A            C2KBON    R        B 18 76REFFLD(BWKBON)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 19  2'Skriver/utkø Hurtigordre . .'
     A            C2PLUQ    R        B 19 31REFFLD(AFRFR/AFOUTQ *LIBL/AFRF)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 19 47'Kode for blanking av selger.'
     A            C2KSEL    R        B 19 76REFFLD(BWKSEL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 21  2'Statuskode aktiv skjerm. . .'
     A            C2AKTI    R        B 21 31REFFLD(BWAKTI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19950221  100300  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A          H                           HLPPNLGRP(D1HELE BO430H)
     A                                      HLPARA(*RCD)
     A                                  2  4' Slette post '
     A                                      DSPATR(RI)
     A                                  4  4'Skjerm/kasse. .'
     A            BWWSID    R        O  4 20REFFLD(BWWSID)
     A                                      DSPATR(UL)
     A                                      TEXT('Konto')
     A                                  6  4'Gruppe. . . . .'
     A            BWWSGR    R        O  6 20REFFLD(BWWSGR)
     A                                      DSPATR(UL)
     A                                  7  4'Tekst . . . . .'
     A            BWTXT1    R        O  7 20REFFLD(BWTXT1)
     A                                      DSPATR(UL)
     A                                      TEXT('Tekst 1')
     A                                  8  4'Plassering. . .'
     A            BWTXT2    R        O  8 20REFFLD(BWTXT2)
     A                                      DSPATR(UL)
     A                                      TEXT('Tekst 2')
     A                                 10  4'Bong-nummer . .'
     A            BWBONG    R        O 10 20REFFLD(BWBONG)
     A                                      DSPATR(UL)
     A                                      TEXT('Antall')
     A                                 14  4'F12=Annuller'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19950221  102931  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA12
     A                                      PUTOVR
     A          H                           HLPPNLGRP(K1HELE BO430H)
     A                                      HLPARA(*RCD)
     A                                  2  4' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  4'Skjerm/kasse. .'
     A            K1WSID    R        B  4 20REFFLD(BWWSID)
     A  31                                  ERRMSGID(BMW0030 *LIBL/QMFSHOP 31)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                  5  4'Tekst . . . . .'
     A            K1TXT1    R        B  5 20REFFLD(BWTXT1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  6  4'Plassering. . .'
     A            K1TXT2    R        B  6 20REFFLD(BWTXT2)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  7  4'Bong-nummer . .'
     A            K1BONG    R        B  7 20REFFLD(BWBONG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  4' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  4'Skjerm/kasse. .'
     A            BWWSID    R        O 11 20REFFLD(BWWSID)
     A                                      DSPATR(UL)
     A                                 12  4'Gruppe. . . . .'
     A            K1WSGR    R        O 12 20REFFLD(BWWSGR)
     A                                      DSPATR(UL)
     A                                 14  4'F12=Annuller'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19950221  101050  AMD         REL-V2R3M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 15 50)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
