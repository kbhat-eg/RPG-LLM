     A*%%TS  SD  20110329  141031  NHO620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA109D
     A* Beskrivelse . . : Selgere, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
     A*
6.20 A* 6.20  290311 NHO  Lagt inn telefonnr
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
6.31 A* 6.30  300518 BOF  Feilretting-drar med seg mailadr. til neste selg.
6.41 A* 6.40  250118 BOF  Lagt inn kode - styring mail vedr vareoppdatering
7.01 A* 6.40  110920 BOF  Lagt på kode for checkbox
7.03 A* 7.00  061020 ASK  Lagt inn kode - Passiv kode på selger
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040128  100657  MHA         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1IKOD    R        O  8  5REFFLD(RAIKOD)
     A            B1ITXT    R        O  8 11REFFLD(RAITXT)
     A            B1IEMA    R   47   O  8 32REFFLD(RAIEMA)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040128  101042  MHA         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av selgere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse        '
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 32'E-post adresse     '
     A                                      COLOR(WHT)
7.xx A            B2IKOD    R        B  5  5REFFLD(RAIKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ITXT    R        B  5 11REFFLD(RAITXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  200930  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av selgere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Selger . . . . . . . . . . .'
     A            C1IKOD    R        B  5 31REFFLD(RAIKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  010138  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Selger finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20110329  141031  NHO620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av selgere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Selger . . . . . .'
     A            C1IKOD    R        O  5 21REFFLD(RAIKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . .'
     A            C2ITXT    R        B  7 21REFFLD(RAITXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'E-mail adresse . .'
     A            C2IEMA    R        B  9 21REFFLD(RAIEMA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
6.31 A                                      OVRDTA
     A                                      CHECK(LC)
6.20 A                                 11  2'Mobilnr. . . . . .'
6.20 A            C2IMOB    R        B 11 21REFFLD(RAIMOB)
6.20 A                                      DSPATR(UL)
6.20 A                                      COLOR(YLW)
6.20 A                                      OVRDTA
6.20 A                                      OVRATR
6.41 A                                 13  2'Mail ved vareoppd.'
6.41 A            C2IK01    R        B 13 21REFFLD(RAIK01)
6.41 A                                      DSPATR(UL)
6.41 A                                      COLOR(YLW)
6.41 A                                      OVRDTA
6.41 A                                      OVRATR
7.01 A                                 13 23'0/1'
7.03 A                                 14  2'Passiv selger'
7.03 A            C2IK02    R        B 14 21REFFLD(RAIK02)
7.03 A                                      DSPATR(UL)
7.03 A                                      COLOR(YLW)
7.03 A                                      OVRDTA
7.03 A                                      OVRATR
7.03 A                                 14 23'0/1'
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020307  010138  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette selger '
     A                                      DSPATR(RI)
     A                                  4  2'Selger . . . . . .'
     A            D1IKOD    R        O  4 21REFFLD(RAIKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAITXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020307  010138  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Selger . . . . . .'
     A            K1IKOD    R        B  4 21REFFLD(RAIKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Selger . . . . . .'
     A            RAIKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAITXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
