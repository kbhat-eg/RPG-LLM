     A*%%TS  SD  20050723  180917  KOB540      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL108D
     A* Beskrivelse . . : Leverandører, flytting
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130802 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050723  180917  KOB540      REL-V5R2M0  5722-WDS
     A            B1VALG         1Y 0B  9  2COLOR(YLW)
     A                                      VALUES(0 2 5 7)
     A                                      EDTCDE(Z)
     A            B1ALFA    R        O  9  4REFFLD(RLALFA)
     A            B1LEVR    R        O  9 23REFFLD(RLLEVR)
     A            B1NAVN    R   26   O  9 30REFFLD(RLNAVN)
     A            B1STED    R   24   O  9 57REFFLD(RLSTED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050723  180917  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF05
     A                                      CF07
     A                                      CF11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Flytting av leverandører og poster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  5 11'5=Vis'
     A                                      COLOR(BLU)
     A                                  5 18'7=Flytt'
     A                                      COLOR(BLU)
     A                                  7  2'V Alfafelt           Levr.'
     A                                      COLOR(WHT)
     A N80                              7 30'Navn'
     A                                      COLOR(WHT)
     A  80                              7 30'Gateadresse'
     A                                      COLOR(WHT)
     A N80                              7 57'Poststed'
     A                                      COLOR(WHT)
     A  80                              7 57'Telefon      Fax  '
     A                                      COLOR(WHT)
     A            B2ALFA    R        B  8  4REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2LEVR    R        B  8 23REFFLD(RLLEVR)
     A  31                                  ERRMSGID(RML0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RML0020 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050723  175502  KOB540      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny  F7=Vis du-
     A                                      bletter  F11=Endre liste'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020813  170759  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Leverandørnummer finnes fra'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'før'
     A          R C2MSG
     A                                      WINDOW(7 45 8 30)
     A*                                     WINDOW(*DFT 8 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Leverandørnr.. . .'
     A            K1LEVR    R        O  2 21REFFLD(RLLEVR)
     A  79                              4  2'Det finnes notatblokk'
     A                                  5  2'Notatblokk blir slettet.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020813  170759  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A*                                     CA05
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A                                  1 18'Kopier leverandør'
     A                                      COLOR(WHT)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandørnr . . .'
     A            K1LEVR    R        B  4 21REFFLD(RLLEVR)
     A  31                                  ERRMSGID(RML0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RML0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Leverandørnr . . .'
     A            RLLEVR    R        O  8 21
     A                                  9  2'Navn . . . . . . .'
     A            RLNAVN    R        O  9 21
     A                                 10  2'Adresse. . . . . .'
     A            RLGATE    R        O 10 21
     A                                 11  2'Adresse 2. . . . .'
     A            RLADR2    R        O 11 21
     A                                 12  2'Postadresse. . . .'
     A            RLSTED    R        O 12 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 17'F5=Adder til'
     A                                      COLOR(BLU)
     A          R V1WIN
     A*%%TS  SD  20050717  110516  KOB540      REL-V5R2M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      PUTOVR
     A                                  2 14'Utvalgskriteria for dubletter'
     A                                      COLOR(WHT)
     A                                  4  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5 11'Kontroller mot:'
     A                                  6 13'Navn. . . . . . . . .:'
     A            V1NAVN         1A  B  6 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Navn')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  6 40'(J=Ja)'
     A                                  7 13'Gateadresse . . . . .:'
     A            V1GATE         1A  B  7 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Gateadresse')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  7 40'(J=Ja)'
     A                                  8 13'Postnummer. . . . . .:'
     A            V1PONR         1A  B  8 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Postnummer')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  8 40'(J=Ja)'
     A                                  9 13'Telefonnr.. . . . . .:'
     A            V1TLFN         1A  B  9 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Telefonnummer')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  9 40'(J=Ja)'
     A                                 10 13'Mobilnr.. . . . . . .:'
     A            V1MOBN         1A  B 10 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 37)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Mobilnummer')
     A                                      COLOR(YLW)
     A                                 10 40'(J=Ja)'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 15 56)
