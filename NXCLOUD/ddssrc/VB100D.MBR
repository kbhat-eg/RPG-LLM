     A*%%TS  SD  20031210  140724  HKO         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VB100D
     A* Beskrivelse . . : Bruksrett-knytning
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  130516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20031205  123115  HKO         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1OGRP    R        O  8  4REFFLD(VBOGRP)
     A            B1HGRP    R        O  8  8REFFLD(VBHGRP)
     A            B1UGRP    R        O  8 12REFFLD(VBUGRP)
     A            B1UTXT        20A  O  8 16
     A            B1MODN    R        O  8 37REFFLD(VBMODN)
     A            B1VARE    R   -7   O  8 46REFFLD(VBVARE)
     A            B1TEK1        24A  O  8 55
     A            B1BRTY    R        O  8 80REFFLD(VBBRTY)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20031210  140724  HKO         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF04
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 10'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 16'Ugr'                    -
7.xx A                                      COLOR(WHT)
7.xx A                                  4 37'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 48'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Ogr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  8'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 37'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 48'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 80'T'
7.xx A**                                6  2'V Ogr Hgr Ugr                     -
7.xx A**                                     Modulnr  Varenr                   -
7.xx A**                                             T'
     A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2OGRP    R        B  5  4REFFLD(VBOGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  8'+'
7.xx A            B2HGRP    R        B  5 10REFFLD(VBHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 14'+'
7.xx A            B2UGRP    R        B  5 16REFFLD(VBUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 35'+'
7.xx A            B2MODN    R        B  5 37REFFLD(VBMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 46'+'
7.xx A            B2VARE    R   -7   B  5 48REFFLD(VBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20030204  085104  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20031205  123204  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(VBOGRP)
     A  31                                  ERRMSGID(VMB0050 *LIBL/QMFOFAK 31)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 40COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . +'
7.xx A            C1HGRP    R        B  6 31REFFLD(VBHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 40COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(VBUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 40COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Modulnr. . . . . . . . . . +'
     A            C1MODN    R        B  8 31REFFLD(VBMODN)
     A  38                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTX1        35A  O  8 40COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varenr . . . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(VBVARE)
     A  39                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1        35A  O  9 40COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20031205  124430  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bruksrett-knytning finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20031210  140724  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-knytning'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(VBOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 40COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(VBHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 40COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(VBUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 40COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Modulnr. . . . . . . . . . .'
     A            C1MODN    R        O  8 31REFFLD(VBMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTX1        35A  O  8 40COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenr . . . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(VBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1        35A  O  9 40COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Bryksrett-type . . . . . . .'
     A            C2BRTY    R        B 11 31REFFLD(VBBRTY)
     A  31                                  ERRMSGID(VMB0200 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TTXT        30A  O 11 40COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 57'/'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VBEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20031205  123204  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette knytning '
     A                                      DSPATR(RI)
     A                                  4  2'Overgruppe . . . .'
     A            D1OGRP    R        O  4 21REFFLD(VBOGRP)
     A                                  5  2'Hovedgruppe. . . .'
     A            D1HGRP    R        O  5 21REFFLD(VBHGRP)
     A                                  6  2'Undergruppe. . . .'
     A            D1UGRP    R        O  6 21REFFLD(VBUGRP)
     A                                  7  2'Modulnr. . . . . .'
     A            D1MODN    R        O  7 21REFFLD(VBMODN)
     A                                  8  2'Varenr . . . . . .'
     A            D1VARE    R        O  8 21REFFLD(VBVARE)
     A                                 10  2'Erstatning vare. .'
     A            VBBRTY    R        O 10 21
     A                                 10 24'1=Ja,0=Nei'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20031205  123204  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  3  2'Overgruppe  . . .'
     A            K1OGRP    R        B  3 20REFFLD(VBOGRP)
     A  31                                  ERRMSGID(VMB0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMB0030 *LIBL/QMFOFAK 32)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4  2'Hovedgruppe . . .'
     A            K1HGRP    R        B  4 20REFFLD(VBHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Undergruppe . . .'
     A            K1UGRP    R        B  5 20REFFLD(VBUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Modulnr . . . . .'
     A            K1MODN    R        B  6 20REFFLD(VBMODN)
     A  38                                  ERRMSGID(VMV0800 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenr. . . . . .'
     A            K1VARE    R   -7   B  7 20REFFLD(VBVARE)
     A  39                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Overgruppe. . . .'
     A            VBOGRP    R        O 10 20OVRDTA
     A                                 11  2'Hovegruppee . . .'
     A            VBHGRP    R        O 11 20OVRDTA
     A                                 12  2'Undergruppe . . .'
     A            VBUGRP    R        O 12 20OVRDTA
     A                                 13  2'Modulnr . . . . .'
     A            VBMODN    R        O 13 20OVRDTA
     A                                 14  2'Varenr. . . . . .'
     A            VBVARE    R        O 14 20OVRDTA
     A                                 15  2'Erstatning vare .'
     A            VBBRTY    R        O 15 20OVRDTA
     A                                 17  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
