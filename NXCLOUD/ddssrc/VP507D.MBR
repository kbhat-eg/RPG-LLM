     A*%%TS  SD  20160629  132421  ASP_G0X     REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A*     bsa  110117    Endringer relatert til ny GUI
     A*-------------------------------------------------------------------
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20160629  132421  ASP_G0X     REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LDOR    R        O  7  5REFFLD(VPLDOR)
     A            B1LNAV        30A  O  7 12
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160629  132421  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velg Leverandør  '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 12'Lev.navn '
     A                                      COLOR(WHT)
7.xx A            B2LDOR    R        B  4  5REFFLD(VPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
