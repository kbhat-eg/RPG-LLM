     A*%%TS  SD  20060507  135804  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
     A*
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051201  101247  HKO550      REL-V5R3M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1KODE    R        O  9  4REFFLD(NKKODE)
7.xx A            B1BESK    R        O  9  9REFFLD(NKBESK)
7.xx A            B1PGMC    R        O  9 40REFFLD(NKPGMC)
7.xx A            B1TELN    R        O  9 51REFFLD(NKTELN)
7.xx A            B1MANU    R        O  9 59REFFLD(NKMANU)
7.xx A            B1FORM    R        O  9 63REFFLD(NKFORM)
7.xx A            B1ZIPP    R        O  9 68REFFLD(NKZIPP)
7.xx A            B1ODAT    R        O  9 71REFFLD(NKODAT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051201  101247  HKO550      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA07
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eksterne ordrekoder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Beskrivelse           '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Ovf.prog'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 58'Man'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 62'ANSI'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 67'Zip'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 71'dato   '
     A                                      COLOR(WHT)
7.xx A            B2KODE    R        B  5  4REFFLD(NKKODE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  7 51'Teller'
     A                                      COLOR(WHT)
7.xx A                                  7 71'Opprettet'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20030930  150629  MHA         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett  F7=Vedlikehold tellere'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051201  144913  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ekstern ordre kode'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kode. . . . . . . . . . . .'
     A            C2KODE    R        O  4 30REFFLD(NKKODE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse . . . . . . . .'
     A            C2BESK    R        B  5 30REFFLD(NKBESK)
     A  31                                  ERRMSGID(NMK0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2'Program for ovf. til ordre.'
     A            C2PGMC    R        B  6 30REFFLD(NKPGMC)
     A  32                                  ERRMSGID(NMK0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Teller-navn . . . . . . . .'
     A            C2TELN    R        B  7 30REFFLD(NKTELN)
     A  33                                  ERRMSGID(NMK0120 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Manuell innlesing . . . . .'
     A            C2MANU    R        B  8 30REFFLD(NKMANU)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 33'1=Ja,0=Nei'
     A                                 10  2'Mottat fil:'
     A                                      COLOR(WHT)
     A                                 11  2'Filformat . . . . . . . . .'
     A            C2FORM    R        B 11 30REFFLD(NKFORM)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 33'0=ASCII,1=ANSI'
     A                                 12  2'Zippet. . . . . . . . . . .'
     A            C2ZIPP    R        B 12 30REFFLD(NKZIPP)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 33'1=Ja,0=Nei'
     A                                 13  2'Innlesing:'
     A                                      COLOR(WHT)
     A                                 14  2'FTP-navn. . . . . . . . . .'
     A            C2FTPN    R        B 14 30REFFLD(NKFTPN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'IFS-mappe . . . . . . . . .'
     A            C2MAPP    R        B 15 30REFFLD(NKMAPP)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'WEB-link/adresse. . . . . .'
     A            C2LINK    R        B 16 30REFFLD(NKLINK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17  2'Filnavn . . . . . . . . . .'
     A            C2FILE    R        B 17 30REFFLD(NKFILE)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Kontaktperson:'
     A                                      COLOR(WHT)
     A                                 19  2'Navn. . . . . . . . . . . .'
     A            C2KPRS    R        B 19 30REFFLD(NKKPRS)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Telefon . . . . . . . . . .'
     A            C2TLFN    R        B 20 30REFFLD(NKTLFN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21  2'Mobil . . . . . . . . . . .'
     A            C2MOBI    R        B 21 30REFFLD(NKMOBI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22  2'E-mail. . . . . . . . . . .'
     A            C2MAIL    R        B 22 30REFFLD(NKMAIL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 57'/'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(NKEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20030930  152212  MHA         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette ekstern ordre kode '
     A                                      DSPATR(RI)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  3'Kode . . . . .'
     A                                  6  3'Beskrivelse. .'
     A            D1KODE    R        O  5 18REFFLD(NKKODE)
     A            NKBESK    R        O  6 18REFFLD(NKBESK)
     A          R C1BLD
     A*%%TS  SD  20030930  152811  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ekstern ordre kode'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  2'Kode . . . . .'
     A            C1KODE    R        B  5 17REFFLD(NKKODE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
