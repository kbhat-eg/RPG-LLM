     A*%%TS  SD  20231123  152722  ASK_800     REL-V7R4M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA001D
     A* Beskrivelse . . : Valg av File-gruppe (databibl.)
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       070504 AMD  Nytt program
7.xx A* 7.xx  170316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040624  103206  AMD         REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  6  3REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
7.xx A            B1FGRP    R        O  6  6REFFLD(AGFGRP)
7.xx A            B1BIBL    R        O  6 11REFFLD(AGBIBL)
7.xx A            B1BESK    R        O  6 22REFFLD(AGBESK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040624  103206  AMD         REL-V5R2M0  5722-WDS
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      WINDOW(B2CMD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 38'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  3  6'Grp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  1'Valg'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  6'Grp.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Bibliotek'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 22'Beskrivelse'
     A                                      COLOR(WHT)
     A            B2FGRP    R        B  4  6REFFLD(AGFGRP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  1  1'Valg av filgruppe/databibliotek'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20040624  103206  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ACLI &W_ACPO 20 52)
     A                                 19  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A            W_ACLI         2S 0P
     A            W_ACPO         3S 0P
     A          R ASUME                     ASSUME
     A                                 24 79' '
