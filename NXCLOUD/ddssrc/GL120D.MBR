     A*%%TS  SD  20161019  132816  NHO630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL120D
     A* Beskrivelse . . : Utplukk til utbetalingsforslag
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*       240904 TSO  Byttet ut ICF med IFS
6.20 A*       170611 NHO  IFS legges som fast, Type lønnsystem fjernes
     A*                   fra bildet, 3 legges inn som default
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A          R A1BLD
     A*%%TS  SD  20100129  130215  ASK610      REL-V5R4M0  5722-WDS
     A                                      TEXT('Hovedbilde')
     A                                      CA03
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF15
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      PRINT
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Faste data - betalingsformidling'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  4'Foretaksidentifikasjon . .:'
     A            A17FIN    R        B  5 32REFFLD(GEFFIN)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A  35                                  DSPATR(PC)
     A  35                                  ERRMSGID(RML0350 *LIBL/QMFOKON 35)
     A                                  6  4'Operatørnummer (tildelt) .:'
     A            A17OPE    R        B  6 32REFFLD(GEFOPE)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  4'Passord. . . . . . . . . .:'
     A            A17PWD    R        B  7 32REFFLD(GEFPWD)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  4'Divisjonsnummer. . . . . .:'
     A            A17DIV    R        B  8 32REFFLD(GEFDIV)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9  4'Utbetalingsforslagsnummer.:'
     A            A17UBF    R        O  9 32REFFLD(GEFUBF)
     A                                      TEXT('Utbetalingsforslagsnummer')
     A                                      EDTCDE(Z)
     A                                 11  4'Bruke sekvensteller. . . .:'
     A            A17EFS    R        B 11 32REFFLD(GEFEFS)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 30)
     A                                 11 41'( E=Egen/F=Felles )'
     A                                 12  4'Egen sekvensteller . . . .:'
     A            A17FOS    R        B 12 32REFFLD(GEFFOS)
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            A17FO2    R        O 12 44REFFLD(GEFFOS)
     A                                      EDTCDE(Z)
     A                                      DSPATR(ND)
     A                                 12 49'('
     A                                      DSPATR(ND)
     A                                 12 51'Endret:'
     A                                      DSPATR(ND)
     A            A17DAT    R        O 12 59REFFLD(GEFDAT)
     A                                      TEXT('Dato for sist endring av hjel-
     A                                      pefelt')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(ND)
     A                                 12 70'/'
     A                                      DSPATR(ND)
     A                                 13  4'Daglig sekvensteller . . .:'
     A            A17DAS    R        B 13 32REFFLD(GEFDAS)
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 13 41'('
     A                                 13 43'Endret:'
     A            A17DDA    R        O 13 51REFFLD(GEFDDA)
     A                                      TEXT('Dato for sist endring av dagl-
     A                                      ig teller')
     A                                      EDTWRD('  .  .    ')
     A                                      DSPATR(UL)
     A                                 13 62')'
     A            A17TIM    R        O 14 70REFFLD(GEFTIM)
     A                                      TEXT('Tidsp. for sist endring av hj-
     A                                      elpefelt')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(ND)
     A                                 14 79')'
     A                                      DSPATR(ND)
     A                                 15  4'Bankkode . . . . . . . . .:'
     A            A17BAN    R        B 15 32REFFLD(GEFBAN)
     A                                      DSPATR(UL)
     A                                      VALUES('DN' 'FD' 'FK' 'KK')
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A                                 15 41'( DN/FD/FK/KK )'
     A                                 16  4'Sortering av forslagsliste:'
     A            A17SBF    R        B 16 32REFFLD(GEFSBF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  33                                  DSPATR(PC)
     A  33                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 33)
     A                                 16 41'( A=Alfabetisk / N=Numerisk )'
     A                                 17  4'Sammenslåing til samlefile:'
     A            A17SAM    R        B 17 32REFFLD(GEFSAM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  34                                  DSPATR(PC)
     A  34                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 34)
     A                                 17 41'( J=Ja / N=Nei )'
6.20 A*                                18  4'Type lønnssystem . . . . .:'
6.20 A*           A17LON    R        B 18 32REFFLD(GEFLON)
6.20 A*                                     DSPATR(UL)
6.20 A*                                     TEXT('Type lønn')
6.20 A*                                     VALUES('M' '3')
6.20 A* 36                                  ERRMSGID(RML0610 *LIBL/QMFOKON 36)
6.20 A* 36                                  DSPATR(PC)
6.20 A*                                     COLOR(YLW)
6.20 A*                                18 41'( 3=3PLønn )'
6.20 A            A17LON    R        O 18 32REFFLD(GEFLON)
6.20 A                                      DSPATR(ND)
     A                                 19  4'Type kommunikasjon . . . .:'
     A            A17KOM    R        O 19 32REFFLD(GEFKOM)
     A                                 20  4'-> Angi mappenavn i IFS. .:'
     A            A17IFS        45A  B 20 32DSPATR(UL)
     A  37                                  DSPATR(PC)
     A                                      TEXT('Mappebane ved IFS')
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(DM00170 *LIBL/QMFBANK 37)
     A                                 22  2'F3 =Gå tilbake'
     A                                      COLOR(BLU)
     A                                 22 18'F8=Betalingsartkoder'
     A                                      COLOR(BLU)
     A                                 22 40'F9=Avtaleregister'
     A                                      COLOR(BLU)
     A                                 23  2'F10=Felles sekvensteller'
     A                                      COLOR(BLU)
     A                                 23 29'F11=Sist brukte sekvensteller'
     A                                      COLOR(BLU)
     A                                 23 61'F15=Bankfiler'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  20030206  102823  RHO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 6 27)
     A                                      TEXT('Sist brukte sekvensnummer')
     A                                      CA03
     A                                      KEEP
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Legg tilbake flg. tellere'
     A                                      DSPATR(UL)
     A            E1DASE         6Y 0O  5 15TEXT('Sist brukte Daglig sekvenstel-
     A                                      ler')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  5  2'Daglig. . .:'
     A                                  4  2'Egen/Felles:'
     A            E1EFSE         4Y 0O  4 15TEXT('Sist brukte Egen/Felles sekve-
     A                                      nsteller')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
