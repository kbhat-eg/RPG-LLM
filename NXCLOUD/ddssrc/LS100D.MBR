     A*%%TS  SD  20140604  132753  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LS100D
     A* Beskrivelse . . : Lagertransaksjoner linjebehandling
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.NU A* R6.30 030614 BOF  Utvidelse vedr. nummerutvidelse
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20061122  155921  KOB         REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 3 4)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1LINE    R        O  8  4REFFLD(LSLINE)
     A            B1VARE    R   -7   O  8  9REFFLD(LSVARE)
     A            B1TYPE    R        O  8 18REFFLD(LSTYPE)
     A            B1LAGE    R        O  8 22REFFLD(LSLAGE)
     A            B1LAST    R        O  8 26REFFLD(LSLAST)
     A            B1ANTA        10Y 2O  8 29EDTWRD('        ,  -')
     A            B1ENH1    R        O  8 42REFFLD(LSENH1)
     A            B1TEK1    R   -3   O  8 46REFFLD(LSTEK1)
     A            B1XRKO    R        O  8 79REFFLD(LFRFR/LSXRKO *LIBL/LFRF)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140604  132753  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF23
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A                                      SFLCSRRRN(&W_CURN)
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Lager transaksjonslinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Lin'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Varenr'
7.xx A                                      COLOR(WHT)
     A                                  4 61'Buntnummer'
     A                                      COLOR(WHT)
     A            B2BUNT    R        O  4 72REFFLD(LQNUMM)
     A                                      DSPATR(RI)
7.xx A                                  6  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  6 11'4=Slett'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Lin'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  9'Varenr '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 18'TP'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 21'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 25'Til'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 31'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 42'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 46'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 78'Å'
7.xx A**                                7 75'Årsak'
7.xx A**                                6  2'V  Lin Varenr   TP Lag Til   Antal-
7.xx A**                                    l     Enh Beskrivelse              -
7.xx A**                                        Årsak'
     A                                      COLOR(WHT)
7.xx A            B2LINE    R        B  5  4REFFLD(LSLINE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5  9'+'
7.xx A            B2VARE    R   -7   B  5 11REFFLD(LSVARE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010727  141501  ASK         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F6=Ny linje'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20061122  145423  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 54)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A                                      PUTOVR
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Lagertransaksjonslinje '
     A                                      DSPATR(RI)
     A                                  4  2'Varenr. . .'
     A            C2VARE    R   -7   B  4 14REFFLD(LSVARE)
     A  31                                  ERRMSGID(LM00180 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.31 A  50                                  DSPATR(PC)
     A            C2TEK1    R        O  5 14REFFLD(LSTEK1)
     A                                      OVRDTA
     A                                  6  2'Antall. . .'
     A            C2ANTA         8Y 2B  6 14EDTWRD('      ,  -')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  DSPATR(PC)
7.xx A                                  6 25'+'
7.xx A            C2ENH1    R        B  6 27REFFLD(LSENH1)
     A  33                                  ERRMSGID(LM00080 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Type. . . .'
     A            C2TYPE    R        B  7 14REFFLD(LSTYPE)
     A  34                                  ERRMSGID(LMS0010 *LIBL/QMFLAGR 34)
     A N38                                  COLOR(YLW)
     A  38                                  DSPATR(PR)
     A                                  7 17'I=Inngang, U=Uttak, J=Justering,'
     A                                  8 17'O=Overføring'
7.xx A                                  9  2'Lager . . +'
     A            C2LAGE         2A  B  9 14
     A  36                                  ERRMSGID(LMS0020 *LIBL/QMFLAGR 36)
     A  40                                  ERRMSGID(LM00290 *LIBL/QMFLAGR 40)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LTEK        20A  O  9 17OVRDTA
     A                                 10  2'Til lager .'
     A  39                                  DSPATR(ND)
     A            C2LAST         2A  B 10 14
     A  35                                  ERRMSGID(LMS0030 *LIBL/QMFLAGR 35)
     A  37                                  ERRMSGID(LMS0020 *LIBL/QMFLAGR 37)
     A  41                                  ERRMSGID(LM00290 *LIBL/QMFLAGR 41)
     A  39                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LATK        20A  O 10 17OVRDTA
     A**39                                  DSPATR(ND)
7.xx A                                 11  2'Årsakskode+'
     A            C2XRKO    R        B 11 14REFFLD(RFRFR/RAXRKO *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2XTXT    R        O 11 17REFFLD(RFRFR/RAXTXT *LIBL/RFRF)
     A                                      OVRDTA
     A                                 13  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20020925  232007  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette linje i transaksjonslinje '
     A                                      DSPATR(RI)
     A            B4VARE    R   -7   O  4  2REFFLD(LFVARE)
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A            B4TEK1    R        O  5  2REFFLD(LFTEK1)
     A                                      OVRDTA
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
