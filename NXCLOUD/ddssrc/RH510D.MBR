     A*%%TS  SD  20101208  111633  SST61P      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH510D
     A* Beskrivelse . . : Hovedbokposteringer, spørring bilag/beløp
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050902 KOB  Nytt program
7.xx A*       030516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19960228  123916  KB          REL-V3R1M0  5763-PW1
     A            B1RAAR    R        H      REFFLD(RBRAAR)
     A            B1SPES    R        H      REFFLD(RBSPES)
     A            B1AVDE    R        H      REFFLD(RBAVDE)
     A            B1RPER    R        H      REFFLD(RBRPER)
     A            B1TIST    R        H      REFFLD(RBTIST)
     A            B1BDAT    R        H      REFFLD(RBBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(RBBILN)
6.NU A**          B1BIDA         6Y 0O  9 11
6.NU A            B1BIDA         6Y 0O  9 13
6.NU A**          B1BILK    R        O  9 18REFFLD(RBBILK)
6.NU A            B1BILK    R        O  9 20REFFLD(RBBILK)
6.NU A**          B1TEXT        18A  O  9 21
6.NU A            B1TEXT        16A  O  9 23
     A            B1NETO        11Y 2O  9 40EDTCDE(J)
     A            B1KONT    R        O  9 56REFFLD(RHKONT)
     A            B1NAVN    R  -12   O  9 63REFFLD(RHTEXT)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101202  131606  SST61P      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.21 A            B2FIRM         3  0O  1 34DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på hovedboksposter'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RH510B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 46'Beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'5=Vis  8=Vis sumpost  9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  5 61'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 77REFFLD(RBRAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Bk'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 23'Tekst          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 46'Beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Navn           '
7.xx A**                                8  2'V Bil.nr   Dato   Bk  Tekst       -
7.xx A**                                              Beløp     Konto  Navn'
     A                                      COLOR(WHT)
7.xx A            B2BILN    R        B  6  4REFFLD(RBBILN)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
6.NU A**          B2BDAT         6Y 0B  8 11
7.xx A            B2BDAT         6Y 0B  6 13
7.xx A                                      COLOR(YLW)
     A  23                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
6.NU A**          B2NETO    R        B  8 42REFFLD(RBNETO)
7.xx A            B2NETO    R        B  6 44REFFLD(RBNETO)
7.xx A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020905  131925  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vise
     A*
     A          R C2WIN
     A*%%TS  SD  20101208  111633  SST61P      REL-V5R4M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 19 61)
     A                                      CA02
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 20'Vis hovedbokspost'
     A                                      COLOR(WHT)
     A                                  3  2'Konto . . . . .:'
     A            RBKONT    R        O  3 19REFFLD(RBKONT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RHTEXT    R        O  3 25REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  4  2'Kostnadsbærer .:'
     A            RBSPES    R        O  4 19REFFLD(RBSPES)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            REBTXT    R        O  4 24REFFLD(REBTXT)
     A                                      OVRDTA
     A                                  5  2'Avdeling. . . .:'
     A            RBAVDE    R        O  5 19REFFLD(RBAVDE)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RAGTXT    R        O  5 24REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                  6  2'Bilagsnr. . . .:'
     A            RBBILN    R        O  6 19REFFLD(RBBILN)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  73                                  COLOR(YLW)
6.NU A**73        C2NOTB         8A  O  6 26DSPATR(RI)
6.NU A  73        C2NOTB         8A  O  6 28DSPATR(RI)
     A                                      OVRDTA
6.NU A**                                6 41'Periode. .:'
6.NU A                                  6 43'Periode. .:'
6.NU A**          RBRPER    R        O  6 53REFFLD(RBRPER)
6.NU A            RBRPER    R        O  6 55REFFLD(RBRPER)
     A                                      OVRDTA
     A                                      DSPATR(UL)
6.NU A**          RBRAAR    R        O  6 56REFFLD(RBRAAR)
6.NU A            RBRAAR    R        O  6 58REFFLD(RBRAAR)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  7  2'Bilagdato . . .:'
     A            C2BDAT         6Y 0O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  7 41'Journal. .:'
     A            RBJOUR    R        O  7 53DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Bilagstekst . .:'
     A            C2BILK    R        O  8 19REFFLD(RBBILK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2TEXT    R        O  8 22REFFLD(RBTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  9  2'Beløp . . . . .:'
     A            RBNETO    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9 41'MVA-kode. . . .:'
     A            RBMVAK    R        O  9 58DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Grunnlag mva. .:'
     A            RBMVAG    R        O 10 19REFFLD(RBMVAG)
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 41'%-andel gr.lag.:'
     A            C2AMVA    R    3 00O 10 58REFFLD(RBAMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2'Beregnet mva. .:'
     A            RBMVAB    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11 41'MVA-% . . . . .:'
     A            C2PMVA    R    2 00O 11 58REFFLD(RBPMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'Valuta. . . . .:'
     A            RBVALK    R        O 12 19REFFLD(RBVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            RBVALB    R        O 12 23REFFLD(RBVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12 41'MVA-overstyrt .:'
     A            RBMVAO    R        O 12 58DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Mengde. . . . .:'
     A            RBMNGK    R        O 13 19DSPATR(UL)
     A                                      OVRDTA
     A            RBMNGD    R        O 13 23DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13 41'MVA-oppgjør . .:'
     A            RBMMRK    R        O 13 58DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Prosjekt/U.Pro.:'
     A            RBHPRO    R        O 14 19DSPATR(UL)
     A                                      OVRDTA
     A            RBAKTI    R        O 15 19DSPATR(UL)
     A                                      OVRDTA
     A                                 14 41'MVA periode . .:'
     A            RBMVAP    R        O 14 58DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Lev.fakturanr .:'
     A            RBLFAK    R        O 16 19REFFLD(RBLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 18 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            RBARBO    R        O 15 26
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            RBUPRO    R        O 14 26
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Aktiv./Arb.Ord.:'
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
