     A*%%TS  SD  20140604  144249  BOF630      REL-V7R1M0  5770-WDS
     A*%%EC
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.NU A* R6.30 030614 BOF  Utvidelse vedr. nummerutvidelse
7.02 A* R7.00 080721 BOF  Justeringer basert på ny GUI og regler + 132 skjerm
     A*
7.02 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140604  144057  BOF630      REL-V7R1M0  5770-WDS
     A            B1LDOR         6S 0H
     A            B1KJOP    R        H      REFFLD(LYTOTI)
     A            B1VALG         2Y 0B 10  2CHKMSGID(FM00010 *LIBL/QMFLAGR)
     A                                      VALUES(0 5)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1AARR    R        O 10  5REFFLD(LYAARR)
     A            B1SUFF    R        O 10 19REFFLD(LYSUFF)
     A            B1OTYP    R        O 10 22REFFLD(LYOTYP)
     A            B1LENA        20A  O 10 25
     A            B1DATO         6Y 0O 10 46EDTWRD('  .  .  ')
     A            B1NAVN    R   -5   O 10 55REFFLD(LYNAVN)
     A            B1NUMM    R        O 10 10REFFLD(LYNUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140604  144249  BOF630      REL-V7R1M0  5770-WDS
7.02 A                                      SFLSIZ(0015)
7.02 A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF15
     A                                      CF16
     A                                      CF22
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.02 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
7.02 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Slettede bestillinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.02 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.02 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
7.02 A                                      ------------------------------------
7.02 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  7 84'NB Årstall må oppgis ved posisjone-
     A                                      ring !'
     A                                      COLOR(BLU)
     A                                  4 84'Ordretype :'
     A                                      COLOR(BLU)
     A            H1OTYP    R        O  4 97REFFLD(LOOTYP)
     A                                      COLOR(BLU)
     A                                  5 84'Bruker. . :'
     A                                      COLOR(BLU)
     A            H1USER    R        O  5 97REFFLD(LOUSER)
     A                                      COLOR(BLU)
     A                                  6 84'Leverandør:'
     A                                      COLOR(BLU)
     A            H1LDOR    R        O  6 98REFFLD(LOLDOR)
     A                                      COLOR(BLU)
     A                                  6  2'5=Vis bestilling'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
7.02 A                                  4  5'År'
     A                                      COLOR(WHT)
     A                                  8 10'Best.'
     A                                      COLOR(WHT)
     A                                  8 19'S'
     A                                      COLOR(WHT)
     A                                  8 21'Ot'
     A                                      COLOR(WHT)
     A                                  8 25'Levalfa  '
     A                                      COLOR(WHT)
     A            B2HEDD         9A  O  8 44COLOR(WHT)
     A            B2HED1        11A  O  8 55COLOR(WHT)
     A            B2HED2        10A  O  8 70COLOR(WHT)
7.02 A            B2AARR    R        B  5  4REFFLD(LYAARR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.02 A            B2SUFF    R        B  5 18REFFLD(LYSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.02 A            B2OTYP    R        B  5 21REFFLD(LYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.02 A            B2LENA        20A  B  5 25COLOR(YLW)
     A                                      OVRDTA
7.02 A            B2DATO         6Y 0B  5 46COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMLOFAK 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.02 A            B2NAVN    R  -12   B  5 55REFFLD(LYNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
7.02 A            B2NUMM    R        B  5  9REFFLD(LYNUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(FMO0010 *LIBL/QMLOFAK 31)
     A  32                                  ERRMSGID(FMO0020 *LIBL/QMLOFAK 32)
     A                                  8  5'År'
     A                                      COLOR(WHT)
7.02 A                                  4  9'Best.'
7.02 A                                      COLOR(WHT)
7.02 A                                  4 19'S'
7.02 A                                      COLOR(WHT)
7.02 A                                  4 25'Levalfa  '
7.02 A                                      COLOR(WHT)
7.02 A                                  4 21'Ot'
7.02 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020313  081720  HKO         REL-V5R1M0  5722-WDS
     A                                 25  2'F1=Bla-alt'
     A                                      COLOR(BLU)
     A                                 25 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 25 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 25 70'F23-Vise 2'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20070305  210634  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 40)
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg ordretype.:'
     A            H1OTYP    R        B  4 19REFFLD(LYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            H1OTY2    R        B  4 22REFFLD(LYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            H1OTY3    R        B  4 25REFFLD(LYOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  4 31'Blank gir'
     A                                  5 31'alle'
     A                                  6  2'Velg bruker....:'
     A            H1USER    R        B  6 19REFFLD(LYUSER)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Velg leverandør:'
     A            H1LDOR    R        B  7 19REFFLD(LYLDOR)
     A                                      DLTCHK
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
