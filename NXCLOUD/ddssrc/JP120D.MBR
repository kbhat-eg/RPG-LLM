     A*%%TS  SD  20150616  130936  BOF630      REL-V7R1M0  5770-WDS
6.33 A* 211216 nho Feilmelding dersom lev/vnr ikke finnes på JVLEL1
7.xx A*  290716  bsa  Endringer relatert til ny GUI
8.01 A*PRG2 300622 BOF : Utvidelse av prisgruppe fra 1 til 2 pos.
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20150616  130101  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B 11  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 7 8 9 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1FPRG    R        O 11  4REFFLD(JPFPRG)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FOGR    R        O 11  7REFFLD(JPFOGR)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FHGR    R        O 11 10REFFLD(JPFHGR)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FUGR    R        O 11 13REFFLD(JPFUGR)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FMOD    R        O 11 17REFFLD(JPFMOD)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FVAR         8A  O 11 26
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FVTY    R        O 11 35REFFLD(JPFVTY)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FLET    R        O 11 37REFFLD(JPFLET)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FKRT    R   -2   O 11 39REFFLD(JPFKRT)
     A                                      EDTCDE(L)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FTON    R   -2   O 11 58REFFLD(JPFTON)
     A                                      EDTCDE(L)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FKUB    R   -2   O 11 70REFFLD(JPFKUB)
     A                                      EDTCDE(L)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A            B1FPRT    R   -1   O 11 51REFFLD(JPFPRT)
     A                                      EDTCDE(L)
6.34 A  50                                  COLOR(BLU)
6.34 A  51                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20150616  130936  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CA12
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Fraktbetingelser'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JP120B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2FLDO    R        O  5 31REFFLD(JPFLDO)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B2LNAV    R        O  5 39REFFLD(JLNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  7  2'Prisgruppe +'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 31'Varetype +'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 51'Leveringstype +'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'Varegrp. +'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 18'+'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 24'+'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 31'Modulnr +'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'Varenr +'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  2'2=Endre  3=Kopier  4=Slett  5=Vis -
6.34 A                                       8=Varer under modul'
     A                                      COLOR(BLU)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'P'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  6'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  9'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 12'Ug'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 16'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 25'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 34'T'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 36'L'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 38'Kr.tillegg'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 50'% till.'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 58'Tonnasje'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 70'M3 Frakt'
7.xx A                                      COLOR(WHT)
7.xx A**                                9  2'V Pg Varegrp  Modulnr  Varenr   T -
7.xx A**                                    L Kr.tillegg  % till. Tonnasje    M-
7.xx A**                                    3 Frakt   '
7.xx A**                                    COLOR(WHT)
7.xx A            B2FPRG    R        B  7 15REFFLD(JPFPRG)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2FOGR    R        B  8 15REFFLD(JPFOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2FHGR    R        B  8 20REFFLD(JPFHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2FUGR    R        B  8 26REFFLD(JPFUGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FMOD    R        B  8 42REFFLD(JPFMOD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FVAR    R   -7   B  8 67REFFLD(JPFVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2FVTY    R        B  7 42REFFLD(JPFVTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2FLET    R        B  7 67REFFLD(JPFLET)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20140704  085955  BOF630      REL-V7R1M0  5770-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake   F4=Spør-
     A                                      ring   F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050613  103726  HKO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Fraktbetingelser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1FLDO    R        O  5 31REFFLD(JPFLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  5 42REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Prisgruppe . . . . . . . . +'
     A            C1FPRG    R        B  6 31REFFLD(JPFPRG)
     A  43                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 43)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        35A  O  6 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Overgruppe . . . . . . . . +'
     A            C1FOGR    R        B  7 31REFFLD(JPFOGR)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  7 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Hovedgruppe. . . . . . . . +'
     A            C1FHGR    R        B  8 31REFFLD(JPFHGR)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  8 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Undergruppe. . . . . . . . +'
     A            C1FUGR    R        B  9 31REFFLD(JPFUGR)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT        35A  O  9 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Modulnummer. . . . . . . . +'
     A            C1FMOD    R        B 10 31REFFLD(JPFMOD)
     A  38                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 42REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Varenummer . . . . . . . . +'
     A            C1FVAR    R   -7   B 11 31REFFLD(JPFVAR)
     A  39                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 39)
6.33 A  44                                  ERRMSGID(JMV0150 *LIBL/QMFVADM 44)
6.33 A  45                                  ERRMSGID(JMV0130 *LIBL/QMFVADM 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 42REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Varetype . . . . . . . . . +'
     A            C1FVTY    R        B 12 31REFFLD(JPFVTY)
     A  40                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 40)
     A  41                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 12 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Leveringstype. . . . . . . +'
     A            C1FLET    R        B 13 31REFFLD(JPFLET)
     A  42                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35   O 13 42COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Fraktbetingelse finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140813  151705  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Fraktbetingelser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1FLDO    R        O  5 31REFFLD(JPFLDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  5 42REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C1FPRG    R        O  6 31REFFLD(JPFPRG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        35A  O  6 42COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Overgruppe . . . . . . . . .'
     A            C1FOGR    R        O  7 31REFFLD(JPFOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  7 42COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Hovedgruppe. . . . . . . . .'
     A            C1FHGR    R        O  8 31REFFLD(JPFHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  8 42COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Undergruppe. . . . . . . . .'
     A            C1FUGR    R        O  9 31REFFLD(JPFUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  9 42COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Modulnummer. . . . . . . . .'
     A            C1FMOD    R        O 10 31REFFLD(JPFMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 42REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Varenummer . . . . . . . . .'
     A            C1FVAR    R   -7   O 11 31REFFLD(JPFVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 42REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Varetype . . . . . . . . . .'
     A            C1FVTY    R        O 12 31REFFLD(JPFVTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 12 42COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Leveringstype. . . . . . . .'
     A            C1FLET    R        O 13 31REFFLD(JPFLET)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 13 42COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Fra-dato . . . . . . . . . .'
     A            C2FFDA         6Y 0B 15 31COLOR(YLW)
     A  32                                  ERRMSGID(JM00250 *LIBL/QMFVADM 32)
     A  34                                  ERRMSGID(JM00260 *LIBL/QMFVADM 34)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 16  2'Til-dato . . . . . . . . . .'
     A            C2FTDA         6Y 0B 16 31COLOR(YLW)
     A  33                                  ERRMSGID(JM00250 *LIBL/QMFVADM 33)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17  2'Kronetillegg pr. prisenhet .'
     A            C2FKRT        11Y 2B 17 31COLOR(YLW)
     A  35                                  ERRMSGID(JMP1200 *LIBL/QMFVADM 35)
     A  36                                  ERRMSGID(JMP1210 *LIBL/QMFVADM 36)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 18  2'Prosent tillegg. . . . . . .'
     A            C2FPRT         5Y 2B 18 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 19  2'kronetillegg pr. tonn  . . .'
     A            C2FTON        11Y 2B 19 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 20  2'Kronetillegg pr. M3  . . . .'
     A            C2FKUB        11Y 2B 20 31COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A            C2FODA         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2FEDA         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2FEUS    R        O 22 70REFFLD(JPFEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3WIN
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du Aktiviserer/deaktiviserer på'
     A                                  3  2'Varenummer-nivå.               '
     A                                  4  2'Tast <enter> hvis OK           '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050613  103726  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 53)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Fraktbetingelse '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgruppe  . .'
     A            D1FPRG    R        O  4 18REFFLD(JPFPRG)
     A                                  5  2'Varegruppe. . .'
     A            D1FOGR    R        O  5 18REFFLD(JPFOGR)
     A            D1FHGR    R        O  5 21REFFLD(JPFHGR)
     A            D1FUGR    R        O  5 24REFFLD(JPFUGR)
     A                                  6  2'Modulnummer . .'
     A            D1FMOD    R        O  6 18REFFLD(JPFMOD)
     A                                  7  2'Varenummer. . .'
     A            D1FVAR    R   -7   O  7 18REFFLD(JPFVAR)
     A                                  8  2'Varetype. . . .'
     A            D1FVTY    R        O  8 18REFFLD(JPFVTY)
     A                                  9  2'Leveringstype .'
     A            D1FLET    R        O  9 18REFFLD(JPFLET)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20140703  104518  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 21 53)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgruppe  . +'
     A            K1FPRG    R        B  4 18REFFLD(JPFPRG)
     A                                      COLOR(YLW)
7.xx A                                  5  2'Varegruppe. . +'
     A            K1FOGR    R        B  5 18REFFLD(JPFOGR)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 21'+'
7.xx A            K1FHGR    R        B  5 23REFFLD(JPFHGR)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 26'+'
7.xx A            K1FUGR    R        B  5 28REFFLD(JPFUGR)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Modulnummer . +'
     A            K1FMOD    R        B  6 18REFFLD(JPFMOD)
     A  38                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Varenummer. . +'
     A            K1FVAR    R   -7   B  7 18REFFLD(JPFVAR)
     A  39                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Varetype. . . +'
     A            K1FVTY    R        B  8 18REFFLD(JPFVTY)
     A  40                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 40)
     A  41                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Leveringstype +'
     A            K1FLET    R        B  9 18REFFLD(JPFLET)
     A  42                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 13  2'Prisgruppe. . .'
     A            JPFPRG    R        O 13 18OVRDTA
     A                                 14  2'Varegruppe. . .'
     A            JPFOGR    R        O 14 18OVRDTA
     A            JPFHGR    R        O 14 21OVRDTA
     A            JPFUGR    R        O 14 24OVRDTA
     A                                 15  2'Modulnummer . .'
     A            JPFMOD    R        O 15 18OVRDTA
     A                                 16  2'Varenummer. . .'
     A            JPFVAR    R        O 16 18OVRDTA
     A                                 17  2'Varetype. . . .'
     A            JPFVTY    R        O 17 18OVRDTA
     A                                 18  2'Leveringstype .'
     A            JPFLET    R        O 18 18OVRDTA
     A                                 20  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20140630  145820  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 10 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Prisgruppe  . . . . . +'
     A            F1FPRG    R        B  4 28REFFLD(JPFPRG)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5  2'Varegrupper . . . . . +'
     A            F1FOGR    R        B  5 28REFFLD(JPFOGR)
     A                                      OVRDTA
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A  36                                  ERRMSGID(JVG0200 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
7.xx A                                  5 32'+'
7.xx A            F1FHGR    R        B  5 34REFFLD(JPFOGR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5 38'+'
7.xx A            F1FUGR    R        B  5 40REFFLD(JPFUGR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  6  2'Modul . . . . . . . . +'
     A            F1FMOD    R        B  6 28REFFLD(JPFMOD)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Logistikkvare . . . .'
     A            F1FLVA    R        B  7 28REFFLD(JPFVAR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'Aktiviserte . . . . .'
     A            F1KO01         1Y 0B  8 28OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVADM)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  8 31'1=Ja,0=Nei'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
