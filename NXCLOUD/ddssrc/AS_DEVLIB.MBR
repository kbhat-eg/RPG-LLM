     A*%%TS  SD  20030105  193929  ASK         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LU100D
     A* Beskrivelse . . : Behandling av lager-tellebunter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
8.01 A* 800   270122  ask  Lagt inn mulighet for "justeringsbunt". Denne erstatt
     A*                    antall i telleliste, IKKE plusser på som i standard.
8.02 A* 800   200422  ask  Mulighet for å slette tellelinjer med 0 på telleliste
     A*                    ved reversering av bunt på syklisk teling.
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990525  223543  ASK         REL-V4R2M0  5769-PW1
8.01 A            B1VALG         2Y 0B  9  2EDTCDE(Z)
8.01 A                                      VALUES(0 2 3 5 4 6 7 8 9 10)
     A                                      CHKMSGID(LMG0010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1NUMM    R        O  9  5REFFLD(LUNUMM)
     A            B1TYPE    R        O  9 12REFFLD(LUTYPE)
     A            B1LAGE    R        O  9 23REFFLD(LULAGE)
     A            B1TEXT    R   -3   O  9 26REFFLD(LUTEXT)
     A            B1WSID    R        O  9 54REFFLD(LUWSID)
     A            B1USER    R        O  9 65REFFLD(LUUSER)
     A            B1VISE         2A  O  9 76
     A            B1KODE         2A  O  9 79
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030105  193929  ASK         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av tellebunter'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LU100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Buntnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 12'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre bunt'
     A                                      COLOR(BLU)
7.xx A                                  6 17'3=Kopier'
     A                                      COLOR(BLU)
7.xx A                                  6 27'4=Slett'
     A                                      COLOR(BLU)
7.xx A                                  6 36'5=Etiketter'
     A                                      COLOR(BLU)
7.xx A                                  6 49'6=Utskrift'
     A                                      COLOR(BLU)
     A                                  4 62'Type  :'
     A                                      COLOR(BLU)
     A            B2TYPV    R        O  4 70REFFLD(LUTYPE)
     A                                      OVRDTA
     A                                      COLOR(BLU)
7.xx A                                  7  2'7=Endre linjer'
     A                                      COLOR(BLU)
7.xx A                                  7 17'8=Overfør til telle-liste'
     A                                      COLOR(BLU)
7.xx A                                  7 44'9=Tilbakefør'
     A                                      COLOR(BLU)
7.xx A                                  7 58'10=Overskriv/erstatt'
     A                                      COLOR(BLU)
     A                                  5 62'Bruker:'
     A                                      COLOR(BLU)
     A            B2USRV    R        O  5 70REFFLD(LUUSER)
     A                                      OVRDTA
     A                                      COLOR(BLU)
7.xx A                                  4 42'Lager :'
     A                                      COLOR(BLU)
7.xx A            B2LAGV         2Y 0O  4 50COLOR(BLU)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Buntnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 12'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 23'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 26'Beskrivelse    '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 54'Arb.stasj.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 65'Bruker-id'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 77'Kode'
7.xx A**                                7  2'V  Buntnr Type          Beskrivels-
7.xx A**                                    e                 Arb.stasj. Bruker-
7.xx A**                                    -id   Kode'
     A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  5REFFLD(LUNUMM)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B2TYPE    R        B  5 12REFFLD(LUTYPE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20020924  154551  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Bla alt.  F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 42'F6=Opprett ny bunt'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020925  085412  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      CF04
     A                                      PUTOVR
     A                                      CSRLOC(W_AWRO W_AWCO)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL +
     A                                      &W_AWRO &W_AWCO)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_AWRO         3S 0H
     A            W_AWCO         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Nytt Buntnr . .'
     A            K1NONR    R        B  4 18REFFLD(LUNUMM)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(LMG0020 *LIBL/QMFLAGR 31)
     A                                      COMP(GE 0)
     A                                      COLOR(YLW)
     A                                  4 30'Blank gir automatisk tildeling'
     A                                  5  2'Ny type . . . .'
     A            K1TYPE    R        B  5 18REFFLD(LUTYPE)
     A                                      OVRDTA
     A  32                                  ERRMSGID(LMG0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Buntnr. . . . .'
     A            K1NUMM    R        O 10 18REFFLD(LUNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  2'Beskrivelse . .'
     A            K1TEXT    R        O 11 18REFFLD(LUTEXT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020925  085412  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 50)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette tellebunt '
     A                                      DSPATR(RI)
     A                                  4  2'Buntnr. . . . .'
     A            D1NUMM    R        O  4 18REFFLD(LUNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse . .'
     A            D1TEXT    R        O  5 18REFFLD(LUTEXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20020925  090522  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 65)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Tilbakefør tellebunt '
     A                                      DSPATR(RI)
     A                                  4  2'Buntnr. . . . .'
     A            B9NUMM    R        O  4 18REFFLD(LUNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse . .'
     A            B9TEXT    R        O  5 18REFFLD(LUTEXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
8.02 A  30                              6  2'Slette linjer .'
8.02 A  30        B9SLET         1A  B  6 18COLOR(YLW)
8.02 A                                      OVRDTA
8.02 A  30                              6 21'1/0'
8.02 A  30                              7  2'Linjer slettes i telleliste dersom-
8.02 A                                       antall = 0 etter reversering.'
8.02 A                                      COLOR(RED)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B10WIN
     A*%%TS  SD  20020925  085412  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Overskriv/erstatt varer'
     A                                      DSPATR(RI)
     A                                  4  2'Buntnr. . . . .'
     A            B10NUM    R        O  4 18REFFLD(LUNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Beskrivelse . .'
     A            B10TXT    R        O  5 18REFFLD(LUTEXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  1'OBS!! Denne jobbben overskriver an-
     A                                      tall i telleliste'
     A                                      COLOR(RED)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R H1WIN
     A*%%TS  SD  20030105  193929  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 40)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AWRO W_AWCO)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL +
     A                                      &W_AWRO &W_AWCO)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_AWRO         3S 0H
     A            W_AWCO         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Bla alternativer '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Velg type . . +'
     A            H1TYPE    R        B  4 18REFFLD(LUTYPE)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  5  2'Velg bruker . .'
     A            H1USER    R        B  5 18REFFLD(LUUSER)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  6  2'Lager . . . . +'
     A            H1LAGE    R        B  6 18REFFLD(LULAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Skrevet ut  . .'
     A            H1UTSK         1Y 0B  7 18EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  7 22'0=Alle'
     A                                  8 22'1=Ikke utskrevne'
     A                                  9 22'2=Bare utskrevne'
     A                                 10  2'Overført  . . .'
     A            H1OVER         1Y 0B 10 18COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 10 22'0=Alle'
     A                                 11 22'1=Ikke overførte'
     A                                 12 22'2=Bare overførte'
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R A1WIN
     A*%%TS  SD  20020924  114602  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 40)
     A                                      CA03
     A                                      KEEP
     A                                  2  2' Feilmelding '
     A                                      DSPATR(RI)
     A                                  5  2'Denne bruker kan ikke lage tellebu-
     A                                      nter'
     A                                      COLOR(GRN)
     A          R A2WIN
     A*%%TS  SD  20020924  114602  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 40)
     A                                      CA03
     A                                      KEEP
     A                                  2  2' Feilmelding '
     A                                      DSPATR(RI)
     A                                  5  2'Denne bruker kan ikke overføre bun-
     A                                      ter '
     A                                      COLOR(GRN)
     A          R A3WIN
     A*%%TS  SD  20020924  114602  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 40)
     A                                      CA03
     A                                      KEEP
     A                                  2  2' Feilmelding '
     A                                      DSPATR(RI)
     A                                  5  2'Det er ikke mulig å reversere DIVE-
     A                                      RSE '
     A                                      COLOR(GRN)
     A          R ASUME                     ASSUME
     A                                 24 79' '
