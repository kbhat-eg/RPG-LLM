     A*%%TS  SD  20120110  152814  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
     A*K00       220720 ask  Nytt skjermbilde
     A*800  8.06 190422 ask  Lagt inn nytt filter på "overførte"
8.12  *800  8.12 031224 ask  Lagt in kontroll om bilagsnummer finnes
8.12  *                      i overføringsfiler NXS-20521
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      PRINT
     A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070328  124845  AMD550      REL-V5R4M0  5722-WDS
     A            B1TMST          Z  H
     A            B1VALG         1Y 0B  6  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 4 5 7 8 9)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1TRTY         4A  O  6  4
     A  50                                  COLOR(RED)
     A            B1DAT1         8A  O  6 10
     A  50                                  COLOR(RED)
     A            B1TIM1         6A  O  6 19
     A  50                                  COLOR(RED)
     A            B1BSUM        12Y 2O  6 27EDTWRD('        0 ,  ')
     A  50                                  COLOR(RED)
     A            B1DAT2         8A  O  6 42
     A  50                                  COLOR(RED)
     A            B1TIM2         6A  O  6 51
     A  50                                  COLOR(RED)
     A            B1DAT3         8A  O  6 60
     A  50                                  COLOR(RED)
     A            B1TIM3         6A  O  6 69
     A  50                                  COLOR(RED)
     A            B1DAT4         8A  O  6 78
     A  50                                  COLOR(RED)
     A            B1TIM4         6A  O  6 87
     A  50                                  COLOR(RED)
     A            B1DAT5         8A  O  6 96
     A  50                                  COLOR(RED)
     A            B1TIM5         6A  O  6105
     A  50                                  COLOR(RED)
     A            B1DAT6         8A  O  6114
     A  50                                  COLOR(RED)
     A            B1TIM6         6A  O  6123
     A  50                                  COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070328  151100  AMD550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NEXSTEP'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Transaksjonslogg økonomi - bunter -
     A                                               '
     A                                      COLOR(WHT)
     A                                  2 38'RF500Z2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2' 5=Vis detaljer  7=Vis bilag  8=Vi-
     A                                      s linjer  9=Omkjøring  4=Slett bunt'
     A                                      COLOR(BLU)
     A                                  5  2'V'
     A                                      COLOR(WHT)
     A                                  5  4'Type'
     A                                      COLOR(WHT)
     A                                  5 10'Dannet'
     A                                      COLOR(WHT)
     A                                  5 19'Tid'
     A                                      COLOR(WHT)
     A                                  5 27'Buntsum'
     A                                      COLOR(WHT)
     A                                  5 42'Overført'
     A                                      COLOR(WHT)
     A                                  5 51'Tid'
     A                                      COLOR(WHT)
     A                                  5 60'Kladd'
     A                                      COLOR(WHT)
     A                                  5 69'Tid'
     A                                      COLOR(WHT)
     A                                  5 78'Journal'
     A                                      COLOR(WHT)
     A                                  5 87'Tid'
     A                                      COLOR(WHT)
     A                                  5 96'Omkjørt'
     A                                      COLOR(WHT)
     A                                  5105'Tid'
     A                                      COLOR(WHT)
     A                                  5114'Slettet'
     A                                      COLOR(WHT)
     A                                  5123'Tid'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050621  150935  ASK530      REL-V5R2M0  5722-WDS
     A                                 26  2'F1=Filter '
     A                                      COLOR(BLU)
     A                                 26 23'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 26 38'F5=Forny'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20051027  101210  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(02 02 14 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Filter'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 49'RF500H1'
     A                                      COLOR(WHT)
     A                                  4  2'Transaksjonstype'
     A            H1TYPE         4A  B  4 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  4 27'(FAKT,INKJ,SHOP,BETR)'
     A                                  5  2'Dato fra'
     A            H1DATO         6Y 0B  5 22EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.06 A                                  6  2'Ta med overførte  '
8.06 A            H1OVFD         1A  B  6 22
8.06 A                                      OVRDTA
8.06 A                                      COLOR(YLW)
8.06 A                                  6 23'(k)'
     A                                  7  2'Ta med oppdaterte '
     A            H1OPPD         1A  B  7 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7 23'(k)'
     A                                  8  2'Ta med omkjørte'
     A            H1OMKJ         1A  B  8 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8 23'(k)'
     A                                  9  2'Ta med slettede'
     A            H1SLET         1A  B  9 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
cc   A                                  9 23'(k)'
     A                                 10  2'Vise bare feil'
     A            H1FEIL         1A  B 10 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 23'(k)'
     A                                 11  2'Søk bilagsnr  '
     A            H1BILN         8Y 0B 11 22
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTWRD('       0')
     A                                 13  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20051020  151154  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 25 80)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Logg detaljer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            B3TRTY         4A  O  1 16
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  1 69'RF500B3'
     A                                      COLOR(WHT)
     A                                  2 53'Buntsum'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Transaksjoner dannet'
     A            B3DAT1         8A  O  3 29
     A            B3TIM1         8A  O  3 39
     A            B3SUM1        12Y 2O  3 50EDTWRD('        0 ,  ')
     A                                  4  4'Dannet av bruker'
     A            B3USR1        10A  O  4 29
     A                                  6  2'Transaksjoner overført'
     A            B3DAT2         8A  O  6 29
     A            B3TIM2         8A  O  6 40
     A            B3SUM2        12Y 2O  6 50EDTWRD('        0 ,  ')
     A  54                                  COLOR(RED)
     A                                  7  4'Overført av bruker'
     A            B3USR2        10A  O  7 29
     A                                  9  2'Kladd dannet'
     A            B3DAT3         8A  O  9 29
     A            B3TIM3         8A  O  9 40
     A            B3SUM3        12Y 2O  9 50EDTWRD('        0 ,  ')
     A  55                                  COLOR(RED)
     A                                 10  4'Dannet av bruker'
     A            B3USR3        10A  O 11 29
     A                                 14  2'Journal kjørt'
     A            B3DAT4         8A  O 14 29
     A            B3TIM4         8A  O 14 40
     A            B3SUM4        12Y 2O 14 50EDTWRD('        0 ,  ')
     A  56                                  COLOR(RED)
     A                                 15  4'Kjørt av bruker'
     A            B3USR4        10A  O 15 29
     A                                 18  2'Bunt omkjørt'
     A            B3DAT5         8A  O 18 29
     A            B3TIM5         8A  O 18 40
     A                                 19  4'Omkjørt av bruker'
     A            B3USR5        10A  O 19 29
     A                                 21  2'Bunt slettet'
     A            B3DAT6         8A  O 21 29
     A            B3TIM6         8A  O 21 40
     A                                 22  4'Slettet av bruker'
     A            B3USR6        10A  O 22 29
     A                                 24  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20020312  190907  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(02 02 14 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bunt  '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  2 25'RF500B4'
     A                                      COLOR(WHT)
     A                                  4  2'Bekrefte sletting av bunt!'
     A                                      COLOR(WHT)
     A                                  6  2' Transtype'
     A            B4TRTY         4A  O  6 14
     A                                  7  2' Tidspunkt'
     A            B4DATE         8A  O  7 14
     A            B4TIME         8A  O  7 23
     A                                  8  2' Buntsum'
     A            B4BSUM        12Y 2O  8 14EDTWRD('        0 ,  ')
     A                                  9  2' Bruker'
     A            B4USER        10A  O  9 14
     A                                 11  2'For å slette, trykk ENTER / OK'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B14MSG1
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bruker har ikke tilgang  '
     A                                      COLOR(WHT)
     A                                  4  2'til sletting av bunt!         '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B8WIN
     A*%%TS  SD  20020312  190907  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(02 02 15 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Omkjøring av bunt '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  2 25'RF500B8'
     A                                      COLOR(WHT)
     A                                  4  2'Bekrefte omkjøring av bunt!'
     A                                      COLOR(WHT)
     A                                  6  2' Transtype'
     A            B8TRTY         4A  O  6 14
     A                                  7  2' Tidspunkt'
     A            B8DATE         8A  O  7 14
     A            B8TIME         8A  O  7 23
     A                                  8  2' Buntsum'
     A            B8BSUM        12Y 2O  8 14EDTWRD('        0 ,  ')
     A                                  9  2' Bruker'
     A            B8USER        10A  O  9 14
     A                                 11  2' Omkjøring til'
     A            B8OKON        20A  O 11 17
     A                                 13  2'For å omkjøre, trykk ENTER / OK'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B18MSG1
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bunt er slettet    '
     A                                      COLOR(WHT)
     A                                  4  2'og kan ikke omkjøres.   '
     A                                      COLOR(WHT)
     A                                  6  2' '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B18MSG2
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bunt er allerede omkjørt '
     A                                      COLOR(WHT)
     A                                  4  2'og kan ikke omkjøres på nytt.  '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B18MSG3
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bruker har ikke tilgang  '
     A                                      COLOR(WHT)
     A                                  4  2'til omkjøring av bunt!         '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.12 A          R B18MSG4
8.12 A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
8.12 A                                      WINDOW(*DFT 8 35)
8.12 A                                      CA03
8.12 A                                      CA12
8.12 A                                      PUTOVR
8.12 A                                  3  2'Bilag finnes allerede'
8.12 A                                      COLOR(WHT)
8.12 A                                  4  2'i overføring - sjekk dette!    '
8.12 A                                      COLOR(WHT)
8.12 A                                  7  2'F3=Gå tilbake'
8.12 A                                      COLOR(BLU)
