     A*%%TS  SD  20140813  150629  BOF630      REL-V7R1M0  5770-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.30 A* R6.30 140114 BOF  Ny kolonne for tilgjengelig lager
6.30 A* R6.30 140114 BOF  Hvis tilbudsordre med vare,så rød pris
     A*
7.xx A*  7.xx 220116 bsa  Endringer relatert til ny GUI
7.05 A*  7.05 230320 ask  Lagt på fargestyring - switch "FARGE_SKAFF_SORTIMENT"
7.06 A* K000  030720 bsa  Utvidet søk
     A*
8.06 A* K000  080124 bsa  Justert felter pga overlapp.
     A*
     A*
     A*----------------------------------------------------------------------
     A*
     A*%%EC
7.03 A                                      DSPSIZ(27 132 *WIDE)
7.03 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140414  114404  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A            B1TEK2    R        H      REFFLD(FDTEK2)
     A            B1VARE    R        H      REFFLD(FDVARE)
     A            B1NOBB    R        H      REFFLD(FDNOBB)
7.03 A*            B1LVAR    R        H      REFFLD(FDLVAR)
     A            B1SENT         1A  H
     A            B1SESS         1A  H
     A            B1KPRI    R        H      REFFLD(VVKPRI)
     A            B1VTYP    R        H      REFFLD(VVVTYP)
     A            B1ANTA    R   -4 -1B  9  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
7.01 A* N22                                  DSPATR(PC)
7.03 A*           B1TEK1    R   -1   O 11 11REFFLD(VVTEK1)
7.03 A            B1TEK1        70A  O  9 11
     A  50                                  COLOR(RED)
7.05 A  53                                  COLOR(BLU)
7.03 A            B1NYPO         8A  O  9 82
6.31 A  51                                  COLOR(BLU)
6.31 A  52                                  COLOR(YLW)
7.03 A            B1BEHL         5Y 0O  9 91EDTCDE(M)
7.03 A            B1ENHE    R        O  9 98REFFLD(VPENHE)
7.03 A            B1SAPR    R        O  9102REFFLD(VPSAPR)
     A  51                                  COLOR(RED)
7.03 A            B1STAT         4A  O  9113
7.03 A            B1LVAR    R   -5   O  9118REFFLD(FDLVAR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140813  150629  BOF630      REL-V7R1M0  5770-WDS
7.03 A                                      SFLSIZ(0016)
7.03 A                                      SFLPAG(0015)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A N56                                  CF08
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CA14
     A                                      CF15
     A                                      CF16
     A                                      CA21
     A                                      CA22
     A                                      CA23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
7.03 A                                  1 46'Ordre/Fakturering'
7.03 A                                      COLOR(WHT)
7.03 A                                  1118'Kl. :'
7.03 A                                      COLOR(WHT)
7.03 A                                  1124TIME
7.03 A                                      COLOR(WHT)
7.03 A                                  2  2'Spørring/Registrering av varelinje-
7.03 A                                      r'
7.03 A                                      COLOR(WHT)
7.03 A                                  2 38'FD102X2'
7.03 A                                      COLOR(WHT)
7.03 A                                  2 46'Bruker:'
7.03 A                                      COLOR(WHT)
7.03 A                                  2 54USER
7.03 A                                      COLOR(WHT)
7.03 A                                  2118'Dato:'
7.03 A                                      COLOR(WHT)
7.03 A                                  2124DATE
7.03 A                                      EDTCDE(Y)
7.03 A                                      COLOR(WHT)
7.03 A                                  3  1'-----------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre . . .'
     A                                      COLOR(BLU)
     A            FONUMM    R        O  4 14
8.06 A*                                 4 50'Kunde. .'
8.06 A*                                     COLOR(BLU)
7.06 A                                  4 30'Nullstill filter'
7.06 A                                      COLOR(BLU)
7.06 A            B2NULL         1Y 0B  4 48
7.06 A                                      COLOR(YLW)
7.06 A                                      OVRDTA
7.06 A                                  4 50'1/0'
7.06 A                                      COLOR(BLU)
7.06 A                                  4 80'Kunde. .'
7.06 A            B2KUND    R        O  4 90REFFLD(FOKUND)
7.06 A            B2KNAV        17A  O  4 97
7.xx A                                  5  2'Varegrp . +'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  5 14REFFLD(VVOGRP)
     A  31                                  ERRMSGID(VMG0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 17'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  5 19REFFLD(VVHGRP)
     A  32                                  ERRMSGID(VMG0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 22'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  5 24REFFLD(VVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.06 A                                  5 30'Leverandør+'
7.06 A                                      COLOR(BLU)
7.06 A            B2LEVR    R        B  5 42REFFLD(VVLDOR)
7.06 A                                      COLOR(YLW)
7.06 A                                      OVRDTA
7.06 A            B2LNVN        30A  O  5 49COLOR(YLW)
7.06 A                                      OVRDTA
7.06 A                                  5 80'K.prosj.'
     A                                      COLOR(BLU)
7.06 A            B2KPRO    R        O  5 90REFFLD(FOKPRO)
7.06 A            B2PNAV        17A  O  5 97
     A                                  6  2'Frisøk. . .'
     A                                      COLOR(BLU)
     A            B2STEK        35A  B  6 14COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.06 A                                  6 80'Levtype.'
     A                                      COLOR(BLU)
7.06 A            B2LTXT         6A  O  6 90
7.06 A                                  6100'Tot:'
     A                                      COLOR(BLU)
7.06 A            B2TOTS    R        O  6105REFFLD(FOTOTS)
     A                                      EDTCDE(4)
     A                                  7  2'Varetekst .'
     A                                      COLOR(BLU)
     A                                  7 50'Varenr .'
     A                                      COLOR(BLU)
     A                                  7 69'Nobbnr . '
     A                                      COLOR(BLU)
     A                                  7 92'Leverandørvarenr . '
     A                                      COLOR(BLU)
     A            B2TEK1    R        B  7 14REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2VARE         8Y 0B  7 59COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            B2NOBB    R        B  7 80REFFLD(VVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.03 A            B2LVAR    R        B  7112REFFLD(VVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Antall  '
     A                                      COLOR(WHT)
7.xx A                                  8 11'Varetekst                         -
     A                                                                         -
     A                                       '
     A                                      COLOR(WHT)
7.03 A                                  8 91'Beh   '
     A                                      COLOR(WHT)
7.03 A  80                              8 82'Varenr  '
     A                                      COLOR(WHT)
7.03 A  81                              8 82'Nobbnr  '
     A                                      COLOR(WHT)
7.03 A  82                              8 82'L.Vnr   '
     A                                      COLOR(WHT)
7.03 A                                  8 98'Enh'
     A                                      COLOR(WHT)
7.03 A N84                              8102'Salgspris '
     A                                      COLOR(WHT)
7.03 A  84                              8102'inkl. mva '
     A                                      COLOR(WHT)
7.03 A                                  8113'CPKT'
     A                                      COLOR(WHT)
7.03 A                                  8118'Levvarenr     '
7.03 A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031016  102331  HKO         REL-V5R2M0  5722-WDS
7.04 A*                                25  2'F2=Tekstl. F3=Gå tilbake F4=Spør-
7.04 A*                                     ring F8=SkaffReg F9=GjentaSøk F1-
7.04 A*                                     3=Vis vare'
7.04 A*                                     COLOR(BLU)
7.04 A                                 25  2'F2=Tekstl. F3=Gå tilbake F4=Spør-
7.04 A                                      ring'
7.04 A                                      COLOR(BLU)
7.04 a N56                             25 39'F8=SkaffReg'
7.04 a                                      COLOR(BLU)
7.04 a                                 25 52'F9=GjentaSøk F13=Vis vare'
7.04 a                                      COLOR(BLU)
7.03 A                                 25 78'F14=KundInf F15=RabMatr F16=Spes-
     A                                      .pris F23=ByttIn'
     A                                      COLOR(BLU)
7.03 A N83                             26  2'F21=Inkl.S'
     A                                      COLOR(BLU)
7.03 A  83                             26  2'F21=Eksl.S'
     A                                      COLOR(BLU)
7.03 A N84                             26 14'F22=InklMva'
     A                                      COLOR(BLU)
7.03 A  84                             26 14'F22=Eksl.mva'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
