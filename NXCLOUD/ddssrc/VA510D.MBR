     A*%%TS  SD  20020226  090208  HKO         REL-V5R1M0  5722-WDS
     A*
7.xx A*   010816 bsa Endringer relatert til ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  094842  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1EENH    R        O  7  5REFFLD(VAEENH)
     A            B1ETXT    R        O  7 10REFFLD(VAETXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  090208  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge enhet '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 10'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Enh'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2EENH    R        B  4  5REFFLD(VAEENH)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2ETXT    R        B  4 10REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  090208  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 45)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
