     A*%%TS  SD  20160905  210321  ASP_G0XA    REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : EHANDEL
     A* Format. . . . . : CK002D
     A* Beskrivelse . . : VEDLIKEHOLD AV KOBLING MELLOM OVERORDNET
     A*                   KJEDEKATEGORI OG KUNDEKATEGORI
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       020916 ASK  Nytt program
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20151026  150945  ASP_G0X     REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFOFAK)
     A            B1GKLA         2Y 0O  8  5
     A            B1GTXT        30A  O  8  8
     A            B1GKAT         4Y 0O  8 39
     A            B1KTXT        20A  O  8 44
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160905  205016  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kobling kjedekategori mot kunde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Ko Kjedkategori                -
     A                                         KKat Kundekategori'
     A                                      COLOR(WHT)
     A            B2GKLA         2Y 0B  7  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20160902  154250  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett  F8=Kjedekategorier'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20160905  205016  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kobling kjedekategori mot kunde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kjedekategori. . .'
     A            C1GKLA         2Y 0B  5 22EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Kundekategori. . .'
     A            C1GKAT         4Y 0B  6 22EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Søk'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20151026  125839  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kjedekategori ikke funnet'
     A                                  3  2'Bruk gyldig kjedekategori'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20151026  125839  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kundekategori ikke funnet'
     A                                  3  2'Bruk gyldig kundekategori'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20151026  125839  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kobling finnes fra før!'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20160905  210321  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ehandelsløsning'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Kopbling kjedekategori mot kunde'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kjedekategori. . . '
     A            C2GKLA         2  0O  5 22COLOR(YLW)
     A                                      OVRDTA
     A            C2GTXT        30A  O  5 28
     A                                  6  2'Kundekategori. . .'
     A            C2GKAT         4Y 0O  6 22COLOR(YLW)
     A                                      OVRDTA
     A            C2KTXT        30A  O  6 28OVRDTA
     A*
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2EODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EEUS    R        O 20 31REFFLD(VVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20160902  154250  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kobling '
     A                                      DSPATR(RI)
     A                                  4  2'Kjedekategori'
     A            D1GKLA         2Y 0O  4 17
     A            D1GTXT        30A  O  4 22
     A                                  6  2'Kundekategori'
     A            D1GKAT         4Y 0O  6 17
     A            D1KTXT        30A  O  6 22
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
