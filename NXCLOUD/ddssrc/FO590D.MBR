     A*%%TS  SD  20080711  144702  AMD560      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO590D
     A* Beskrivelse . . : Administrering av plukking
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290316  bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*K0000  120122  bsa  Skriver om til bredt skjermbilde.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01 A**                                    DSPSIZ(24 80 *DS3)
8.01 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20080711  144702  AMD560      REL-V5R4M0  5722-WDS
     A            B1KUND    R        H      REFFLD(FOKUND)
8.02 A            B1LKUN    R        H      REFFLD(FOKUND)
     A            B1KPRO    R        H      REFFLD(FOKPRO)
     A            B1ITYP    R        H      REFFLD(FOOTYP)
     A            B1BDAT    R        H      REFFLD(FOBDAT)
     A            B1ADR1    R        H      REFFLD(FOADR1)
     A            B1KODE         2A  H
     A            B1KOD2         1A  H
8.01 A**          B1LMTX    R        H      REFFLD(FOLMTX)
7.xx A            B1VALG         2Y 0B  9  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 2 4 5 6 7 8 9 60)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A            B1LDAT         6Y 0O  9  5EDTWRD('  .  .  ')
7.xx A            B1OTYP    R        O  9 14REFFLD(FOOTYP)
7.xx A            B1KJOR    R        O  9 17REFFLD(FOKJOR)
7.xx A            B1KJNR    R        O  9 20REFFLD(FOKJNR)
7.fb A  45                                  COLOR(RED)
7.fb A N45                                  COLOR(GRN)
7.xx A            B1NUMM    R        O  9 26REFFLD(FONUMM)
7.fb A  45                                  COLOR(RED)
7.fb A N45                                  COLOR(GRN)
7.xx A            B1SUFF    R        O  9 35REFFLD(FOSUFF)
8.01 A            B1KNAV        20A  O  9 38
8.01 A            B1LNAV        20A  O  9 60
8.01 A            B1VEKT         6Y 0O  9 82EDTCDE(4)
     A  80                                  COLOR(RED)
8.01 A            B1VOLU         5Y 0O  9 89EDTCDE(4)
     A  80                                  COLOR(RED)
8.01 A            B1LMAT    R        O  9 95REFFLD(FOLMAT)
8.01 A            B1INF1    R        O  9 98REFFLD(FOINF1)
8.01 A            B1LMTX        12A  O  9112
8.01 A            B1ALLE         5A  O  9125
     A  43                                  COLOR(BLU)
     A  44                                  COLOR(RED)
     A  41                                  COLOR(WHT)
     A  42                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080711  144702  AMD560      REL-V5R4M0  5722-WDS
8.01 A                                      SFLSIZ(0017)
8.01 A                                      SFLPAG(0016)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CF13
     A                                      CF14
8.01 A**                                    CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Administrering av plukking'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO590B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 14'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 17'KR'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 26'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A N51                              4 38'Kunde  '
7.xx A                                      COLOR(WHT)
7.xx A  51                              4 38'Lev.adr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre      4=Tilbakestill    6=V-
     A                                      is kunde/kundeprosjekt  60=Logg    -
     A                                               '
     A                                      COLOR(BLU)
7.xx A                                  7  2'5=Vis ordre  7=Behandle ordre  8=H-
     A                                      vem behandler ordre      9=Send til-
     A                                       plukking'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Lev.dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Ot'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 17'KR'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'KRnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 26'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 36'S'
7.xx A                                      COLOR(WHT)
8.01 A*N51                              8 38'Kunde  '
8.01 A                                  8 38'Kunde  '
7.xx A                                      COLOR(WHT)
8.01 A* 51                              8 38'Lev.adr'
8.01 A                                  8 60'Lev.adr'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 84'KG'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 91'M3'
7.xx A                                      COLOR(WHT)
8.01 A                                  8 95'Fm'
7.xx A                                      COLOR(WHT)
8.01 A*N51                              8 98'Informasjon'
8.01 A                                  8 98'Informasjon'
7.xx A                                      COLOR(WHT)
8.01 A* 51                              8 98'Lev.måte   '
8.01 A                                  8112'Lev.måte   '
7.xx A                                      COLOR(WHT)
7.xx A*N51                              6  2'V  Lev.dato Ot KR KRnr  Ordre    S-
7.xx A*                                       Kunde         KG     M3  Fm  Info-
7.xx A*                                     rmasjon'
7.xx A**                                    COLOR(WHT)
7.xx A* 51                              6  2'V Lev.dato Ot KR KR-nr Ordre     S-
7.xx A**                                      Lev.adr       KG     M3  Fm  Lev.-
7.xx A**                                    måte'
7.xx A**                                    COLOR(WHT)
7.xx A            B2LDAT         6Y 0B  5  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A            B2OTYP    R        B  5 14REFFLD(FOOTYP)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2KJOR    R        B  5 17REFFLD(FOKJOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NUMM    R        B  5 26REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KNAV    R  -19   B  5 38REFFLD(FOKUNA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20080620  145548  BOF560      REL-V5R4M0  5722-WDS
8.01 A                                 26  2'F1=Filter'
     A                                      COLOR(BLU)
8.01 A                                 26 13'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.01 A                                 26 28'F5=Forny'
     A                                      COLOR(BLU)
8.01 A                                 26 38'F13=Ordre som plukkes'
     A                                      COLOR(BLU)
8.01 A                                 26 61'F14=Restede ordre'
     A                                      COLOR(BLU)
8.01 A**                               26 80'F23=Bytt Info'
8.01 A**                                    COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20060204  231104  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 46)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Tildeling av plukkeansvar '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenr . . .'
     A            C2NUMM    R        O  4 16REFFLD(FONUMM)
     A                                      OVRDTA
6.NU A            C2SUFF    R        O  4 25REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                  5  2'Kunde . . . .'
     A            C2KNAV        30A  O  5 16OVRDTA
     A                                  6  2'Kundeprosjekt'
     A            C2PNAV        30A  O  6 16OVRDTA
7.xx A                                  9  2'Selgernr. . +'
     A            C2PLSE    R        B  9 16REFFLD(FOPLSE)
     A  31                                  ERRMSGID(FRA0910 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRA0920 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2SNAV        20A  O  9 21COLOR(YLW)
     A                                      OVRDTA
7.03 A                                 10  2'Kjørerute . .'
7.03 A            C2KJOR    R        B 10 16REFFLD(FOKJOR)
7.03 A  33                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 33)
7.03 A                                      COLOR(YLW)
7.03 A                                      CHECK(RB)
7.03 A                                      OVRDTA
7.03 A            C2KJNA        20A  O 10 21COLOR(YLW)
7.03 A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C4WIN
     A*%%TS  SD  20071024  152107  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Avbryt klar til plukking '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenr . . . .'
     A            C4NUMM    R        O  4 18REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.NU A            C4SUFF    R        O  4 27REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Kunde . . . . .'
     A            C4KNAV        30A  O  5 18OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Kundeprosjekt .'
     A            C4PNAV        30A  O  6 18OVRDTA
     A                                      COLOR(YLW)
     A                                  8 20'Dato'
     A                                      COLOR(WHT)
     A                                  8 30'Kl'
     A                                      COLOR(WHT)
     A                                  8 36'Bruker'
     A                                      COLOR(WHT)
     A                                  8 47'Skjerm'
     A                                      COLOR(WHT)
     A                                  9  2'Klar til plukk'
     A            C4UPDA         6Y 0O  9 18EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A            C4UPTI         6Y 0O  9 27EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A            C4UPUS        10A  O  9 36COLOR(YLW)
     A            C4UPWS        10A  O  9 47COLOR(YLW)
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20071101  133306  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 11 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
6.30 A                                      RTNCSRLOC(&W_ARC1 &W_AFL1)
     A                                      PUTOVR
6.30 A            W_ARC1        10A  H
6.30 A            W_AFL1        10A  H
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Oppgi ønsket lager  . . . +'
     A            F1LAGE    R        B  4 30REFFLD(FÅLAGE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  4 34'(Blank = alle)'
6.30 A                                  5  2'Sjekker oppgitt lager på ordre-lin-
     A                                      je-nivå.'
6.30 A                                  6  2'Tar med ordre som har en ordre-lin-
     A                                      je med'
6.30 A                                  7  2'oppgitt lager.'
7.xx A                                  8  2'Ordretype . . . . . . . . +'
6.30 A            F1OTY1         2A  B  8 30
6.30 A  34                                  ERRMSGID(FMA0260 *LIBL/QMFOFAK 34)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  8 33'+'
6.30 A            F1OTY2         2A  B  8 35
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  8 38'+'
7.xx A            F1OTY3         2A  B  8 40
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  9  2'Forsendelsesmåte. . . . . +'
6.30 A            F1LMA1    R        B  9 30REFFLD(FOLMAT)
6.30 A  35                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 35)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  9 33'+'
7.xx A            F1LMA2    R        B  9 35REFFLD(FOLMAT)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
7.xx A                                  9 38'+'
7.xx A            F1LMA3    R        B  9 40REFFLD(FOLMAT)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10 18'F4=Spørring  '
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
