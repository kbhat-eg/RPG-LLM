     A*%%TS  SD  20170112  134130  ASP_G0XA    REL-V7R1M0  5770-WDS
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20170112  121126  ASP_G0XA    REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(4)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1TTIL    R        O  9  5REFFLD(RUTTIL)
     A            B1TTEK    R        O  9 26REFFLD(RUFTEK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20170111  205329  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilganger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  3'Kunde. . . . :'
     A            B2NAVN    R        O  4 18REFFLD(RKNAVN)
     A                                      COLOR(YLW)
     A                                  5  3'Kontaktperson:'
     A            B2KPER    R        O  5 18REFFLD(RUENAV)
     A                                      COLOR(YLW)
     A                                  7  2'4=Slett'
     A                                      COLOR(BLU)
     A                                  8  2'V  Tilgang              Beskrivels-
     A                                      e'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20170111  133657  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F6=Opprett'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20170112  134130  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2KTSQ    R        H      REFFLD(RUKTSQ)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tilganger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. . . . .'
     A            C2NAVN    R        O  4 17REFFLD(RKNAVN)
     A                                      OVRDTA
     A                                  5  2'Kontaktperson '
     A            C2KPER    R        O  5 17REFFLD(RUENAV)
     A                                      OVRDTA
     A                                  7  2'Valgt tilgang'
     A            C2TTIL    R        B  7 17REFFLD(RUTTIL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TTEK    R        O  8 17REFFLD(RUFTEK)
     A                                      OVRDTA
     A                                 22 48'Sist endret'
     A            C2EDAT         6Y 0O 22 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 69'/'
     A            C2EUSR         3A  O 22 71COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C2MSG1
     A*
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Denne tilgangen er ikke gyldig.'
     A                                  3  2'Velg gyldig tilgang.        '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20170111  215703  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 12 60)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette tilganger '
     A                                      DSPATR(RI)
     A                                  4  2'Kunde. . . . . . .'
     A            D1NAVN    R        O  4 21REFFLD(RKNAVN)
     A                                  5  2'Kontaktperson  . .'
     A            D1KPER    R        O  5 21REFFLD(RUENAV)
     A                                  7  2'Tilgang  . . . . .'
     A            D1TTIL    R        O  7 21REFFLD(RUTTIL)
     A            D1TTEK    R        O  8 21REFFLD(RUFTEK)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
