     A*%%TS  SD  20031107  002239  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RR105D
     A* Beskrivelse . . : Resultatrapport - vedlikehold rapportlinje
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       160902 KOB  Nytt program
7.xx A*       120516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020916  162533  KOB         REL-V5R1M0  5722-WDS
     A            B15RAP    R        H      REFFLD(RR5RAP)
     A            B1VALG    R    2   B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 7 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      EDTCDE(Z)
     A            B15LIN    R        O 10  5REFFLD(RR5LIN)
     A            B15TXT    R        O 10 11REFFLD(RR5TXT)
     A            B15KOD    R        O 10 53REFFLD(RR5KOD)
     A            B15SLN    R        O 10 56REFFLD(RR5SLN)
     A            B15NUL    R        O 10 63REFFLD(RR5NUL)
     A            B15ABL    R        O 10 66REFFLD(RR5ABL)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020916  223136  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rapport:'
     A            B20RAP    R        O  4 11REFFLD(RR0RAP)
     A                                      OVRDTA
     A            B20TXT    R        O  4 14REFFLD(RR0TXT)
     A                                      OVRDTA
7.xx A                                  6  5'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       7=Fra-til konto'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Beskrivelse              '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 53'T'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 56'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'N'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 66'B'
7.xx A**                                9  2'V  Linje Beskrivelse              -
7.xx A**                                                     T  Linje  N  B'
     A                                      COLOR(WHT)
7.xx A            B25LIN    R        B  7  5REFFLD(RR5LIN)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A            B25TXT    R        B  7 11REFFLD(RR5TXT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020916  150943  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjenummer. . . . . . . . .'
     A            C15LIN    R        B  5 31REFFLD(RR5LIN)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020916  150943  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  1'Linjenummer finnes fra før'
     A                                      COLOR(BLU)
     A                                  6  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20031107  002239  KOB         REL-V5R2M0  5722-WDS
     A                                      CA02
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av resultatrapport'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RR105B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjenummer. . .'
     A            C15LIN    R        O  5 19REFFLD(RR5LIN)
     A                                      EDTCDE(Z)
     A                                  6  2'Beskrivelse. . .'
     A            C25TXT    R        B  6 19REFFLD(RR5TXT)
     A  36 81                               ERRMSGID(RMH0200 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(PC)
     A                                      OVRDTA
     A                                  7  2'Linjekode. . . .'
     A            C25KOD    R        B  7 19REFFLD(RR5KOD)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      OVRATR
     A                                      COLOR(YLW)
7.xx A                                  7 27'K=Hente tall fra konto,'
7.xx A                                  7 52'B=Sub-total,'
7.xx A                                  8 27'L=Hente tall fra linje,'
7.xx A                                  8 52'N=Non-printtot,'
7.xx A                                  9 27'S=Sumlinje,'
7.xx A                                  9 52'T=Fri tekst'
     A                                 12  2'Sammenlign mot.:'
     A            C25SLN    R        B 12 19REFFLD(RR5SLN)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 27'Linjenr.'
     A                                 13  2'Nullstill . . .:'
     A            C25NUL    R        B 13 19REFFLD(RR5NUL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      VALUES(' ' 'N' 'J')
     A                                 14  2'Antall blanke .:'
     A            C25ABL    R        B 14 19REFFLD(RR5ABL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 27'Antall blanke linjer 0,1,2,3,S=Ny -
     A                                      side'
     A                                 20  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 21 31REFFLD(RÅEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 27'J=Ja (Nullstiller uten utskrift)'
     A          R D1WIN
     A*%%TS  SD  20020918  003747  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette linje '
     A                                      DSPATR(RI)
     A                                  4  2'Linjenummer.'
     A            D15LIN    R        O  4 15REFFLD(RR5LIN)
     A  32                                  ERRMSGID(RMH0250 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMH0260 *LIBL/QMFOKON 33)
     A                                  6  2'Beskrivelse.'
     A            RR5TXT    R   40   O  6 15REFFLD(RR5TXT)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020916  150943  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Linjenummer.'
     A            K15LIN    R        B  4 15REFFLD(RR5LIN)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Linjenummer.'
     A            RR5LIN    R        O  8 15
     A                                  9  2'Beskrivelse.'
     A            RR5TXT    R   40   O  9 15
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
