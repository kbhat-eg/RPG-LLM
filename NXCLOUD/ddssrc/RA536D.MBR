     A*%%TS  SD  20020327  213716  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA536D
     A* Beskrivelse . . : Personroller, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       170921 bsa  Nytt program
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3 -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1OROL        20A  O  7  5
     A            B1OBES        20   O  7 27
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020327  213716  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge personrolle'
     A                                      DSPATR(RI)
     A                                  3  5'Rolle'
     A                                      COLOR(WHT)
     A                                  3 27'Beskrivelse'
     A                                      COLOR(WHT)
     A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
     A            B2OROL        20A  B  4  5
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  5'Rolle'
     A                                      COLOR(WHT)
     A                                  6 27'Beskrivelse'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
