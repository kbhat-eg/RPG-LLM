     A*%%TS  SD  19950221  130107  AS          REL-V2R3M0  5738-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL701D
     A* Beskrivelse . . : Melding om overføring av banktranser ICF
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A          R A1WIN
     A*%%TS  SD  19940916  105122  AS          REL-V2R3M0  5738-PW1
     A                                      WINDOW(6 35 8 40 *NOMSGLIN)
     A                                      PUTOVR
     A            A1MSG1        38A  O  2  1OVRDTA
     A            A1MSG2        38A  O  3  1OVRDTA
     A            A1MSG3        21A  O  4  1OVRDTA
     A            A1MSG4        38A  O  5  1OVRDTA
     A            A1TELL         6Y 0O  4 23COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                  1  1'Overføringsstatus:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            A1TOTL         6Y 0O  4 33COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            A1SKIL         2A  O  4 30OVRDTA
     A            A1FEIT         5   O  6  1COLOR(RED)
     A                                      OVRDTA
     A            A1FEIM        38   O  7  1COLOR(RED)
     A                                      OVRDTA
     A          R ASUME                     ASSUME
     A                                 24 79' '
     A          R A2WIN
     A*%%TS  SD  19941101  140232  ASP         REL-V2R3M0  5738-PW1
     A                                      WINDOW(2 2 8 75)
     A                                  2  1'Retur:'
     A                                  5  1'Send:'
     A            A2RETU        70A  B  3  3
     A            A2SEND        70A  B  6  3
     A                                  1 31'IO-bilde'
     A                                      DSPATR(RI)
     A          R A3WIN
     A*%%TS  SD  19950221  130107  AS          REL-V2R3M0  5738-PW1
     A                                      WINDOW(7 30 8 40)
     A                                  2  2'P A S S O R D  -  F E I L !'
     A                                      COLOR(RED)
     A                                  4  2'Angi passord:'
     A            A3NYPW         8A  B  4 16DSPATR(ND)
     A            A3MSG1        38A  B  6  2DSPATR(PR)
     A            A3MSG2        38A  B  7  2DSPATR(PR)
