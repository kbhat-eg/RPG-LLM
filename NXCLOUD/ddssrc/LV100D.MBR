     A*%%TS  SD  20071218  194935  ASK550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LV100D
     A* Beskrivelse . . : Registrering/Vedlikehold av tellebunter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.10 A* R6.10 181109 BSA  EAN koden utvidet fra 13 til 14 posisjoner
6.30 A* R6.30 030117 BOF  Vise 0 foran komma hvis kun desimaler.
7.xx A* 7.xx  190516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20030114  213211  ASK         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 4 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
7.xx A            B1LINE    R        O  9  4REFFLD(LVLINE)
7.xx A            B1VARE         8A  O  9 11
7.xx A            B1TEK1    R        O  9 20REFFLD(LVTEK1)
7.xx A            B1ANTA        11Y 3O  9 56EDTWRD('      0 ,   -')
7.xx A            B1ENHE    R        O  9 70REFFLD(LVENH1)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030116  115118  ASK         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av tellebunter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Buntnr'
     A            B2NUMM    R        O  4  9REFFLD(LUNUMM)
     A                                      EDTCDE(Z)
     A            B2TEXT    R        O  4 16REFFLD(LUTEXT)
     A                                  4 64'Type'
     A            B2TYPE    R        O  4 69REFFLD(LUTYPE)
7.xx A                                  5  4'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 13'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  4=Slett  7=Fortløpende'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Linje'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Varetekst       '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 58'Antall'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 70'Enh'
7.xx A**                                6  2'V Linje  Varenr   Varetekst       -
7.xx A**                                                          Antall      E-
7.xx A**                                    nh'
     A                                      COLOR(WHT)
7.xx A            B2LINE    R        B  6  4REFFLD(LELINE)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  6 11'+'
7.xx A            B2VARE    R   -7   B  6 13REFFLD(LEVARE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20071218  194935  ASK550      REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 40'F6=Opprett'
     A                                      COLOR(BLU)
     A                                 23 52'F7=Bla bak/fram'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20030115  225239  ASK         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(9 16 9 38)
     A                                  1  2'Vedlikeholde telleinformasjon'
     A                                      DSPATR(RI)
     A            C2VARE    R   -7   O  3  2REFFLD(LEVARE)
     A            C2TEK1    R        O  4  2REFFLD(LETEK1)
     A                                  6  2'Antall    :'
     A            C2ANTT    R        B  6 15REFFLD(LEANTT)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C2ENH1    R        B  6 29REFFLD(LVENH1)
     A  31                                  ERRMSGID(LM00080 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3WIN
     A*%%TS  SD  20030630  211757  ASK         REL-V5R2M0  5722-WDS
     A                                      WINDOW(9 16 12 50)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                  1  2'Registrere telling diversevare'
     A                                      DSPATR(RI)
     A                                  3  2'Vare  . . :'
     A            C3VARE    R   -7   B  3 15REFFLD(LEVARE)
     A  31                                  ERRMSGID(LMV0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A            C3TEK1    R        B  4 15REFFLD(LETEK1)
     A  32                                  ERRMSGID(LMV0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                  6  2'Antall  . :'
     A            C3ANTT    R        B  6 15REFFLD(LEANTT)
     A  33                                  ERRMSGID(LMV0050 *LIBL/QMFLAGR 33)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            C3ENH1    R        B  6 29REFFLD(LVENH1)
     A  34                                  ERRMSGID(LMV0030 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                  8  2'Salgspris :'
     A            C3SPNY    R        B  8 15REFFLD(LESPNY)
     A  35                                  ERRMSGID(LMV0040 *LIBL/QMFLAGR 35)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8 27'Kostpris  :'
     A            C3KPNY    R        B  8 40REFFLD(LEKPNY)
     A  36                                  ERRMSGID(LMV0040 *LIBL/QMFLAGR 36)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9  2'Salgspris :'
     A            C3SPMV    R        B  9 15REFFLD(LESPNY)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  9 27'(Inkl.mva)'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 11 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C4WIN
     A*%%TS  SD  20030630  211757  ASK         REL-V5R2M0  5722-WDS
     A                                      WINDOW(9 16 10 50)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                  1  2'Registrere manuell telling'
     A                                      DSPATR(RI)
     A                                  3  2'Vare  . . :'
     A            C4VARE    R   -7   B  3 15REFFLD(LEVARE)
     A  31                                  ERRMSGID(LMV0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A            C4TEK1    R        O  4 15REFFLD(LETEK1)
     A                                  6  2'Antall  . :'
     A            C4ANTT    R        B  6 15REFFLD(LEANTT)
     A  32                                  ERRMSGID(LMV0050 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A  39                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            C4ENH1    R        B  6 29REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A            C4ENH2    R        O  6 33REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  81                                  DSPATR(RI)
     A            C4ENH3    R        O  6 37REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  82                                  DSPATR(RI)
     A                                  9  2'F1=Bytt enhet'
     A                                      COLOR(BLU)
     A                                  9 17'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 32'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C5WIN
     A*%%TS  SD  20030630  211757  ASK         REL-V5R2M0  5722-WDS
     A                                      WINDOW(9 16 10 50)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                  1  2'Telling ved scanning'
     A                                      DSPATR(RI)
7.xx A                                  3  2'Vare  . . +'
6.10 A            C5VARE        14A  B  3 15COLOR(YLW)
     A  31                                  ERRMSGID(LMV0010 *LIBL/QMFLAGR 31)
6.10 A                                      CHECK(RB)
     A                                      CHECK(ER)
     A            C5TEK1    R        O  4 15REFFLD(LETEK1)
     A                                  6  2'Antall  . :'
     A            C5ANTT    R        B  6 15REFFLD(LEANTT)
     A  32                                  ERRMSGID(LMV0050 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  39                                  DSPATR(PC)
     A            C5ENH1    R        O  6 29REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A            C5ENH2    R        O  6 33REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  81                                  DSPATR(RI)
     A            C5ENH3    R        O  6 37REFFLD(LVENH1)
     A                                      COLOR(YLW)
     A  82                                  DSPATR(RI)
     A                                  9  2'F1=Bytt enhet'
     A                                      COLOR(BLU)
     A                                  9 17'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 32'F4=Spørring'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20030115  223312  ASK         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 11 38)
     A                                  1  2' Slette tellelinje '
     A                                      DSPATR(RI)
     A                                  3  2'Linje'
     A            B4LINE    R        O  3  9REFFLD(LELINE)
     A            B4VARE    R   -7   O  5  2REFFLD(LEVARE)
     A            B4TEK1    R        O  6  2REFFLD(LETEK1)
     A                                  8  2'Opptalt'
     A            B4ANTT    R        O  8 10REFFLD(LEANTT)
     A                                      EDTWRD('        ,   -')
     A            B4ENH1    R        O  8 24REFFLD(LVENH1)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B5WIN
     A*%%TS  SD  20071227  133418  ASK540      REL-V5R4M0  5722-WDS
     A                                      CA08
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 7 38)
     A                                  1  2'  Antallskontroll  '
     A                                      DSPATR(RI)
     A                                  3  2'Opptalt'
     A            B5ANTT    R        O  3 10REFFLD(LEANTT)
     A                                      EDTWRD('        ,   -')
     A            B5ENH1    R        O  3 24REFFLD(LVENH1)
     A                                  6  2'F8=Bekreft antall'
     A                                      COLOR(BLU)
     A                                  6 21'Enter=Nytt antall'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
