     A*%%TS  SD  20140122  092201  SST620      REL-V7R1M0  5770-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       010816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1WSF                     SFL
     A*%%TS  SD  20140122  092201  SST620      REL-V7R1M0  5770-WDS
     A            B1VALG         1A  B  6  3COLOR(YLW)
     A                                      VALUES('1' '2' '5' ' ')
     A                                      CHKMSGID(JAV0010 *LIBL/QMFVADM)
     A            B1REGL    R        O  6  6REFFLD(JA2RGL)
     A            B1RTXT    R        O  6 14REFFLD(JA2TXT)
     A          R B2WCT                     SFLCTL(B1WSF)
     A*%%TS  SD  20140122  092201  SST620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'Velg post og trykk enter'
     A                                      COLOR(BLU)
     A                                  2  2'1=Valg'
     A                                      COLOR(BLU)
     A                                  2 11'2=Endre'
     A                                      COLOR(BLU)
     A                                  2 20'5=Se på'
     A                                      COLOR(BLU)
7.xx A                                  3  6'Regel'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 14'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A            B2REGL    R        B  4  6REFFLD(JA2RGL)
     A                                      DLTCHK
     A  31                                  ERRMSGID(JAV0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JAV0030 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2RTXT    R        B  4 14REFFLD(JA2TXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  3'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  6'Regel'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Beskrivelse          '
7.xx A                                      COLOR(WHT)
7.xx A**                                4  1'Valg Regel   Beskrivelse          -
7.xx A**                                             '
7.xx A**                                    COLOR(WHT)
      *                                     OVRATR
     A          R B2CMD
     A*%%TS  SD  19931104  101232  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(&WACTLI &WACTPO 15 50)
     A                                      CA12
     A                                      OVERLAY
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 18'F5=Forny'
     A                                      COLOR(BLU)
     A                                 14 29'F6=Opprette'
     A                                      COLOR(BLU)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A          R C2WIN
     A*%%TS  SD  19931105  143417  AMD         REL-V2R2M0  5738-PW1
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(&WACTLI &WACTPO 15 50)
     A                                  4  2'Regel . . . . .'
     A                                  5  2'Beskrivelse . .'
     A                                 14  2'F12=Annuller'
     A                                      COLOR(BLU)
     A            C1REGL    R        O  4 18REFFLD(JA2RGL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RTXT    R        B  5 18REFFLD(JA2TXT)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A          R ASUME                     ASSUME
     A                                 24 79' '
