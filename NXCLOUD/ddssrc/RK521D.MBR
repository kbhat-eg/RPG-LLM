     A*%%TS  SD  20051006  132123  KOB540      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK521D
     A* Beskrivelse . . : Kundeposteringer, spørring - rente/inkasso
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
7.xx A*       060516 bsa  Justeringer basert på ny GUI og regler
7.01 A*       Spesialversjon
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010130  231007  KOB         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(4 5 8 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KUND    R        O 10  4REFFLD(RKKUND)
7.01 A  80                                  COLOR(RED)
     A            B1NAVN    R        O 10 11REFFLD(RKNAVN)
7.01 A  80                                  COLOR(RED)
     A            B1STED        23   O 10 42
7.01 A  80                                  COLOR(RED)
     A            B1SALD    R        O 10 66REFFLD(RKSALD)
7.01 A  80                                  COLOR(RED)
     A            B1ALFA    R        H      REFFLD(RKALFA)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050417  220306  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold uttrekk kunder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Frisøk .'
     A            B2STEK        35A  B  5 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Kundenavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'4=Slett '
     A                                      COLOR(BLU)
7.xx A                                  8 11'5=Se på  8=Posteringer'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Kundenavn                '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 42'Postnr/sted              '
7.xx A**                                9  2'V Kunde  Kundenavn                -
7.xx A**                                        '
     A                                      COLOR(WHT)
7.xx A                                  9 69'Sum poster'
     A                                      COLOR(WHT)
7.xx A            B2KUND    R        B  7  4REFFLD(RKKUND)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  7 11REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20011123  000309  KOB         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 31'F9=Gjenta søk'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051006  132123  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            D1KUND    R        O  3  1REFFLD(RKKUND)
     A                                      OVRDTA
     A            D1NAVN    R        O  3  8REFFLD(RKNAVN)
     A                                      OVRDTA
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  1'Alle utvalgte poster på denne kund-
     A                                      en'
     A                                  6  1'blir slettet fra arbeidsregisteret'
     A                                  2  1'Kunde:'
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
