     A*%%TS  SD  20111220  134945  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
     A*
8.01 A*K0000  200723 bsa  Nytt program
8.02 A*K0000  150224 bsa  Fjernet til ordretyper
8.03 A*K0000  110424 bsa  Mulighet for "gammel" versjon.
8.04 A*K0000  230524 bsa  Miks av ordretyper
     A*
     A*
     A*
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3 -
     A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R D9BLD
     A*%%TS  SD  20060818  145631  AMD540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(4 20 17 56)
     A                                      CA03
     A                                      CF04
8.03 A                                      CF07
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utplukk til fakturering'
     A                                      COLOR(WHT)
     A                                  4  2'Fra ordretype +'
     A            D9OTY1         2A  B  4 18
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSG('Kun ordretyper av type -
     A                                      pakkseddel kan velges' 32)
     A  33                                  ERRMSG('Kan ikke mikse interne -
     A                                      og eksterne ordretyper' 33)
     A  34                                  ERRMSG('Du må legge inn minst e-
     A                                      n ordretype' 34)
8.04 A  35                                  ERRMSG('Du kan ikke mikse med l-
8.04 A                                      ager/stat oppdatering' 35)
     A                                      COLOR(YLW)
     A                                  4 21'+'
     A            D9OTY2         2A  B  4 23
     A                                      OVRDTA
     A                                      COLOR(YLW)
8.03 A                                  4 26'+'
8.03 A            D9OTY3         2A  B  4 28
8.03 A                                      OVRDTA
8.03 A                                      COLOR(YLW)
8.03 A                                  4 31'+'
8.03 A            D9OTY4         2A  B  4 33
8.03 A                                      OVRDTA
8.03 A                                      COLOR(YLW)
8.03 A                                  4 36'+'
8.03 A            D9OTY5         2A  B  4 38
8.03 A                                      OVRDTA
8.03 A                                      COLOR(YLW)
8.03 A                                  4 41'+'
8.03 A            D9OTY6         2A  B  4 43
8.03 A                                      OVRDTA
8.03 A                                      COLOR(YLW)
     A*                                 4 26'+'
     A*           D9OTY3         2A  B  4 28
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
     A*                                 4 31'+'
     A*           D9OTY4         2A  B  4 33
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
     A*                                 4 36'+'
     A*           D9OTY5         2A  B  4 38
     A*                                     OVRDTA
     A*                                     COLOR(YLW)
8.02 A*                                 5  2'Til ordretype +'
     A            D9TOT1         2A  B  5 18
8.02 A                                      DSPATR(ND)
8.03 A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  35                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSG('Kun ordretyper av type -
     A                                      faktura/kreditnota kan velges' 36)
     A  37                                  ERRMSG('Ordretypen er ikke blan-
     A                                      dt neste gyldige' 37)
8.02 A*                                     COLOR(YLW)
8.02 A*                                 5 21'+'
     A            D9TOT2         2A  B  5 23
8.02 A                                      DSPATR(ND)
8.03 A                                      DSPATR(PR)
     A                                      OVRDTA
8.02 A*                                     COLOR(YLW)
8.02 A*                                 5 26'Hvis blank brukes '
8.02 A*                                 6 26'neste ordretype'
     A                                  8  2'Seleksjon . . .'
     A            D9SVAR    R        B  8 18REFFLD(FÅSVAR)
     A                                      OVRDTA
     A                                      VALUES(0 1 2 3 4 5 6)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  8 24'1=fra/til'
     A                                  9 24'2=sekvens'
     A                                 10 24'3=tilfeldig'
     A                                 11 24'4=andre valg'
     A                                 13  2'Droppe faste. .'
     A                                 14  2'tekstlinjer . .'
     A            D9PTXT    R        B 14 18REFFLD(FKPTXT)
     A                                      OVRDTA
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 14 24'1=Ja,0=Nei'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
8.03 A                                 16 30'F7=Separate OT'
8.03 A                                      COLOR(BLU)
     A*
     A          R D91BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(10 36 12 40)
     A                                  2  2'Ordrenummer      fra      til'
     A                                      DSPATR(UL)
     A            D9FONR    R        B  3 16REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(FM00020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A            D9TONR    R        B  3 25REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  34                                  ERRMSGID(FM00030 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R D92BLD
     A*%%TS  SD  20111220  134945  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(3 33 19 43)
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  2  2'Seleksjon vha sekvens'
     A                                      COLOR(WHT)
     A                                  4  2'Sekvenser. . . . .'
     A            D92SEK1        2Y 0B  4 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(FM00040 *LIBL/QMFOFAK 31)
     A            D92SEK2        2Y 0B  4 25EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D92SEK3        2Y 0B  4 29EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D92SEK4        2Y 0B  4 33EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5  2'Ta med sekv. 00 ?'
     A            D92SEK0        1Y 0B  5 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 33'1=Ja,0=Nei'
     A                                  7  2'Valg på avdeling .'
     A            D92AVDE        4Y 0B  7 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8  2'Ta med avd. 0 ?. .'
     A            D92AVD0        1Y 0B  8 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  8 33'1=Ja,0=Nei'
     A                                 10  2'T.o.m. ordredato .'
     A            D92BDAT        6Y 0B 10 21COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 32)
     A                                      EDTWRD('  .  .  ')
     A                                 11  2'T.o.m. lev.dato  .'
     A            D92LDAT        6Y 0B 11 21COLOR(YLW)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 33)
     A                                      EDTWRD('  .  .  ')
     A                                 12  2'Beløpsgrense . . .'
     A            D92BELG        8Y 0B 12 21OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 13  2'Kode . . . . . . .'
     A            D92BELK        1A  B 13 21OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 13 33'U=Under'
     A                                 14 33'O=Over'
     A                                 15  2'Ordrenummer fra. .'
     A            D92FONR   R        B 15 21REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  34                                  ERRMSGID(FM00030 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                 16  2'Ordrenummer til. .'
     A            D92TONR   R        B 16 21REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R D93BLD
     A*%%TS  SD  19970603  135911  AMD         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(10 36 12 40)
     A                                  2  2'Tilfeldige ordrenummer'
     A                                      COLOR(WHT)
     A            D93UONR   R        O  4  7REFFLD(FONUMM)
     A                                      OVRDTA
     A            D93USUF   R        O  4 16REFFLD(FOSUFF)
     A                                      OVRDTA
     A            D93IONR   R        B  6  7REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0040 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A            D93ISUF   R        B  6 16REFFLD(FOSUFF)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      CHECK(ER)
     A                                      COLOR(YLW)
     A                                  8  2'ENTER gir oppdatering og mulighet'
     A                                  9  2'for inntasting av flere nummer'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D94BLD
     A*%%TS  SD  20020503  125018  AMD         REL-V5R1M0  5722-WDS
     A                                      WINDOW(3 33 13 43)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Seleksjon andre valg '
     A                                      COLOR(WHT)
     A                                  4  2'Valg på avdeling .'
     A            D94AVDE        4Y 0B  4 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5  2'Ta med avd. 0 ?. .'
     A            D94AVD0        1Y 0B  5 21EDTCDE(Z)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  5 33'1=Ja,0=Nei'
     A                                  7  2'Kundenummer. . . .'
     A            D94KUND        6Y 0B  7 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                  8  2'Kundeprosjekt. . .'
     A            D94KPRO        6Y 0B  8 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('      ')
     A                                 10  2'Infokode . . . . .'
     A            D94ITYP        2A  B 10 21COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 12 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
