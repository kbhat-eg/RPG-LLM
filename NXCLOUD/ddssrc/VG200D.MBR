     A*%%TS  SD  20181026  092630  NHO630      REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : VG200D
6.30 A* 241018 NHO Nytt program, vedlikehold av gj.kostpris pr.vare
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.  Dato   Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       241018 NHO  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/VGKPST)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20181026  092630  NHO630      REL-V7R3M0  5770-WDS
     A            B1VALG         1Y 0B  8  2EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5 7)
     A            B1VARE    R   -7   O  8  4REFFLD(VGVARE)
     A            B1VTEK        35A     8 13
     A            B1GDAT         6Y 0O  8 64EDTWRD('  .  .  ')
     A            B1ENHE         3A  O  8 60
     A            B1COST    R        O  8 49REFFLD(VGCOST)
     A                                      EDTWRD('     0 ,  ')
     A            B1TYPE         1A  O  8 74
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20181025  092749  NHO630      REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA07
     A                                      CA08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Gj.kostpris pr.vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  4  2'Lager +'
     A  35                                  DSPATR(ND)
     A            B2LAGE    R        B  4 12REFFLD(VGLAGE)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A  35                                  DSPATR(ND)
     A  35                                  DSPATR(PR)
     A            B2LATX        30   O  4 16
     A                                      OVRDTA
     A  35                                  DSPATR(ND)
     A                                  5  2'Varesøk +'
     A            B2VARE    R   -7   B  5 12REFFLD(VGVARE)
     A                                      OVRDTA
     A  22                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A                                  6  2'2=Ny kostpris på vare'
     A                                      COLOR(BLU)
     A                                  6 24'5=Vis'
     A                                      COLOR(BLU)
     A                                  6 30'7=Vis historikk'
     A                                      COLOR(BLU)
     A                                  7  4'Varenr.'
     A                                      COLOR(WHT)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7 13'Varetekst'
     A                                      COLOR(WHT)
     A                                  7 64'Gj.fra'
     A                                      COLOR(WHT)
     A                                  7 60'Enh'
     A                                      COLOR(WHT)
     A                                  7 49'Kostpris'
     A                                      COLOR(WHT)
     A                                  7 74'Ty'
     A                                      COLOR(WHT)
     A          R B2CMD
     A                                 23  2'F3=Gå tilbake  F4=Søk  F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 35'F6=Opprett ny vare'
     A                                      COLOR(BLU)
     A                                 23 55'F7=Kostprisfaktorer'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020307  000913  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Varenummer finnes ikke'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20020307  000913  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Dato fra må angis'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20020307  000913  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kostpris må angis'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20181025  115349  NHO630      REL-V7R3M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av gj.kostpris pr.vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A  30                              5  2'Varenummer  '
     A N30                              5  2'Varenummer +'
     A            C2VARE         8Y 0B  5 21
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2VTEK        35A  O  5 30
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Gjeldende fra dato'
     A            C2GDAT         6Y 0B  7 21COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8  2'Kostpris '
     A            C2COST    R        B  8 21REFFLD(VGCOST)
     A                                      COLOR(YLW)
     A                                      EDTWRD('       ,  ')
     A                                      CHECK(RB)
     A  31                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 33'Lagerenhet '
     A            C2ENHE    R        O  8 45REFFLD(VGENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Type beregning A/M '
     A            C2TYPE    R        O 10 23REFFLD(VGTYPE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Bestilling'
     A            C2BEST    R        O 12 13REFFLD(VGBEST)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 23'Suffix'
     A            C2SUFF    R        O 12 30REFFLD(VGSUFF)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 35'Linje'
     A            C2LINE    R        O 12 41REFFLD(VGLINE)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Gammel gjennomsnittspris'
     A            C2GMKP    R        O 14 29REFFLD(VGGMKP)
     A                                      EDTWRD('       ,  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14 42'Pris i kalkulasjon'
     A            C2INKP    R        O 14 66REFFLD(VGINKP)
     A                                      EDTWRD('       ,  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PKOD         3A  O 14 77COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Beholdning før'
     A            C2BEHL    R        O 15 27REFFLD(VGBEHL)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 42'Antall i kalkulasjon'
     A            C2ANTA    R        O 15 64REFFLD(VGANTA)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Tid beregnet'
     A            C2TTIM         4Y 0O 18 31EDTWRD('  :  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Sist dato beregnet'
     A            C2BDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av'
     A            C2BUSR    R        O 20 31REFFLD(VGBUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
