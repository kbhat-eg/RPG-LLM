     A*%%TS  SD  20120307  093455  SST620      REL-V7R1M0  5770-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.20 A* R6.20 121110 NHO  Fjernet akt., konto og spes.  Lagt inn under-
     A*                   prosjekt
6.11 A* R6.20 110210 BSA  Lagt inn felt for kort.
6.32 A* R6.30 020616 BOF  Lagt inn test på depositum
7.xx A* R7.00 210116 BSA  Endringer relatert til ny GUI
7.04 A* R7.00 211118 BSA  Lagt på spørring på aktivitet
     A*
7.fb A* R7.10 040419 BOF  Hvis depostiumsløsnig, så ikke ikke input.
8.01 A*       021221 BOF  Endret editc for datoer
     A*
     A* ----- ------ ---- -------------------------------------------------
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A*
     A          R C2BLD
     A*%%TS  SD  20080521  103319  AMD560      REL-V5R4M0  5722-WDS
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innlegging av faste opplysninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Avgiftsfri ? (FK). . . . .'
7.xx A*           C2MKOD    R        B  4 29REFFLD(FOMKOD)
7.xx A            C2MKOD    R        B  4 29REFFLD(FÅSVA2)
7.xx A                                      OVRDTA
7.xx A                                      VALUES(0 1 2)
7.xx A                                      COLOR(YLW)
7.xx A                                  4 32'0=Nei,1=Ja,2=Utland'
7.xx A                                  4 53'Depositum Krav:'
     A            C2DEPO    R   -2   B  4 70REFFLD(FODEPO)
     A                                      OVRDTA
     A                                      CHECK(RB)
6.32 A  42                                  ERRMSGID(FMO0750 *LIBL/QMFOFAK 42)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
7.fb A  50                                  DSPATR(PR)
7.xx A                                  5  2'Netto-faktura ? (FK) . . .'
7.xx A*            C2NETO    R        B  5 29REFFLD(FONETO)
7.xx A            C2NETO    R        B  5 29REFFLD(FÅSVA2)
7.xx A                                      OVRDTA
7.xx A                                      VALUES(0 1 2)
7.xx A                                      COLOR(YLW)
7.xx A                                  5 32'0=Nei,1=Ja,2=Utland'
     A                                  5 53'Innbetalt . . :'
     A            C2DEPB    R   -2   O  5 70REFFLD(FODEPB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                  6 53'Fratrukket. . :'
     A            C2DEPT    R   -2   O  6 70REFFLD(FODEPT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
7.xx A                                  8  2'Avdeling (FK). . . . . . . . +'
     A            C2AVDE    R        B  8 33REFFLD(FOAVDE)
     A  32                                  ERRMSGID(FMA0140 *LIBL/QMFOFAK 32)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
6.22 A            C2AVTX        30A  O  8 42OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  9  2'Selgernummer (VK). . . . . . +'
     A            C2SELG    R        B  9 33REFFLD(FOSELG)
     A  33                                  ERRMSGID(FMA0150 *LIBL/QMFOFAK 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2SETX        20A  O  9 42OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 10  2'Lagernummer (VK) . . . . . . +'
     A            C2LAGE    R        B 10 33REFFLD(FOLAGE)
     A  34                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 34)
     A  39                                  ERRMSGID(FMA0300 *LIBL/QMFOFAK 39)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LATX        20A  O 10 42OVRDTA
     A                                      COLOR(YLW)
     A                                 12  2'Betalingsbetingelser (FK). . .'
     A            C2BETB    R        B 12 33REFFLD(FOBETB)
     A  35                                  ERRMSGID(FMA0170 *LIBL/QMFOFAK 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2BTX1        20A  O 12 42OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'Fakturatype (FK) . . . . . . .'
     A            C2FATY    R        B 13 33REFFLD(FOFATY)
     A  41                                  ERRMSGID(FMA0280 *LIBL/QMFOFAK 41)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2BTX2        20A  O 13 42OVRDTA
     A                                      COLOR(YLW)
     A                                 14  2'Ordrerabatt (FK) . . . . . . .'
     A            C2RABP    R        B 14 33REFFLD(FFRFR/FORABP *LIBL/FFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                 15  2'Valuta (FK). . . . . . . . . .'
     A            C2VALK    R        B 15 33REFFLD(FOVALK)
     A  36                                  ERRMSGID(FMA0180 *LIBL/QMFOFAK 36)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2VATX        20A  O 15 42OVRDTA
     A                                      COLOR(YLW)
     A                                 17  2'Speditør (VK). . . . . . . . .'
     A            C2SPED    R        B 17 33REFFLD(FOSPED)
     A  37                                  ERRMSGID(FMA0190 *LIBL/QMFOFAK 37)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2SPTX        20A  O 17 42OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 18  2'Forsendelsesmåte (VK). . . . +'
     A            C2LMAT    R        B 18 33REFFLD(FOLMAT)
     A  38                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 38)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LMTX    R        B 18 42REFFLD(FOLMTX)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                 19  2'Leveringsbetingelser (VK). . .'
     A            C2LBET    R        B 19 33REFFLD(FOLBET)
     A  40                                  ERRMSGID(FMA0210 *LIBL/QMFOFAK 40)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C2LTX1        25A  B 19 42CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LTX2        25A  B 20 42CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C2LTX3        25A  B 21 42CHECK(LC)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 22  2'Kjørerute  (VK). . . . . . . +'
     A            C2KJOR    R        B 22 33REFFLD(FLKJOR)
     A  48                                  ERRMSGID(FRA0950 *LIBL/QMFOFAK 48)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KJTX    R  -10   O 22 42REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22 64'Rekkefølge'
     A            C2KJNR    R        B 22 75REFFLD(FLKJNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20120307  093455  SST620      REL-V7R1M0  5770-WDS
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Innlegging av faste opplysninger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Fakturadato . . . . . . . . . . .'
     A            C3FKDA         6Y 0B  5 36COLOR(YLW)
     A  31                                  ERRMSGID(FM01130 *LIBL/QMFOFAK 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
8.01 A                                      EDTWRD('  .  .  ')
8.01 A*                                      EDTWRD('      ')
     A                                  6  2'Forfallsdato. . . . . . . . . . .'
     A            C3FFDA         6Y 0B  6 36COLOR(YLW)
     A  32                                  ERRMSGID(FM01140 *LIBL/QMFOFAK 32)
     A                                      OVRDTA
     A                                      CHECK(RB)
8.01 A                                      EDTWRD('  .  .  ')
8.01 A*                                      EDTWRD('      ')
     A                                  7  2'Kredittreferanse. . . . . . . . .'
     A            C3KREF    R        B  7 36REFFLD(FOKREF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  9  2'Prosjektnummer. . . . . . . . . +'
     A            C3PROJ    R        B  9 36REFFLD(FOPROJ)
     A  34                                  ERRMSGID(FRH0220 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C3PRTX        30A  O  9 45OVRDTA
     A                                      COLOR(YLW)
6.20 A**                               10  2'Aktivitet . . . . . . . . . . . .'
6.20 A**          C3AKTI    R        B 10 36REFFLD(FOAKTI)
6.20 A**35                                  ERRMSGID(FMA0220 *LIBL/QMFOFAK 35)
6.20 A**                                    OVRDTA
6.20 A**                                    COLOR(YLW)
6.20 A**          C3AKTX        30A  O 10 45COLOR(YLW)
7.xx A                                 10  2'Underprosjekt . . . . . . . . . +'
     A            C3UPRO    R        B 10 36REFFLD(FOUPRO)
     A  35                                  ERRMSGID(RS88001 *LIBL/QMFOFAK 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            C3UPTX        30A  O 10 45OVRDTA
     A                                      COLOR(YLW)
7.04 A                                 11  2'Aktivitet . . . . . . . . . . . +'
7.04 A            C3AKTI    R        B 11 36REFFLD(FOUPRO)
7.04 A  35                                  ERRMSGID(RS88001 *LIBL/QMFOFAK 35)
7.04 A                                      OVRDTA
7.04 A                                      COLOR(YLW)
6.20 A**                               11  2'Konto . . . . . . . . . . . . . .'
6.20 A**          C3KONT         5Y 0B 11 36COLOR(YLW)
6.20 A**36                                  ERRMSGID(FRH0020 *LIBL/QMFOFAK 36)
6.20 A**                                    OVRDTA
6.20 A**                                    EDTCDE(Z)
6.20 A**                                    CHECK(RB)
6.20 A**          C3KOTX        30A  O 11 45OVRDTA
6.20 A**                                    COLOR(YLW)
6.20 A**                               12  2'Spesifikasjon . . . . . . . . . .'
6.20 A**          C3SPES    R        B 12 36REFFLD(FOSPES)
6.20 A**                                    OVRDTA
6.20 A**                                    COLOR(YLW)
6.20 A**                                    CHECK(RB)
7.xx A                                 13  2'Korttype. . . . . . . . . . . . +'
     A            C3PSUF    R        B 13 36REFFLD(FOPSUF)
     A  37                                  ERRMSG('Valgt kort må være kreditko-
     A                                      rt')
     A  38                                  ERRMSG('Kreditkort ikke registrert -
     A                                      på kunde')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C3KTYP        20A  O 13 45OVRDTA
     A                                      COLOR(YLW)
     A                                 14  2'Ikke utskrift av ordrebekr (VK) .'
     A            C3KSOB    R        B 14 36REFFLD(FOKSOB)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 14 45'1=Ja,0=Nei'
     A                                 16  2'Ikke automatisk fakturagebyr (FK)'
     A            C3KGFA    R        B 16 36REFFLD(FOKGFA)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 16 45'1=Ja,0=Nei'
     A                                 17  2'Ikke automatisk fraktgebyr (VK) .'
     A            C3KGFR    R        B 17 36REFFLD(FOKGFR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 17 45'1=Ja,0=Nei'
     A                                 18  2'Ikke automatisk gebyr (VK). . . .'
     A            C3KGEK    R        B 18 36REFFLD(FOKGEK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 18 45'1=Ja,0=Nei'
     A                                 20  2'Midlertidig leverandør. . . . . .'
     A            C3LDOR    R        B 20 36REFFLD(FOLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
