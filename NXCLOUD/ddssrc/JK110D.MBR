     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021021  130850  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 4 5 8 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVADM)
     A            B1QUNO    R        O  8  4REFFLD(JVQUNO)
     A            B1TEK1        24A  O  8 13
     A            B1QLDO    R        O  8 38REFFLD(JVQLDO)
     A            B1LNAV        24A  O  8 45
     A            B1HLEV         1S 0O  8 70
     A            B1QOFA         2S 2O  8 72
     A            B1PENH    R        O  8 75REFFLD(JVPENH)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A*                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av undervare    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Logistikkvare:'
     A                                      COLOR(BLU)
     A            B2QVNR    R        O  4 18REFFLD(JVQVNR)
     A                                      COLOR(YLW)
     A            B2LOGT    R        O  4 34REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A            B2NOBB    R        B  5  3REFFLD(JVQUNO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 34'Varegruppe:'
     A                                      COLOR(BLU)
     A            B2OGRP    R        O  5 46REFFLD(JVOGRP)
     A                                      COLOR(YLW)
     A            B2HGRP    R        O  5 49REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A            B2UGRP    R        O  5 52REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                  6  2'2=Endre  4=Slett  5=Vis  8=-
     A                                      Varekort'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'NOBBnr'
     A                                      COLOR(WHT)
     A                                  7 13'Varetekst'
     A                                      COLOR(WHT)
     A                                  7 38'Lev'
     A                                      COLOR(WHT)
     A                                  7 45'Leverandør'
     A                                      COLOR(WHT)
     A                                  7 70'H'
     A                                      COLOR(WHT)
     A                                  7 72'OF'
     A                                      COLOR(WHT)
     A                                  7 75'Penh'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20021001  150320  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av undervare    '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Logistikkvare  .'
     A            C2QVNR    R        O  5 20REFFLD(JVQVNR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LOGT    R        O  5 36REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisenhet  . . .'
     A            C2PENH    R        O  6 20REFFLD(JVPENH)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 36'Varegruppe:'
     A            C2OGRP    R        O  6 48REFFLD(JVOGRP)
     A                                      COLOR(YLW)
     A            C2HGRP    R        O  6 51REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A            C2UGRP    R        O  6 54REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  9  2'NOBBnr . . . . '
     A            C2QUNO    R        B  9 20REFFLD(JVQUNO)
     A                                      COLOR(YLW)
     A  40                                  DSPATR(PR)
     A                                      OVRDTA
     A  31                                  ERRMSGID(JMM1010 *LIBL/QMFVADM 31)
     A  35                                  ERRMSGID(JMV0130 *LIBL/QMFVADM 35)
     A  36                                  ERRMSGID(JMV0030 *LIBL/QMFVADM 36)
     A  37                                  ERRMSGID(JML0030 *LIBL/QMFVADM 37)
     A  38                                  ERRMSGID(JMV0160 *LIBL/QMFVADM 38)
     A  42                                  ERRMSGID(JMV0410 *LIBL/QMFVADM 42)
     A            C2TEK1    R        O  9 29REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Leverandør'
     A            C2QLDO    R        B 10 20REFFLD(JVQLDO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2QLNA    R   -5   O 10 29REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 60'Flere prisgivere'
     A                                      COLOR(RED)
     A  43                                  DSPATR(ND)
     A                                      OVRATR
     A                                 11  2'Prisenhet  . . .'
     A            C2UPEH    R        O 11 20REFFLD(JVPENH)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 36'Varegruppe:'
     A            C2UOGR    R        O 11 48REFFLD(JVOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UHGR    R        O 11 51REFFLD(JVHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2UUGR    R        O 11 54REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Omregningsfaktor'
     A            C2QOFA    R        B 12 20REFFLD(JVQOFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A  39                                  ERRMSGID(JMW0040 *LIBL/QMFVADM 39)
     A  41                                  ERRMSGID(JMW0050 *LIBL/QMFVADM 41)
     A  32                             14  2'Varen er hovedvare og kan ikke end-
     A                                      res her.'
     A                                      COLOR(BLU)
     A  32                             15  2'Gå til vedlikehold av +
     A                                       logistikkvare for å endre +
     A                                       undervaren.'
     A                                      COLOR(BLU)
     A  32                             16  2'Hovedvare rediger på logistikkvare'
     A                                      COLOR(BLU)
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(JVQEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030929  132220  MHA         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 72)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette undervare    '
     A                                      DSPATR(RI)
     A                                  5  2'Vare       .'
     A            D1QUNO    R        O  5 21REFFLD(JVQUNO)
     A            D1TEK1    R        O  5 30REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                  6  2'Leverandør .'
     A            D1QLDO    R        O  6 21REFFLD(JVQLDO)
     A            D1LNAV    R        O  6 30REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A  32                              8  2'Varen er hovedvare og kan ikke sle-
     A                                      ttes her.'
     A                                      COLOR(BLU)
     A  32                              9  2'Gå til vedlikehold av +
     A                                       logistikkvare for å endre +
     A                                       undervaren.'
     A                                      COLOR(BLU)
     A  32                             10  2'Hovedvare rediger på logistikkvare'
     A                                      COLOR(BLU)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
