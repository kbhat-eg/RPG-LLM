     A*%%TS  SD  20120308  103155  ASP_SGR     REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL100D
     A* Beskrivelse . . : Lager, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.11 A* 6.10  201109 BSA  Viser feil tekst ved sletting av lagerposter
6.12 A* 6.10  050210 BSA  Filtrerer på lager
6.13 A* 6.10  041010 BOF  Endring av vtype fra L->S, gi melding
6.20 A* 6.20  221110 BOF  Utvidet med leverandør (sentrallager-løsning)
6.24 A* 6.20  260511 BSA  Utvidet muligheter ved filtrering.
6.25 A* 6.20  270511 BSA  Overgang til beholdninger.
6.26 A* 6.20  270511 BSA  Bekrefte endring av varetype
6.27 A* 6.20  030611 BOF  Utvidet med varetype i subfile
6.28 A* 6.20  110811 BSA  Utvidet filtrering med lokasjon og ABC kode(telling)
6.29 A* 6.20  101111 BOF  Viser sist endret dato og bruker
6.2A A* 6.20  220112 BOF  Tatt bort mulighet for sletting
6.2B A* 6.20  093012 BOF  Viser hovedleveandør
6.2C A* 6.20  241013 nho  Får ikke opprette varetype dersom varetype
     A*                   er D eller T i vvarpf, feilmelding
6.30 A* 6.30  060515 BOF  Får opprette T /D hvis vare er T/D
6.31 A* 6.30  050615 BSA  Endret ledetekst på skjermbilde
6.32 A* 6.30  230615 BOF  Hvis utfyll av prio.lev, så sjekk pris/distribusjon
6.33 A* 6.30  080715 ASK  Lagt ut lager beskrivelse i C2 bilde
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 6.30  130217 NHO  Lagt inn div endringer som ikke var merket
7.01 A*                   grunnet oppgradering 18.11.17
7.04 A* 6.30  301018 BSA  Endrer ledeteksten på bestillingspunkt og mengde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20110603  121520  BOF620      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 5 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   O  8  4REFFLD(VLVARE)
     A            B1LAGE    R        O  8 13REFFLD(VLLAGE)
     A            B1VTYP    R        O  8 16REFFLD(VLVTYP)
     A            B1TEK1    R   -5   O  8 18REFFLD(VVTEK1)
     A            B1MINI    R        O  8 49REFFLD(VLMINI)
     A            B1BMEN    R        O  8 63REFFLD(VLBMEN)
     A            B1LTID    R        O  8 77REFFLD(VLLTID)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20120222  071459  BOF620      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lager'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  4'Vareummer +'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 27'Lag +'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6 29'9=Beholdning'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 12'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 16'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 18'Beskrivelse          '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 51'Min.lager'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 63'Best.mengde'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 76'L.tid'
     A                                      COLOR(WHT)
7.xx A            B2VARE    R   -7   B  5 17REFFLD(VLVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LAGE         2A  B  5 34COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020923  081514  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Filter  F3=Gå tilbake   F4=Spør-
     A                                      ring  F5=Forny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020912  152030  ASK         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B  5 31REFFLD(VLVARE)
     A  31                                  ERRMSGID(LM00180 *LIBL/QMFLAGR 31)
6.2c A  45                                  ERRMSGID(VMV0530 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1TEK1    R        O  5 40REFFLD(VVTEK1)
     A                                      OVRDTA
7.xx A                                  6  2'Lager  . . . . . . . . . . +'
     A            C1LAGE    R        B  6 31REFFLD(VLLAGE)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LTEK        30   O  6 40
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020912  150837  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Lagerpost finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20120308  103155  ASP_SGR     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lager'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL100C2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . .'
     A            C1VARE    R   -7   O  5 25REFFLD(VLVARE)
     A                                      OVRDTA
     A                                  6  2'Lager  . . . . . . . .'
     A            C1LAGE    R        O  6 25REFFLD(VLLAGE)
     A                                      OVRDTA
6.33 A            C2LTEK        20   O  6 28
6.33 A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . .'
     A            C2TEK1    R        O  7 25REFFLD(VVTEK1)
     A                                      OVRDTA
7.xx A                                  9  2'Lagerplass . . . . . . . . . +'
7.xx A            C2PLAS    R        B  9 34REFFLD(VLPLAS)
     A  30                                  DSPATR(PR)
     A  32                                  ERRMSGID(MMD0010 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 49'Alt1+'
7.xx A            C2PLA1    R    8   B  9 55REFFLD(VLPLA1)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(MMD0010 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 64'Alt2+'
7.xx A            C2PLA2    R    8   B  9 70REFFLD(VLPLA2)
     A  30                                  DSPATR(PR)
     A  34                                  ERRMSGID(MMD0010 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            C2ENHL    R        B 10 49REFFLD(VLENHL)
     A                                      DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 10  2'Minimumslager. . . . . . . . .'
7.xx A            C2MINI    R        B 10 34REFFLD(VLMINI)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                 11  2'Maksimumslager . . . . . . . .'
7.xx A            C2MAKS    R        B 11 34REFFLD(VLMAKS)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Minimumsbestilling (anbrekk) .'
7.xx A            C2BMIN    R        B 12 34REFFLD(VLBMIN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.04 A**                               13  2'Bestillingspunkt . . . . . . .'
7.04 A            C2BPTX        30A    13  2
7.xx A            C2BPUN    R        B 13 34REFFLD(VLBPUN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.04 A**                               14  2'Bestillingsmengde  . . . . . .'
7.04 A            C2BMTX        30A    14  2
7.xx A            C2BMEN    R        B 14 34REFFLD(VLBMEN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 15  2'Ledetid. . . . . . . . . . . .'
7.xx A            C2LTID    R        B 15 34REFFLD(VLLTID)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16  2'Sperre Auto. Oppdatering dato'
7.xx A            C2STDA         6Y 0B 16 34
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSGID(LMD0190 *LIBL/QMFLAGR 31)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16 44'Til Dato'
7.xx A                                 17  2'Lager abc-kode . . . . . . . .'
7.xx A            C2ABCK         2A  B 17 34COLOR(YLW)
     A                                      VALUES('  ' 'AX' 'AY' 'AZ' 'BX' 'BY-
     A                                      ' 'BZ' 'CX' 'CY' 'CZ')
     A                                      OVRDTA
7.xx A                                 17 37'(abc-kode)'
7.xx A                                 18  2'Låst abc-kode  . . . . . . . .'
7.xx A            C2LABC    R        B 18 34REFFLD(VLLABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 18 37'1=Ja, 0=Nei'
7.xx A                                 21  2'Varetype . . . . . . . . . . .'
7.xx A            C2VTYP    R        B 21 34REFFLD(VLVTYP)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'L' 'S')
     A                                      CHKMSGID(VMA7020 *LIBL/QMFOFAK)
     A                                      OVRDTA
7.xx A                                 21 36'(kode)'
7.xx A                                 19  2'Syklisk telling abc-kode . . .'
7.xx A            C2ABCT         2A  B 19 34COLOR(YLW)
     A                                      VALUES('  ' 'AX' 'AY' 'AZ' 'BX' 'BY-
     A                                      ' 'BZ' 'CX' 'CY' 'CZ')
     A                                      OVRDTA
7.xx A                                 19 37'(abc-kode)'
7.xx A                                 20  2'Prioritert Leverandør  . . . +'
7.xx A                                 22  2'Utgått dato  . . . . . . . . .'
     A            C2SUDA         6Y 0B 22 36COLOR(YLW)
     A  36                                  ERRMSGID(LMD0190 *LIBL/QMFLAGR 36)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A            C2LNAV        30A  O 20 41OVRDTA
7.xx A                                 21 43'Hov.lev.'
7.xx A            C2HLEV    R        O 21 52REFFLD(FÅLDOR)
     A                                      OVRDTA
7.xx A            C2HLNA        20A  O 21 59OVRDTA
     A                                 22 56'Sist endret '
     A            C2EDAT         6Y 0O 22 69COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 56'Endret av   '
     A            C2EUSR    R        O 23 69REFFLD(VLEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
7.xx A            C2SLDO    R        B 20 34REFFLD(FÅLDOR)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(LMH0050 *LIBL/QMFLAGR 35)
6.32 A  37                                  ERRMSGID(LMO0700 *LIBL/QMFLAGR 37)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A          R H1WIN
     A*%%TS  SD  20040910  133006  ASK530      REL-V5R2M0  5722-WDS
6.28 A                                      WINDOW(*DFT 12 55)
     A                                      CA03
6.28 A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Velg leverandør+'
     A            H1LDOR         6Y 0B  4 19CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            H1LNAV        30   O  4 26
     A                                      OVRDTA
7.xx A                                  5  2'Velg Lager. . .+'
6.12 A            H1LAGE         2Y 0B  5 19COLOR(YLW)
6.12 A                                      RANGE(0 99)
6.11 A                                      EDTCDE(Z)
6.28 A                                      OVRDTA
6.12 A            H1LTEK        30   O  5 26
6.12 A                                      OVRDTA
7.xx A                                  6  2'Velg varetype .+'
6.24 A            H1VTYP         1A  B  6 19COLOR(YLW)
6.24 A                                      OVRDTA
6.24 A                                  7  2'Velg ABC kode .:'
6.24 A                                  7 22'Prognose'
6.24 A            H1KABC         2A  B  7 19COLOR(YLW)
6.24 A                                      OVRDTA
6.24 A                                  8  2'Velg oppr.dato.:'
6.24 A            H1ODAT         6Y 0B  8 19COLOR(YLW)
6.24 A  36                                  ERRMSGID(LMD0190 *LIBL/QMFLAGR 36)
6.24 A                                      CHECK(RB)
6.24 A                                      EDTWRD('  .  .  ')
6.24 A                                      OVRDTA
6.28 A                                  9  2'Velg ABC kode .:'
6.28 A                                  9 22'Telling'
6.28 A            H1TABC         2A  B  9 19COLOR(YLW)
6.28 A                                      OVRDTA
7.xx A                                 10  2'Lagerplass. . .+'
6.28 A                                 10 28'(NB lager)'
6.28 A            H1PLAS         8A  B 10 19COLOR(YLW)
6.28 A                                      OVRDTA
6.28 A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.28 A                                 11 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020916  122034  ASK         REL-V5R1M0  5722-WDS
7.01 A**                                    WINDOW(*DFT 15 56)
7.01 A                                      WINDOW(*DFT 16 56)
     A                                      CA03
6.28 A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Vare . +'
     A            K1VARE    R   -7   B  4 11REFFLD(VLVARE)
     A  31                                  ERRMSGID(LM00180 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LM00012 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1TEK1    R        O  4 20REFFLD(VVTEK1)
     A                                      OVRDTA
7.01 A                                  5  2'Lager. +'
     A            K1LAGE    R        B  5 11REFFLD(VLLAGE)
     A  32                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LTEK        30   O  5 20
     A                                      OVRDTA
7.01 A                                  6  2'Overføre lagerinfo :'
7.01 A            K1OVEF    R        B  6 23REFFLD(FÅJANE)
7.01 A                                      VALUES(0 1)
7.01 A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.01 A                                      COLOR(YLW)
7.01 A                                      OVRDTA
7.01 A                                  6 25'1=Ja'
7.01 A**                                7  2' Kopiere fra: '
7.01 A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
7.01 A                                 10  2'Vare . .'
7.01 A            K1FVAR    R   -7   O 10 11REFFLD(VLVARE)
     A                                      OVRDTA
7.01 A                                 11  2'Lager. .'
7.01 A            K1FLAG    R        O 11 11REFFLD(VLLAGE)
     A                                      OVRDTA
7.01 A                                 12  2'Tekst   '
7.01 A            K1FTEK    R        O 12 11REFFLD(VVTEK1)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
7.01 A**                                5  2'Lager. +'
7.01 A**                               10  2'Lager. .'
     A          R C3MSG
     A*%%TS  SD  20101111  092820  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 9 43)
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                      CA23
     A                                  2  2'Beholdningstall finnes på lageret.-
     A                                             '
     A                                  5  2'Endring av varetype fra L til'
     A            C3VTYP    R        O  5 32REFFLD(VVVTYP)
     A                                  5 34'må derfor'
     A                                  6  2'bekreftes med F23 !!'
     A                                  8  2'F3=Gå tilbake  F13=Vis lager  F23=-
     A                                      Bekreft'
     A                                      COLOR(BLU)
6.26 A          R C4MSG
6.26 A*%%TS  SD  20101111  092820  BOF610      REL-V5R4M0  5722-WDS
6.26 A                                      WINDOW(*DFT 8 43)
6.26 A                                      CA03
6.26 A                                      CA12
6.26 A                                      CA23
6.26 A                                  3  2'Endring av varetype fra S til L'
6.26 A                                  3 34'må '
6.26 A                                  4  2'bekreftes med F23 !!'
6.26 A                                  7  2'F3=Gå tilbake  F23=-
6.26 A                                      Bekreft'
6.26 A                                      COLOR(BLU)
