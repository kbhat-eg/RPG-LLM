     A*%%TS  SD  20020924  134519  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL640D
     A* Beskrivelse . . : Utskrift av hyllevarmer - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20020924  134519  HKO         REL-V5R1M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av hyllevarmere'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5 35'Fra'
7.xx A                                  5 51'Til'
7.xx A                                  6  2'Varenr . . . . . . . . . . +'
     A            A1FVAR         8D 0B  6 31CHECK(RZ)
     A  30                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 30)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A                                  6 47'+'
7.xx A            A1TVAR         8D 0B  6 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Lager. . . . . . . . . . . +'
     A            A1FLAG         2D 0B  7 31CHECK(RZ)
     A  31                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
7.xx A                                  7 47'+'
7.xx A            A1TLAG         2D 0B  7 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Varegruppe . . . . . . . . +'
     A            A1FOGR         2D 0B  8 31CHECK(RZ)
     A  32                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
7.xx A                                  8 34'+'
7.xx A            A1FHGR         2D 0B  8 36CHECK(RZ)
     A  33                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
7.xx A                                  8 39'+'
7.xx A            A1FUGR         3D 0B  8 41CHECK(RZ)
     A  34                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
7.xx A                                  8 47'+'
7.xx A            A1TOGR         2D 0B  8 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8 52'+'
7.xx A            A1THGR         2D 0B  8 54CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8 57'+'
7.xx A            A1TUGR         3D 0B  8 59CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                 10  2'Produktansvarlig . . . . . +'
     A            A1SAKB         4A  B 10 31
     A  38                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
7.xx A                                 11  2'Leverandørnummer . . . . . +'
     A            A1LVNR         6D 0B 11 31CHECK(RZ)
     A  35                                  ERRMSGID(LM06000 *LIBL/QMFLAGR 35)
     A                                      COLOR(YLW)
     A                                 14  2'Listevariant . . . . . . . .'
     A            A1VARI         1D 0B 14 31CHECK(RZ)
     A  36                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
     A                                 14 35'1=Pr. varenummer'
     A                                 16  2'Type analyse . . . . . . . .'
     A            A1TYPE         1D 0B 16 31CHECK(RZ)
     A  37                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 37)
     A                                      COLOR(YLW)
     A                                 16 35'1=Omsetning'
6.30 A                                 18  2'Tidshorisont . . . . . . . .'
6.30 A            A1HORI         1D 0B 18 31CHECK(RZ)
6.30 A  38                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 38)
6.30 A                                      COLOR(YLW)
6.30 A                                 18 35'0=Innev.år, 1=Siste 12 mnd, 2=Fjor-
6.30 A                                      år'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
