     A*%%TS  SD  20140603  142137  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO801D
     A* Beskrivelse . . : Varemottak, Hovedrutine
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240402 ASK  Nytt program
6.20 A* 6.20  300511 BSA  Lagt inn F tast for å vise loggkoder
6.21 A* 6.20  300511 BSA  Legger ut leverandørens ordrenummer.
6.22 A* 6.20  070312 BSA  Legger ut siste loggkode for bestillingen
6.NU A* 6.30  300514 bof  Utvidelse mtp. nummerutvidelse
7.xx A* 7.xx  090316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20140603  142137  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Varemottak - hovedrutine '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Ordretype . . . .'
     A            A1OTYP    R        O  5 20REFFLD(LOOTYP)
     A                                      COLOR(YLW)
     A                                  6  2'Bestillingsdato .'
     A            A1BDAT         6Y 0O  6 20EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  6 32'Siste loggkode'
     A            A1LOGK         1A  O  6 47COLOR(YLW)
     A            A1LBES        20A  O  6 49COLOR(YLW)
     A                                  7  2'Bestillingsnummer'
     A            A1NUMM    R        O  7 20REFFLD(LONUMM)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            A1SUFF    R        O  7 29REFFLD(LOSUFF)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  7 32'Kundeordre'
     A            A1ORNR    R        O  7 43REFFLD(LOORNR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            A1ORSU    R        O  7 52REFFLD(LOORSU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  7 55'Lev. onr:'
     A            A1SORN    R        O  7 65REFFLD(LOSORN)
     A                                      COLOR(YLW)
     A                                  9  2'Leveringsadresse:'
     A                                  9 51'Leverandør:'
     A            A1LDOR    R        O  9 63REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 10  2'Navn. . . . . . .'
     A            A1NAVN    R        O 10 20REFFLD(LONAVN)
     A                                      COLOR(YLW)
     A            A1LENA    R        O 10 51REFFLD(LOLENA)
     A                                      COLOR(YLW)
     A                                 11  2'Gate. . . . . . .'
     A            A1ADR1    R        O 11 20REFFLD(LOADR1)
     A                                      COLOR(YLW)
     A                                 12  2'Adresse . . . . .'
     A            A1ADR2    R        O 12 20REFFLD(LOADR2)
     A                                      COLOR(YLW)
     A                                 12 51'Vår ref'
     A**          A1VREF    R        O 12 61REFFLD(LOVREF)
     A**                                    COLOR(YLW)
     A                                 13  2'Postadresse . . .'
     A            A1STED    R        O 13 20REFFLD(LOSTED)
     A                                      COLOR(YLW)
     A            A1VREF    R        O 13 51REFFLD(LOVREF)
     A                                      COLOR(YLW)
     A                                 14 51'Deres ref'
     A**          A1DREF    R        O 13 61REFFLD(LODREF)
     A**                                    COLOR(YLW)
7.xx A                                 15  2'Lager . . . . . +'
     A            A1LAGE    R        B 15 20REFFLD(LOLAGE)
     A  31                                  ERRMSGID(LSO0230 *LIBL/QMFLAGR 31)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            A1LATX        20A  O 15 27COLOR(YLW)
     A            A1DREF    R        O 15 51REFFLD(LODREF)
     A                                      COLOR(YLW)
7.xx A                                 16  2'Innkjøper . . . +'
     A            A1INNK    R        B 16 20REFFLD(LOINNK)
     A  32                                  ERRMSGID(LSO0220 *LIBL/QMFLAGR 32)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            A1INTX        20A  O 16 27COLOR(YLW)
     A                                 17  2'Leveringsdato . .'
     A            A1LDAT         6Y 0B 17 20EDTCDE(Z)
     A  33                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                 19  2'Godta resting . .'
     A            A1REST         1Y 0B 19 20DSPATR(UL)
     A  34                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 34)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 19 27'1=Ja,0=Nei'
     A                                 20  2'Etikett kode. . .'
     A            A1ETIK         1Y 0B 20 20DSPATR(UL)
     A  35                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 35)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                 20 27'1=Ja,0=Nei'
     A                                 21  2'Pakksedd.etikett.'
     A            A1EPAK         1Y 0B 21 20DSPATR(UL)
     A  36                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 36)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 21 27'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 32'F10=Logg'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
