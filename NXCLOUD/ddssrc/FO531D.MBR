     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
     A*
7.xx A*     BSA  180216    Endringer relatert til ny GUI
     A*---------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19980930  154338  HKO         REL-V4R2M0  5769-PW1
     A            B1LINJ         3  0H
     A            B1KASS         3A  H
     A            B1AARR    R        H      REFFLD(SOAARR)
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 7)
     A*                                     VALUES(5 6 7)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1BONG    R        O  8  4REFFLD(SOBONG)
     A            B1BDAT         6Y 0O  8 17EDTCDE(Z)
     A            B1KUNA        25A  O  8 24
     A            B1KUND    R        O  8 51REFFLD(SOKUND)
     A            B1FSUM    R        O  8 58REFFLD(SOFSUM)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Bonger betalt med kreditkort'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Bong     '
7.xx A                                      COLOR(WHT)
7.xx A            B2BONG    R        B  5  4REFFLD(SOBONG)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  6  2'5=Betalingsinfo  7=Vis bong'
     A*                                 5  2'5=Betalingsinfo  6=Utskrift  7=Vis-
     A*                                      bong'
     A                                      COLOR(BLU)
     A                                  7  2'V Bong          Dato  Kunde       -
     A                                                     Kundnr   Fakturasum'
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19980108  144330  HKO         REL-V3R7M0  5716-PW1
     A                                 23  2'F1=Filter  F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040217  123227  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Bonger betalt med kreditkort'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bong . . . . . . . . .'
     A            C1BONG    R        O  5 25REFFLD(SOBONG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Betalingsreferanse . .'
     A            C2REFN        14A  O  7 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kortnummer . . . . . .'
     A            C2KKNR        25  0O  8 25
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Betalt dato/tid. . . .'
     A            C2EDAT         6Y 0O  9 25COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ETIM         6  0O  9 36COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Kasse id . . . . . . .'
     A            C2KASS         3A  O 10 25
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Beløp. . . . . . . . .'
     A            C2SUMB    R    9  2O 11 25REFFLD(SOFSUM)
     A                                      EDTWRD('     0 ,  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A*                                11  2'Operatør . . . . . . .'
     A*           C2OUSR        10A  O 11 64
     A*                                     COLOR(YLW)
     A*                                     OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1MSG
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' INFOMASJONSMELDING'
     A                                      DSPATR(RI)
     A            B1MSG1        25A  O  5  2
     A            B1MSG2        25A  O  6  2
     A            B1MSG3        25A  O  7  2
     A                                 14  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 35)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Bla alternativer'
     A                                      DSPATR(RI)
     A                                  5  2'Velg år........:'
     A            F1AARR    R        B  5 19REFFLD(SOAARR)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Kasse id.......:'
     A            F1KASS         3A  B  6 19
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  7  2'Kunde..........+'
     A            F1KUND    R        B  7 19REFFLD(SOKUND)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 14  2'F3=Tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
