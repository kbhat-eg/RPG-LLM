     A*%%TS  SD  20220505  122916  ASK_800     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA117D
     A* Beskrivelse . . : Forsendelsesmåter, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
6.20 A*       181110 BOF  Utvidet med kode for behandles i kjørekontor
6.30 A*       120215 BSA  Utvidet med kode for forsendelsesmåte og SMS
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.01  300811 bof  Nytt felt - direkte valgt (TATT INN FRA BM)
     A*
8.01 A* 800   010422 ask  Lagt inn kobling til Regionalt Distribusjons Senter
8.02 A* 800   180321 bsa  Utvidet med felter på sideregister
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101118  115838  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
8.01 A                                      VALUES(0 2 3 4 5 8)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1QKOD    R        O  8  5REFFLD(RAQKOD)
     A            B1QTXT    R        O  8 11REFFLD(RAQTXT)
     A            B1QKJO    R        O  8 35REFFLD(RAQKJO)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20220505  122916  ASK_800     REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forsendelsesmåter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  5'Kode'
     A                                      COLOR(WHT)
     A                                  4 11'Beskrivelse'
     A                                      COLOR(WHT)
     A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                       8=Kobling til eksternt lager'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  5'Kode'
     A                                      COLOR(WHT)
     A                                  7 11'Beskrivelse       '
     A                                      COLOR(WHT)
     A            B2QKOD    R        B  5  5REFFLD(RAQKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2QTXT    R        B  5 11REFFLD(RAQTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 33'Kj.k.'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  223027  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forsendelsesmåter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Forsendelsesmåte . . . . . .'
     A            C1QKOD    R        B  5 31REFFLD(RAQKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020325  223027  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Forsendelsesmåte finnes'
     A                                  3  2'fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020730  130347  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av forsendelsesmåter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Forsendelsesmåte . . . . . .'
     A            C1QKOD    R        O  5 31REFFLD(RAQKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2QTXT    R        B  7 31REFFLD(RAQTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  8  2'Behandles i kjørekontor  . .'
     A            C2QKJO    R        B  8 31REFFLD(RAQKJO)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 34'1=Ja'
6.30 A                                 10  2'SMS informasjon ved varemottak'
6.30 A                                 11  2'Informere kunde  .  .  . . .'
6.30 A            C2QSMK    R        B 11 31REFFLD(RAQSMK)
6.30 A                                      VALUES(0 1)
6.30 A                                      COLOR(YLW)
6.30 A  30                                  DSPATR(PR)
6.30 A                                      OVRDTA
6.30 A                                 11 34'1=Ja'
6.30 A                                 12  2'Informere selger .  .  . . .'
6.30 A            C2QSMS    R        B 12 31REFFLD(RAQSMS)
6.30 A                                      VALUES(0 1)
6.30 A                                      COLOR(YLW)
6.30 A  30                                  DSPATR(PR)
6.30 A                                      OVRDTA
6.30 A                                 12 34'1=Ja'
7.01 A                                 13  2'Direkte er valgt . . . . . .'
7.01 A            C2QDIR    R        B 13 31REFFLD(RAQDIR)
7.01 A                                      COLOR(YLW)
7.01 A  30                                  DSPATR(PR)
7.01 A                                      OVRDTA
7.01 A                                 13 34'1=Ja'
8.02 A                                 15  2'Flåtestyring. .  . . . . . .'
8.02 A            C22FLT    R        B 15 31REFFLD(RAQDIR)
8.02 A                                      COLOR(YLW)
8.02 A  30                                  DSPATR(PR)
8.02 A                                      OVRDTA
8.02 A                                 15 34'1=Ja'
8.02 A                                 16  2'Avvikende flåteid. . . . . .'
8.02 A            C22FID        35A  B 16 31
8.02 A                                      COLOR(YLW)
8.02 A  30                                  DSPATR(PR)
8.02 A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  223027  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette forsendelsesmåte '
     A                                      DSPATR(RI)
     A                                  4  2'Forsendelsesmåte .'
     A            D1QKOD    R        O  4 21REFFLD(RAQKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RAQTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020325  223027  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Forsendelsesmåte .'
     A            K1QKOD    R        B  4 21REFFLD(RAQKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Forsendelsesmåte .'
     A            RAQKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RAQTXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
