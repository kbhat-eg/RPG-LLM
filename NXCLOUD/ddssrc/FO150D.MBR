     A*%%TS  SD  20071109  150039  BOF550      REL-V5R4M0  5722-WDS
     A*
7.xx A*     BSA  180216    Endringer relatert til ny GUI
     A*
8.01 A*K000 bsa  130122 Takler brede skjermbilder
     A*
     A*-------------------------------------------------------------------
     A*%%EC
8.01 A                                      DSPSIZ(24 80 *DS3 -
8.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1WIN
     A*%%TS  SD  20071109  150039  BOF550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 22 62)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Endring av ordreinformasjon '
     A                                      DSPATR(RI)
     A                                  3  2'Ordre . . . . . .'
     A            B1NUMM    R        O  3 20REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.NU A            B1SUFF    R        O  3 29REFFLD(FOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4  2'Kunde . . . . . .'
     A            B1KUND    R        O  4 20REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B1KNAV        30A  O  4 27COLOR(YLW)
     A                                      OVRDTA
     A            B1KGAT        30A  O  5 27COLOR(YLW)
     A                                      OVRDTA
     A            B1KAD2        30A  O  6 27COLOR(YLW)
     A                                      OVRDTA
     A            B1KPNR         4S 0O  7 27COLOR(YLW)
     A                                      OVRDTA
     A            B1KPST        30A  O  7 32COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Kundeprosjekt . .'
     A            B1KPRO    R        O  8 20REFFLD(FOKPRO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B1PNAV    R        O  8 27REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Navn. . . . . . .'
     A            B1NAVN    R        B 10 20REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 11  2'Gate. . . . . . .'
     A            B1ADR1    R        B 11 20REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2'Adresse . . . . .'
     A            B1ADR2    R        B 12 20REFFLD(FOADR2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 13  2'Postadresse . . .'
     A            B1STED    R        B 13 20REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 14  2'Mobilnr . . . . .'
     A            B1MOBI    R        B 14 20REFFLD(FOMOBI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16  2'Ordre info.kode +'
     A            B1ITYP    R        B 16 20REFFLD(VAITYP)
     A  31                                  ERRMSGID(VMA8020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA8250 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B1ITXT    R        O 16 23REFFLD(VAITXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 17  2'Plukkes av  . . +'
     A            B1PLSE    R        B 17 20REFFLD(FOPLSE)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(FMA0150 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 18  2'Leveringsdato . .'
     A            B1LDAT         6Y 0B 18 20COLOR(YLW)
     A  33                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMO0255 *LIBL/QMFOFAK 34)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 19  2'Informasjon . . .'
     A            B1INF1    R        B 19 20REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            B1INF2    R        B 20 20REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 21  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
