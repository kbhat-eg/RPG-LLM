     A*%%TS  SD  20020913  151018  MHA         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AM100D
     A* Beskrivelse . . : MAILSERVERE, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120502 MH   Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020605  103440  MHA         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFUMAL)
     A            B1MFIR    R        O  8  5REFFLD(AMMFIR)
     A            B1MSVR    R        O  8 40REFFLD(AMMSVR)
     A            B1FNAV    R        O  8  9REFFLD(AFRFR/AAFNAV *LIBL/AFRF)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020605  103440  MHA         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'SYSTEMVEDLIKEHOLD'
     A                                      COLOR(WHT)
     A                                  1 66'KL. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'VEDLIKEHOLD AV MAILSERVERE'
     A                                      COLOR(WHT)
     A                                  2 46'BRUKER:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'DATO:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Filgruppe. . .'
7.xx A            B2CLIB        10A  O  4 17
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  6  2'Firma:'
7.xx A                                      COLOR(WHT)
7.xx A            B2MFIR    R        B  6 10REFFLD(AMMFIR)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  5'Firm'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 10'Firmanavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'Mailserver'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020228  085206  MHA         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'SYSTEMVEDLIKEHOLD'
     A                                      COLOR(WHT)
     A                                  1 66'KL. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'VEDLIKEHOLD AV MAILSERVERE'
     A                                      COLOR(WHT)
     A                                  2 46'BRUKER:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firma. . . . . . . . . . . .'
     A            C1MFIR    R        B  5 31REFFLD(AMMFIR)
     A  31                                  ERRMSGID(MMR0010 *LIBL/QMFUMAL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020227  163052  MHA         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Mailserver på firma finnes'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  3  2'fra før.'
     A          R C2BLD
     A*%%TS  SD  20020913  151018  MHA         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'SYSTEMVEDLIKEHOLD'
     A                                      COLOR(WHT)
     A                                  1 66'KL. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'VEDLIKEHOLD AV MAILSERVERE'
     A                                      COLOR(WHT)
     A                                  2 46'BRUKER:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'DATO:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firma. . . . . . . . . . . .'
     A            C1MFIR    R        O  5 31REFFLD(AMMFIR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Mailserver . . . . . . . . .'
     A            C2MSVR    R        B  7 31REFFLD(AMMSVR)
     A  31                                  ERRMSGID(MMR0100 *LIBL/QMFUMAL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(AMEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020227  163052  MHA         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette mailserver '
     A                                      DSPATR(RI)
     A                                  4  2'Firma. . . . . . .'
     A            D1MFIR    R        O  4 21REFFLD(AMMFIR)
     A                                  6  2'Mailserver . . . .'
     A            AMMSVR    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020227  163052  MHA         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Firma. . . . . . .'
     A            K1MFIR    R        B  4 21REFFLD(AMMFIR)
     A  31                                  ERRMSGID(MMR0010 *LIBL/QMFUMAL 31)
     A  32                                  ERRMSGID(MMR0030 *LIBL/QMFUMAL 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Firma. . . . . . .'
     A            AMMFIR    R        O  8 21
     A                                  9  2'Mailserver . . . .'
     A            AMMSVR    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
