     A*%%TS  SD  20140604  115241  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LQ100D
     A* Beskrivelse . . : Lagertransaksjoner bunt behandling
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.NU A* R6.30 030614 BOF  Utvidelse vedr. nummerutvidelse
7.xx A* 7.xx  180516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20140604  115045  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      VALUES(0 2 3 4 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1TYPE    R        O  9 13REFFLD(LQTYPE)
     A            B1LAGE    R        O  9 16REFFLD(LQLAGE)
     A            B1LAST    R        O  9 20REFFLD(LQLAST)
     A            B1TEXT    R        O  9 23REFFLD(LQTEXT)
     A            B1USER    R        O  9 55REFFLD(LQUSER)
     A            B1RDAT         6Y 0O  9 66EDTWRD('  .  .  ')
     A            B1RTIM         4Y 0O  9 75EDTWRD('  .  ')
     A            B1NUMM    R        O  9  4REFFLD(LQNUMM)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140604  115241  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CF08
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av lagertransaksjoner'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Buntnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 13'Ty'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 24'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre bunt'
     A                                      COLOR(BLU)
7.xx A                                  6 18'3=Kopier'
     A                                      COLOR(BLU)
7.xx A                                  6 28'4=Slett bunt'
     A                                      COLOR(BLU)
7.xx A                                  6 42'6=Utskrift'
     A                                      COLOR(BLU)
7.xx A                                  6 54'7=Endre linjer'
     A                                      COLOR(BLU)
7.xx A                                  7  2'8=Oppdater bunt'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Buntnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'Ty'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 16'Lg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 19'Til'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 23'Beskrivelse      '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 55'Bruker'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 66'  Dato  '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 75' Kl. '
7.xx A**                                7  2'V Buntnr   Ty Lg Til  Beskrivelse -
7.xx A**                                                       Bruker       Dat-
7.xx A**                                    o    Kl.'
     A                                      COLOR(WHT)
7.xx A            B2TYPE    R        B  5 13REFFLD(LQTYPE)
     A                                      OVRDTA
     A  33                                  ERRMSGID(LMH0020 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
7.xx A            B2TEXT    R  -15   B  5 23REFFLD(LQTEXT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
7.xx A            B2NUMM    R        B  5  4REFFLD(LQNUMM)
     A                                      OVRDTA
     A  31                                  ERRMSGID(LMQ0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMQ0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20020924  153617  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 29'F6=Ny bunt'
     A                                      COLOR(BLU)
     A                                 23 42'F8=Oppdater regnskap'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20140604  115045  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 7 32)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette transaksjonsbunt '
     A                                      DSPATR(RI)
     A                                  4  2'Bunt nr:'
     A                                      COLOR(WHT)
     A            B4NUMM    R        O  4 11REFFLD(LQNUMM)
     A                                      COLOR(WHT)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2WIN
     A*%%TS  SD  20061122  145608  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 16 50)
     A                                      CA03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Behandling av bunt-hode lagertrans'
     A                                      COLOR(WHT)
     A            C2FUNK        10A  O  1 40COLOR(GRN)
     A                                  3  2'Buntnummer  . .'
     A            C2NUMM    R        B  3 18REFFLD(LQNUMM)
     A  31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A N31                                  COLOR(YLW)
     A                                  4 27'(Blank gir neste)'
     A                                  5  2'Type  . . . . .'
     A            C2TYPE    R        B  5 18REFFLD(LQTYPE)
     A                                      CHKMSGID(LMQ0310 *LIBL/QMFLAGR)
     A                                      VALUES('I ' 'U ' 'J ' 'O' '  ')
     A  31                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 24'I=Inngang'
     A                                  6 24'U=Uttak'
     A                                  7 24'J=Justering'
     A                                  8 24'O=Overføring'
7.xx A                                 10  2'Lager-nummer. +'
     A            C2LAGE         2A  B 10 18
     A  33                                  ERRMSGID(LMQ0320 *LIBL/QMFLAGR 33)
     A  34                                  ERRMSGID(LMQ0330 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2LTEK        20   O 10 21OVRDTA
     A            C2LAST         2A  B 11 18
     A  35                                  ERRMSGID(LMQ0320 *LIBL/QMFLAGR 35)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LATK        20   O 11 21OVRDTA
     A                                 13  2'Beskrivelse . .'
     A            C2TEXT    R        B 13 18REFFLD(LQTEXT)
     A  32                                  ERRMSGID(LMQ0300 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                 11  2'Overføre til. +'
7.xx A                                 12  2'Årsakskode. . +'
     A            C2XRKO    R        B 12 18REFFLD(RFRFR/RAXRKO *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2XTXT    R        O 12 21REFFLD(RFRFR/RAXTXT *LIBL/RFRF)
     A                                      OVRDTA
     A                                 15 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20020924  153617  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 8 40)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Feilmelding '
     A                                      DSPATR(RI)
     A                                  4  2'Feil valg for oppdatering av bunt.'
     A                                      COLOR(WHT)
     A                                  5  2'Valg ikke godkjent i koderegister.'
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B8WIN
     A*%%TS  SD  20070326  221358  KOB         REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 10 33)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Oppdatere lager '
     A                                      DSPATR(RI)
     A                                  4  2'Oppdatere bunt nr:'
     A                                      COLOR(WHT)
     A            B8NUMM    R        O  4 21REFFLD(LQNUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A  31                              5  2'Regnskapstransaksjoner dannes'
     A N31                              5  2'Regnskap oppdateres ikke'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A  32                              7  2'Skriv pakkseddel'
     A            C8PAKK         1Y 0B  7 19
     A N32                                  DSPATR(ND)
     A N32                                  DSPATR(PR)
     A  32                              7 21'1=Ja,0=Nei'
     A          R ASUME                     ASSUME
     A                                 24 79' '
