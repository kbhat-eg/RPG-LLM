     A*%%TS  SD  20140821  151243  BOF630      REL-V7R1M0  5770-WDS
     A* 210814 BOF : Gul tekst hvis notatark på vare i EVR
     A*%%EC
7.xx A* 040316 bsa Endringer relatert til ny GUI
8.01 A*PRG2 140622 BOF : utvidet prisgruppe fra 1 til 2 posisjoner
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050624  144949  HKO530      REL-V5R2M0  5722-WDS
     A            B1VARE    R        H      REFFLD(JVVARE)
     A            B1NOBB    R        H      REFFLD(JVNOBB)
     A            B1LVAR    R   -5   H      REFFLD(JVLVAR)
     A            B1SEPR         9Y 2H
     A            B1SKOD    R        H      REFFLD(JVDSKO)
6.3m A            B1LOGV         2S 0H
6.3m A            B1UDEV         2S 0H
7.xx A            B1VALG         2Y 0B 13  2COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(2 3 5 6 7 8 9 10 11 12 -
6.m  A                                             13 14 15 17 19 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1TEK1    R   -3   O 13  5REFFLD(JVTEK1)
     A  50                                  COLOR(PNK)
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1NYPO        13A  O 13 38
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1S1PR    R        O 13 52REFFLD(JBS1PR)
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1ENDR         5Y 2O 13 64EDTCDE(M)
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1MIDL         1A  O 13 72
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1PKOD    R        O 13 74REFFLD(JVDPKO)
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1VTYP         1A  O 13 76
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1OSTS         1A  O 13 78
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
7.xx A            B1HKOD         1A  O 13 80
6.3k A  51                                  COLOR(BLU)
6.3k A  52                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140821  151243  BOF630      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0009)
7.xx A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
7.xx A                                  2  2'Oppdatering av pris og vareinfo'
     A                                      COLOR(WHT)
7.xx A                                  2 38'JV126B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Lev. '
     A                                      COLOR(BLU)
7.xx A            B2LDOR    R        O  4  9REFFLD(JVLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2NAVN    R  -18   O  4 16REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  4 35'Prisgiv.'
     A                                      COLOR(BLU)
7.xx A            B2LDOP    R        O  4 44REFFLD(JVLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2PNAV    R  -18   O  4 51REFFLD(JLNAVN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  2'Varegrp.'
     A                                      COLOR(BLU)
7.xx A            B2OGRP    R        O  5 11REFFLD(JVOGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2HGRP    R        O  5 14REFFLD(JVHGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2UGRP    R        O  5 17REFFLD(JVUGRP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 21'Sort.'
     A                                      COLOR(BLU)
7.xx A            B2OSOR    R   -3   O  5 27REFFLD(JVSORT)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 35'Lev.type'
     A                                      COLOR(BLU)
     A            B2LETY         1S 0O  5 44COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 53'Prisgruppe'
     A                                      COLOR(BLU)
8.01 A            B2PRGR         2A  O  5 64COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5 67'Ant.F'
     A                                      COLOR(BLU)
7.xx A            B2ANTF         4Y 0O  5 73COLOR(BLU)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  6  2'Frisøk .'
7.xx A                                      COLOR(BLU)
7.xx A            B2STEK    R        B  6 11REFFLD(JSSTEK)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      OVRDTA
7.xx A                                  7  5'Varetekst 1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 38'Nobbnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 56'Lev. varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 47'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A            B2TEK1    R   -3   B  8  5REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2NOBB    R        B  8 38REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LVAR    R   -5   B  8 56REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2VARE         8Y 0B  8 47COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  9  2'2=Endre  3=Hold tilbake  5=Vis  6=-
7.xx A                                      Frigi  7=Pris  8=Enhet'
7.xx A                                      COLOR(BLU)
7.xx A                                 10  2'9=Søketekst  10=Betingelse  11=Pro-
7.xx A                                      duktinfo  12=Bytte enhet  13=Dupl. -
7.xx A                                      GTIN'
7.xx A                                      COLOR(BLU)
7.xx A                                 11  2'14=Logi.vare  15=Kalkulasjon  17=S-
7.xx A                                      lett midlertidig pris  19=Tvangsove-
     A                                      rfør'
7.xx A                                      COLOR(BLU)
7.xx A                                 12  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 12  5'Varetekst 1                    '
7.xx A                                      COLOR(WHT)
7.xx A  80                             12 38'Varenr'
7.xx A  80                                  COLOR(WHT)
7.xx A  81                             12 38'Nobbnr'
7.xx A  81                                  COLOR(WHT)
7.xx A  82                             12 38'Lev. varenr'
     A  82                                  COLOR(WHT)
7.xx A                                 12 53'Salgspris'
     A                                      COLOR(WHT)
6.3l A  81                             12 64'%-endr'
6.3l A  81                                  COLOR(WHT)
6.3l A  80                             12 64'DG %  '
6.3l A  80                                  COLOR(WHT)
7.xx A  82                             12 64'%-kost'
     A  82                                  COLOR(WHT)
7.xx A                                 12 72'M'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 74'P'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 76'T'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 78'S'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 80'H'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20031014  132820  HKO         REL-V5R2M0  5722-WDS
     A                                 22  2'F1=Filter  F3=Gå tilbake  F5=Forny-
     A                                        F9=Gjenta søk  F13=Status  F14=Gr-
     A                                      p.behandl.'
     A                                      COLOR(BLU)
     A                                 23  2'F15=Betingel.  F16=Påslag  F17=For-
     A                                      pakning  F18=Sortiment  F20=Overfør-
     A*                                      F23=Bytt'
     A                                        F23=Bytt'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Tvangoverføring gyldig kun '
     A                                  3  2'for status 7, G eller O'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
