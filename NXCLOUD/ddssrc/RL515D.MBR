     A*%%TS  SD  20040302  093520  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK515D
     A* Beskrivelse . . : Leverandørposter, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       030304 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20031025  183108  KOB         REL-V5R2M0  5722-WDS
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1SAMB    R        O  9  4REFFLD(ROSAMB)
6.NU A            B1BILN    R        O  9 13REFFLD(ROBILN)
6.NU A            B1BIDA         6Y 0O  9 22EDTCDE(Y)
6.NU A            B1BILK    R        O  9 31REFFLD(ROBILK)
6.NU A            B1TEXT        16   O  9 34
     A            B1BELØ        11Y 2O  9 51EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040302  093520  KOB         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på samlegiro'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør:'
     A                                      COLOR(WHT)
     A            B2LEVR    R        O  5 14REFFLD(RLLEVR)
     A            B2NAVN    R        O  5 21REFFLD(RLNAVN)
     A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
6.NU A                                  8  2'V Sam.gir  Bil.nr     Dato   Bk   -
6.NU A                                        Tekst             Beløp'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20040216  210330  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 75)
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 29'Leverandørpostering'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        O  4 19REFFLD(ROBILK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2TEXT    R        O  4 22REFFLD(ROTEXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        O  6 19REFFLD(ROBETB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2CTXT    R        O  6 22REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        O  7 19REFFLD(ROREKL)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlegiro-nr.. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        O  8 19REFFLD(ROREFN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        O  9 19REFFLD(ROAVDE)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2GTXT    R   18   O  9 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        O  9 61REFFLD(ROREST)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjonskode:'
     A            C2ATTS    R        O 10 19REFFLD(ROATTS)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        O 11 19REFFLD(ROBETK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        O 12 19REFFLD(RORRF1)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        O 13 19REFFLD(RORRF2)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        O 13 61REFFLD(ROVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        O 14 19REFFLD(RODECK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        O 15 17REFFLD(ROKIDI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 43'Lev.fakturanr. .:'
     A            C2LFAK    R        O 16 61REFFLD(ROLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
