     A*%%TS  SD  20140526  140146  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LD511D
     A* Beskrivelse . . : Vise kundeordre mot bestilling
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.NU A*       260514      Utvidelse ihht nummerserieutvidelse
7.xx A* 7.xx  190516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140526  140027  BOF630      REL-V7R1M0  5770-WDS
     A            B1NUMM    R        O 10  4REFFLD(FDNUMM)
     A            B1SUFF_O  R        O 10 13REFFLD(FDSUFF)
     A N33                                  COLOR(GRN)
     A  33                                  COLOR(RED)
     A            B1OTYP_O  R        O 10 16REFFLD(FOOTYP)
     A            B1VARE    R   -7   O 10 19REFFLD(FDVARE)
     A            B1TEK1    R   -6   O 10 28REFFLD(FDTEK1)
     A            B1ANTA_O  R   -2 03O 10 58REFFLD(FDANTA)
     A                                      EDTWRD('      ,   ')
     A            B1ENH1_O  R        O 10 69REFFLD(FDENH1)
     A            B1BDAT_O       6Y 0O 10 73EDTWRD('  .  .  ')
     A            B1VALG    R        B 11  2REFFLD(FÅVALA)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '5' ' ')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM_B  R        O 11  4REFFLD(FDNUMM)
     A N33                                  COLOR(GRN)
     A  33                                  COLOR(RED)
     A                                      DSPATR(UL)
     A            B1SUFF_B  R        O 11 13REFFLD(FDSUFF)
     A N33                                  COLOR(GRN)
     A  33                                  COLOR(RED)
     A                                      DSPATR(UL)
     A            B1OTYP_B  R        O 11 16REFFLD(FOOTYP)
     A                                      DSPATR(UL)
7.xx A            B1LVAR    R   -7   O 11 19REFFLD(FDVARE)
7.xx A            B1TEK1_B  R   -6   O 11 28REFFLD(FDTEK1)
7.xx A**          B1ITYP    R        O 11 55REFFLD(FOOTYP)
     A            B1ANTA_B  R   -2 03O 11 58REFFLD(FDANTA)
     A                                      EDTWRD('      ,   ')
     A            B1ENH1_B  R        O 11 69REFFLD(FDENH1)
     A            B1BDAT_B       6Y 0O 11 73EDTWRD('  .  .  ')
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140526  140146  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA02
     A                                      CA03
     A                                      CA05
     A                                      CA08
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise kundeordre opp mot bestilling'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A                                      COLOR(BLU)
     A            B2KUND    R        O  4 11REFFLD(FOKUND)
     A                                      OVRDTA
     A            B2NAVN    R        O  4 20REFFLD(FONAVN)
     A                                      OVRDTA
     A            B3NAVN    R        O  4 51REFFLD(FONAVN)
     A                                      OVRDTA
     A                                  5  2'Ordre. .'
     A                                      COLOR(BLU)
     A            B2NUMM    R        O  5 11REFFLD(FONUMM)
     A                                      OVRDTA
     A            B2GATE    R        O  5 20REFFLD(FOADR1)
     A                                      OVRDTA
     A            B3GATE    R        O  5 51REFFLD(FOADR1)
     A                                      OVRDTA
     A            B2STED    R        O  6 20REFFLD(FOSTED)
     A                                      OVRDTA
     A            B3STED    R        O  6 51REFFLD(FOSTED)
     A                                      OVRDTA
     A                                  7  2'Kundens ref. .:'
     A                                      COLOR(BLU)
     A            B2DREF    R        O  7 20REFFLD(FODREF)
     A                                      OVRDTA
     A                                  7 51'Telefon:'
     A                                      COLOR(BLU)
     A            B2TLFN        11A  O  7 60OVRDTA
     A                                  8  2'2=Endre  5=Vis bestilling'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
     A                                      COLOR(WHT)
     A                                  9  4' Ordre'
     A                                      COLOR(WHT)
     A                                  9 13'S'
     A                                      COLOR(WHT)
     A                                  9 16'OT'
     A                                      COLOR(WHT)
     A                                  9 19'Varenr. '
     A                                      COLOR(WHT)
     A                                  9 29'Varetekst'
     A                                      COLOR(WHT)
     A                                  9 60'  Antall'
     A                                      COLOR(WHT)
     A                                  9 69'Enh'
     A                                      COLOR(WHT)
     A                                  9 74'O.dato'
     A                                      COLOR(WHT)
7.xx A**                                9 55'Bs'
7.xx A**                                    COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20000222  152018  AMD         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 33'F8=Dato-kontroll'
     A                                      COLOR(BLU)
