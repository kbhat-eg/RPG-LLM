     A*%%TS  SD  20050331  121521  BOF530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL605D
     A* Beskrivelse . . : Utskrift av lagerverdiliste - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  190516  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.01  140319  ask  Lagt inn muliget for lagerverdi til gj.sn. kostpris
7.01 A* 7.01  140319  ask  Alle felter er løftet to posisjoner opp i skjermbilde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20050331  121521  BOF530      REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av lagerverdiliste'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LL605A1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  3 35'Fra'
     A                                  3 51'Til'
7.xx A                                  4  2'Varenr . . . . . . . . . . +'
     A            A1FVAR         8D 0B  4 31CHECK(RZ)
     A  30                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 30)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A                                  4 47'+'
7.xx A            A1TVAR         8D 0B  4 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  5  2'Lager. . . . . . . . . . . +'
     A            A1FLAG         2D 0B  5 31CHECK(RZ)
     A  31                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
7.xx A                                  5 47'+'
7.xx A            A1TLAG         2D 0B  5 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Varegruppe . . . . . . . . +'
     A            A1FOGR         2D 0B  6 31CHECK(RZ)
     A  32                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
7.xx A                                  6 34'+'
7.xx A            A1FHGR         2D 0B  6 36CHECK(RZ)
     A  33                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
7.xx A                                  6 39'+'
7.xx A            A1FUGR         3D 0B  6 41CHECK(RZ)
     A  34                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
7.xx A                                  6 47'+'
7.xx A            A1TOGR         2D 0B  6 49CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  6 52'+'
7.xx A            A1THGR         2D 0B  6 54CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  6 57'+'
7.xx A            A1TUGR         3D 0B  6 59CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Produktansvarlig . . . . . +'
     A            A1SAKB         4A  B  7 31
     A  38                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Leverandørnummer . . . . . +'
     A            A1LVNR         6D 0B  8 31CHECK(RZ)
     A  35                                  ERRMSGID(LM06000 *LIBL/QMFLAGR 35)
     A                                      COLOR(YLW)
     A                                 10  2'Sortering. . . . . . . . . .'
     A            A1SORT         1D 0B 10 31CHECK(RZ)
     A  36                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
     A                                 10 35'1 = Pr. varenummer/lager'
     A                                 11 35'2 = Pr. varegruppe/varenummer/lage-
     A                                      r'
     A                                 12 35'3 = Pr. kontohenv./varenummer/lage-
     A                                      r'
     A                                 13 35'4 = Pr. lager/varenummer'
     A                                 14 35'5 = Pr. lager/varegruppe/varenumme-
     A                                      r'
     A                                 15 35'6 = Pr. lager/kontohenv./varenumme-
     A                                      r'
     A                                 17  2'Listetype. . . . . . . . . .'
     A            A1TYPE         1D 0B 17 31CHECK(RZ)
     A  37                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 37)
     A                                      COLOR(YLW)
     A                                 17 35'0=Pr. dato, 1=Pr. 01.01, 2=Lager t-
     A                                      elling'
     A                                 18  2'Dato (hvis pr. dato) . . . .'
     A            A1DATO    R        B 18 31REFFLD(LÅDATR)
     A  40                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 40)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
7.01 A  41                             19  2'Bruke gj.sn. kostpris  . . .'
7.01 A  41        A1GJKO         1D 0B 19 31
7.01 A  41                                  COLOR(YLW)
7.01 A  41                             19 33'1=Ja,0=Nei'
     A                                 21  2'Skriv kun sumlinjer. . . . .'
     A            A1VLIN         1D 0B 21 31CHECK(RZ)
     A  39                                  ERRMSGID(LM00010 *LIBL/QMFLAGR 39)
     A                                      COLOR(YLW)
     A                                 21 35'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
