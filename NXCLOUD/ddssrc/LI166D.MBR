     A*%%TS  SD  20070122  095226  ASP_MSX     REL-V5R4M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A* R6.30 090916 BSA  Nytt program
9.01 A* R6.30 251016 BSA  Omorganisere litt på feltene
9.02 A* R6.30 310517 BSA  Legger ut utregning av linje
     A*
8.02 A* K0000 261121 bsa  Takler å komme fra brede skjermbilder
     A*
     A*
     A*%%EC
8.02 A                                      DSPSIZ(24 80 *DS3 -
8.02 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A            B1MLIN    R        H      REFFLD(LIMLIN)
     A            B1VARE         8A  H
     A            B1TEK1        35A  H
     A            B1ANTA         9S 3H
     A            B1ENHE         3A  H
     A            B1PRIS        15S 3H
     A            B1VALG    R        B  9  2REFFLD(LÅVALG)
9.02 A                                      VALUES(0 5 6)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            B1TEXT        76A  O  9  4
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070118  145308  ASP_MGR     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF21
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise utregning av EDI melding'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Fakt.nr.:'
     A            B2FANR        15A  O  4 12OVRDTA
     A                                      COLOR(YLW)
     A                                  4 38'Fakt.tot.'
     A                                      COLOR(WHT)
     A            B2TOTF        11Y 2O  4 48EDTCDE(M)
     A                                      OVRDTA
     A            B2TOTM        11Y 2O  4 62EDTCDE(M)
     A                                      OVRDTA
     A                                  5  2'Levr.nr.:'
     A            B2LEVR         6Y 0O  5 12OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B2LNAV        15A  O  5 19OVRDTA
     A                                  5 38'Diff.'
     A                                      COLOR(WHT)
     A            B2DIFS        11Y 2O  5 48EDTCDE(M)
     A                                      OVRDTA
     A            B2DIFM        11Y 2O  5 62EDTCDE(M)
     A                                      OVRDTA
     A                                  6  2'Kundenr.:'
     A            B2KUND         6Y 0O  6 12OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B2KNAV        15A  O  6 19OVRDTA
     A                                  6 38'Kalkulert'
     A                                      COLOR(WHT)
     A            B2KALS        11Y 2O  6 48EDTCDE(M)
     A                                      OVRDTA
     A            B2KALM        11Y 2O  6 62EDTCDE(M)
     A                                      OVRDTA
9.02 A                                  7  4'5=Vise  6=Vis utregning'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A            B2OVER        76A  O  8  4COLOR(WHT)
     A                                      OVRDTA
     A*N70                             13  6'Varenr   Varetekst'
     A*                                     COLOR(WHT)
     A*N70                             13 39'Antall   Enh'
     A*                                     COLOR(WHT)
     A*N70                             13 53'Salgspris  Rab-1  Rab-2  Kod'
     A*                                     COLOR(WHT)
     A* 70                             13  6'Varetekst'
     A*                                     COLOR(WHT)
     A* 70                             13 49'Sum netto      Sum kost    DG'
     A*                                     COLOR(WHT)
     A          R C5WIN
     A*%%TS  SD  20070122  095226  ASP_MSX     REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 70)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Vise linjedetajer'
     A                                      DSPATR(RI)
9.02 A            C5STAT        15A  O  2 25OVRDTA
9.01 A                                  4  2'Varenr. :'
     A                                      COLOR(WHT)
9.01 A            C5VARE        15A  O  4 12OVRDTA
9.01 A            C5TEK1        35A  O  4 28OVRDTA
9.02 A                                  5  2'Antall. :'
     A                                      COLOR(WHT)
9.01 A            C5ANTA        11Y 3O  5 12
     A                                      EDTCDE(M)
     A                                      OVRDTA
9.01 A                                  5 30'Enhet:'
9.01 A                                      COLOR(WHT)
9.01 A            C5ENHE         3A  O  5 37OVRDTA
9.01 A                                  6  2'Pris. . :'
     A                                      COLOR(WHT)
9.01 A            C5SAPR         9Y 2O  6 14EDTCDE(M)
     A                                      OVRDTA
9.01 A                                  6 30'Enhet:'
9.01 A                                      COLOR(WHT)
9.01 A            C5PENH         3A  O  6 37OVRDTA
9.01 A                                  6 42'Kval:'
     A                                      COLOR(WHT)
     A            C5PRKV         3A  O  6 48OVRDTA
9.02 A                                  7  2'Fra/til1:'
     A                                      COLOR(WHT)
     A            C5RAB1         5Y 2O  7 12EDTCDE(M)
     A                                      OVRDTA
9.01 A                                  7 30'Beløp:'
9.01 A                                      COLOR(WHT)
9.01 A            C5RSU1        11Y 2O  7 37EDTCDE(M)
     A                                      OVRDTA
9.01 A            C5SIG1         3A  O  7 52OVRDTA
9.01 A            C5KOD1         3A  O  7 60OVRDTA
9.02 A                                  8  2'Fra/til2:'
     A                                      COLOR(WHT)
     A            C5RAB2         5Y 2O  8 12EDTCDE(M)
     A                                      OVRDTA
9.01 A                                  8 30'Beløp:'
9.01 A                                      COLOR(WHT)
9.01 A            C5RSU2        11Y 2O  8 37EDTCDE(M)
     A                                      OVRDTA
9.01 A            C5SIG2         3A  O  8 52OVRDTA
9.01 A            C5KOD2         3A  O  8 60OVRDTA
9.02 A                                  9  2'Fra/til3:'
     A                                      COLOR(WHT)
     A            C5RAB3         5Y 2O  9 12EDTCDE(M)
     A                                      OVRDTA
9.01 A                                  9 30'Beløp:'
9.01 A                                      COLOR(WHT)
9.01 A            C5RSU3        11Y 2O  9 37EDTCDE(M)
     A                                      OVRDTA
9.01 A            C5SIG3         3A  O  9 52OVRDTA
9.01 A            C5KOD3         3A  O  9 60OVRDTA
     A                                 10  2'Brutto. :'
     A                                      COLOR(WHT)
     A            C5BTOT        11Y 2O 10 12EDTWRD('       0 ,  -')
     A                                      OVRDTA
9.01 A                                 10 30'Netto:'
     A                                      COLOR(WHT)
9.01 A            C5LSUM        11Y 2O 10 37EDTWRD('       0 ,  -')
     A                                      OVRDTA
9.01 A                                 10 52'MVA:'
     A                                      COLOR(WHT)
9.01 A            C5MVAP         5Y 2O 10 57EDTCDE(M)
     A                                      OVRDTA
9.01 A                                 11  2'GTIN nr :'
     A                                      COLOR(WHT)
9.01 A            C5GTIN        15Y 0O 11 12EDTCDE(M)
     A                                      OVRDTA
9.01 A                                 11 30'GTIN enhet:'
     A                                      COLOR(WHT)
9.01 A            C5GENH         3A  O 11 42OVRDTA
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
9.02 A          R C6WIN
9.02 A*%%TS  SD  20070122  095226  ASP_MSX     REL-V5R4M0  5722-WDS
9.02 A                                      WINDOW(*DFT 18 60)
9.02 A                                      CA03
9.02 A                                      CA12
9.02 A                                      PUTOVR
9.02 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
9.02 A            W_ARCD        10A  H
9.02 A            W_AFLD        10A  H
9.02 A                                  2  2' Vise utregning'
9.02 A                                      DSPATR(RI)
9.02 A            C6STAT        15A  O  2 25OVRDTA
9.02 A                                  4  2'Varenr. :'
9.02 A                                      COLOR(WHT)
9.02 A            C6VARE         8A  O  4 12OVRDTA
9.02 A            C6TEK1        35A  O  4 21OVRDTA
9.02 A                                  5 15'EDI tall'
9.02 A                                      COLOR(BLU)
9.02 A                                  5 30'Beregnet'
9.02 A                                      COLOR(BLU)
9.02 A                                  5 44'NX kalk.'
9.02 A                                      COLOR(BLU)
9.02 A                                  7  2'Antall. :'
9.02 A                                      COLOR(WHT)
9.02 A            C6ANTA        11Y 3O  7 12EDTWRD('      0 ,   -')
9.02 A**                                    EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A            C6ENHE         3A  O  7 26OVRDTA
9.02 A                                  8  2'Pris. . :'
9.02 A                                      COLOR(WHT)
9.02 A            C6SAPR         9Y 2O  8 14EDTWRD('     0 ,  -')
9.02 A                                      OVRDTA
9.02 A            C6PENH         3A  O  8 26OVRDTA
9.02 A            C6PRKV         3A  O  8 30OVRDTA
9.02 A            C6NXPR         9Y 2O  8 42EDTWRD('     0 ,  -')
9.02 A                                      OVRDTA
9.02 A                                  8 54'NTO'
9.02 A                                  9  2'Brutto. :'
9.02 A                                      COLOR(WHT)
9.02 A            C6BTOT        11Y 2O  9 12EDTWRD('       0 ,  -')
9.02 A                                      COLOR(YLW)
9.02 A                                      OVRDTA
9.02 A            C6KTOT        11Y 2O  9 26EDTWRD('       0 ,  -')
9.02 A                                      COLOR(YLW)
9.02 A                                      OVRDTA
9.02 A                                 11  2'Fra/til1:'
9.02 A                                      COLOR(WHT)
9.02 A            C6RSU1        11Y 2O 11 12EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A            C6SIG1         1A  O 11 26OVRDTA
9.02 A            C6RAB1         5Y 2O 11 28EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A                                 11 36'%'
9.02 A                                 12  2'Fra/til2:'
9.02 A                                      COLOR(WHT)
9.02 A            C6RSU2        11Y 2O 12 12EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A            C6SIG2         1A  O 12 26OVRDTA
9.02 A            C6RAB2         5Y 2O 12 28EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A                                 12 36'%'
9.02 A                                 13  2'Fra/til3:'
9.02 A                                      COLOR(WHT)
9.02 A            C6RSU3        11Y 2O 13 12EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A            C6SIG3         1A  O 13 26OVRDTA
9.02 A            C6RAB3         5Y 2O 13 28EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A                                 13 36'%'
9.02 A                                 14  2'Netto. :'
9.02 A                                      COLOR(WHT)
9.02 A            C6LSUM        11Y 2O 14 12EDTWRD('       0 ,  -')
9.02 A                                      COLOR(YLW)
9.02 A                                      OVRDTA
9.02 A            C6KSUM        11Y 2O 14 26EDTWRD('       0 ,  -')
9.02 A                                      COLOR(YLW)
9.02 A                                      OVRDTA
9.02 A            C6NXTO        11Y 2O 14 40EDTWRD('       0 ,  -')
9.02 A                                      COLOR(YLW)
9.02 A                                      OVRDTA
9.02 A                                 15  2'Diff.. :'
9.02 A                                      COLOR(WHT)
9.02 A            C6DIF1        11Y 2O 15 12EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A            C6DIF2        11Y 2O 15 26EDTCDE(M)
9.02 A                                      OVRDTA
9.02 A                                 17  2'F3=Gå tilbake'
9.02 A                                      COLOR(BLU)
     A*
     A          R B2CMD
     A*%%TS  SD  20070107  220950  ASP_MSX     REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F8=Tillegg/fradrag -
     A                                       F9=Totaler'
     A                                      COLOR(BLU)
     A*
     A          R B8WIN
     A*%%TS  SD  20020924  124732  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Tillegg/fradrag '
     A                                      DSPATR(RI)
     A            B8TEK0        30A  O  4  2
     A            B8BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B8TEK1        30A  O  5  2
     A            B8BEL1        12Y 2O  5 33EDTWRD('          , 0-')
     A            B8TEK2        30A  O  6  2
     A            B8BEL2        12Y 2O  6 33EDTWRD('          , 0-')
     A            B8TEK3        30A  O  7  2
     A            B8BEL3        12Y 2O  7 33EDTWRD('          , 0-')
     A            B8TEK4        30A  O  8  2
     A            B8BEL4        12Y 2O  8 33EDTWRD('          , 0-')
     A            B8TEK5        30A  O  9  2
     A            B8BEL5        12Y 2O  9 33EDTWRD('          , 0-')
     A            B8TEK6        30A  O 10  2
     A            B8BEL6        12Y 2O 10 33EDTWRD('          , 0-')
     A            B8TEK7        30A  O 11  2
     A            B8BEL7        12Y 2O 11 33EDTWRD('          , 0-')
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20041130  151712  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Ordretotaler '
     A                                      DSPATR(RI)
     A            B9TEK0        30A  O  4  2
     A            B9BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B9TEK1        30A  O  5  2
     A            B9BEL1        12Y 2O  5 33EDTWRD('          , 0-')
     A            B9TEK4        30A  O  6  2
     A            B9BEL4        12Y 2O  6 33EDTWRD('          , 0-')
     A            B9TEK5        30A  O  7  2
     A            B9BEL5        12Y 2O  7 33EDTWRD('          , 0-')
     A            B9TEK6        30A  O  8  2
     A            B9BEL6        12Y 2O  8 33EDTWRD('          , 0-')
     A            B9TEK7        30A  O  9  2
     A            B9BEL7        12Y 2O  9 33EDTWRD('          , 0-')
     A                                 11  2'Inkluderte tillegg/fradrag :'
     A                                      COLOR(BLU)
     A            B9TEK2        30A  O 12  2
     A            B9BEL2        12Y 2O 12 33EDTWRD('          , 0-')
     A            B9TEK3        30A  O 13  2
     A            B9BEL3        12Y 2O 13 33EDTWRD('          , 0-')
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
