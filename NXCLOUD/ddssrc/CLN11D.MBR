     A*%%TS  SD  20160922  104934  ASP_G0XA    REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . :
     A* Format. . . . . : CLN11D
     A* Beskrivelse . . : Nobbkategorier, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       210916 ASK  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  7  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1NKKA    R        O  7  5REFFLD(RAGKOD)
     A            B1NKTX    R        O  7 14REFFLD(RAGTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20160922  104934  ASP_G0XA    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Velge nobbkategori '
     A                                      DSPATR(RI)
     A                                  4  2'1=Velg'
     A                                      COLOR(BLU)
     A                                  5  2'V  Kat.     Beskrivelse  '
     A                                      COLOR(WHT)
     A            B2NKKA    R        B  6  5REFFLD(RAGKOD)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
