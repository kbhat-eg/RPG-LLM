     A*%%TS  SD  20020226  094824  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : VF511D
     A* Beskrivelse . . : Alt.undergruppe, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  300916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020226  094753  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1UHGR    R        O  7  5REFFLD(VFUHGR)
     A            B1UUGR    R        O  7 10REFFLD(VFUUGR)
     A            B1UTXT    R        O  7 15REFFLD(VFUTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  094824  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(B2CMD)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge alternativ undergruppe '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 10'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 15'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 10'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 15'Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2UHGR    R        B  4  5REFFLD(VFUHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2UUGR    R        B  4 10REFFLD(VFUUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2UTXT    R        B  4 15REFFLD(VFUTXT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
