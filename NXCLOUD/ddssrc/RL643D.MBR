     A*%%TS  SD  20080321  122521  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK640D
     A* Beskrivelse . . : Kunder, utskrift av kontoutdrag - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240702 KOB  Nytt program
8.01 A*K0000  040425 bsa  Omskrevet åpen post liste til PROA
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20080321  122521  KOBASP      REL-V5R4M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A  98                                  PUTOVR
     A                                      RTNCSRLOC(&W_ACRD &W_AFLD)
     A            W_ACRD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av leverandørinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  7 10'Velg alternativ. :'
     A            A2VALG         1A  B  7 29TEXT('Bare åpne poster')
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES(' ' '1' '2' '3')
     A                                  7 32'1=Kontoutdrag'
     A                                  8 32'2=Oversikt over åpne poster'
     A                                 11 10'Ved valg av kontoutdrag må periode-
     A                                       fra - til angis'
     A                                      COLOR(WHT)
     A                                 12 10'Ved valg av åpne poster benyttes b-
     A                                      are til periode'
     A                                      COLOR(WHT)
     A                                 14 31'Fra  -  Til'
     A                                 15 11'Periode:. . . . :'
     A            A2PPEF    R        B 15 29REFFLD(RKPPER)
     A  37                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(RML0560 *LIBL/QMFOKON 37)
     A            A2PÅRF    R        B 15 32REFFLD(RKPRÅR)
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                 15 37'-'
     A            A2PPET    R        B 15 39REFFLD(RKPPER)
     A  45                                  DSPATR(PC)
     A  45 38                               ERRMSGID(RML0560 *LIBL/QMFOKON 38)
     A  45 39                               ERRMSGID(RMH0070 *LIBL/QMFOKON 39)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A            A2PÅRT    R        B 15 42REFFLD(RKPRÅR)
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
8.01 A*                                17 11'Sendes som mail :'
8.01 A            A2MAIL         1A  B 17 29
8.01 A*                                     CHECK(FE)
8.01 A*                                     CHECK(RB)
8.01 A                                      DSPATR(PR)
8.01 A                                      DSPATR(ND)
8.01 A*                                     DSPATR(UL)
8.01 A*                                     DSPATR(MDT)
8.01 A*                                     COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES('0' '1' ' ')
8.01 A*                                17 32'1=Ja, 0=Nei'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
