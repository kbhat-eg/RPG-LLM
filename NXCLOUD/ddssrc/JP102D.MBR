     A*%%TS  SD  20150722  084508  ASP_G0X     REL-V7R1M0  5770-WDS
6.33 A* R6.30 201014 BSA  Innfører F tast for visning av NOBB info. Erstatter
6.33 A*                   koding i jacada, samt gir tilsvarenede tilgang
6.33 A*                   i grønt.
6.34 A* R6.30 210515 BOF  sjekk om aktivisert på flere aktiv. på en vare
6.35 A* R6.30 310515 BOF  Utvidet med prisgruppe
6.36 A* R6.30 140316 NHO  Endret startposisjon for tekst
6.37 A* R6.30 060416 bsa  (Flyttet litt i skjermbildet pga jacada problematikk)
     A*                   6.37 ikke nødvendig i GUI
6.3d A* R6.30 311018 BOF  Farge på trelast-sortiment vare
7.xx A* 280716 bsa Endringer relatert til ny GUI
8.01 A*PRG2   130622 BOF  utvidet prisgr. fra 1 til 2 posisjoner
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20150531  110113  BOF630      REL-V7R1M0  5770-WDS
     A            B1RSTS    R        H      REFFLD(JPRSTS)
     A            B1LINE    R        H      REFFLD(JPLINE)
     A            B1FAKT         8S 5H
     A            B1NTPR         9S 2H
6.37 A            B1PRGR    R        O 10  5REFFLD(JPPRGR)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1OGRP    R        O 10  8REFFLD(JPOGRP)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1HGRP    R        O 10 11REFFLD(JPHGRP)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1UGRP    R        O 10 14REFFLD(JPUGRP)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1MODN    R        O 10 18REFFLD(JPMODN)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1VARE         8A  O 10 27
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1VTYP    R        O 10 36REFFLD(JPVTYP)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1LETY    R        O 10 38REFFLD(JPLETY)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
8.01 A            B1BETI    R  -10   O 10 40REFFLD(JPBETI)
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
6.37 A            B1FANT        10A  O 10 66
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
6.37 A            B1AKTI         2A  O 10 77
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
6.37 A            B1RKOD         1A  O 10 80
6.3d A  50                                  COLOR(BLU)
6.3d A  51                                  COLOR(YLW)
6.37 A            B1VALG         2Y 0B 10  2COLOR(YLW)
6.3d A                                      VALUES(2 3 4 5 7 8 9 0 15 16)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20150531  110113  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CF10
     A                                      CA12
6.3d A                                      CF13
     A                                      CA23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betingelser'
     A                                      COLOR(WHT)
     A                                  2 38'JP102B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Leverandør . . . . . . . . .'
     A                                      COLOR(BLU)
7.xx A            B2LDOR    R        O  4 31REFFLD(JPLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A            B2LNAV    R        O  4 39REFFLD(JLNAVN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5  4'Pg'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 12'Varegruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 26'Modulnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 37'Varenr '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 48'T'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 52'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 56'Betingelse'
7.xx A                                      COLOR(WHT)
6.3d A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
6.3d A                                       7=Rabattelementer'
     A                                      COLOR(BLU)
6.3d A                                  8  2'8=Vise notat  9=Aktiviser/passivis-
6.3d A                                      er'
     A                                      COLOR(BLU)
6.3d A                                  8 40'15=Varer under modul'
     A                                      COLOR(BLU)
6.3d A                                  8 63'16=Overfør Frakt'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Pg'
7.xx A                                      COLOR(WHT)
8.01 A                                  9  8'Og'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 11'Hg'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 14'Ug'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 18'Modulnr'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 28'Varenr '
7.xx A                                      COLOR(WHT)
8.01 A                                  9 36'T'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 38'L'
7.xx A                                      COLOR(WHT)
8.01 A                                  9 40'Betingelse     '
7.xx A                                      COLOR(WHT)
7.xx A            B2OVER         9A  O  9 67COLOR(WHT)
7.xx A                                  9 80'R'
7.xx A                                      COLOR(WHT)
7.xx A**                                9  2'V  Pg Varegr.  Modulnr  Varenr   T-
7.xx A**                                     L Betingel'
7.xx A**                                    COLOR(WHT)
7.xx A**          B2OVER         9A  O  9 67COLOR(WHT)
7.xx A**                                9 80'R'
7.xx A**                                    COLOR(WHT)
7.xx A                                  6  2'+'
7.xx A            B2PRGR    R        B  6  4REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  7'+'
7.xx A            B2OGRP    R        B  6  9REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 12'+'
7.xx A            B2HGRP    R        B  6 14REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 17'+'
7.xx A            B2UGRP    R        B  6 20REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 24'+'
7.xx A            B2MODN    R        B  6 26REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6 35'+'
7.xx A            B2VARE    R   -7   B  6 37REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 46'+'
7.xx A            B2VTYP    R        B  6 48REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 50'+'
7.xx A            B2LETY    R        B  6 52REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6 54'+'
7.xx A            B2BETI    R  -10   B  6 56REFFLD(JPBETI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20150521  143517  ASP_B02     REL-V7R1M0  5770-WDS
7.xx A                                 22  2'F1=Filter  F3=Gå tilbake   F4=Spør-
6.3d A                                      ring   F5=Forny   F6=Opprett  F13=I-
6.3d A                                      nfo'
     A                                      COLOR(BLU)
6.10 A**          B2CMDKEY      20A  O 23 60COLOR(BLU)
7.xx A                                 23  2'F9=Aktiviser/Deaktiviser'
     A                                      COLOR(BLU)
7.xx A                                 23 29'F10=Sjekk aktivisering'
6.34 A                                      COLOR(BLU)
7.xx A N35                             23 54'F23=Vise nettopriser'
     A                                      COLOR(BLU)
7.xx A  35                             23 54'F23=Vise faktor'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20050613  103726  HKO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1RSTS    R        H      REFFLD(JPRSTS)
     A            C1LINE    R        H      REFFLD(JPLINE)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betingelser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  5 31REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  5 42REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Prisgruppe . . . . . . . . +'
     A            C1PRGR    R        B  6 31REFFLD(JPPRGR)
     A  43                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 43)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        35A  O  6 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  7 31REFFLD(JPOGRP)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  7 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  8 31REFFLD(JPHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  8 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  9 31REFFLD(JPUGRP)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT        35A  O  9 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Modulnummer. . . . . . . . +'
     A            C1MODN    R        B 10 31REFFLD(JPMODN)
     A  38                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 42REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B 11 31REFFLD(JPVARE)
     A  39                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 42REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 12  2'Varetype . . . . . . . . . +'
     A            C1VTYP    R        B 12 31REFFLD(JPVTYP)
     A  40                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 40)
     A  41                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 12 42COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Leveringstype. . . . . . . +'
     A            C1LETY    R        B 13 31REFFLD(JPLETY)
     A  42                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35   O 13 42COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Betingelse . . . . . . . . .'
     A            C1BETI    R        B 15 31REFFLD(JPBETI)
     A  31                                  ERRMSGID(JMP0010 *LIBL/QMFVADM 31)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Betingelse finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050613  103726  HKO530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF13
6.33 A                                      CF18
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            C1RSTS    R        H      REFFLD(JPRSTS)
     A            C1LINE    R        H      REFFLD(JPLINE)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av betingelser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  5 31REFFLD(JPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV    R        O  5 42REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C1PRGR    R        O  6 31REFFLD(JPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1PTXT        35A  O  6 42COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  7 31REFFLD(JPOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  7 42COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  8 31REFFLD(JPHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  8 42COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  9 31REFFLD(JPUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  9 42COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Modulnummer. . . . . . . . .'
     A            C1MODN    R        O 10 31REFFLD(JPMODN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1MTE1    R        O 10 42REFFLD(JUMTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O 11 31REFFLD(JPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O 11 42REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Varetype . . . . . . . . . .'
     A            C1VTYP    R        O 12 31REFFLD(JPVTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTXT        30A  O 12 42COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 13 31REFFLD(JPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 13 42COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Nobbnummer . . . . . . . . .'
     A            C2NOBB    R        O 15 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Betingelse . . . . . . . . .'
     A            C1BETI    R        B 16 31REFFLD(JPBETI)
     A  31                                  ERRMSGID(JMP0010 *LIBL/QMFVADM 31)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Fra-dato . . . . . . . . . .'
     A            C2FDAT         6Y 0B 18 31COLOR(YLW)
     A  32                                  ERRMSGID(JM00250 *LIBL/QMFVADM 32)
     A  34                                  ERRMSGID(JM00260 *LIBL/QMFVADM 34)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Til-dato . . . . . . . . . .'
     A            C2TDAT         6Y 0B 19 31COLOR(YLW)
     A  33                                  ERRMSGID(JM00250 *LIBL/QMFVADM 33)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endringsdato avtaleeier. . .'
     A            C2DATO         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(JPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F13=Rabattelemente-
6.33 A                                      r  F18=NOBB info'
     A                                      COLOR(BLU)
     A          R C3WIN
     A*%%TS  SD  20140819  105115  BOF630      REL-V7R1M0  5770-WDS
7.xx A                                      WINDOW(07 05 07 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
7.xx A                                  1 28'JP102C3'
     A                                  2  2'Du aktiviserer/deaktiviserer på'
     A                                  3  2'varenummer-nivå.               '
     A            C3VALG         1Y 0B  4  2COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  4  4'1=Aktiviser 0=Fjern '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C4WIN
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du overfører til Fraktbetingelse'
     A                                  3  2'Hvis OK, tast <Enter>           '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C4WINA
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du kan kun overføre på varenivå'
     A                                  3  2'sjekk betingelse <enter>        '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C4WINB
     A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Du kan kun overføre kronetillegg'
     A                                  3  2'sjekk betingelse <enter>        '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.34 A          R C5MSG
6.34 A*%%TS  SD  19980518  105221  HKO         REL-V3R7M0  5716-PW1
6.34 A                                      WINDOW(*DFT 7 35)
6.34 A                                      CA03
6.34 A                                      CA12
6.34 A                                  2  2'Samme varenr har blitt aktivisert'
6.34 A                                  3  2'på to forskjellige betingelser   '
6.34 A                                  4  2'Eks. :'
6.34 A            C5VARE    R        O  4 10REFFLD(JPVARE)
6.34 A                                  6  2'F3=Gå tilbake'
6.34 A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050613  103726  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 53)
     A                                      CA03
     A                                      CA12
     A            D1RSTS    R        H      REFFLD(JPRSTS)
     A            D1LINE    R        H      REFFLD(JPLINE)
     A                                  2  2' Slette betingelse '
     A                                      DSPATR(RI)
     A                                  4  2'Prisgruppe  . .'
     A            D1PRGR    R        O  4 18REFFLD(JPPRGR)
     A                                  5  2'Varegruppe. . .'
     A            D1OGRP    R        O  5 18REFFLD(JPOGRP)
     A            D1HGRP    R        O  5 21REFFLD(JPHGRP)
     A            D1UGRP    R        O  5 24REFFLD(JPUGRP)
     A                                  6  2'Modulnummer . .'
     A            D1MODN    R        O  6 18REFFLD(JPMODN)
     A                                  7  2'Varenummer. . .'
     A            D1VARE    R   -7   O  7 18REFFLD(JPVARE)
     A                                  8  2'Varetype. . . .'
     A            D1VTYP    R        O  8 18REFFLD(JPVTYP)
     A                                  9  2'Leveringstype .'
     A            D1LETY    R        O  9 18REFFLD(JPLETY)
     A                                 11  2'Betingelse. . .'
     A            JPBETI    R        O 11 18REFFLD(JPBETI)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20150531  111653  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 22 53)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            K1RSTS    R        H      REFFLD(JPRSTS)
     A            K1LINE    R        H      REFFLD(JPLINE)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgruppe  . +'
     A            K1PRGR    R        B  4 18REFFLD(JPPRGR)
     A                                      COLOR(YLW)
7.xx A                                  5  2'Varegruppe. . +'
     A            K1OGRP    R        B  5 18REFFLD(JPOGRP)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A  46                                  ERRMSGID(JMP0030 *LIBL/QMFVADM 46)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 21'+'
7.xx A            K1HGRP    R        B  5 23REFFLD(JPHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 26'+'
7.xx A            K1UGRP    R        B  5 28REFFLD(JPUGRP)
     A  36                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Modulnummer . +'
     A            K1MODN    R        B  6 18REFFLD(JPMODN)
     A  38                                  ERRMSGID(JMU0020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Varenummer. . +'
     A            K1VARE    R   -7   B  7 18REFFLD(JPVARE)
     A  39                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Varetype. . . +'
     A            K1VTYP    R        B  8 18REFFLD(JPVTYP)
     A  40                                  ERRMSGID(JVA7020 *LIBL/QMFVADM 40)
     A  41                                  ERRMSGID(JVA7040 *LIBL/QMFVADM 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Leveringstype +'
     A            K1LETY    R        B  9 18REFFLD(JPLETY)
     A  42                                  ERRMSGID(JVY0020 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Betingelse. . .'
     A            K1BETI    R        B 10 18REFFLD(JPBETI)
     A  31                                  ERRMSGID(JMP0010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 13  2'Prisgruppe. . .'
     A            JPPRGR    R        O 13 18OVRDTA
     A                                 14  2'Varegruppe. . .'
     A            JPOGRP    R        O 14 18OVRDTA
     A            JPHGRP    R        O 14 21OVRDTA
     A            JPUGRP    R        O 14 24OVRDTA
     A                                 15  2'Modulnummer . .'
     A            JPMODN    R        O 15 18OVRDTA
     A                                 16  2'Varenummer. . .'
     A            JPVARE    R        O 16 18OVRDTA
     A                                 17  2'Varetype. . . .'
     A            JPVTYP    R        O 17 18OVRDTA
     A                                 18  2'Leveringstype .'
     A            JPLETY    R        O 18 18OVRDTA
     A                                 19  2'Betingelse. . .'
     A            JPBETI    R        O 19 18OVRDTA
     A                                 21  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R F1WIN
     A*%%TS  SD  20150722  084508  ASP_G0X     REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 11 70)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Filter for visning :'
     A                                      COLOR(WHT)
     A                                  4  2'Prisgrupper . . . . . . '
     A            F1PRGR    R        B  4 28REFFLD(JPPRGR)
     A  43                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 43)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5  2'Varegrupper . . . . . + '
     A            F1OGRP    R        B  5 28REFFLD(JPOGRP)
     A                                      OVRDTA
     A  33                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 33)
     A  35                                  ERRMSGID(JVG0120 *LIBL/QMFVADM 35)
     A  36                                  ERRMSGID(JVG0200 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
7.xx A                                  5 31'+'
7.xx A            F1HGRP    R        B  5 33REFFLD(JPOGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  5 36'+'
7.xx A            F1UGRP    R        B  5 38REFFLD(JPUGRP)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  6  2'Modul . . . . . . . . +'
     A            F1MODN    R        B  6 28REFFLD(JPMODN)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  7  2'Varenummer  . . . . . .'
     A            F1VARE    R        B  7 28REFFLD(JPVARE)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.3d A                                  8  2'Logistikkvare . . . .'
6.3d A            F1LVAR    R        B  8 28REFFLD(JPVARE)
6.3d A                                      OVRDTA
6.3d A                                      COLOR(YLW)
6.3d A                                  9  2'Aktiviserte . . . . .'
6.3d A            F1KO01         1Y 0B  9 28OVRDTA
     A                                      VALUES(0 1 2)
     A                                      CHKMSGID(FM00010 *LIBL/QMFVADM)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
6.3d A                                  9 31'1=Aktiviserte, 2=Ikke Aktiviserte'
6.3d A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.3d A          R B13WIN
6.3d A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
6.3d A                                      WINDOW(*DFT 15 55)
6.3d A                                      CA03
6.3d A                                      CA12
6.3d A                                      PUTOVR
6.3d A                                  2  2' Info '
6.3d A                                      DSPATR(RI)
6.3d A                                  4  2'Gul farge = Varen er en logistikkv-
6.3d A                                      are'
6.3d A                                      COLOR(YLW)
6.3d A                                  5  2'Blå farge = Varen er en undervare -
6.3d A                                       av en logistikkvare'
6.3d A                                      COLOR(BLU)
6.3d A                                 14  2'F3=Tilbake'
6.3d A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
