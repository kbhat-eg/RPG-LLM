     A*%%TS  SD  20110315  114412  NHO620      REL-V5R4M0  5722-WDS
6.20 A*     NHO  20110315  Vise undergr./transport i ordrereg
     A*%%EC
7.xx A* 7.00  040216  bsa  Justeringer basert på ny GUI og regler
7.01 A* 7.00  040521  bof  utvidet slik at UNSPC vises
     A*
8.01 A*K0000  141124  bsa  Justeringer etter utvidet til skjermbilde
     A*
     A*
     A*
     A*---------------------------------------------------------------
     A                                      DSPSIZ(27 132 *WIDE)
     A*                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990415  135445  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B 11  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1UUGR    R        O 11  4REFFLD(VGUUGR)
     A            B1UTXT    R        O 11  8REFFLD(VGUTXT)
     A            B1UKHV    R        O 11 44REFFLD(VGUKHV)
     A            B1UDEK    R        O 11 49REFFLD(VGUDEK)
     A            B1UPRA    R        O 11 56REFFLD(VGUPRA)
     A            B1UENS    R        O 11 61REFFLD(VGUENS)
     A            B1UENL    R        O 11 65REFFLD(VGUENL)
     A            B1UENP    R        O 11 69REFFLD(VGUENP)
     A            B1UAKT    R        O 11 73REFFLD(VGUAKT)
     A            B1UAVR    R        O 11 76REFFLD(VGUAVR)
     A            B1ULAG    R        O 11 79REFFLD(VGULAG)
     A            B1UFRI    R        O 11 82REFFLD(VGUFRI)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  101722  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av undergruppe'
     A                                      COLOR(WHT)
     A                                  2 38'VD102B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Overgruppe . . . . .'
     A                                      COLOR(BLU)
7.xx A            B2UOGR    R        O  4 23REFFLD(VGUOGR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2OTXT    R        O  4 26REFFLD(VGOTXT)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe. . . . .'
     A                                      COLOR(BLU)
7.xx A            B2UHGR    R        O  5 23REFFLD(VGUOGR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A            B2HTXT    R        O  5 26REFFLD(VGHTXT)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  6  4'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  8'Beskrivelse                 '
7.xx A                                      COLOR(WHT)
     A                                  8  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  8'Beskrivelse                 '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 44'Kto.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 51'DG'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 56'Prod'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 61'Sta'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 65'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 69'Pbk'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 73'Ak'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 76'Av'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 79'La'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 82'UNSPSC-kode'
7.xx A                                      COLOR(WHT)
7.xx A**                                            Kto.   DG   Prod Sta Lag Pb-
7.xx A**                                    k Ak Av La'
7.xx A            B2UUGR    R        B  7  4REFFLD(VGUUGR)
     A  31                                  ERRMSGID(VMG3010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMG3020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2UTXT    R        B  7  8REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A                                 10 44'henv'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 56'ansv'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 61'enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 65'enh'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 69'enh'
7.xx A**                               10 44'henv        ansv enh enh enh'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20020226  101816  HKO         REL-V5R1M0  5722-WDS
8.01 A                                 26  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  101816  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av undergruppe'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1UOGR    R        O  5 31REFFLD(VGUOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT    R        O  5 35REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1UHGR    R        O  6 31REFFLD(VGUOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT    R        O  6 35REFFLD(VGHTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Undergruppe. . . . . . . . .'
     A            C1UUGR    R        B  7 31REFFLD(VGUUGR)
     A  31                                  ERRMSGID(VMG3010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
8.01 A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990114  131806  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Undergruppe finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20110315  114412  NHO620      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av undergruppe'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'

     A                                      COLOR(WHT)
     A                                  4  2'Overgruppe . . . . . . . . .'
     A            C1UOGR    R        O  4 31REFFLD(VGUOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT    R        O  4 35REFFLD(VGOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Hovedgruppe. . . . . . . . .'
     A            C1UHGR    R        O  5 31REFFLD(VGUHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT    R        O  5 35REFFLD(VGHTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Undergruppe. . . . . . . . .'
     A            C1UUGR    R        O  6 31REFFLD(VGUUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2UTXT    R        B  8 31REFFLD(VGUTXT)
     A  31                                  ERRMSGID(VMG3100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A                                  9  2'Kontohenvisning. . . . . . +'
     A            C2UKHV    R        B  9 31REFFLD(VGUKHV)
     A  32                                  ERRMSGID(VMA2020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2KTXT    R        O  9 36REFFLD(VAKTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Produktansvarlig . . . . . +'
     A            C2UPRA    R        B 10 31REFFLD(VGUPRA)
     A  38                                  ERRMSGID(FRA2020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ZTXT    R        O 10 36REFFLD(RFRFR/RAZTXT *LIBL/RFRF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11  2'Statistikkenhet. . . . . . +'
     A            C2UENS    R        B 11 31REFFLD(VGUENS)
     A  33                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 12  2'Lagerenhet . . . . . . . . +'
     A            C2UENL    R        B 12 31REFFLD(VGUENL)
     A  34                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 13  2'Prisbokenhet . . . . . . . +'
     A            C2UENP    R        B 13 31REFFLD(VGUENP)
     A  35                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 14  2'Prissammenligningsenhet. . +'
     A            C2UEP1    R        B 14 31REFFLD(VGUEP1)
     A  36                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.xx A                                 15  2'Prissammenligningsenhet. . +'
     A            C2UEP2    R        B 15 31REFFLD(VGUEP2)
     A  37                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Standard dekningsgrad (DG) .'
     A            C2UDEK    R        B 16 31REFFLD(VGUDEK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'Undergruppe=aktivitet. . . .'
     A            C2UAKT    R        B 17 31REFFLD(VGUAKT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 41'1=Ja,0=Nei'
     A                                 18  2'Ikke avrunding . . . . . . .'
     A            C2UAVR    R        B 18 31REFFLD(VGUAVR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 41'1=Ja,0=Nei'
     A                                 19  2'Ikke lageroppdatering. . . .'
     A            C2ULAG    R        B 19 31REFFLD(VGULAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 41'1=Ja,0=Nei'
6.20 A            C2UTRA    R        B 20 31REFFLD(VGUTRA)
6.20 A                                      COLOR(YLW)
6.20 A  30                                  DSPATR(PR)
6.20 A                                      OVRDTA
6.20 A                                 20 41'1=Ja,0=Nei'
6.20 A                                 20  2'Vise undergr./transport. . .'
     A                                 21  2'Sats for påslag. . . . . . .'
     A            C2USTS    R        B 21 31REFFLD(VGUSTS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                 21 41'Varenummer+'
     A            C2UVAR         8Y 0B 21 53COLOR(YLW)
     A  38                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 38)
     A  39                                  ERRMSGID(VMG3110 *LIBL/QMFOFAK 39)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                 22  2'UNSPSC-kode  . . . . . . . .'
     A            C2UFRI    R        B 22 31REFFLD(VGUFRI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 26  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                 26 48'Sist endret'
     A            C2UEDA         6Y 0O 26 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 26 69'/'
     A            C2UEUS    R        O 26 71REFFLD(FLEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20020226  101816  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Slette undergruppe '
     A                                      DSPATR(RI)
     A                                  4  2'Overgruppe . . . .'
     A            D1UOGR    R        O  4 21REFFLD(VGUOGR)
     A                                  5  2'Hovedgruppe. . . .'
     A            D1UHGR    R        O  5 21REFFLD(VGUHGR)
     A                                  6  2'Undergruppe. . . .'
     A            D1UUGR    R        O  6 21REFFLD(VGUUGR)
     A                                  7  2'Beskrivelse. . . .'
     A            VGUTXT    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  101816  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      WINDOW(*DFT 15 56)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Undergruppe. . . .'
     A            K1UUGR    R        B  4 21REFFLD(VGUUGR)
     A  31                                  ERRMSGID(VMG3010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMG3030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Overgruppe . . . .'
     A            VGUOGR    R        O  8 21OVRDTA
     A                                  9  2'Hovedgruppe. . . .'
     A            VGUHGR    R        O  9 21OVRDTA
     A                                 10  2'Undergruppe. . . .'
     A            VGUUGR    R        O 10 21OVRDTA
     A                                 11  2'Beskrivelse. . . .'
     A            VGUTXT    R        O 11 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
