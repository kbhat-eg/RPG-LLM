     A*%%TS  SD  20140528  141924  BOF630      REL-V7R1M0  5770-WDS
6.nu A* 280514 BOF Utvidet mtp nummerutvidelse
7.02 A* 150819 Bsa Takler brede skjermbilder
7.03 A* 020321 Bsa Utvider til å vise 14 siffer i GTIN
7.05 A* 220621 Bsa Utvidet medlingsnummer fra 6-8 siffer
7.06 A* 220621 bof Utvidet KID felt til 30
     A*
8.01 A* 271221 bsa Utvidet skjermbilde og legger ut akseptansekode
8.02 A* 290923 ask Utvidet felt for leverandørens fakturanummer
     A*
     A*---------------------------------------------------------------
     A*
     A*%%EC
8.01 A                                      DSPSIZ(27 132 *WIDE)
8.01 A**                                    DSPSIZ(24 80 *DS3 -
8.01 A**                                           27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  19990525  223543  ASK         REL-V4R2M0  5769-PW1
     A            B1LINE    R        H      REFFLD(LIMLIN)
     A            B1VALG    R        B 11  2REFFLD(LÅVALG)
6.32 A*                                     VALUES(0 5 6 7 8 9)
6.32 A                                      VALUES(0 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
8.01 A            B1TEK1        35   O 11  4
8.01 A            B1OKVA         7Y 2O 11 40EDTCDE(M)
8.01 A            B1BKVA         7Y 2O 11 50EDTCDE(M)
8.01 A            B1BENH         3   O 11 60
8.01 A            B1LKVA         7Y 2O 11 64EDTCDE(M)
8.01 A            B1LENH         3   O 11 74
8.01 A            B1STAT        12   O 11 78
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140528  141924  BOF630      REL-V7R1M0  5770-WDS
8.01 A                                      SFLSIZ(0014)
8.01 A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      CA14
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            B2BESN        18A  H
     A            B2VREF        30A  H
     A            B2DREF        30A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
8.01 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.01 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Detaljer'
     A                                      COLOR(WHT)
     A            B2TYPE        10A  O  2 11
     A N31                                  COLOR(WHT)
     A  31                                  COLOR(RED)
7.xx A                                  2 38'LI106B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
8.01 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.01 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
8.01 A                                      ------------------------------------
8.01 A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2'Meld.dato'
     A            B2MDAT         6Y 0O  4 12EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
8.01 A                                  4 27'Lev.nr.'
8.01 A            B2LDOR    R        O  4 35REFFLD(LILDOR)
     A                                      COLOR(YLW)
8.01 A                                  4 68'Status'
8.01 A            B2STAT        15A  O  4 75COLOR(YLW)
     A                                  5  2'Best.nr.'
     A            B2NUMM    R        O  5 12REFFLD(LINUMM)
     A                                      COLOR(YLW)
     A  30                              5 21'/'
8.01 A                                  5 27'Fakt.nr'
8.02 A            B2FAKN        30A  O  5 35COLOR(YLW)
8.01 A                                  5 68'Forfall'
8.01 A            B2FDAT         6Y 0O  5 76EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  6  2'Varelev.'
     A            B2LNAV        30A  O  6 12COLOR(YLW)
     A            B2LEAN        13S 0O  6 43COLOR(YLW)
8.01 A                                  6 68'Bes.tot'
8.01 A            B2BBEL        12Y 2O  6 76EDTWRD('        0 ,  ')
     A                                      COLOR(YLW)
     A                                  7  2'Faktlev.'
     A            B2FNAV        30A  O  7 12COLOR(YLW)
     A            B2FEAN        13Y 0O  7 43COLOR(YLW)
8.01 A                                  7 68'Fak.tot'
8.01 A            B2FBEL        12Y 2O  7 76EDTWRD('        0 ,  ')
     A                                      COLOR(YLW)
     A                                  8  2'Meld.nr.'
7.05 A            B2MLNR         8S 0O  8 12COLOR(YLW)
8.01 A  33        B2VTXT        20A  O  8 21COLOR(RED)
6.30 A  32                              8 42'Ordre'
6.30 A  32                                  COLOR(RED)
6.nu A  32        B2ORNR         8S 0O  8 48COLOR(RED)
8.01 A                                  8 68'Kost   '
8.01 A            B2KBEL        12Y 2O  8 76EDTWRD('        0 ,  ')
6.30 A                                      COLOR(YLW)
6.32 A                                  9  2'5=Vis linje-detaljer'
6.32 A*                                 9  2'5=Vis linje-detaljer  6=Oppd. m/an-
6.32 A*                                     tall  7=Oppd. m/pris  8=Oppd. m/avv-
6.32 A*                                     ik'
     A                                      COLOR(BLU)
7.xx A*                                10  2'V Vare                       Ord.a-
7.xx A*                                     nt  Best.ant  Enh Fakt.ant  Enh Sta-
7.xx A*                                     tus'
7.xx A*                                     COLOR(WHT)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'Vare'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 40'Ord.ant'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 50'Best.ant'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 60'Enh'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 64'Fakt.ant'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 74'Enh'
7.xx A                                      COLOR(WHT)
8.01 A                                 10 78'Status'
7.xx A                                      COLOR(WHT)
     A            B2SUFF    R        O  5 23REFFLD(LISUFF)
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20040423  092139  ASK         REL-V5R2M0  5722-WDS
8.01 A                                 25  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.01 A                                 25 17'F6=Skriv ut'
6.21 A                                      COLOR(BLU)
8.01 A                                 25 30'F7=Partsinfo'
     A                                      COLOR(BLU)
8.01 A                                 25 45'F8=Tillegg/fradrag'
     A                                      COLOR(BLU)
8.01 A                                 25 65'F9=Beløpsinfo'
     A                                      COLOR(BLU)
8.01 A                                 25 80'F11=Referanseinformasjon'
     A                                      COLOR(BLU)
8.01 A                                 25106'F10=Se bestilling'
     A                                      COLOR(BLU)
8.01 A                                 26  2'F14=Oppdater ordre'
     A                                      COLOR(BLU)
     A          R B5WIN
     A*%%TS  SD  20041202  081654  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 20 60)
     A                                      CA03
     A                                      CF08
     A                                      CA12
     A                                  2  2' Linjedetaljer '
     A                                      DSPATR(RI)
     A                                  2 30'Status . . .'
     A            B5STAT        12A  O  2 43
     A                                  4  2'Linjenr. .'
     A            B5LINN         6Y 0O  4 13EDTCDE(Z)
     A                                  4 30'Lev.linjenr.'
     A            B5LLIN         6Y 0O  4 43EDTCDE(Z)
     A                                  5  2'GTIN-nr. .'
7.03 A            B5EANN        14Y 0O  5 13EDTCDE(Z)
     A                                  5 30'Nobbnr . . .'
     A            B5NOBB         8Y 0O  5 43EDTCDE(Z)
     A                                  6  2'Varenr . .'
     A            B5EVNR         8Y 0O  6 13
     A                                  6 30'Lev.varenr..'
     A            B5LVNR        15A  O  6 43
     A            B5TEK1        35A  O  8  2
     A            B5TEK2        35A  O  9  2
6.12 A                                 11  2'Best:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
6.12 A            B5BNUM         6A  O 11  9
6.12 A                                      COLOR(WHT)
     A                                 11 30'Fakt:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Ant.:'
     A            B5BKVA        14Y 3O 12  8EDTWRD('         0 ,   ')
     A            B5BENH         3A  O 12 24
     A                                 12 30'Ant.:'
     A            B5LKVA        14Y 3O 12 36EDTWRD('         0 ,   ')
     A            B5LENH         3A  O 12 52
     A                                 13  2'Pris:'
     A            B5BPRI        13Y 2O 13  8EDTWRD('         0 ,  ')
     A            B5BPEN         3A  O 13 24
     A                                 13 30'Netp:'
     A            B5LPRI        13Y 2O 13 36EDTWRD('         0 ,  ')
     A            B5PENH         3A  O 13 52
     A                                 14  2'Kost:'
     A            B5BKOS        13Y 2O 14  8EDTWRD('         0 ,  ')
     A            B5BKEN         3A  O 14 24
     A                                 14 30'Omr.:'
     A            B5OPRI        13Y 2O 14 36EDTWRD('           ,  ')
     A            B5OENH         3A  O 14 52
6.12 A                                 15  2'Ordre:'
6.12 A                                      DSPATR(UL)
6.12 A                                      COLOR(WHT)
6.12 A            B5ONUM         6A  O 15  9
6.12 A                                      COLOR(WHT)
     A                                 15 30'Nobp:'
     A            B5NPRI        13Y 2O 15 36EDTWRD('         0 ,  ')
     A            B5NENH         3A  O 15 52
6.12 A                                 16  2'Ant.:'
6.12 A            B5FKVA        14Y 3O 16  8EDTWRD('         0 ,   ')
6.12 A            B5FENH         3A  O 16 24
     A                                 16 30'Frad:'
     A            B5FRAD        13Y 2O 16 36EDTWRD('         0 ,  ')
6.12 A                                 17  2'Kost:'
6.12 A            B5FKOS        14Y 3O 17  8EDTWRD('         0 ,   ')
     A                                 17 30'Till:'
     A            B5TILL        13Y 2O 17 36EDTWRD('         0 ,  ')
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 19 17'F8=Detaljer'
     A                                      COLOR(BLU)
     A          R B6WIN
     A*%%TS  SD  20031028  145444  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 12 54)
     A                                  2  2' Tillegg/fradrag '
     A                                      DSPATR(RI)
     A                                  2 25'A=Fradrag C=Tillegg'
     A            B6SIG1         1A  O  4  2
     A            B6KOD1         3A  O  4  4
     A            B6TEK1        35A  O  4  8
     A            B6BEL1         9Y 2O  4 44EDTWRD('       , 0')
     A            B6SIG2         1A  O  5  2
     A            B6KOD2         3A  O  5  4
     A            B6TEK2        35A  O  5  8
     A            B6BEL2         9Y 2O  5 44EDTWRD('       , 0')
     A            B6SIG3         1A  O  6  2
     A            B6KOD3         3A  O  6  4
     A            B6TEK3        35A  O  6  8
     A            B6BEL3         9Y 2O  6 44EDTWRD('       , 0')
     A            B6SIG4         1A  O  7  2
     A            B6KOD4         3A  O  7  4
     A            B6TEK4        35A  O  7  8
     A            B6BEL4         9Y 2O  7 44EDTWRD('       , 0')
     A            B6SIG5         1A  O  8  2
     A            B6KOD5         3A  O  8  4
     A            B6TEK5        35A  O  8  8
     A            B6BEL5         9Y 2O  8 44EDTWRD('       , 0')
     A            B6SIG6         1A  O  9  2
     A            B6KOD6         3A  O  9  4
     A            B6TEK6        35A  O  9  8
     A            B6BEL6         9Y 2O  9 44EDTWRD('       , 0')
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20080227  164406  TSV         REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 16 65)
     A                                  1  2' Partsinformasjon '
     A                                      DSPATR(RI)
     A            B7NUM01       13Y 0O  3 21EDTCDE(Z)
     A            B7TXT01       30A  O  3 35
     A            B7HDG01       18A  O  3  2
     A            B7HDG02       18A  O  4  2
     A            B7NUM02       13Y 0O  4 21EDTCDE(Z)
     A            B7TXT02       30A  O  4 35
     A            B7HDG03       18A  O  5  2
     A            B7NUM03       13Y 0O  5 21EDTCDE(Z)
     A            B7TXT03       30A  O  5 35
     A            B7HDG04       18A  O  6  2
     A            B7NUM04       13Y 0O  6 21EDTCDE(Z)
     A            B7TXT04       30A  O  6 35
     A            B7HDG05       18A  O  7  2
     A            B7NUM05       13Y 0O  7 21EDTCDE(Z)
     A            B7TXT05       30A  O  7 35
     A            B7HDG06       18A  O  8  2
     A            B7NUM06       13Y 0O  8 21EDTCDE(Z)
     A            B7TXT06       30A  O  8 35
     A            B7HDG07       18A  O  9  2
     A            B7NUM07       13Y 0O  9 21EDTCDE(Z)
     A            B7TXT07       30A  O  9 35
     A            B7HDG08       18A  O 10  2
     A            B7NUM08       13Y 0O 10 21EDTCDE(Z)
     A            B7TXT08       30A  O 10 35
     A            B7HDG09       18A  O 11  2
     A            B7NUM09       13Y 0O 11 21EDTCDE(Z)
     A            B7TXT09       30A  O 11 35
     A            B7HDG10       18A  O 12  2
     A            B7NUM10       13Y 0O 12 21EDTCDE(Z)
     A            B7TXT10       30A  O 12 35
     A            B7HDG11       18A  O 13  2
     A            B7NUM11       13Y 0O 13 21EDTCDE(Z)
     A            B7TXT11       30A  O 13 35
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B8WIN
     A*%%TS  SD  20020924  124732  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Tillegg/fradrag '
     A                                      DSPATR(RI)
     A            B8TEK0        30A  O  4  2
     A            B8BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B8TEK1        30A  O  5  2
     A            B8BEL1        12Y 2O  5 33EDTWRD('          , 0-')
     A            B8TEK2        30A  O  6  2
     A            B8BEL2        12Y 2O  6 33EDTWRD('          , 0-')
     A            B8TEK3        30A  O  7  2
     A            B8BEL3        12Y 2O  7 33EDTWRD('          , 0-')
     A            B8TEK4        30A  O  8  2
     A            B8BEL4        12Y 2O  8 33EDTWRD('          , 0-')
     A            B8TEK5        30A  O  9  2
     A            B8BEL5        12Y 2O  9 33EDTWRD('          , 0-')
     A            B8TEK6        30A  O 10  2
     A            B8BEL6        12Y 2O 10 33EDTWRD('          , 0-')
     A            B8TEK7        30A  O 11  2
     A            B8BEL7        12Y 2O 11 33EDTWRD('          , 0-')
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20050706  135833  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 46)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Ordretotaler '
     A                                      DSPATR(RI)
     A            B9TEK0        30A  O  4  2
     A            B9BEL0        12Y 2O  4 33EDTWRD('          , 0-')
     A            B9TEK1        30A  O  5  2
     A            B9BEL1        12Y 2O  5 33EDTWRD('        0 ,  -')
     A            B9TEK4        30A  O  6  2
     A            B9BEL4        12Y 2O  6 33EDTWRD('        0 ,  -')
     A            B9TEK5        30A  O  7  2
     A            B9BEL5        12Y 2O  7 33EDTWRD('        0 ,  -')
     A            B9TEK6        30A  O  8  2
     A            B9BEL6        12Y 2O  8 33EDTWRD('        0 ,  -')
     A            B9TEK7        30A  O  9  2
     A            B9BEL7        12Y 2O  9 33EDTWRD('        0 ,  -')
     A            B9TEK8        30A  O 10  2
     A            B9BEL8        12Y 2O 10 33EDTWRD('        0 ,  -')
     A                                 12  2'Inkluderte tillegg/fradrag :'
     A                                      COLOR(BLU)
     A            B9TEK2        30A  O 13  2
     A            B9BEL2        12Y 2O 13 33EDTWRD('          , 0-')
     A            B9TEK3        30A  O 14  2
     A            B9BEL3        12Y 2O 14 33EDTWRD('          , 0-')
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B11WIN
     A*%%TS  SD  20041202  125051  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 19 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Referanseinformasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Vår referanse . . . . .'
     A            B11VRF        30   O  4 26
     A                                  5  2'Deres referanse . . . .'
     A            B11DRF        30A  O  5 26
     A                                  6  2'Bestillingsnr./kjøper .'
     A            B11BST        20A  O  6 26
     A                                  7  2'Ordrenr./selger . . . .'
     A            B11ORD        20A  O  7 26
     A                                  8  2'Fakt.nr (krediteres). .'
     A            B11FAK        20A  O  8 26
     A                                  9  2'Pakkseddelnr. . . . . .'
     A            B11PAK        20A  O  9 26
     A                                 10  2'Kundereferansenr. . . .'
     A            B11KUN        20A  O 10 26
     A                                 11  2'Prosjektnr. . . . . . .'
     A            B11PRO        20A  O 11 26
     A                                 12  2'Leverandørs fakturanr .'
     A            B11LVF        20A  O 12 26
     A                                 13  2'Lev.fakt.nr. (kredit.).'
     A            B11OFA        20A  O 13 26
     A                                 14  2'Prislistenr.. . . . . .'
     A            B11PLN        20A  O 14 26
     A                                 15  2'Kontraktsnr.. . . . . .'
     A            B11KON        20A  O 15 26
     A                                 16  2'KID nr. . . . . . . . .'
     A            B11KID        30A  O 16 26
     A                                 18  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.21 A          R BSWIN
6.21 A*%%TS  SD  20050523  130713  GRO530      REL-V5R2M0  5722-WDS
6.21 A                                      WINDOW(*DFT 9 35)
6.21 A                                      CA03
6.21 A                                      CA12
6.21 A                                  2  2' Velg utskriftstype :'
6.21 A                                      DSPATR(RI)
6.21 A                                  5  2'Skriv dokumentkopi . .'
6.21 A            BSDKOP         1A  B  5 25COLOR(YLW)
7.xx A                                  5 28'1/0'
6.21 A                                  6  2'Skriv avviksliste. . .'
6.21 A            BSAVVK         1A  B  6 25COLOR(YLW)
7.xx A                                  6 28'1/0'
6.21 A                                  8  2'F3=Gå tilbake'
6.21 A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
