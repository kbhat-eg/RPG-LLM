     A*%%TS  SD  20040510  092457  HKO         REL-V5R2M0  5722-WDS
     A*%%EC
     A* 270315 BOF, RETTET FEILMELDING
7.xx A* 7.xx  240216  bsa  Justeringer basert på ny GUI og regler
     A*---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040504  120651  HKO         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LDOR    R        O  9  4REFFLD(VJLDOR)
     A            B1OGRP    R        O  9 11REFFLD(VJOGRP)
     A            B1HGRP    R        O  9 14REFFLD(VJHGRP)
     A            B1UGRP    R        O  9 17REFFLD(VJUGRP)
     A            B1KPRI    R        O  9 21REFFLD(VJKPRI)
     A            B1VARE    R   -7   O  9 23REFFLD(VJVARE)
     A            B1LETY    R        O  9 32REFFLD(VJLETY)
     A            B1KOPA    R        O  9 35REFFLD(VJKOPA)
     A            B1KOBE    R        O  9 43REFFLD(VJKOBE)
     A            B1SAPA    R        O  9 53REFFLD(VJSAPA)
     A            B1SABE    R        O  9 61REFFLD(VJSABE)
     A            B1SAPR    R   -1   O  9 71REFFLD(VJSAPR)
     A                                      EDTCDE(4)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040504  120651  HKO         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 62'Påslag:'
     A                                      COLOR(BLU)
     A            B2PASL    R        O  4 70REFFLD(VJPASL)
     A                                      COLOR(YLW)
7.xx A                                  4  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 15'Varegruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 27'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 31'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 42'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 14'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 17'Hgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 21'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 23'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 35'Kost'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 45'Kost'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 53'Salg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 62'Salg'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 71'Salgspris'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 35'%-sats'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'beløp'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'%-sats'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 62'beløp'
7.xx A                                      COLOR(WHT)
7.xx A**                                6  2'V Lev.   Varegrp.  P Varenr.  L   -
7.xx A**                                6  2'V Lev.   Varegrp.  P Varenr.  L   -
7.xx A**                                6  2'V Lev.   Varegrp.  P Varenr.  L   -
7.xx A**                                      Kostpris          Salgspris      -
7.xx A**                                    Salgspris'
7.xx A**                                    COLOR(WHT)
7.xx A**                                7 35'%-sats    beløp   %-sats   beløp'
7.xx A**                                    COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  5  2'+'
7.xx A            B2LDOR    R        B  5  4REFFLD(VJLDOR)
     A  31                                  ERRMSGID(VMJ0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMJ0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 11'+'
7.xx A            B2OGRP    R        B  5 13REFFLD(VJOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 16'+'
7.xx A            B2HGRP    R        B  5 18REFFLD(VJHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 21'+'
7.xx A            B2UGRP    R        B  5 23REFFLD(VJUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2KPRI    R        B  5 27REFFLD(VJKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 29'+'
7.xx A            B2VARE    R   -7   B  5 31REFFLD(VJVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 40'+'
7.xx A            B2LETY    R        B  5 42REFFLD(VJLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  103706  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny   F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040510  090952  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  4 31REFFLD(VJLDOR)
     A  31                                  ERRMSGID(VMJ0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMJ0060 *LIBL/QMFOFAK 32)
     A  39                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LNAV        30A  O  4 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(VJOGRP)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Hovedgruppe. . . . . . . . +'
     A            C1HGRP    R        B  6 31REFFLD(VJHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 41COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Undergruppe. . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(VJUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 41COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Priskode . . . . . . . . . .'
     A            C1KPRI    R        B  8 31REFFLD(VJKPRI)
     A  38                                  ERRMSGID(VMV0120 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varenummer . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(VJVARE)
     A  45                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  9 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Leveringstype. . . . . . . +'
     A            C1LETY    R        B 10 31REFFLD(VJLETY)
     A  41                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 10 41COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990820  121151  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Påslag finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20040510  091114  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av påslag'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  4 31REFFLD(VJLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  4 42COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Overgruppe . . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(VJOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OTXT        35A  O  5 42COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Hovedgruppe. . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(VJHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HTXT        35A  O  6 42COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Undergruppe. . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(VJUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UTXT        35A  O  7 42COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Priskode . . . . . . . . . .'
     A            C1KPRI    R        O  8 31REFFLD(VJKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Varenummer . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(VJVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        O  9 42REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Leveringstype. . . . . . . .'
     A            C1LETY    R        O 10 31REFFLD(VJLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LBES        35A  O 10 42COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Basisenhet . . . . . . . . .'
     A            C2ENHE    R        O 12 31REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Overstyrt salgspris:'
     A                                      COLOR(WHT)
     A                                 15  2'Salgspris inkl.mva . . . . .'
     A            C2SAIM    R        B 15 31REFFLD(VJSAPR)
     A  33                                  ERRMSGID(VMJ0110 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(VMJ0120 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        B 16 31REFFLD(VJSAPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18  2'Påslag/dekning:'
     A                                      COLOR(WHT)
     A                                 18 42'Beløpspåslag:'
     A                                      COLOR(WHT)
     A                                 19  2'Påslag på innkjøpspris . (%)'
     A            C2INPA    R        B 19 31REFFLD(VJINPA)
     A  31                                  ERRMSGID(VMJ0100 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(M)
     A                                 19 42'Påslag på innkjøpspris .(kr)'
     A            C2INBE    R        B 19 71REFFLD(VJINBE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  2'Påslag på kostpris . . . (%)'
     A            C2KOPA    R        B 20 31REFFLD(VJKOPA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20 42'Påslag på kostpris . . .(kr)'
     A            C2KOBE    R        B 20 71REFFLD(VJKOBE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21  2'Påslag på salgspris. . . (%)'
     A            C2SAPA    R        B 21 31REFFLD(VJSAPA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21 42'Påslag på salgspris. . .(kr)'
     A            C2SABE    R        B 21 71REFFLD(VJSABE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22  2'Dekningsgrad . . . . . . (%)'
     A            C2DEKN    R        B 22 31REFFLD(VJDEKN)
     A  32                                  ERRMSGID(VMJ0130 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C2EDAT         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C2EUSR    R        O 23 71REFFLD(VJEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20040510  092457  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette påslag '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør . . . .'
     A            D1LDOR    R        O  4 21REFFLD(VJLDOR)
     A                                  5  2'Varegruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(VJOGRP)
     A            D1HGRP    R        O  5 24REFFLD(VJHGRP)
     A            D1UGRP    R        O  5 27REFFLD(VJUGRP)
     A                                  6  2'Priskode . . . . .'
     A            D1KPRI    R        O  6 21REFFLD(VJKPRI)
     A                                  7  2'Varenummer . . . .'
     A            D1VARE    R        O  7 21REFFLD(VJVARE)
     A                                  8  2'Leveringstype. . .'
     A            D1LETY    R        O  8 21REFFLD(VJLETY)
     A                                 10  2'Kostpris-faktor. .'
     A            VJKOPA    R        O 10 21
     A                                 11  2'Kostpris-beløp . .'
     A            VJKOBE    R        O 11 21
     A                                 12  2'Salgspris-faktor .'
     A            VJSAPA    R        O 12 21
     A                                 13  2'Dekningsgrad . . .'
     A            VJDEKN    R        O 13 21
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20001109  145308  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 17 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Leverandør . . . +'
     A            K1LDOR    R        B  3 21REFFLD(VJLDOR)
     A  31                                  ERRMSGID(VMJ0050 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMJ0060 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMJ0030 *LIBL/QMFOFAK 33)
     A  39                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4  2'Varegruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(VJOGRP)
     A  35                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 24'+'
7.xx A            K1HGRP    R        B  4 26REFFLD(VJHGRP)
     A  36                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 29'+'
7.xx A            K1UGRP    R        B  4 31REFFLD(VJUGRP)
     A  37                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Priskode . . . . .'
     A            K1KPRI    R        B  5 21REFFLD(VJKPRI)
     A  38                                  ERRMSGID(VMV0120 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Varenummer . . . +'
     A            K1VARE    R        B  6 21REFFLD(VJVARE)
     A  45                                  ERRMSGID(FMV0020 *LIBL/QMFOFAK 45)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Leveringstype. . +'
     A            K1LETY    R        B  7 21REFFLD(VJLETY)
     A  41                                  ERRMSGID(YMY0020 *LIBL/QMFOFAK 41)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Leverandør . . . .'
     A            VJLDOR    R        O 10 21OVRDTA
     A                                 11  2'Varegruppe . . . .'
     A            VJOGRP    R        O 11 21OVRDTA
     A            VJHGRP    R        O 11 24OVRDTA
     A            VJUGRP    R        O 11 27OVRDTA
     A                                 12  2'Priskode . . . . .'
     A            VJKPRI    R        O 12 21OVRDTA
     A                                 13  2'Varenummer . . . .'
     A            VJVARE    R        O 13 21OVRDTA
     A                                 14  2'Leveringstype. . .'
     A            VJLETY    R        O 14 21OVRDTA
     A                                 16  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
