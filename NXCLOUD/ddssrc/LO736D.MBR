     A*%%TS  SD  20140530  133055  BOF630      REL-V7R1M0  5770-WDS
6.20 A* 6.20 310311  NHO  Brukerkontroll fra IP til IF
6.NU A* 6.30 300514  bof  Utvidelse mtp. nummerutvidelse
     A*
8.02 A*K0000 280923  bsa  Feil visning av bestilt antall, når
8.02 A*K0000              antallet er < 0.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20021210  155743  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 60)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Ordre-type  .'
     A                                      COLOR(WHT)
     A            A1FOTY    R        O  2 20REFFLD(LOOTYP)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            A1FTXT    R        O  2 26REFFLD(LONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  3  2'Best_nummer .'
     A                                      COLOR(WHT)
     A            A1NUMM    R        O  3 16REFFLD(LONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            A1SUFF    R        O  3 25REFFLD(LOSUFF)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            A1LENA    R        O  3 28REFFLD(LOLENA)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  5  5'Til ordre-type.'
     A            A1OTYP    R        B  5 22REFFLD(LOOTYP)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  31                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMA0260 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LM00310 *LIBL/QMFLAGR 33)
6.20 A  35                                  ERRMSGID(LM00301 *LIBL/QMFLAGR 35)
     A                                      COLOR(YLW)
     A                                  6  5'Varelinjer  . .'
     A            A1VLIN    R        B  6 22REFFLD(LÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1' '2')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A                                  6 26'0=Ingen spesifisering av linjer'
     A                                  7 26'1=Plukk bare linjer som oppgis'
     A                                  8 26'2=Plukk alle linjer med unntak'
     A                                 10  5'Til Lager . . .'
     A N49                                  DSPATR(ND)
     A            A1LAGE    R        B 10 22REFFLD(LOLAGE)
     A  34                                  ERRMSGID(LMA0160 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A N49                                  DSPATR(ND)
     A N49                                  DSPATR(PR)
     A            A1LATX        20A  O 10 26OVRDTA
     A                                      OVRATR
     A N49                                  DSPATR(ND)
     A                                 11  5'Tilleggsoppl. .'
     A            A1TILL    R        B 11 23REFFLD(LÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A                                 11 26'1=Ja, 0=Nei'
     A                                 12  5'Dropp fast tekst'
     A            A1PTXT    R        B 12 23REFFLD(LÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A                                 12 26'1=Ja, 0=Nei'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140530  133055  BOF630      REL-V7R1M0  5770-WDS
     A            B1VALG         2A  B  8  2COLOR(YLW)
     A                                      VALUES('2 ' '4 ' '7 ' '  ')
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1SUFF    R        O  8 14REFFLD(LKPSUF)
     A            B1OTYP    R        O  8 17REFFLD(LKPOTY)
     A            B1WSID    R        O  8 21REFFLD(LKPWSD)
     A            B1USER    R        O  8 33REFFLD(LKPUSR)
     A            B1PALT    R        O  8 45REFFLD(LKPALT)
     A            B1PTXT    R        O  8 55REFFLD(LKPTXT)
     A            B1NUMM    R        O  8  5REFFLD(LKPONR)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  082057  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CA03
     A                                      CF05
     A                                      CF07
     A                                      CF15
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise utplukkede bestillingsnummer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre detaljlinjer'
     A                                      COLOR(BLU)
     A                                  5 25'4=Slett utplukk'
     A                                      COLOR(BLU)
     A                                  5 43'7=Endre tilleggsopplysninger'
     A                                      COLOR(BLU)
     A                                  6  2'V  Bestnr      OT  Skjerm-id   Bru-
     A                                      ker-id   Alt.                      -
     A                                             '
     A                                      COLOR(WHT)
     A            B2NUMM    R        B  7  5REFFLD(LKPONR)
     A                                      DLTCHK
     A                                      OVRDTA
     A  31                                  ERRMSGID(LMO0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMO0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20050420  084258  GRO530      REL-V5R2M0  5722-WDS
     A                                 23  2'F1=Bla-alt'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 60'F15=Start utskrift'
     A                                      COLOR(BLU)
     A                                 23 42'F7=Dato-utplukk'
     A                                      COLOR(BLU)
     A          R B4BLD
     A*%%TS  SD  19950920  153356  ASP         REL-V2R3M0  5738-PW1
6.12 A                                      WINDOW(*DFT 10 32)
     A                                      CA03
     A                                      PUTOVR
6.12 A                                  2  2' Slette utplukk for bestilling'
6.12 A                                      DSPATR(RI)
6.12 A                                  4  2'Bestilling . . . .'
6.12 A            B4NUMM    R        O  4 21REFFLD(LONUMM)
     A                                      OVRDTA
6.12 A                                      COLOR(YLW)
6.12 A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7BLD
     A*%%TS  SD  19961118  185955  KRKRI       REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 14 65)
     A                                      CA03
     A                                      PUTOVR
     A                                  2  2'Bestillingsnr . :'
     A                                      COLOR(WHT)
     A            B7NUMM    R        O  2 21REFFLD(LONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  4  2'Antall kolli. . .'
     A            B7KOLI    R        B  4 21REFFLD(LOKOLI)
     A                                      OVRDTA
     A                                      EDTWRD('        ,   ')
     A                                      CHECK(RB)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  5  2'Netto KG  . . . .'
     A            B7NETT    R        B  5 21REFFLD(LONETT)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                  6  2'Brutto KG . . . .'
     A            B7BRUT    R        B  6 21REFFLD(LOBRUT)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                  7  2'Forsendelsesmåte.'
     A            B7LMTX    R        B  7 21REFFLD(LOLMTX)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9 19'Beløp'
     A                                  9 31'Tekst'
     A                                 10  2'Frakt     . :'
     A            B7FRPR         9Y 2B 10 17EDTWRD('     0 ,  ')
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            B7FRTE    R        B 10 31REFFLD(LDTEK1)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  2'Emballasje  :'
     A            B7EMPR         9Y 2B 11 17EDTWRD('     0 ,  ')
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            B7EMTE    R        B 11 31REFFLD(LDTEK1)
     A                                      OVRDTA
     A*                                     OVRATR
     A                                      COLOR(YLW)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1SFL                     SFL
     A*%%TS  SD  20140530  133055  BOF630      REL-V7R1M0  5770-WDS
     A            D1KODE         1A  B  8  2
     A N21                                  DSPATR(PR)
     A  21                                  COLOR(YLW)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      VALUES('1' '3' '4' '5' '9' ' ')
     A            D1LINE    R        O  8  4REFFLD(LDLINE)
     A  55
     AO 56
     AO 57
     AO 58                                  DSPATR(RI)
     A  55                                  COLOR(RED)
     A  56                                  COLOR(TRQ)
     A  58                                  COLOR(PNK)
     A  57                                  COLOR(BLU)
     A  59                                  COLOR(GRN)
8.02 A            D1LEVE         8Y 3B  8 24EDTWRD('     ,   ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
8.02 A            D1ANTA    R   -3   O  8 34REFFLD(LDANTA)
8.02 A                                      EDTCDE(M)
8.02 A**                                    EDTWRD('     ,   ')
     A            D1ENH1    R        O  8 45REFFLD(LDENH1)
     A            D1TEK1    R   -6   O  8 49REFFLD(LDTEK1)
     A            D1EVAR    R        H      REFFLD(LDVARE)
     A            D1VARE    R   -5   O  8  9REFFLD(LFRFR/LDVARE *LIBL/LFRF)
     A            D1VTYP    R        O  8 79REFFLD(FFRFR/VVVTYP *LIBL/FFRF)
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20140530  133055  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
8.xx A                                      CF10
8.xx A                                      CF11
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
8.xx A* 15                                  SFLEND(*MORE)
8.xx A  15                                  SFLEND
     A            SVRN02         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av enkeltlinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bestnr'
     A                                      COLOR(WHT)
     A            D2SUFF    R        O  4 18REFFLD(LOSUFF)
     A                                  4 21'Lev.dør'
     A                                      COLOR(WHT)
     A            D2LDOR    R        O  4 29REFFLD(LOLDOR)
     A            D2LENA    R        O  4 36REFFLD(LOLENA)
     A                                  5  3'1=Plukkes'
     A                                      COLOR(BLU)
     A                                  5 14'3=Kopieres'
     A                                      COLOR(TRQ)
     A                                  5 26'4=Holdes tilbake'
     A                                      COLOR(RED)
     A                                  5 44'5=Ikke restnoter'
     A                                      COLOR(PNK)
     A                                  5 62'9=Opphev'
     A                                      COLOR(BLU)
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Linje'
     A                                      COLOR(WHT)
     A                                  6 11'Varenr.'
     A                                      COLOR(WHT)
     A                                  6 27'Antall'
     A                                      COLOR(WHT)
     A                                  6 36'Bestilt'
     A                                      COLOR(WHT)
     A                                  6 45'Enh'
     A                                      COLOR(WHT)
     A                                  6 49'Varetekst'
     A                                      COLOR(WHT)
     A            D2LINE    R        B  7  4REFFLD(LDLINE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  6 79'T'
     A                                      COLOR(WHT)
     A            D2NUMM    R        O  4  9REFFLD(LONUMM)
     A          R D2CMD
     A*%%TS  SD  19950920  153356  ASP         REL-V2R3M0  5738-PW1
     A                                 23 25'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 40'F5=Forny'
     A                                      COLOR(BLU)
     A          R H1BLD
     A*%%TS  SD  20021210  145330  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 40)
     A                                      CA03
     A                                      PUTOVR
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre-type'
     A            H1OTYP    R        B  4 14REFFLD(LOOTYP)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 19'Blank gir alle'
     A                                  6  2'Bla bakfra'
     A            H1VALG    R        B  6 14REFFLD(LÅVALG)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  6 19'1=Ja, 0=Nei'
     A          R F7BLD
     A*%%TS  SD  20050419  152521  GRO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 9 40)
     A                                      CA03
     A                                      CF04
     A                                      CF18
     A                                      KEEP
     A                                      PUTOVR
     A                                  2  2'Angi fra- og til fakturadato for p-
     A                                      lukk'
     A                                      COLOR(WHT)
     A                                  4  2'Ordretype. . . . . .'
     A            F7OTYP    R        B  4 24REFFLD(LOOTYP)
     A  31                                  ERRMSGID(LMO0140 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  5  2'Fra fakturadato. . .'
     A            F7FDAT         6Y 0B  5 24OVRDTA
     A  32                                  ERRMSGID(LMO0141 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  6  2'Til fakturadato. . .'
     A            F7TDAT         6Y 0B  6 24OVRDTA
     A  33                                  ERRMSGID(LMO0142 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
