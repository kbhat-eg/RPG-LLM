     A*%%TS  SD  20020820  091844  HKO         REL-V5R1M0  5722-WDS
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       020816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20020820  091844  HKO         REL-V5R1M0  5722-WDS
     A*                                     WINDOW(*DFT 15 64)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av matchede varer '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Leverandør. . .+'
     A            B1LDO2    R        B  4 19REFFLD(JOLDO2)
     A  31                                  ERRMSGID(JML0020 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1LNAV    R   -5   O  4 34REFFLD(JLNAVN)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Egen varegruppe+'
7.xx A            B1OGRP    R        B  6 19REFFLD(JOOGRP)
     A  32                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
7.xx A                                  6 22'+'
7.xx A            B1HGRP    R        B  6 24REFFLD(JOHGRP)
     A  33                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
7.xx A                                  6 27'+'
7.xx A            B1UGRP    R        B  6 29REFFLD(JOUGRP)
     A  34                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1UTXT        30A  O  6 34COLOR(YLW)
     A                                  8  2'Matche-status .'
7.xx A            B1KODE    R        B  8 19REFFLD(JOKODE)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3 4)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
