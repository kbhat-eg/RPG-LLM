7.xx  * 7.xx  190318 bkv  Endringer reltart til ny GUI.
7.01  * 7.00  270619 bsa  Mottak av EDI bestilling/ordre
7.02  * 7.00  090719 bsa  Viser tekst på EDI melding
7.04  * 7.00  281019 bsa  Gir mulighet for å bytte varenummer.
7.05  * 7.01  260319 ask  Lagt inn visning av varetkst 2 pga. Winner integrasjon
7.07  * 7.01  030120 bsa  Viser egen kode for om varen er overført.
      *
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20021021  130850  HKO         REL-V5R1M0  5722-WDS
     A            B1LINE    R        H      REFFLD(NDLINE)
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
7.04 A                                      VALUES(2 4 5 8 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VARE         8A  O  8  4
7.07 A            B1TEK1        22A  O  8 13
7.07 A            B1ANTA    R        O  8 36REFFLD(NDANTA)
7.07 A            B1ENHE    R        O  8 50REFFLD(NDENHE)
7.07 A            B1NTPR    R        O  8 54REFFLD(NDNTPR)
7.07 A            B1LETY    R        O  8 66REFFLD(NDLETY)
7.07 A            B1LDAT         6Y 0O  8 68EDTWRD('  .  .  ')
7.07 A            B1STAT         1Y 0O  8 77EDTCDE(Z)
7.07 A            B1OVER         1Y 0O  8 79EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF13
7.02 A                                      CF15
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eksterne ordrelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 29'Referanse . . .'
     A                                      COLOR(BLU)
     A            B2NREF    R        O  5 45REFFLD(NONREF)
     A                                      COLOR(BLU)
     A            B2KNAV        16A  O  5 61COLOR(BLU)
7.04 A                                  6  2'2=Endre  4=Slett  5=Vis  8=Bytt-
7.04 A                                       varenummer'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  4'Varenr'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 13'Varetekst/melding'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 42'Antall'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 50'Enh'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 54'Nettopris'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 66'L'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 68'Lev.dato'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 77'S'
7.xx A                                      COLOR(WHT)
7.07 A                                  7 79'O'
7.xx A                                      COLOR(WHT)
7.xx A*                                 7  2'V Varenr   Varetekst/melding      -
7.xx A*                                           Antall    Enh  Nettopris  L L-
7.xx A*                                     ev.dato S'
7.xx A*                                      COLOR(WHT)
7.xx A                                  4  2'Vnummr'
7.xx A                                      COLOR(WHT)
7.xx A            B2VARE         8A  B  4  9COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5  2'Vtekst'
7.xx A                                      COLOR(WHT)
7.xx A            B2TEK1        19A  B  5  9COLOR(YLW)
     A                                      OVRDTA
     A                                  4 29'Ekst. system. .'
     A                                      COLOR(BLU)
     A            B2FSYS    R        O  4 45REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A                                      COLOR(BLU)
     A            NKBESK    R        O  4 50REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
     A                                      COLOR(BLU)
     A          R B2CMD
     A*%%TS  SD  20021001  150320  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F13=Sta-
     A                                      tuskoder'
7.02 A                                      COLOR(BLU)
7.02 A                                 23 46'F15=Tekst'
7.02 A                                      COLOR(BLU)
7.02 A  32                                  DSPATR(RI)
     A          R C2BLD
     A*%%TS  SD  20030930  102532  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
7.02 A                                      CF15
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NREF    R        H      REFFLD(NDNREF)
     A            C2LINE    R        H      REFFLD(NDLINE)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eksterne ordrelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Varetekst. . . . . . . . . .'
     A            C2TEK1    R        O  6 31REFFLD(NDTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.05 A            C2TEK2    R        O  7 31REFFLD(NDTEK2)
7.05 A                                      COLOR(YLW)
7.05 A                                      OVRDTA
7.05 A                                  8  2'Nettopris. . . . . . . . . .'
7.05 A            C2NTPR    R        O  8 31REFFLD(NDNTPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.05 A                                 10  2'Varenr . . . . . . . . . . +'
7.05 A            C2VARE         8A  B 10 31COLOR(YLW)
     A  31                                  ERRMSGID(VMV0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 32)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.05 A            C2VTXT    R        O 10 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.05 A                                 11  2'Antall . . . . . . . . . . .'
7.05 A            C2ANTA    R        B 11 31REFFLD(NDANTA)
     A  33                                  ERRMSGID(FMD0151 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.05 A                                 12  2'Enhet. . . . . . . . . . . +'
7.05 A            C2ENHE    R        B 12 31REFFLD(NDENHE)
     A  34                                  ERRMSGID(FMD0160 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.05 A                                 13  2'GTIN nummer. . . . . . . . .'
7.05 A            C2GTIN    R        O 13 31REFFLD(FDEANN)
7.01 A                                      OVRDTA
7.05 A            C2GNOB    R        O 13 47REFFLD(FDNOBB)
7.01 A                                      OVRDTA
7.05 A            C2GENH    R        O 13 56REFFLD(NDENHE)
7.01 A                                      OVRDTA
7.05 A                                 14  2'Lev.type . . . . . . . . . +'
7.05 A            C2LETY    R        B 14 31REFFLD(NDLETY)
     A  35                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.05 A            C2TTXT    R        O 14 34REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.05 A                                 15  2'Lev.dato . . . . . . . . . .'
7.05 A            C2LDAT         6Y 0B 15 31COLOR(YLW)
     A  36                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(FMD0205 *LIBL/QMFOFAK 37)
     A                                      EDTWRD('  .  .  ')
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(NDEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
7.02 A                                 23 32'F15=Tekst'
7.02 A                                      COLOR(BLU)
7.02 A  32                                  DSPATR(RI)
     A                                  4  2'Eksternt system. . . . . . .'
     A            NDFSYS    R        O  4 31REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A                                      COLOR(YLW)
     A            NKBESK    R        O  4 36REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
     A                                      COLOR(YLW)
     A          R C3BLD
     A*%%TS  SD  20030929  093955  MHA         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av eksterne ordrelinje-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Melding:'
     A            C3MELD        70A  O  6  2COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20030929  132220  MHA         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Ekstern ordre - linje '
     A                                      DSPATR(RI)
     A                                  4  2'Referansenr. . . .'
     A            D1NREF    R        O  4 21REFFLD(NDNREF)
     A                                  5  2'Linjenummer. . . .'
     A            D1LINE    R        O  5 21REFFLD(NDLINE)
     A                                  8  2'Varenr . . . . . .'
     A            NDVARE    R        O  8 21
     A                                  9  2'Varetekst. . . . .'
     A            NDTEK1    R        O  9 21
     A                                 10  2'Antall . . . . . .'
     A            NDANTA    R        O 10 21
     A                                 11  2'Enhet. . . . . . .'
     A            NDENHE    R        O 11 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  6  2'Eksternt system. .'
     A            NDFSYS    R        O  6 21REFFLD(NODTPFR/NDFSYS *LIBL/NODTPF)
     A            NKBESK    R        O  6 26REFFLD(NKEOPFR/NKBESK *LIBL/NKEOPF)
7.02  *
7.02 A          R B15WIN
7.02 A*%%TS  SD  20050311  130040  GRO530      REL-V5R2M0  5722-WDS
7.02 A                                      WINDOW(*DFT 11 72)
7.02 A                                      CA03
7.02 A                                      CA12
7.02 A                                  2  2'Tekst '
7.02 A                                      DSPATR(RI)
7.02 A            B15TX1        70      4  2
7.02 A            B15TX2        70      5  2
7.02 A            B15TX3        70      6  2
7.02 A            B15TX4        70      7  2
7.02 A            B15TX5        70      8  2
7.02 A                                 10  2'F3=Gå tilbake'
7.02 A                                      COLOR(BLU)
7.04 A          R S8WIN
7.04 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.04 A                                      WINDOW(*DFT 11 60)
7.04 A                                      CA03
7.04 A                                      CF04
7.04 A                                      CA12
7.04 A                                      PUTOVR
7.04 A                                  2  2' Bytte varenyummer '
7.04 A                                      DSPATR(RI)
7.04 A                                  4  2'Varenummer . . . . . . . . .'
7.04 A            S8VARE         8A  O  4 32
7.04 A                                      COLOR(YLW)
7.04 A                                      OVRDTA
7.04 A                                  6  2'Erstatt med varenummer . . +'
7.04 A            S8NYVA         8A  B  6 32
7.04 A  31                                  ERRMSGID(FMD0730 *LIBL/QMFOFAK 31)
7.04 A                                      COLOR(YLW)
7.04 A                                      OVRDTA
7.04 A                                 10  2'F3=Gå tilbake'
7.04 A                                      COLOR(BLU)
7.04 A                                 10 17'F4=Spørring'
7.04 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
