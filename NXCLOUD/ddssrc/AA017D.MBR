     A*%%TS  SD  20140618  063503  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA017D
     A* Beskrivelse . . : Nummer-serier, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230604 AMD  Nytt program
6.NU A*       180614 BOF  Utvidet nummerserie til 8 siffer
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20040623  102446  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1FIRM    R        O  8  5REFFLD(ANFIRM)
     A            B1FELL    R        O  8 10REFFLD(ANFELL)
     A            B1TYPE    R        O  8 18REFFLD(ANTYPE)
     A            B1FAST    R        O  8 22REFFLD(ANFAST)
     A            B1TXT1    R        O  8 35REFFLD(ANTXT1)
     A            B1SIST    R        O  8 69REFFLD(ANSIST)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20040624  085306  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nummer-serier  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Firma'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 10'Ident'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Firma'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 10'Ident '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 18'Tp'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 22'Fast begrep'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 35'Beskrivelse        '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 67'Sist brukte'
     A                                      COLOR(WHT)
7.xx A            B2FIRM    R        B  5  5REFFLD(ANFIRM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2FELL    R        B  5 10REFFLD(ANFELL)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040624  124024  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nummer-serier  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firma. . . . . . . . . . . .'
     A            C1FIRM    R        B  5 31REFFLD(ANFIRM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  6  2'Identifikasjonsbegrep. . . .'
     A            C1FELL    R        B  6 31REFFLD(ANFELL)
     A  31                                  ERRMSGID(AMN0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Type . . . . . . . . . . . .'
     A            C1TYPE    R        B  7 31REFFLD(ANTYPE)
     A                                      COLOR(YLW)
     A                                  8  2'Fast begrep. . . . . . . . .'
     A            C1FAST    R        B  8 31REFFLD(ANFAST)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040623  094318  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kombinasjon finnes fra før '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140618  063503  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av nummer-serier  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Firma. . . . . . . . . . . .'
     A            C1FIRM    R        O  5 31REFFLD(ANFIRM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Identifikasjonsbegrep. . . .'
     A            C1FELL    R        O  6 31REFFLD(ANFELL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Type . . . . . . . . . . . .'
     A            C1TYPE    R        O  7 31REFFLD(ANTYPE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Fast begrep. . . . . . . . .'
     A            C1FAST    R        O  8 31REFFLD(ANFAST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Beskrivelse 1. . . . . . . .'
     A            C2TXT1    R        B 11 31REFFLD(ANTXT1)
     A  31                                  ERRMSGID(AMN0100 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 12  2'Beskrivelse 2. . . . . . . .'
     A            C2TXT2    R        B 12 31REFFLD(ANTXT2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 14  2'Start pånytt ved slutt . . .'
     A            C2WRAP    R        B 14 31REFFLD(ANWRAP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15  2'Nullstilles ved årslutt. . .'
     A            C2ÅRNU    R        B 15 31REFFLD(ANÅRNU)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  2'Manuell/Automatisk serie . .'
     A            C2AUTO    R        B 16 31REFFLD(ANAUTO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      VALUES('A' 'M')
     A                                 17  2'Max 8 posisjoner . . . . . .'
     A            C2LENG    R        B 17 31REFFLD(ANLENG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  35                                  ERRMSGID(AMN0120 *LIBL/QMFAGPL 35)
     A  36                                  ERRMSGID(AMN0130 *LIBL/QMFAGPL 36)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      VALUES(0 1)
     A                                      EDTCDE(Z)
     A                                 17 34'1=Ja'
     A                                 19  2'Laveste verdi.(start fra). .'
     A            C2NFOM    R        B 19 31REFFLD(ANNFOM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A  32                                  ERRMSGID(AMN0040 *LIBL/QMFAGPL 32)
     A  33                                  ERRMSGID(AMN0050 *LIBL/QMFAGPL 33)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 20  2'Høyeste verdi. . . . . . . .'
     A            C2NTOM    R        B 20 31REFFLD(ANNTOM)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 21  2'Sist brukte  . . . . . . . .'
     A            C2SIST    R        B 21 31REFFLD(ANSIST)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A*                                 9  2'Tilleggsfelt-1 . . . . . . .'
     A*           C2TFL1         5A  B  9 31COLOR(YLW)
     A* 30                                  DSPATR(PR)
     A*                                     OVRDTA
     A*                                10  2'Tilleggsfelt-2 . . . . . . .'
     A*           C2TFL2         5A  B 10 31COLOR(YLW)
     A* 30                                  DSPATR(PR)
     A*                                     OVRDTA
     A*                                18  2'Opprettet dato . . . . . . .'
     A*           C2ODAT         6Y 0O 18 31COLOR(YLW)
     A*                                     EDTWRD('  .  .  ')
     A*                                     OVRDTA
     A*                                19  2'Sist endret dato . . . . . .'
     A*           C2EDAT         6Y 0O 19 31COLOR(YLW)
     A*                                     EDTWRD('  .  .  ')
     A*                                     OVRDTA
     A*                                20  2'Endret av. . . . . . . . . .'
     A*           C2EUSR    R        O 20 31REFFLD(MREUSR)
     A*                                     COLOR(YLW)
     A*                                     OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040624  122923  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette post         '
     A                                      DSPATR(RI)
     A                                  4  2'Firma. . . . . . .'
     A            D1FIRM    R        O  4 21REFFLD(ANFIRM)
     A                                  5  2'Identifikasjon . .'
     A            D1FELL    R        O  5 21REFFLD(ANFELL)
     A                                  6  2'Type . . . . . . .'
     A            D1TYPE    R        O  6 21REFFLD(ANTYPE)
     A                                  7  2'Fast begrep. . . .'
     A            D1FAST    R        O  7 21REFFLD(ANFAST)
     A                                  9  2'Beskrivelse. . . .'
     A            ANTXT1    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040624  122923  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  3  2'Firma. . . . . . .'
     A            K1FIRM    R        B  3 21REFFLD(ANFIRM)
     A  31                                  ERRMSGID(AMN0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMN0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4  2'Identifikasjon . .'
     A            K1FELL    R        B  4 21REFFLD(ANFELL)
     A  31                                  ERRMSGID(AMN0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMN0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Type . . . . . . .'
     A            K1TYPE    R        B  5 21REFFLD(ANTYPE)
     A  31                                  ERRMSGID(AMN0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMN0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Fast begrep. . . .'
     A            K1FAST    R        B  6 21REFFLD(ANFAST)
     A  31                                  ERRMSGID(AMN0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMN0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Firma. . . . . . .'
     A            ANFIRM    R        O  9 21OVRDTA
     A                                 10  2'Identifikasjon . .'
     A            ANFELL    R        O 10 21OVRDTA
     A                                 11  2'Type . . . . . . .'
     A            ANTYPE    R        O 11 21OVRDTA
     A                                 12  2'Fast begrep. . . .'
     A            ANFAST    R        O 12 21OVRDTA
     A                                 13  2'Beskrivelse. . . .'
     A            ANTXT1    R        O 13 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
