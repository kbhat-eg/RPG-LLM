     A*%%TS  SD  20030615  104518  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK650D
     A* Beskrivelse . . : Kunder, betalingsvarsel - paramter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       150902 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20030615  104518  KOB         REL-V5R2M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Betalingsvarsel - endre dato/antal-
     A                                      l purr.'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  8'Dato for siste varsel. .:'
     A            A2PDAT    R        B  6 34REFFLD(RKPKDA)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 31)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      TEXT('Dato siste varsel')
     A                                  8  8'Dato for forrige varsel :'
     A            A2FDAT    R        B  8 34REFFLD(RKPKDA)
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 32)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      TEXT('Dato siste varsel')
     A                                 10  8'Reduser antall ganger. .:'
     A            A2GNGR         1A  B 10 34TEXT('Oppdater antall ganger purret-
     A                                      ')
     A  36                                  DSPATR(PC)
     A  36                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 36)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(MDT)
     A                                      COLOR(YLW)
     A                                      VALUES('J' ' ')
     A                                 10 37'(J=Ja)'
     A                                 12  8'Rutinen oppdaterer alle kundeposte-
     A                                      ringer som ble varslet på dato'
     A                                      COLOR(RED)
     A                                 13  8'for siste varsel. Dato for forrige-
     A                                       varsel legges ut på posteringene'
     A                                      COLOR(RED)
     A                                 14  8'og antall ganger varslet reduseres-
     A                                       med 1 dersom feltet "Reduser antal-
     A                                      l'
     A                                      COLOR(RED)
     A                                 15  8'ganger" er utfylt med '
     A                                      COLOR(RED)
     A                                 15 31'  '
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
