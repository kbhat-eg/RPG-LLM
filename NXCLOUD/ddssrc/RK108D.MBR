     A*%%TS  SD  20060102  001136  KOB550      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK108D
     A* Beskrivelse . . : Kunder, flytting
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
6.30 A*       010616 NHO  Skal notatblokk kopieres
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050723  180637  KOB540      REL-V5R2M0  5722-WDS
     A            B1VALG         1Y 0B  9  2COLOR(YLW)
     A                                      VALUES(0 2 5 7)
     A                                      EDTCDE(Z)
     A            B1ALFA    R        O  9  4REFFLD(RKALFA)
     A            B1KUND    R        O  9 23REFFLD(RKKUND)
     A            B1NAVN    R   26   O  9 30REFFLD(RKNAVN)
     A                                      TEXT('Kundenavn')
     A            B1STED    R   24   O  9 57REFFLD(RKSTED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050723  180805  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      CF07
     A                                      CF11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Flytting av kunder og poster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  5 11'5=Vis'
     A                                      COLOR(BLU)
     A                                  5 18'7=Flytt'
     A                                      COLOR(BLU)
     A                                  7  2'V Alfafelt           Kunde'
     A                                      COLOR(WHT)
     A  80                              7 30'Navn'
     A                                      COLOR(WHT)
     A  82                              7 30'Gateadresse'
     A                                      COLOR(WHT)
     A  83                              7 30'Gateadresse'
     A                                      COLOR(WHT)
     A  80                              7 57'Poststed'
     A                                      COLOR(WHT)
     A  82                              7 57'Telefon      Mobil'
     A                                      COLOR(WHT)
     A  83                              7 57'Foretaksnummer'
     A                                      COLOR(WHT)
     A            B2ALFA    R        B  8  4REFFLD(RKALFA)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            B2KUND    R        B  8 23REFFLD(RKKUND)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0020 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050723  175304  KOB540      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny  F7=Vis du-
     A                                      bletter  F11=Endre liste'
     A                                      COLOR(BLU)
     A                                 23 65' '
     A                                 23 67' '
     A          R C1MSG
     A*%%TS  SD  20020106  202047  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kundenummer finnes fra før '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2MSG
     A*%%TS  SD  20050717  110115  KOB540      REL-V5R2M0  5722-WDS
     A                                      WINDOW(7 45 8 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kundenr. . . . . .'
     A            K1KUND    R        O  2 21REFFLD(RKKUND)
     A  78                              3  2'Det finnes spesialavtaler'
     A  79                              4  2'Det finnes notatblokk'
     A                                  5  2'Disse blir slettet.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020106  202047  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A*                                     CA05
     A                                      CF05
     A                                      CA12
     A                                      PUTOVR
     A                                  1 18'Kopier kunde'
     A                                      COLOR(WHT)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr. . . . . .'
     A            K1KUND    R        B  4 21REFFLD(RKKUND)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A  33                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 33)
     A  34                                  ERRMSGID(RMK0020 *LIBL/QMFOKON 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
6.30 A                                  7  2'Kopiere notatblokk'
6.30 A            RKNOTA         1   B  7 21
6.30 A                                  7 23'J/N'
     A                                  8  2'Kundenr. . . . . .'
     A            RKKUND    R        O  8 21
     A                                  9  2'Navn . . . . . . .'
     A            RKNAVN    R        O  9 21
     A                                 10  2'Adresse. . . . . .'
     A            RKGATE    R        O 10 21
     A                                 11  2'Adresse 2. . . . .'
     A            RKADR2    R        O 11 21
     A                                 12  2'Postadresse. . . .'
     A            RKSTED    R        O 12 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 17'F5=Adder til'
     A                                      COLOR(BLU)
     A          R V1WIN
     A*%%TS  SD  20060102  001136  KOB550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      PUTOVR
     A                                  2 14'Utvalgskriteria for dubletter'
     A                                      COLOR(WHT)
     A                                  4  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5 11'Kontroller mot:'
     A                                  6 13'Navn. . . . . . . . .:'
     A            V1NAVN         1A  B  6 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Navn')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  6 40'(J=Ja)'
     A                                  7 13'Gateadresse . . . . .:'
     A            V1GATE         1A  B  7 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Gateadresse')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  7 40'(J=Ja)'
     A                                  8 13'Postnummer. . . . . .:'
     A            V1PONR         1A  B  8 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Postnummer')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  8 40'(J=Ja)'
     A                                  9 13'Telefonnr.. . . . . .:'
     A            V1TLFN         1A  B  9 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Telefonnummer')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                  9 40'(J=Ja)'
     A                                 10 13'Mobilnr.. . . . . . .:'
     A            V1MOBN         1A  B 10 36
     A  32                                  DSPATR(PC)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Mobilnummer')
     A                                      COLOR(YLW)
     A                                 10 40'(J=Ja)'
     A                                 11 13'Foretaksnummer. . . .:'
     A            V1FTNR         1A  B 11 36
     A  32                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A                                      TEXT('Foretaksnummer')
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(RM00010 *LIBL/QMFOKON 32)
     A                                 11 40'(J=Ja)'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 15 56)
