     A*%%TS  SD  20050308  082917  BOF530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSERV
     A* Format. . . . . : MB101D
     A* Beskrivelse . . : ABC-detaljer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       240804 bø   Nytt program
7.xx A* 7.00  180316 bsa  Endringer pga ny GUI.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050307  200829  BOF530      REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1VARE    R   -7   H      REFFLD(MBVARE)
     A            B1NAVN        35A  H
7.xx A            B1OABC    R        O  9 43REFFLD(MBOABC)
7.xx A            B1ANTO    R   -2   O  9 47REFFLD(MBANTO)
     A                                      EDTCDE(L)
7.xx A            B1ABPR         6Y 3O  9 72EDTCDE(L)
7.xx A            B1OMSE    R        O  9 58REFFLD(MBOMSE)
     A                                      EDTCDE(L)
7.xx A            B1LABC         1A  O  9  4
7.xx A            B1TEXT        36A  O  9  6
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050307  195945  BOF530      REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF11
     A                                      CF13
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'ABC-analyse detaljer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'MB101B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 24'ABC-kode'
     A                                      COLOR(WHT)
     A            B2KABC    R        O  4 33REFFLD(MBKABC)
     A                                      COLOR(WHT)
     A                                      OVRDTA
7.xx A                                  5  6'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 46'Ord.linjer'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 60'Omsetning'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre kode  5=Mer info         '
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  6'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 22'Varenavn    '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 42'OAbc'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 47'Ord.linjer'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 60'Omsetning'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 74'%vis'
     A                                      COLOR(WHT)
7.xx A                                  6  4'+'
7.xx A            B2VARE    R   -7   B  6  6REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ANTO    R   -4   B  6 48REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2OMSE    R        B  6 58REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A**                                7 15'F4'
     A          R B2CMD
     A*%%TS  SD  20050307  204531  BOF530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny  F11=Skif-
     A                                      t  F13=Info'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050307  100441  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre kode på ABC analyse  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenavn   . . . . . . . . .'
     A            C2NAVN        30A  O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Antall ordrelinjer . . . . .'
     A            C2ANTO    R        O  8 31REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                  9  2'Omsetning  . . . . . . . . .'
     A            C2OMSE    R        O  9 31REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 10  2'ABC-kode - analyse . . . . .'
     A            C2OABC    R        O 10 31REFFLD(MBOABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'ABC-kode - endre . . . . . .'
     A            C2EABC    R        B 11 31REFFLD(MBKABC)
     A  31                                  ERRMSGID(MMB0070 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2KLAB    R        B 11 35REFFLD(LÅJANE)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 38'1=Låst 0=Ikke låst'
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2USER    R        O 20 31REFFLD(MBUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R C3BLD
     A*%%TS  SD  20050307  160140  BOF530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre kode på ABC analyse  '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R        O  5 31REFFLD(MBVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varenavn   . . . . . . . . .'
     A            C3NAVN        30A  O  7 31COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Antall ordrelinjer . . . . .'
     A            C3ANTO    R        O  8 31REFFLD(MBANTO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                  9  2'Antall . . . . . . . . . . .'
     A            C3ANTA    R        O  9 31REFFLD(MBANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 10  2'Omsetning  . . . . . . . . .'
     A            C3OMSE    R        O 10 31REFFLD(MBOMSE)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 11  2'Kostpris . . . . . . . . . .'
     A            C3PRIS        11S 2O 11 31COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'ABC-kode opprinnelig . . . .'
     A            C3OABC    R        O 12 31REFFLD(MBOABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'ABC-kode endret  . . . . . .'
     A            C3EABC    R        O 13 31REFFLD(MBKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'ABC-kode låst  . . . . . . .'
     A            C3KLAB    R        O 14 31REFFLD(MBKABC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C3ODAT         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C3EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C3USER    R        O 20 31REFFLD(MBUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20050308  082917  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                  2  2'Forklaring :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'* '
     A                                  4  5'Hvis en vare har låst ABC-kode vil-
     A                                         '
     A                                  5  5'disse være merket med *           -
     A                                          '
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
