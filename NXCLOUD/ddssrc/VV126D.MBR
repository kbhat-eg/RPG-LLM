     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A**          B1TVAR    R        O  8  5REFFLD(VVTVAR)
     A            B1TSPR    R        O  8  5REFFLD(VVTSPR)
     A            B1TTE1    R        O  8  8REFFLD(VVTTE1)
     A            B1TTE2    R        O  8 44REFFLD(VVTTE2)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av språktekster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer:'
     A            B2TVAR    R        O  4 14REFFLD(VVTVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2TTE1    R        O  4 30REFFLD(VVTTE1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V Spr Beskrivelse'
     A                                      COLOR(WHT)
     A**          B2TVAR    R        B  7  5REFFLD(VVTVAR)
     A**                                    COLOR(YLW)
     A**                                    OVRDTA
     A            B2TSPR    R        B  7  5REFFLD(VVTSPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av språktekster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare . . . . . . . . . . . .'
     A            C1TVAR    R        O  5 31REFFLD(VVTVAR)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TTE1    R        O  5 47REFFLD(VVTEK1)
     A                                      OVRDTA
     A                                  7  2'Språkkode. . . . . . . . . .'
     A            C1TSPR    R        B  7 31REFFLD(VVTSPR)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 33'(1=Norsk, 2=Engelsk, 3=Tysk)'
     A                                  8 33'(4=Fransk,5=Spansk)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Vare/språkkode finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20001120  090641  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av språktekster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Vare . . . . . . . . . . . .'
     A            C1TVAR    R        O  5 31REFFLD(VVTVAR)
     A  30                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Språkkode. . . . . . . . . .'
     A            C1TSPR    R        O  7 31REFFLD(VVTSPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 33'(1=Norsk, 2=Engelsk, 3=Tysk)'
     A                                  8 33'(4=Fransk,5=Spansk)'
     A                                 10  2'Tekst 1. . . . . . . . . . .'
     A            C2TTE1    R        B 10 31REFFLD(VVTTE1)
     A  33                                  ERRMSGID(VMI0100 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEK1    R        O 11 31REFFLD(VVTEK1)
     A                                      OVRDTA
     A                                 13  2'Tekst 2. . . . . . . . . . .'
     A            C2TTE2    R        B 13 31REFFLD(VVTTE2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
6.NU A            C2TEK2    R        O 14 31REFFLD(VVTEK2)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(VIEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Slette språktekst'
     A                                      DSPATR(RI)
     A                                  4  2'Vare.  . . . . . .'
     A            D1TVAR    R        O  4 21REFFLD(VVTVAR)
     A                                  5  2'Språkkode. . . . .'
     A            D1TSPR    R        O  5 21REFFLD(VVTSPR)
     A                                  6  2'Tekst 1. . . . . .'
     A            VVTTE1    R        O  6 21
6.NU A                                  7  2'Tekst 2. . . . . .'
6.NU A            VVTTE2    R        O  7 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020226  103238  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 15 56)
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Vare . . . . . . .'
     A            K1TVAR    R        B  4 21REFFLD(VVTVAR)
     A  31                                  ERRMSGID(VMU0010 *LIBL/QMFOFAK 31)
     A  34                                  ERRMSGID(FM01200 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                  5  2'Språkkode. . . . .'
     A            K1TSPR    R        B  5 21REFFLD(VVTSPR)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                  5 23'(1=Norsk, 2=Engelsk, 3=Tysk)'
     A                                  6 23'(4=Fransk,5=Spansk)'
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Vare . . . . . . .'
     A            VVTVAR    R        O  9 21OVRDTA
     A                                 10  2'Språkkode. . . . .'
     A            VVTSPR    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
