     A*%%TS  SD  20050524  122033  AMD530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AX014D
     A* Beskrivelse . . : Rutineregister, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       080925 bsa  Nytt program
     A*
     A*
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL1                    SFL
     A*%%TS  SD  20040301  140108  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1RUTI    R        O  8  4REFFLD(AFRUTI)
     A            B1FORM    R        O  8 16REFFLD(AFFORM)
     A            B1OUTQ    R        O  8 28REFFLD(AFOUTQ)
     A            B1ACPI    R        O  8 42REFFLD(AFACPI)
     A            B1ALIN    R        O  8 48REFFLD(AFALIN)
     A            B1AKOP    R        O  8 55REFFLD(AFAKOP)
     A            B1HLDQ    R        O  8 63REFFLD(AFHLDQ)
     A            B1INST    R        O  8 69REFFLD(AFINST)
     A            B1PRFI    R        O  8 76REFFLD(AFPRFI)
     A          R B2CTL1                    SFLCTL(B1SFL1)
     A*%%TS  SD  20040301  162228  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A*                                     CF06
     A                                      CF11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rutine-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  5 39'Antall Antall Antall Holdes Innst -
     A                                      Skrive'
     A                                      COLOR(WHT)
     A                                  6  2'V Rutine      Formular    Kø/Skriv-
     A                                      er   cpi  linjer kopier  i kø  linj-
     A                                      e firmaopl'
     A                                      COLOR(WHT)
     A            B2RUTI    R        B  7  4REFFLD(AFRUTI)
     A                                      DLTCHK
     A  22                                  DSPATR(PC)
     A  31                                  ERRMSGID(AMF0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMF0020 *LIBL/QMFAGPL 32)
     A  33                                  ERRMSGID(AMF0030 *LIBL/QMFAGPL 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B1SFL2                    SFL
     A*%%TS  SD  20040301  141328  AMD         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1RUTI    R        O  8  4REFFLD(AFRUTI)
     A            B1FORM    R        O  8 16REFFLD(AFFORM)
     A            B1OUTQ    R        O  8 28REFFLD(AFOUTQ)
     A            B1PDFA         1A  O  8 40
     A            B1TXT1        37A  O  8 43
     A**          B1TXT1    R        O  8 40REFFLD(AFTXT1)
     A          R B2CTL2                    SFLCTL(B1SFL2)
     A*%%TS  SD  20040301  141328  AMD         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A*                                     CF06
     A*                                     CF07
     A                                      CF11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rutine-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V Rutine      Formular    Kø/Skriv-
     A                                      er PDF Rapport-tekst        '
     A*                                     er  Rapport-tekst           '
     A                                      COLOR(WHT)
     A            B2RUTI    R        B  7  4REFFLD(AFRUTI)
     A                                      DLTCHK
     A  22                                  DSPATR(PC)
     A  31                                  ERRMSGID(AMF0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMF0020 *LIBL/QMFAGPL 32)
     A  33                                  ERRMSGID(AMF0030 *LIBL/QMFAGPL 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20040301  144314  AMD         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A*                                23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A*                                     ett'
     A                                      COLOR(BLU)
     A* 35                             23 42'F7=Aktiver PROA'
     A*                                     COLOR(BLU)
     A*N35                             23 42'F7=Deaktiver PROA'
     A*                                     COLOR(BLU)
     A                                 23 42'F11=Skift bilde'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040302  150050  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rutine-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rutine . . . . . . . . . . .'
     A            C1RUTI    R        B  5 31REFFLD(AFRUTI)
     A  31                                  ERRMSGID(AMF0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  AMD         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Rutinenavn finnes fra før  '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050524  122033  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A N35                                  CF07
     A*                                     CF08
     A*                                     CF09
     A*                                     CF10
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1  2'Databibliotek:'
     A                                      COLOR(WHT)
     A            L_SBIB        10A  O  1 17COLOR(WHT)
     A                                  1 46'Drifts-system'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av rutine-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rutine . . . . . . . . . . .'
     A            C1RUTI    R        O  5 31REFFLD(AFRUTI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 44'Antall cpi . . . . . . . . .'
     A            C2ACPI    R        O  5 76REFFLD(AFACPI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 44'Antall kopier. . . . . . . .'
     A            C2AKOP    R        O  6 75REFFLD(AFAKOP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Formularid . . . . . . . . .'
     A            C2FORM    R        B  7 31REFFLD(AFFORM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7 44'Antall posisjoner pr. linje.'
     A            C2APOS    R        O  7 75REFFLD(AFAPOS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Utkø / skriver . . . . . . .'
     A            C2OUTQ    R        B  8 31REFFLD(AFOUTQ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8 44'Antall linjer pr. side . . .'
     A            C2ALIN    R        O  8 75REFFLD(AFALIN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Call til program . . . . . .'
     A            C2PROG    R        O  9 31REFFLD(AFPROG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 44'Overflow på linje. . . . . .'
     A            C2OFLW    R        O  9 75REFFLD(AFOFLW)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Endre verdier v/kjøring ?. .'
     A            C2CHGV    R        B 10 31REFFLD(AFCHGV)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 44'Ingen rotasjon (=1). . . . .'
     A            C20ROT    R        O 10 77REFFLD(AF0ROT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Innstillingslinje. . . . . .'
     A            C2INST    R        O 11 31REFFLD(AFINST)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 44'Tvungen batch  (=1). . . . .'
     A            C2TVBT    R        O 11 77REFFLD(AFTVBT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Holdes i skrivekø. . . . . .'
     A            C2HLDQ    R        B 12 31REFFLD(AFHLDQ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12 33'0=Nei 1=Ja'
     A                                 13  2'Save av print ?. . . . . . .'
     A            C2SAVE    R        B 13 31REFFLD(AFSAVE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13 33'0=Nei 1=Ja'
     A                                 14  2'Prioritet i skrivekø . . . .'
     A            C2PRTY    R        B 14 31REFFLD(AFPRTY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14 33'0 til 9'
     A                                 15  2'Skrive firma/avd.opplysn . .'
     A            C2PRFI    R        B 15 31REFFLD(AFPRFI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15 33'0=Nei 1=Firma 2=Avdeling'
     A                                 17  2'Rapporttekst . . . . . . . .'
     A            C2TXT1    R        B 17 31REFFLD(AFTXT1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A  31                                  ERRMSGID(AMF0140 *LIBL/QMFAGPL 31)
     A                                 18  2'Kommentarer  . . . . . . . .'
     A            C2TXT2    R        B 18 31REFFLD(AFTXT2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 19  2'Melding 1. . . . . . . . . .'
     A            C2MLD1    R        B 19 31REFFLD(AFMLD1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 20  2'Melding 2. . . . . . . . . .'
     A            C2MLD2    R        B 20 31REFFLD(AFMLD2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 21  2'Melding 3. . . . . . . . . .'
     A            C2MLD3    R        B 21 31REFFLD(AFMLD3)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A    N35                          23 18'F7=PROA print'
     A    N35                               COLOR(BLU)
     A*                                23 33'F8=Printstyring'
     A*                                     COLOR(BLU)
     A*                                23 50'F9=Tilleggs-opplysninger'
     A*                                     COLOR(BLU)
     A**                               23 63'F10=Multisupport'
     A**                                    COLOR(BLU)
     A*                                24  2'F10=emailadresser'
     A*                                     COLOR(BLU)
      *                                18  2'Opprettet dato . . . . . . .'
      *           C2ODAT         6Y 0O 18 31COLOR(YLW)
      *                                     EDTWRD('  .  .  ')
      *                                     OVRDTA
      *                                19  2'Sist endret dato . . . . . .'
      *           C2EDAT         6Y 0O 19 31COLOR(YLW)
      *                                     EDTWRD('  .  .  ')
      *                                     OVRDTA
      *                                20  2'Endret av. . . . . . . . . .'
      *           C2EUSR    R        O 20 31REFFLD(MREUSR)
      *                                     COLOR(YLW)
      *                                     OVRDTA
      *                                23  2'F3=Gå tilbake   F4=Spørring'
      *                                     COLOR(BLU)
     A          R C3WIN
     A*%%TS  SD  20021015  155854  AMD         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(&W_ALIN &W_APOS 17 55)
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ALIN         2S 0P
     A            W_APOS         3S 0P
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Tilleggs-opplysninger:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Rutine. . . . .'
     A            C1RUTI    R        O  4 18REFFLD(AFRUTI)
     A                                      DSPATR(UL)
     A                                  6  2'Bankgiro. . . .'
     A            C3BGIR    R        B  6 18REFFLD(AFBGIR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  6 22'(0=Uten  1=Separat  2=Påhengt)'
     A                                  7  2'Merverdiavgift.'
     A            C3MVAK    R        B  7 18REFFLD(AFMVAK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  7 22'(0=Uten  1=Med)'
     A                                  8  2'Kiddfelt. . . .'
     A            C3KIDK    R        B  8 18REFFLD(AFKIDK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  8 22'(0=Uten  1=Lang  2=Kort  3=Faktnr)'
     A                                  9  2'Ocr-skrift. . .'
     A            C3OCRK    R        B  9 18REFFLD(AFOCRK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  9 22'(0=Uten  1=Med Ocr  2=Axis boks)'
     A                                 10 22'(3=5224  Skriver    4=Nettprint)'
     A                                 11  2'*SCS / *IPDS. .'
     A            C3IPDS    R        B 11 18REFFLD(AFIPDS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                 11 22'(0=*SCS  1=*IPDS)'
     A                                 13  2'Styrestreng 1 .'
     A            C3HEX1    R        B 13 18REFFLD(AFHEX1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                 13 41'(Benyttes ved'
     A                                 14  2'Styrestreng 2 .'
     A            C3HEX2    R        B 14 18REFFLD(AFHEX2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                 14 42'kode 2 og 4)'
     A                                 16  2'F12=Annuller'
     A                                      COLOR(BLU)
     A          R C4WIN
     A*%%TS  SD  20040303  095729  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      PUTOVR
     A                                      WINDOW(&W_ALIN &W_APOS 12 50)
     A            W_ALIN         2S 0P
     A            W_APOS         3S 0P
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Integrasjon Multi-support :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Rutine. . . . .'
     A            C1RUTI    R        O  4 18REFFLD(AFRUTI)
     A                                      DSPATR(UL)
     A                                  6  2'Arkivering. . . . . . . . . . .'
     A            C4ARKI    R        B  6 35REFFLD(AFARKI)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  6 38'1=Ja,'
     A                                  6 44'0=Nei'
     A                                  7  2'Etikett . . . . . . . . . . . .'
     A            C4ETIK    R        B  7 35REFFLD(AFETIK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  7 38'1=Ja,'
     A                                  7 44'0=Nei'
     A                                  8  2'Work-flow . . . . . . . . . . .'
     A            C4WORK    R        B  8 35REFFLD(AFWORK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  8 38'1=Ja,'
     A                                  8 44'0=Nei'
     A                                  9  2'Benytte PDF-format. . . . . . .'
     A            C4PDFA    R        B  9 35REFFLD(AFPDFA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  9 38'1=Ja,'
     A                                  9 44'0=Nei'
     A                                 11  2'F12=Annuller'
     A                                      COLOR(BLU)
     A          R C5WIN
     A*%%TS  SD  20050524  114907  AMD530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_APOS 12 50)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      PUTOVR
     A            W_ALIN         2S 0P
     A            W_APOS         3S 0P
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Tilleggs-funksjoner printerstyring-
     A                                       :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Rutine. . . . .'
     A            C1RUTI    R        O  4 18REFFLD(AFRUTI)
     A                                      DSPATR(UL)
     A                                  6  2'Inn- skuff. . . . . . . . . . .'
     A            C5SKIN    R        B  6 35REFFLD(AFSKIN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                  7  2'Ut - skuff. . . . . . . . . . .'
     A            C5SKUT    R        B  7 35REFFLD(AFSKUT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                  8  2'Duplex. . . . . . . . . . . . .'
     A            C5DPLX    R        B  8 35REFFLD(AFDPLX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                  9  2'Print - kvalitet. . . . . . . .'
     A            C5PKVA    R        B  9 35REFFLD(AFPKVA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                 11  2'F12=Annuller'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040301  151509  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 62)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Rutine       '
     A                                      DSPATR(RI)
     A                                  4  2'Rutine . . . . . .'
     A            D1RUTI    R        O  4 21REFFLD(AFRUTI)
     A                                  6  2'Rapporttekst . . .'
     A            AFTXT1    R        O  6 21REFFLD(AFTXT1)
     A                                  7  2'Kommentarer. . . .'
     A            AFTXT2    R        O  7 21REFFLD(AFTXT2)
     A                                  8  2'Melding 1. . . . .'
     A            AFMLD1    R        O  8 21REFFLD(AFMLD1)
     A                                  9  2'Melding 2. . . . .'
     A            AFMLD2    R        O  9 21REFFLD(AFMLD2)
     A                                 10  2'Melding 3. . . . .'
     A            AFMLD3    R        O 10 21REFFLD(AFMLD3)
     A                                 11  2'Formularid . . . .'
     A            AFFORM    R        O 11 21REFFLD(AFFORM)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040301  152140  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 62)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Rutine . . . . . .'
     A            K1RUTI    R        B  4 21REFFLD(AFRUTI)
     A  31                                  ERRMSGID(AMF0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMF0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Rutine . . . . . .'
     A            AFRUTI    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            AFTXT1    R        O  9 21REFFLD(AFTXT1)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
