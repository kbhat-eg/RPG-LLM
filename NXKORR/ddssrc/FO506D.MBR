     A*%%TS  SD  20080501  213524  KOB560      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO506D
     A* Beskrivelse . . : Vise sletttede ordrelinjer
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  270916  bsa  Justeringer basert på ny GUI og regler
     A*
8.02 A*K0000  040925  bsa  Skrevet om til bredt skjermbilde
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
8.02 A*                                     DSPSIZ(24 80 *DS3)
8.02 A                                      DSPSIZ(27 132 *WIDE)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20080501  150200  KOB560      REL-V5R4M0  5722-WDS
     A            B1NUMM    R        H      REFFLD(FONUMM)
     A            B1SUFF    R        H      REFFLD(FOSUFF)
     A            B1KTXT    R        H      REFFLD(FOKTXT)
     A            B1LINE    R        H      REFFLD(FDLINE)
     A            B1VALG    R        B 11  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1LETY    R        O 11  4REFFLD(FDLETY)
8.02 A*           B1TEXT        70A  O 11  6
8.02 A            B1VARE         8A  O 11  6
8.02 A            B1TEK1        35A  O 11 16
8.02 A            B1TEK2        35A  O 11 53
8.02 A            B1ANTA        13A  O 11 90
8.02 A            B1ENH1         3A  O 11105
8.02 A            B1RABA         6A  O 11109
8.02 A            B1SALG        13A  O 11116
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080501  212805  KOB560      REL-V5R4M0  5722-WDS
8.02 A                                      SFLSIZ(0015)
8.02 A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF14
     A                                      CA21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 42'Ordre/Fakturering'
     A                                      COLOR(WHT)
8.02 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
8.02 A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise slettede ordrelinjer'
     A                                      COLOR(WHT)
8.02 A*                                 2 34'FO506B2'
8.02 A                                  2 38'FO506B2'
7.xx A                                      COLOR(WHT)
8.02 A*                                 2 42'Bruker:'
8.02 A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
8.02 A*                                 2 52USER
8.02 A                                  2 54USER
     A                                      COLOR(WHT)
8.02 A                                  2118'Dato:'
     A                                      COLOR(WHT)
8.02 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
8.02 A                                      ------------------------------------
8.02 A                                      ---------------------------'
8.02 A*                                     -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A            B2KUND    R        O  4 11REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV    R   -7   O  4 18REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 42'Ordrenr. . .'
     A            B2NUMM    R        O  4 55REFFLD(FONUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.NU A            B2SUFF    R        O  4 64REFFLD(FOSUFF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5 42'Ordredato. .'
     A            B2BDAT         6Y 0O  5 55EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kunde-  '
     A            B2NAVN    R        O  5 11REFFLD(FONAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'prosjekt'
     A            B2ADR1    R        O  6 11REFFLD(FOADR1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 42'Vår ref. . .'
     A            B2VREF        25A  O  6 55COLOR(YLW)
     A                                      OVRDTA
     A            B2STED    R        O  7 11REFFLD(FOSTED)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 42'Kundens ref.'
     A            B2DREF        25A  O  7 55COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'5=Vis'
     A                                      COLOR(BLU)
8.02 A*                                10  2'V L Varenr.  Varetekst            -
8.02 A*                                                 Antall Enh  Rab       B-
8.02 A*                                     eløp  '
8.02 A                                 10  2'V'
8.02 A                                      COLOR(WHT)
8.02 A                                 10  4'L'
8.02 A                                      COLOR(WHT)
8.02 A                                 10  6'Varenr.'
8.02 A                                      COLOR(WHT)
8.02 A                                 10 16'Varetekst 1'
8.02 A                                      COLOR(WHT)
8.02 A                                 10 53'Varetekst 2'
8.02 A                                      COLOR(WHT)
8.02 A                                 10 93'Antall'
8.02 A                                      COLOR(WHT)
8.02 A                                 10105'Enh'
8.02 A                                      COLOR(WHT)
8.02 A                                 10110'Rab'
8.02 A                                      COLOR(WHT)
8.02 A                                 10120'Beløp'
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  20080501  181026  KOB560      REL-V5R4M0  5722-WDS
8.02 A                                 26  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
