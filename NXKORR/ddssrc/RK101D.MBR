     A*%%TS  SD  20120113  134032  BOF620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK101D
     A* Beskrivelse . . : Kunder, vedlikehold - subprogram
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
     A*       250505 KOB  Lagt inn nytt bilde E2-Besøksadresse
     A*       260505 KOB  Fjernet ubrukte koder fra F2-bildet
     A*       260505 KOB  Fjernet ubrukte koder fra H2-bildet
     A*       250705 SST  Lagt inn tabell 2 vedr service-kunder
     A*       241105 KOB  Flyttet Avgiftsfritt til F2-bildet
     A*       070307 BOF  Lagt til utenlandsk postnr.
     A*       160413 BOF  Lagt på ny feilmelding på eank fakt.
     A*       230813 BOF  Lagt på ny feilmelding på F2EBIZ
6.30 A*       260314 BOF  Nytt felt kontantkunde - tilbud
6.31 A*       110515 BOF  Test på mobil, styres fra RAKFAX (ra11pf)
6.32 A*       271017 NHO  Test på E-MAIL dersom RKFATY = 22 eller
     A*                   25  (Kopiering fra annen kunde)
7.xx A* 7.xx  100117  bsa  Justeringer basert på ny GUI og regler
     A*
7.01 A* 7.00  151019  bsa  Endret på ledeteksten til felt "ordrebekreftelse"
8.01 A* K000  100221  bsa  Utvidet med felt for profil.
8.09 A* K000  040923  bsa  Legger inn feilmelding
8.12 A* 800   270723  ask  Lagt inn kredittsøk mot Bisnode hvis switch er på
8.13 A* 800   240524  bsa  Lagt inn felt for å utelate samling av positive og
8.13 A* 800                negative ordretyper.
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R C2BLD
     A*%%TS  SD  20120113  134032  BOF620      REL-V7R1M0  5770-WDS
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      CA13
8.10 A                                      CA18
     A                                      CA24
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. '
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kunder'
     A                                      COLOR(WHT)
7.xx A                                  2 37'RK101B2'
7.xx A                                  2 46'Bruker'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.xx A                                  2 66'Dato'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Kundenr.  '
     A            C2KUND    R        O  4 13REFFLD(RKKUND)
     A  30                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  94        C2NOTB         8A  O  4 44DSPATR(RI)
     A            C2NYEN        11A  O  4 69DSPATR(RI)
7.xx A                                  5  2'Navn.  .  '
     A            C2NAVN    R        B  5 13REFFLD(RKNAVN)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A                                      COLOR(YLW)

     A  46 81                               ERRMSGID(RMK0390 *LIBL/QMFOKON 81)
     A  47 81                               ERRMSGID(RMK0005 *LIBL/QMFOKON 81)
7.xx A                                  5 44'Alfa . . .  '
     A            C2ALFA    R        B  5 57REFFLD(RKALFA)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  6  2'Fornavn.  '
     A            C2FNAV        30A  B  6 13
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A  67 81                               ERRMSGID(RMK0570 *LIBL/QMFOKON 81)
     A                                      TEXT('Fornavn')
7.xx A N30                              6 44'Postnr.. .+ '
7.xx A  30                              6 44'Postnr.. .'
     A            C2PONR    R        B  6 57REFFLD(RKPONR)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  66 81                               ERRMSGID(RMK0540 *LIBL/QMFOKON 81)
     A  32 81                               ERRMSGID(RM01000 *LIBL/QMFOKON 81)
7.xx A                                      COLOR(YLW)
7.xx A                                  6 63'Utenl. '
     A            C2UPNR    R        B  6 71REFFLD(RKUPNR)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  76                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A  76 81                               ERRMSGID(RMK0610 *LIBL/QMFOKON 81)
     A  78 81                               ERRMSGID(RMK0630 *LIBL/QMFOKON 81)
7.xx A                                      COLOR(YLW)
7.xx A                                  7  2'Adresse.  '
     A            C2GATE    R        B  7 13REFFLD(RKGATE)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  28                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A                                      COLOR(YLW)
     A  65 81                               ERRMSGID(RMK0530 *LIBL/QMFOKON 81)
     A  27 81                               ERRMSG('Ugyldige data i feltet' 81)
7.xx A N30                              7 44'Land . . .+ '
7.xx A  30                              7 44'Land . . .'
     A            C2LAND    R        B  7 57REFFLD(RKLAND)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41 81                               ERRMSGID(RM01120 *LIBL/QMFOKON 81)
     A  77 81                               ERRMSGID(RMK0620 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2LTXT    R   20   O  7 60REFFLD(RALTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Adresse2  '
     A            C2ADR2    R        B  8 13REFFLD(RKADR2)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A N30                              8 44'Sperret. .J'
7.xx A  30                              8 44'Sperret'
     A            C2SPRK    R        B  8 57REFFLD(RKSPRK)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  9  2'Postadr.  '
     A            C2STED    R        B  9 13REFFLD(RKSTED)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
7.xx A N30                              9 44'Passiv . .J'
7.xx A  30                              9 44'Passiv . .'
     A            C2PASS    R        B  9 57REFFLD(RKPASS)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  43                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      VALUES('J' ' ')
7.xx A                                 10  2'Telefon   '
     A            C2TLFN    R        B 10 13REFFLD(RKTLFN)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                      COLOR(YLW)
     A  68 81                               ERRMSGID(RMK0550 *LIBL/QMFOKON 81)
     A  24 81                               ERRMSG('Ugyldige data i feltet' 81)
     A                                 10 26'Mobil'
     A            C2MOBN    R        B 10 32REFFLD(RKMOBN)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.31 A  69 81                               ERRMSGID(RMK0590 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                      COLOR(YLW)
7.xx A                                 10 44'Renteberegn '
     A            C2RENK    R        B 10 57REFFLD(RKRENK)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             10 60'(Rente ,1,2,3)'
7.xx A                                 11  2'Bankgiro  '
     A            C2BGIR    R        B 11 13REFFLD(RKBGIR)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  29                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  23 81                               ERRMSG('Kontrollsiffer er feil' 81)
7.xx A                                 11 26'Fax. '
     A            C2TLFX    R        B 11 32REFFLD(RKTLFX)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A  25 81                               ERRMSG('Ugyldige data i feltet' 81)
7.xx A                                 11 44'Kred.type.  '
     A            C2KRES    R        B 11 57REFFLD(RKKRES)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  44                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             11 60'(Kred ,1,2,3)'
7.xx A                                 12  2'Foretaknr '
     A            C2FTNR    R        B 12 13REFFLD(RKFTNR)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  48 81                               ERRMSG('Samme foretaksnummer regist-
     A                                      ert på annen kunde' 81)
     A  49 81                               ERRMSG('Foretaksnummer skal registr-
     A                                      eres på denne kundekat.' 81)
     A  21 81                               ERRMSG('Kontrollsiffer er feil' 81)
7.xx A                                 12 26'P.nr '
     A            C2PENR    R        B 12 32REFFLD(RKPENR)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  72 81                               ERRMSG('Personnummer skal registrer-
     A                                      es på denne kundekat.' 81)
     A  73 81                               ERRMSG('Samme personnummer register-
     A                                      t på annen kunde' 81)
     A  22 81                               ERRMSG('Kontrollsiffer er feil' 81)
7.xx A                                 12 44'Kred.grense '
     A            C2GRNS    R        B 12 57REFFLD(RKGRNS)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  51                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             12 64'Låst J '
7.xx A  30                             12 64'Låst '
7.xx A            C2KGRK    R        B 12 72REFFLD(RKKGRK)
7.xx A N30                                  DSPATR(UL)
7.xx A                                      COLOR(YLW)
7.xx A  30                                  DSPATR(PR)
     A                                      VALUES('J' ' ')
6.38 A  52                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A N30                             13  2'Betalbet+ '
7.xx A  30                             13  2'Betalbet'
     A            C2BETB    R        B 13 13REFFLD(RKBETB)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01030 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2CTXT    R        O 13 16REFFLD(RACTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13 44'Spesialavt. '
     A            C2SPAV    R        B 13 57REFFLD(RKSPAV)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  74                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A  74 81                               ERRMSG('Spesialavtale finnes, kode -
     A                                      må settes til 1' 81)
7.xx A N30                             13 59'1/0'
7.xx A N30                             14  2'Valuta .+ '
7.xx A  30                             14  2'Valuta .'
     A            C2VALK    R        B 14 13REFFLD(RKVALK)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A  42 81                               ERRMSGID(RM01160 *LIBL/QMFOKON 81)
     A            C2PMNA    R        O 14 17REFFLD(RAPMNA)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A N30                             14 44'Selger . .+ '
7.xx A  30                             14 44'Selger . .'
     A            C2SLGR    R        B 14 57REFFLD(RKSLGR)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38 81                               ERRMSGID(RM01090 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2ITXT    R   -1   O 14 62REFFLD(RAITXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A N30                             15  2'Avdeling+ '
7.xx A  30                             15  2'Avdeling'
     A            C2AVDE    R        B 15 13REFFLD(RKAVDE)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  36 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A            C2GTXT    R   20   O 15 18REFFLD(RAGTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A N30                             15 44'Lager. . .+ '
7.xx A  30                             15 44'Lager. . .'
     A            C2LAGR    R        B 15 57REFFLD(RKLAGR)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39 81                               ERRMSGID(RM01100 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2JTXT    R        O 15 60REFFLD(RAJTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A N30                             16  2'Rabattka+ '
7.xx A  30                             16  2'Rabattka'
     A            C2RABK    R        B 16 13REFFLD(RKRABK)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01060 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2FTXT    R        O 16 17REFFLD(RAFTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16 44'Spes.fakt.  '
     A            C2SPFA    R        B 16 57REFFLD(RKSPFA)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16 60'Lønn'
     A            C2SPL1    R        B 16 65REFFLD(RKSPL1)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2SPL2    R        B 16 67REFFLD(RKSPL2)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2SPL3    R        B 16 69REFFLD(RKSPL3)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2SPL4    R        B 16 71REFFLD(RKSPL4)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2SPL5    R        B 16 73REFFLD(RKSPL5)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             17  2'Bet.måte+ '
7.xx A  30                             17  2'Bet.måte'
     A            C2BETM    R        B 17 13REFFLD(RKBETM)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01020 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2BTXT    R        O 17 15REFFLD(RABTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 17 44'Påslagstab. '
     A            C2PTAB    R        B 17 57REFFLD(RKPTAB)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            C2PTA2    R        B 17 62REFFLD(RKPTA2)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             18  2'Kundekat+ '
7.xx A  30                             18  2'Kundekat'
     A            C2KATG    R        B 18 13REFFLD(RKKATG)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  50                                  CHECK(ME)
7.xx A                                      COLOR(YLW)
     A  40 81                               ERRMSGID(RM01110 *LIBL/QMFOKON 81)
     A  71 81                               ERRMSGID(RMK0580 *LIBL/QMFOKON 81)
     A  75                                  ERRMSGID(RMK0570 *LIBL/QMFOKON 75)
     A            C2KTXT    R        O 18 18REFFLD(RAKTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 18 44'Frie koder  '
     A            C2FRIE    R        B 18 57REFFLD(RKFRIE)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
6.38 A  53                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             19  2'Distrikt+ '
7.xx A  30                             19  2'Distrikt'
     A            C2DIST    R        B 19 13REFFLD(RKDIST)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  37 81                               ERRMSGID(RM01080 *LIBL/QMFOKON 81)
     A            C2HTXT    R        O 19 18REFFLD(RAHTXT)
7.xx A N30                                  DSPATR(UL)
7.xx A  30                                  COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 20  2'E-mail .  '
     A            C2EMAL    R        B 20 13REFFLD(RKEMAL)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A  59                                  ERRMSG('E-mailadresse må fylles ut -
     A                                      for ehandelskunde')
6.32 A  82                                  ERRMSG('E-mailadr. må fylles ut sje-
6.32 A                                      kk fakturatype')
     A  70 81                               ERRMSGID(RMK0510 *LIBL/QMFOKON 81)
     A  26 81                               ERRMSG('Ugyldige data i feltet' 81)
7.xx A                                 21  2'Sist endret dato '
     A            C2EDAT         6Y 0O 21 20
7.xx A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
7.xx A N30                                  DSPATR(UL)
     A                                      OVRDTA
8.10 A                                 21 30'Endret av.  '
8.10 A            C2ESIG    R        O 21 43REFFLD(RKESIG)
7.xx A                                      COLOR(YLW)
7.xx A N30                                  DSPATR(UL)
     A                                      OVRDTA
8.10 A  91                             21 68'F18=Bisnode '
8.10 A                                      COLOR(BLU)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20050601  082148  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 23  2'F7=Fakt./EDI  F8=Autogiro  F9=Fors-
     A                                      endelse  F10=Kontaktpers.  F11=Besø-
     A                                      ksadr.'
     A                                      COLOR(BLU)
     A                                 22  2'F2=Notatark  F3=Gå tilbake  F4=Spø-
     A                                      rring  F5=Poster  F6=Flere koder  F-
     A                                      13=Kort'
     A                                      COLOR(BLU)
     A*
     A          R E2WIN
     A*%%TS  SD  20051030  113217  KOB         REL-V5R3M0  5722-WDS
7.xx A                                      WINDOW(6 6 17 50)
     A                                      PUTOVR
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Besøkadresse '
     A                                      DSPATR(RI)
     A                                  4  2'Kundenr.. . . .'
     A            E2KUND    R        O  4 18REFFLD(RKKUND)
     A                                  5  2'Navn. . . . . .'
     A            E2NAVN    R        O  5 18REFFLD(RKNAVN)
     A                                  6  2'Adresse . . . .'
     A            E2BGAT    R        B  6 18REFFLD(RKBGAT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  7  2'Adresse 2 . . .'
     A            E2BADR    R        B  7 18REFFLD(RKBADR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
7.xx A N30                              8  2'Poststed. . . +'
7.xx A  30                              8  2'Poststed. . .'
     A            E2BPNR    R        B  8 18REFFLD(RKBPNR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            E2BSTE    R   25   O  8 23REFFLD(RKSTED)
     A                                      TEXT('Poststed')
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 16 17'F4=Spørring'
     A                                      COLOR(BLU)
7.xx A                                 10  2'Web . . '
     A            E2WEBA    R        B 10 11REFFLD(RKWEBA)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(LC)
7.xx A N30                             12  2'Mobiloperatør +'
7.xx A  30                             12  2'Mobiloperatør'
     A            E2MOBO    R        B 12 18REFFLD(RKMOBO)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  47                                  DSPATR(PC)
     A  47 81                               ERRMSGID(RM01260 *LIBL/QMFOKON 81)
     A            E2MTXT    R        O 12 21REFFLD(RECTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A          R F2WIN
     A*%%TS  SD  20080406  105115  KOB560      REL-V5R4M0  5722-WDS
8.01 A                                      WINDOW(5 6 18 71)
     A                                      CA03
8.01 A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Koder Nexstep handel'
     A                                      COLOR(WHT)
8.13 A                                  3  2'Splitte pos/neg otype'
8.13 A            F22FAS    R        B  3 24REFFLD(RKFAGB)
8.13 A N30                                  COLOR(YLW)
8.13 A                                      DSPATR(UL)
8.13 A  30                                  DSPATR(PR)
8.13 A                                      OVRDTA
8.13 A                                      OVRATR
8.13 A                                  3 26'1/0'
8.xx A                                  3 38'Utelat fra factoring '
8.xx A            F2FACK    R    1   B  3 60REFFLD(RKFACK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  4  2'Fakturagebyr . . . . '
     A            F2FAGB    R        B  4 24REFFLD(RKFAGB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  4 26'1/0'
7.xx A                                  4 38'Fraktgebyr . . . . . '
7.xx A            F2FRGB    R    1   B  4 60REFFLD(RKFRGB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5  2'Ekspedisjonsgebyr .  '
     A            F2EKGB    R        B  5 24REFFLD(RKEKGB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5 26'1/0'
7.01 A**                                5 38'Ordrebekreftelse . . '
7.01 A                                  5 38'EDI m/ordrebekr. . . '
     A            F2ORDB    R        B  5 60REFFLD(RKORDB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5 62'1/0'
7.xx A                                  6  2'Pris/Pakkseddel. . . '
     A            F2PRPA    R        B  6 24REFFLD(RKPRPA)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  6 26'1/0'
7.xx A                                  6 38'Samlefaktura . . . . '
     A            F2SAMF    R        B  6 60REFFLD(RKSAMF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A*                                     VALUES(0 1 2 3 4)
7.xx A                                  6 63'(Samf ,)'
7.xx A                                  7  2'Fakturasekvens . . . '
     A            F2SEKV    R        B  7 24REFFLD(RKSEKV)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  7 38'Krav til rekvisisjon '
     A            F2REKV    R        B  7 60REFFLD(RKREKV)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  7 62'1/0'
7.xx A                                  8  2'Fakturagrense. . . . '
     A            F2FAGR    R        B  8 24REFFLD(RKFAGR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  8 38'Antall kopier '
7.xx A                                  9  2'Rabattsperre . . . . '
     A            F2RASP    R        B  9 24REFFLD(RKRASP)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A N30                                  COLOR(YLW)
7.xx A                                  9 26'1/0'
7.xx A                                  9 41'Faktura . . . . . '
     A            F2FCOP    R        B  9 60REFFLD(RKFCOP)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  9 62'(ant)'
7.xx A                                 10  2'Pris på faktura. . . '
     A            F2NETO    R        B 10 24REFFLD(RKNETO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A  30                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A N30                                  COLOR(YLW)
7.xx A                                 10 26'1/0'
7.xx A                                 10 41'Ordre . . . . . . '
     A            F2OCOP    R        B 10 60REFFLD(RKOCOP)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 10 62'(ant)'
7.xx A                                 11  2'Fakt. e/prisbokenhet '
     A            F2PBOK    R        B 11 24REFFLD(RKPBOK)
     A                                      DSPATR(UL)
7.xx A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 11 26'1/0'
7.xx A                                 11 41'Pakkseddel. . . . '
     A            F2PCOP    R        B 11 60REFFLD(RKPCOP)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 11 62'(ant)'
7.xx A                                 12  2'Frie statistikk-kode '
     A            F2FRSK    R        B 12 24REFFLD(RKFRSK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 12 38'E-handelskunde . . . '
     A            F2EBIZ    R        B 12 60REFFLD(RKEBIZ)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
7.xx A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  85                                  ERRMSGID(RMK0530 *LIBL/QMFOKON 85)
     A*                                     ERRMSG('Gyldige verdier 0 og 1')
7.xx A                                 12 62'(kode)'
7.xx A                                 13  2'Ordrerabatt. . . . . '
     A            F2ORAB    R        B 13 24REFFLD(RKORAB)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTCDE(1)
7.xx A                                 13 38'Leveringstype. . . . '
     A            F2LETY    R        B 13 60REFFLD(RKLETY)
     A                                      DSPATR(UL)
7.xx A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
7.xx A                                 13 62'(kode)'
7.xx A                                 14  2'Avgiftsfri . . . . . '
     A            F2MKOD    R        B 14 24REFFLD(RKMKOD)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                 14 26'0/1/2'
7.xx A                                 14 38'Kontantkunde tilbud. '
6.30 A            F2KOTI    R        B 14 60REFFLD(RKKOTI)
6.30 A                                      DSPATR(UL)
7.xx A  30                                  DSPATR(PR)
6.30 A                                      OVRDTA
6.30 A N30                                  COLOR(YLW)
7.xx A                                 14 62'1/0'
7.xx A                                 15  2'Opprettet av . . . . '
     A            F2NSIG    R        O 15 24REFFLD(RKNSIG)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 15 38'Opprettet dato . . '
     A            F2NDAT         6Y 0O 15 58EDTWRD('  .  .  ')
     A                                      DSPATR(UL)
     A                                      OVRDTA
8.01 A                                 16  2'PM profil +'
8.01 A            F2PMTX        50   B 16 14DSPATR(UL)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      OVRDTA
8.01 A            F2NOIT         7  0O 16 65
8.01 A                                      EDTCDE(Z)
8.01 A N30                                  COLOR(YLW)
8.01 A                                      OVRDTA
8.01  *
8.01 A                                 17  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
      *
     A          R G2WIN
     A*%%TS  SD  20080406  115008  KOB560      REL-V5R4M0  5722-WDS
7.xx A                                      WINDOW(6 6 15 65)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 20'Koder for fakturakunde/EDI-koder'
     A                                      COLOR(WHT)
7.xx A                                  4  2'EDI-Kode ........... '
     A            G2EDIK    R        B  4 24REFFLD(RKEDIK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A                                  5  2'EAN-Kunde .......... '
     A            G2EANK    R        B  5 24REFFLD(RKEANK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  51 81                               ERRMSGID(RMK0450 *LIBL/QMFOKON 81)
7.xx A                                  7  2'Fakturakunde ....... '
     A            G2FAKU    R        B  7 24REFFLD(RKFAKU)
8.09 A     83                               ERRMSG('Feil kundenummer i +
8.09 A                                      feltet' 83)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
6.NU A            G2NAVN    R        O  7 35REFFLD(RKNAVN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                  8  2'EAN-fakt.kunde ..... '
     A            G2EANF    R        B  8 24REFFLD(RKEANF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A  61 81                               ERRMSGID(RMK0640 *LIBL/QMFOKON 61)
7.xx A                                  9  2'Konsern kundenr. ... '
     A            G2KOKU    R        B  9 24REFFLD(RFRFR/RKKOKU *LIBL/RFRF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            G2KONA    R        O  9 31REFFLD(RKNAVN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                 10  2'Factoring-nummer ... '
     A            G2FACN    R        B 10 24REFFLD(RKFACN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                             12  2'Fakturatype ........+'
7.xx A  30                             12  2'Fakturatype ........'
     A            G2FATY    R        B 12 24REFFLD(RKFATY)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  81                                  DSPATR(PC)
     A  49 81                               ERRMSGID(RMK0435 *LIBL/QMFOKON 81)
     A  50 81                               ERRMSGID(RMK0440 *LIBL/QMFOKON 81)
     A  52 81                               ERRMSGID(RMK0460 *LIBL/QMFOKON 81)
     A  53 81                               ERRMSGID(RMK0470 *LIBL/QMFOKON 81)
     A  54 81                               ERRMSGID(RMK0480 *LIBL/QMFOKON 81)
     A  55 81                               ERRMSGID(RMK0490 *LIBL/QMFOKON 81)
     A  56 81                               ERRMSGID(RMK0500 *LIBL/QMFOKON 81)
     A  57 81                               ERRMSGID(RMK0510 *LIBL/QMFOKON 81)
     A  58 81                               ERRMSGID(RMK0520 *LIBL/QMFOKON 81)
     A                                      OVRDTA
     A                                      OVRATR
     A            G2TBES        20A  O 12 27
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20050526  100703  KOB         REL-V5R2M0  5722-WDS
7.xx A                                      WINDOW(6 6 15 65)
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Autogiro'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Gyldig fra-dato .... '
     A            H2AFDA         6Y 0B  4 24
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  .  ')
7.xx A                                  5  2'Gyldig til-dato .... '
     A            H2ATDA         6Y 0B  5 24
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('  .  .  ')
7.xx A                                  6  2'Fullm. sendt ....... '
     A            H2SENT    R        B  6 24REFFLD(RKSENT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                  6 26'1/0'
     A          R I2WIN
     A*%%TS  SD  20020106  200725  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      WINDOW(6 6 15 65)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  2 19'Koder for forsendelse'
     A                                      COLOR(WHT)
7.xx A N30                              4  2'Forsendelsesmåte+'
7.xx A  30                              4  2'Forsendelsesmåte'
     A            I2FORM    R        B  4 20REFFLD(RKFORM)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43 81                               ERRMSGID(RM01170 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            I2QTXT    R        O  4 23REFFLD(RAQTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A N30                              5  2'Speditørnr......+'
7.xx A  30                              5  2'Speditørnr......'
     A            I2SPED    R        B  5 20REFFLD(RKSPED)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45 81                               ERRMSGID(RM01220 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            I2VTXT    R        O  5 24REFFLD(RAVTXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A N30                              6  2'Leveringsbet....+'
7.xx A  30                              6  2'Leveringsbet....'
     A            I2LBET    R        B  6 20REFFLD(RKLBET)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44 81                               ERRMSGID(RM01180 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            I2RTX1    R        O  6 23REFFLD(RARTX1)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                  7  2'Lev.adresse..... '
     A            I2LADR    R        B  7 20REFFLD(RKLADR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
7.xx A N30                              8  2'Kjørerute ......+'
7.xx A  30                              8  2'Kjørerute ......'
     A            I2KJOR    R        B  8 20REFFLD(RKKJØR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46 81                               ERRMSGID(RM01180 *LIBL/QMFOKON 81)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      OVRATR
     A            I2YTXT    R        O  8 23REFFLD(RFRFR/RAYTXT *LIBL/RFRF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
7.xx A                                  9  2'Rekkefølge rute. '
     A            I2KJNR    R        B  9 20REFFLD(RFRFR/RKKJNR *LIBL/RFRF)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                  7 22'1/0'
     A          R J2WIN
     A*%%TS  SD  20050920  155801  AMD540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 15 52)
     A                                      CA03
     A                                      CF04
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A            C2NYEN        11A  O  2  2DSPATR(RI)
7.xx A                                  4  2'Spesialavtale '
     A            J2SAT1    R        B  5 19REFFLD(RKSAT1)
     A            J2SAT2    R        B  6 19REFFLD(RKSAT2)
     A            J2SAT3    R        B  7 19REFFLD(RKSAT3)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R S2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme mobiltelefonnummer er'
     A                                  3  2'registrert på kundenr.'
     A            S2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            S2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R T2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme ean-kundenummer er'
     A                                  3  2'registrert på kundenr.'
     A            T2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            T2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R U2MSG
     A*%%TS  SD  20050925  104732  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme foretaksnummer er '
     A                                  3  2'registrert på kundenr.'
     A            U2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            U2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R V2MSG
     A*%%TS  SD  20050925  104732  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme personnummer er'
     A                                  3  2'registrert på kundenr.'
     A            V2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            V2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R W2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme alfakode og postnr. er'
     A                                  3  2'registrert på kundenr.'
     A            W2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            W2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R X2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme bankgironr. er    '
     A                                  3  2'registrert på kundenr.'
     A            X2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            X2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Y2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme telefonnummer er'
     A                                  3  2'registrert på kundenr.'
     A            Y2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Y2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R Z2MSG
     A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      HELP
     A            W_ALIN         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2'Samme e-mail adresse er'
     A                                  3  2'registrert på kundenr.'
     A            Z2KUND    R        O  5  1REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            Z2NAVN    R   -7   O  5  8REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.10 A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
8.10 A          R G3WIN
8.10 A                                      WINDOW(&W_ALIN &W_ACOL 11 49)
8.10 A                                      CA03
8.10 A                                      CA12
8.10 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.10 A                                      OVERLAY
8.10 A                                      PUTOVR
8.10 A            W_ALIN         2S 0P
8.10 A            W_ACOL         3S 0P
8.10 A            W_ARCD        10A  H
8.10 A            W_AFLD        10A  H
8.10 A                                  2  2' Oppslag mot Bisnode '
8.10 A                                      DSPATR(RI)
8.10 A                                  4  3'Foretaks nummer. .'
8.10 A            G3FNUM         9A  B  4 22COLOR(YLW)
8.10 A                                      OVRDTA
8.10 A                                  6  3'Person nummer. . .'
8.10 A            G3PNUM        11A  B  6 22COLOR(YLW)
8.10 A                                      OVRDTA
8.10 A                                  7  3'Personnummer benyttes kun til opps-
8.10 A                                      lag.'
8.10 A                                      COLOR(RED)
8.10 A                                  8  3'Dette vil ikke bli lagret.'
8.10 A                                      COLOR(RED)
8.10 A                                 10  2'F3=Tilbake'
8.10 A                                      COLOR(BLU)
8.10 A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
8.10 A          R G4WIN
8.10 A                                      WINDOW(&W_ALIN &W_ACOL 11 49)
8.10 A                                      CA03
8.10 A                                      CA12
8.10 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.10 A                                      OVERLAY
8.10 A                                      PUTOVR
8.10 A            W_ALIN         2S 0P
8.10 A            W_ACOL         3S 0P
8.10 A            W_ARCD        10A  H
8.10 A            W_AFLD        10A  H
8.10 A                                  2  2' Kredittsjekk mot Bisnode '
8.10 A                                      DSPATR(RI)
8.10 A  63                              4  3'Foretaks nummer. .'
8.10 A  63        G4FNUM         9Y 0B  4 22COLOR(YLW)
8.10 A N63                              6  3'Person nummer. . .'
8.10 A N63        G4PNUM        11Y 0B  6 22DSPATR(UL)
8.10 A                                      EDTCDE(Z)
8.10 A                                      COLOR(YLW)
8.10 A                                      OVRDTA
8.10 A N63                              7  3'Personnummer benyttes kun til kred-
8.10 A                                      ittsjekk.'
8.10 A N63                                  COLOR(RED)
8.10 A N63                              8  3'Dette vil ikke bli lagret.'
8.10 A N63                                  COLOR(RED)
8.10 A                                 10  2'F3=Tilbake'
8.10 A                                      COLOR(BLU)
8.10 A*%%TS  SD  20050915  112130  KOB540      REL-V5R3M0  5722-WDS
8.10 A          R G5WIN
8.10 A                                      WINDOW(&W_ALIN &W_ACOL 13 49)
8.10 A                                      CA03
8.10 A                                      CA12
8.10 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
8.10 A                                      OVERLAY
8.10 A                                      PUTOVR
8.10 A            W_ALIN         2S 0P
8.10 A            W_ACOL         3S 0P
8.10 A            W_ARCD        10A  H
8.10 A            W_AFLD        10A  H
8.10 A                                  2  2' Kredittsjekk mot Bisnode '
8.10 A                                      DSPATR(RI)
8.10 A  63                              4  3'Foretaks nummer'
8.10 A  63        G5FNUM         9Y 0O  4 22COLOR(YLW)
8.10 A N63                              6  3'Person nummer'
8.10 A N63        G5PNUM        11Y 0O  6 22DSPATR(UL)
8.10 A                                      COLOR(YLW)
8.10 A                                      EDTCDE(Z)
8.10 A                                      OVRDTA
8.10 A            G5SCOR        40A  O  8  3COLOR(GRN)
8.10 A                                      OVRDTA
8.10 A N64                             10  3'Skal kredittgrense oppdateres J'
8.10 A N64        G5GOPD         1A  B 10 35COLOR(YLW)
8.10 A                                      OVRDTA
8.10 A                                 12  1'F3=Tilbake'
8.10 A                                      COLOR(BLU)
8.10 A          R G3MSG1
8.10 A*%%TS  SD  20050915  120525  KOB540      REL-V5R3M0  5722-WDS
8.10 A                                      WINDOW(&W_ALIN &W_ACOL 8 30)
8.10 A                                      CA03
8.10 A                                      CA12
8.10 A                                      PUTOVR
8.10 A                                      HELP
8.10 A            W_ALIN         2S 0P
8.10 A            W_ACOL         3S 0P
8.10 A                                  2  2'Kredittgrenser for Bisnode '
8.10 A                                  3  2'er ikke satt opp!     '
8.10 A                                  5  2'Må settes opp før bruk.'
8.10 A                                  7  2'F3=Gå tilbake'
8.10 A                                      COLOR(BLU)
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="B2">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+  <record-write record-format="C2BLD" />
     A*%%RS+  <record-write record-format="B2CMD" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
