     A*%%TS  SD  20231128  112446  ASP_MTT     REL-V7R4M0  5770-WDS
     A*%%EC
8.00 A*      210614 SST Mulighet til å kjøre dagsoppgjør fra jobbplan
8.03 A*      100624 bsa Skiller mellom dagsoppgjør og oppdatering av
8.03 A*      100624     økonomi.
8.07 A*      050925 bsa Fjerner oppsett for dagsoppgjør å batch.
8.08 A*      081025 bsa Feil visning i newlook.
     A*
     A*
     A*--------------------------------------------------------------------*/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BFRF BFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20231128  112446  ASP_MTT     REL-V7R4M0  5770-WDS
     A                                      CF01
     A                                      CF03
     A                                      CF04
8.07 A*                                     CF06
     A                                      CF12
     A                                      KEEP
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Butikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utkjøring av dagsoppgjør'
     A                                      COLOR(WHT)
8.08 A*                                 2 38'BO120B1'
8.08 A*                                     COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  3'Kjøredato   . . . . . . .'
     A            B1DATO         6Y 0B  4 29COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  5  3'Klokkeslett for start . .'
     A            B1TIME         6Y 0B  5 29EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  5 45'Blank, startes umiddelbart'
8.07 A*                                 6  5'Aut. dagsopp fra jobbpl'
8.07 A*           B1JOBP         1A  B  6 29COLOR(YLW)
8.07 A*                                     VALUES(' ' 'J')
8.07 A* 21                                  DSPATR(PC)
8.07 A*                                 6 33'J=Ja'
8.07 A*                                 7  5'Aut. oppdatering av øko'
8.07 A*           B1OKOO         1A  B  7 29COLOR(YLW)
8.07 A*                                     VALUES(' ' 'J')
8.07 A* 21                                  DSPATR(PC)
8.07 A*                                 7 33'J=Ja'
8.08 A                                  8  3'Avstemmingsliste. . . . .'
8.08 A            B1AVST    R        B  8 29REFFLD(BÅVALG)
     A                                      COLOR(YLW)
8.08 A                                      VALUES(0 1)
8.08 A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
     A  21                                  DSPATR(PC)
8.08 A                                  8 33'(kode)'
8.08 A                                  9  5'0=Kontrolliste/1=Avst.liste'
8.08 A                                 10  3'Totaler . . . . . . . . .'
8.08 A            B1TOTA    R        B 10 29REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(BM00110 *LIBL/QMFSHOP 37)
     A  38                                  ERRMSGID(BM00130 *LIBL/QMFSHOP 38)
     A  39                                  ERRMSGID(BM00135 *LIBL/QMFSHOP 39)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
8.03 A                                 10 33'1=Ja,0=Nei'
8.03 A                                 11  3'Detaljer. . . . . . . . .'
8.03 A            B1DETA    R        B 11 29REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
8.03 A                                 11 33'1=Ja,0=Nei'
8.03 A                                 12  3'Dekningsliste . . . . . .'
8.03 A            B1DEKN    R        B 12 29REFFLD(BÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(BM00180 *LIBL/QMFSHOP)
8.03 A                                 12 33'1=Ja,0=Nei'
     A                                 13  3'Utelat kreditsalg . . . .'
     A            B1KRED    R        O 13 29REFFLD(BÅVALG)
     A                                      COLOR(GRN)
     A                                 13 33'1=Ja,0=Nei'
     A                                 14  3'Arbeidsstasjonsgruppe . .'
     A            B1WSGR    R        B 14 29REFFLD(BWWSGR)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(BM00070 *LIBL/QMFSHOP 33)
     A  35                                  ERRMSGID(BM00150 *LIBL/QMFSHOP 35)
     A  52                                  ERRMSGID(BM00310 *LIBL/QMFSHOP 52)
     A                                 14 51'Gruppe av arbeidsstasjoner'
     A                                 15  4'eller'
     A                                 16  3'Arbeidsstasjoner. . . . .'
     A            B1WSID    R        B 16 29REFFLD(BDWSID)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 32)
     A  34                                  ERRMSGID(BM00120 *LIBL/QMFSHOP 34)
     A  36                                  ERRMSGID(BM00140 *LIBL/QMFSHOP 36)
     A                                 16 51'*ALL =Alle   arbeidsstasjoner'
     A                                 17 45'eller Blank=Flere  arbeidsstasjone-
     A                                      r'
     A                                 18 45'eller oppgi Enkelt arbeidsstasjon'
     A                                 19  3'Ordre-type  . . . . . . .'
     A            B1OTYP         4A  B 19 29COLOR(YLW)
     A  31                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 31)
     A                                 19 51'*ALL =Alle   ordretyper'
     A                                 20 45'eller Blank=Flere  ordretyper'
     A                                 21 45'eller oppgi enkelt ordretype'
     A                                 23  2'F3=Gå tilbake  F4=Jobbkø'
     A                                      COLOR(BLU)
     A  53                             22  2'F1=Kjør dagsoppgjør'
     A                                      COLOR(BLU)
8.07 A*                                23 30'F6=Legges på jobbplan for daglig k-
8.07 A*                                     jøring'
8.07 A*                                     COLOR(BLU)
8.07 A* 55                                  DSPATR(ND)
     A          R B2BLD
     A*%%TS  SD  20041222  124943  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 8 22)
     A                                      CF03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2  2'Periode/Virkedager'
     A                                      COLOR(WHT)
     A                                  4  2'Periode (MMÅÅ)'
     A            B2PERI    R   -2   B  4 18REFFLD(BOPERI)
     A                                      EDTCDE(Z)
     A                                      COMP(NE 0)
     A                                      CHKMSGID(BM00200 *LIBL/QMFSHOP)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(BM00200 *LIBL/QMFSHOP 31)
     A  32                                  ERRMSGID(BM00220 *LIBL/QMFSHOP 32)
     A                                      COLOR(YLW)
     A                                  5  2'Virkedager '
     A            B2VDAG    R        B  5 18REFFLD(BUVDAG)
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(BM00230 *LIBL/QMFSHOP 33)
     A                                      COLOR(YLW)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R B3BLD
     A*%%TS  SD  20041222  124943  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 7 22)
     A                                      CF03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2  2'Dekningsliste'
     A                                      COLOR(WHT)
     A                                  4  2'Virkedag i per. .'
     A            B3VDAG    R        B  4 20REFFLD(BUVDAG)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(BM00230 *LIBL/QMFSHOP 31)
     A                                      COLOR(YLW)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4BLD
     A*%%TS  SD  20041222  124943  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 17 22)
     A                                      CF03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2  2'Valg av skjermer'
     A                                      COLOR(WHT)
     A                                  4  2'Arbeidsstasjons-id:'
     A            B4WS01    R        B  5  9REFFLD(BWWSID)
     A  31                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 31)
     A  41                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 41)
     A  51                                  ERRMSGID(BM00190 *LIBL/QMFSHOP 51)
     A                                      COLOR(YLW)
     A            B4WS02    R        B  6  9REFFLD(BWWSID)
     A  32                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 32)
     A  42                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 42)
     A                                      COLOR(YLW)
     A            B4WS03    R        B  7  9REFFLD(BWWSID)
     A  33                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 33)
     A  43                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 43)
     A                                      COLOR(YLW)
     A            B4WS04    R        B  8  9REFFLD(BWWSID)
     A  34                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 34)
     A  44                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 44)
     A                                      COLOR(YLW)
     A            B4WS05    R        B  9  9REFFLD(BWWSID)
     A  35                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 35)
     A  45                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 45)
     A                                      COLOR(YLW)
     A            B4WS06    R        B 10  9REFFLD(BWWSID)
     A  36                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 36)
     A  46                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 46)
     A                                      COLOR(YLW)
     A            B4WS07    R        B 11  9REFFLD(BWWSID)
     A  37                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 37)
     A  47                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 47)
     A                                      COLOR(YLW)
     A            B4WS08    R        B 12  9REFFLD(BWWSID)
     A  38                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 38)
     A  48                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 48)
     A                                      COLOR(YLW)
     A            B4WS09    R        B 13  9REFFLD(BWWSID)
     A  39                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 39)
     A  49                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 49)
     A                                      COLOR(YLW)
     A            B4WS10    R        B 14  9REFFLD(BWWSID)
     A  40                                  ERRMSGID(BM00060 *LIBL/QMFSHOP 40)
     A  50                                  ERRMSGID(BM00160 *LIBL/QMFSHOP 50)
     A                                      COLOR(YLW)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B5BLD
     A*%%TS  SD  20041222  124943  HKO530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(&WACTLI &WACTPO 17 22)
     A                                      CF03
     A                                      CF04
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A                                  2  2'Valg av ordretyper'
     A                                      COLOR(WHT)
     A                                  4  6'Ordretyper:'
     A            B5OT01    R        B  5 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  31                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 31)
     A  51                                  ERRMSGID(BM00170 *LIBL/QMFSHOP 51)
     A                                      COLOR(YLW)
     A            B5OT02    R        B  6 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  32                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 32)
     A                                      COLOR(YLW)
     A            B5OT03    R        B  7 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  33                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 33)
     A                                      COLOR(YLW)
     A            B5OT04    R        B  8 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  34                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 34)
     A                                      COLOR(YLW)
     A            B5OT05    R        B  9 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  35                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 35)
     A                                      COLOR(YLW)
     A            B5OT06    R        B 10 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  36                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 36)
     A                                      COLOR(YLW)
     A            B5OT07    R        B 11 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  37                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 37)
     A                                      COLOR(YLW)
     A            B5OT08    R        B 12 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  38                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 38)
     A                                      COLOR(YLW)
     A            B5OT09    R        B 13 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  39                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 39)
     A                                      COLOR(YLW)
     A            B5OT10    R        B 14 10REFFLD(BAAOKD)
     A                                      DLTCHK
     A  40                                  ERRMSGID(BM00050 *LIBL/QMFSHOP 40)
     A                                      COLOR(YLW)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
