     A*%%TS  SD  20080313  100004  AMD550      REL-V5R4M0  5722-WDS
     A*
6.30 A*     BSA  20140806  Omskrevet for å redusere bilder og taste-
6.30 A*                    trykk.
6.31 A*     BSA  20140808  Innført sjekk mot origo via F-tast
6.32 A*     BSA  20140811  Utvider kontrollre mot kortkode og F-tast
7.xx A*     BSA  210116    Endringer relatert til ny GUI
9.xx A*     BKV  180818    Kundetilpassinger
Q.04 A*     bsa  010319    Quick wins tilpasninger - bytt kunde
Q.111A*     bsa  270619    QW - Rettelse ved feilømedling
7.26 A*     bsa  121119    Mulighet for å bytte varenummer
7.26 A*                    (Tatt fra forskuddsbetaling)
7.29 A*     sst  171019    Åpne for F17, Hente budsjett til Prosjekt
7.30 A*     sst  260320    Åpne for F19, Hente varelinjer fra excel
7.31 A*     ask  240420    Lagt inn kobling til Proffportal tilbud
7.2e A*     bsa  130120    Feilmelding ved scroll
7.2L A*     sst  180320    Sperre funksjonstaster for brukere
7.2lxA*     ask  240320    Lagt inn fargekodestyring på flere felter
7.2S A*K000 bsa  291220    Flyttet på plassering av kudnenummer, da
7.2S A*K000                siste siffer forsvinner i cloud.
7.2u A*K000 bsa  050121    Mulighet for å skrive plukkliste direkte
7.2u A*K000                fra valg til plukking.
7.2v A*K000 bsa  140521    Hvis negativ beholdning settes rød farge.
7.2v A*K000                Benytter samme farge som utgått vare. Betyr
7.2v A*K000                at det kan forvirre, hvis både negativ
7.2v A*K000                beholdning og utgått vare.
     A*
7.fb A*     bof  040419    Utvidet med forskuddsbetaling.
7.f5 A*     bsa  170620    Endret måten å registrere depositumskrav
7.f0 A*     bsa  180920    Mulighet for å slette depositum.
7.fa A*7.00 bsa  210920    Reduserer antall tegn i felt til 2.
7.fa A*7.00                Eget felt for 100% forhåndsbetaling.
7.fc A*7.00 bsa  220920    Låser muligheten for å slette, og endre
7.fc A*7.00                depositum etter det er innbetalt.
7.fe A*7.00 bsa  021020    Viser krav til depositum.
     A*
8.02 A*K000 bsa  170122    Feil bruk av indikator
8.06 A*K000 bsa  250522    Legger ut suffiks
8.13 A*K000 bsa  200323    Fjerner endring 8.06, da dette skaper problemer
8.13 A*K000 bsa            i newlook.
     A*
     A*----------------------------------------------------------------
     A*%%EC
7.25 A                                      DSPSIZ(27 132 *WIDE)
7.25 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
7.xx A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20010924  094005  HKO         REL-V4R4M0  5769-PW1
      * --> START IKKE OA VERSJON  <--
     A*            B1SUMN    R        H      REFFLD(FDSUMS)
     A*            B1SUMK    R        H      REFFLD(FDSUMK)
     A*            B1SUMB    R        H      REFFLD(FDSUMK)
     A*            B1GRAD         5Y 2H
     A*            B1VALG    R        B 13  2REFFLD(FÅVALG)
7.xx A*                                      COLOR(YLW)
7.xx A*                                      VALUES(0 2 4 5 9)
     A*                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A*                                      EDTCDE(Z)
     A*            B1LETY         1Y 0O 13  4EDTCDE(Z)
7.xx A*            B1TEXT        75A  O 13  6
7.xx A*            B1VARE         8A  H
     A*            B1TEK1    R        H      REFFLD(FDTEK1)
7.xx A*            B1ANTA    R   -1   H      REFFLD(FDANTA)
7.xx A*            B1ENHE    R        H      REFFLD(FDENH1)
     A*            B1SAPR         8S 2H
7.2C A            B1RABA    R        H      REFFLD(FDRAB1)
7.xx A*            B1KPRI         3A  O 13 77
      *
7.xx A*            B1TEK2    R        H      REFFLD(FDTEK2)
7.xx A*            B1NETO        12A  H
7.xx A*            B1KOST        12A  H
7.xx A*            B1LBEH        12A  H
7.xx A*            B1DEKG         6A  H
7.xx A*            B1LINE    R        H      REFFLD(FDLINE)
7.2L A            B1LKTX         1Y 0H
      * --> SLUTT IKKE OA VERSJON  <--
      * --> START OA VERSJON  <--
     A            B1LINE    R        H      REFFLD(FDLINE)
     A            B1SUMN    R        H      REFFLD(FDSUMS)
     A            B1SUMK    R        H      REFFLD(FDSUMK)
     A            B1SUMB    R        H      REFFLD(FDSUMK)
     A            B1GRAD         5Y 2H
7.xx A            B1TEK2    R        H      REFFLD(FDTEK2)
7.xx A            B1TEK1    R        H      REFFLD(FDTEK1)
7.25 A*            B1NETO        12A  H
7.25 A*            B1KOST        12A  H
7.25 A*           B1LBEH        12A  H
7.25 A            B1DEKG         7A  H
7.25 A            B1VALG    R        B 11  2REFFLD(FÅVALG)
7.xx A                                      COLOR(YLW)
7.fb A                                      VALUES(0 2 4 5 6 8 9)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      EDTCDE(Z)
7.25 A            B1LETY         1Y 0O 11  4EDTCDE(Z)
7.25 A            B1VARE         8A  O 11  6
7.08 A  53                                  COLOR(YLW)
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
7.2C A            B1TEXT        35A    11 15
7.2C A  52
7.2lxAO 44                                  COLOR(BLU)
7.2lxA  47                                  COLOR(RED)
6.3l A  53                                  COLOR(YLW)
7.2C A            B1TEX1        31A    11 51
7.2C A  52
7.2lxAO 44                                  COLOR(BLU)
7.2lxA  47                                  COLOR(RED)
7.2C A  53                                  COLOR(YLW)
7.25 A            B1ANTA    R   -1   O 11 83REFFLD(FDANTA)
7.xx A                                      EDTCDE(M)
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
7.25 A            B1ENHE    R        O 11 96REFFLD(FDENH1)
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
7.25 A            B1SAPR    R        O 11100REFFLD(FDSAPR)
7.25 A                                      EDTWRD('     0 ,  -')
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
7.2h A            B1TAL1         6A  O 11112
7.2h A*            B1TAL1    R   -1   O 11112REFFLD(FDRAB1)
7.xh A*                                      EDTCDE(M)
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
7.25 A            B1KPRI         3A  O 11119
9.xx A  45                                  COLOR(BLU)
9.xx A  46                                  COLOR(RED)
7.2h A            B1LBEH         6A  O 11123
7.2h A*           B1LBEH         5  2O 11123EDTCDE(M)
7.25 A  45                                  COLOR(BLU)
7.2v A  46
7.2v AO 47                                  COLOR(RED)
7.08 A* N46                                  COLOR(GRN)
      * --> SLUTT OA VERSJON  <--
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051110  002952  AMD550      REL-V5R3M0  5722-WDS
7.25 A                                      SFLSIZ(0015)
7.25 A                                      SFLPAG(0014)

7.03 A                                      ROLLUP(10)
7.2e A                                      ROLLDOWN(11)
7.2L A                                      VLDCMDKEY(16)
     A                                      CF01
     A                                      CF02
     A                                      CA03
     A                                      CF04
7.25 A                                      CF05
     A                                      CF06
8.02 A  58                                  CF07
7.2D A N56                                  CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF13
     A                                      CF14
6.31 A                                      CF15
7.19 A                                      CF16
7.29 A                                      CF17
8.02 A  59                                  CF19
     A                                      CF20
     A                                      CF21
     A                                      CF23
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
7.25 A*  15                                  SFLEND(*MORE)
7.25 A  15                                  SFLEND
     A                                      SFLCSRRRN(&W_CURN)
7.02 A*                                     RTNCSRLOC(&W_ARCDB2 &W_AFLDB2)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
7.02 A*           W_ARCDB2      10A  H
7.02 A*           W_AFLDB2      10A  H
     A            B2ANTA    R        H      REFFLD(FDANTA)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
7.25 A                                  1118'Kl. :'
7.25 A                                      COLOR(WHT)
7.25 A                                  1124TIME
7.25 A                                      COLOR(WHT)
     A                                  2  2'Registrering av varelinjer'
7.xx A                                      COLOR(WHT)
8.13 A*                                 2 38'FD100Y3'
8.13 A                                  2 38'FD100Y2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.25 A                                  2118'Dato:'
7.25 A                                      COLOR(WHT)
7.25 A                                  2124DATE
7.25 A                                      EDTCDE(Y)
7.25 A                                      COLOR(WHT)
7.25 A                                  3  1'-----------------------------------
7.25 A                                      ------------------------------------
7.25 A                                      ------------------------------------
7.25 A                                      ---------------------------'
7.25 A                                      COLOR(WHT)
7.25 A                                  4  2'Ordre:'
     A                                      COLOR(WHT)
     A            B2ONUM    R        O  4  9REFFLD(FONUMM)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
8.13 A*           B2OSUF    R        O  4 18REFFLD(FOSUFF)
8.13 A*                                     OVRDTA
8.13 A* 45                                  COLOR(RED)
8.13 A* 46                                  COLOR(BLU)
8.13 A*           B2OTYP    R        O  4 21REFFLD(FOOTYP)
8.13 A            B2OTYP    R        O  4 19REFFLD(FOOTYP)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
8.13 A*           B2NAVN    R   -1   O  4 24REFFLD(FONAVN)
8.13 A            B2NAVN    R   -1   O  4 22REFFLD(FONAVN)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
6.NU A            B3NAVN    R   -1   O  4 56REFFLD(FONAVN)
     A                                      OVRDTA
     A                                  5  2'Kunde:'
     A                                      COLOR(WHT)
7.2s A            B2KUND    R        O  5  9REFFLD(FOKUND)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
8.13 A*           B2ADR1    R   -1   O  5 24REFFLD(FOADR1)
8.13 A            B2ADR1    R   -1   O  5 22REFFLD(FOADR1)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
6.NU A            B3ADR1    R   -1   O  5 56REFFLD(FOADR1)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
     A                                  6  2'L.kun:'
     A                                      COLOR(WHT)
7.xx A            B2LKUN    R        O  6 11REFFLD(FOLKUN)
     A                                      OVRDTA
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
8.13 A*           B2STED    R   -1   O  6 24REFFLD(FOSTED)
8.13 A            B2STED    R   -1   O  6 22REFFLD(FOSTED)
     A                                      OVRDTA
6.NU A            B3STED    R   -1   O  6 56REFFLD(FOSTED)
     A                                      OVRDTA
     A                                  7  2'Leveringstype:'
     A                                      COLOR(WHT)
7.xx A            B3LETY    R        O  7 19REFFLD(FOLETY)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BESK        30A  O  7 21OVRDTA
Q.01 A  48        B2VALG         2  0B  7 52EDTCDE(Z)
Q.111A                                      OVRDTA
Q.01 A*            B2VALG         2  0B  7 52EDTCDE(Z)
7.25 A                                  4113'Tlf:'
7.xx A                                      COLOR(WHT)
7.25 A            B2TLFN        13A  O  4118OVRDTA
7.25 A            B2OTXT    R        O  7 56REFFLD(VAOTXT)
     A                                      COLOR(TRQ)
     A                                      OVRDTA
7.25 A  75                              6 91'Bonusavtale'
     A  75                                  COLOR(RED)
     A N75                                  DSPATR(ND)
7.25 A                                  5113'Mob:'
7.xx A                                      COLOR(WHT)
7.25 A            B2MOBN        13A  O  5118OVRDTA
7.25 A                                  8  2'LT'
7.xx A                                      COLOR(WHT)
7.25 A                                  8  9'L'
7.xx A                                      COLOR(WHT)
7.25 A                                  8 17'Vare . +'
     A                                      COLOR(WHT)
7.25 A  49                              4 91'DG'
     A                                      COLOR(WHT)
7.25 A  49        B2GRAD         5Y 2O  4102EDTCDE(M)
     A                                      OVRDTA
7.25 A                                  6113'Netto'
     A                                      COLOR(WHT)
7.25 A            B2NETT        11Y 2O  6119EDTCDE(M)
     A                                      OVRDTA
7.25 A            B2LTYP    R        B  8  5REFFLD(FDLTYP)
     A  31                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.25 A            B2LETY    R        B  8 11REFFLD(FDLETY)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.25 A                                  8 13'(k)'
7.25 A                                      COLOR(WHT)
7.25 A            B2VARE        33A  B  8 26COLOR(YLW)
6.30 A**          B2VARE        26A  B 10  7COLOR(YLW)
     A  33                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
     A  51                                  ERRMSGID(FMD0730 *LIBL/QMFOFAK 51)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.25 A                                  8 64'Frisøk i varelinjer'
7.25 A            B2FSOK        20A  B  8 84COLOR(YLW)
7.25 A                                      OVRDTA
7.25 A  22                                  DSPATR(PC)
7.25 A            B2FSO2        20A  O  8106COLOR(WHT)
7.25 A                                      OVRDTA
7.25 A  49                              5 91'Dekn'
     A                                      COLOR(WHT)
7.25 A  49        B2DEKN        11Y 2O  5 96EDTCDE(M)
     A                                      OVRDTA
7.25 A  49                              7113'Kost'
     A                                      COLOR(WHT)
7.25 A  49        B2KOST        11Y 2O  7119EDTCDE(M)
     A                                      OVRDTA
7.xx A**                               11  2'1=Sett inn linje  2=Endre  4=Slett-
7.xx A**                                      5=Vis'
7.fb A**                                9  2'2=Endre  4=Slett  5=Vis  8=Bytt va-
7.fb A**                                    renummer  9=Sett inn linje'
7.26 A                                  9  2'2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.fb A  50 57                           9 27'6=Bytt varelinje'
7.fb A                                      COLOR(BLU)
7.fb A                                  9 68'8=Bytt varenummer'
7.fb A                                      COLOR(BLU)
7.fb A                                  9 87'9=Sett inn linje'
7.fb A                                      COLOR(BLU)
7.25 A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.25 A                                 10  4'L'
     A                                      COLOR(WHT)
      * --> START IKKE OA VERSJON  <--
7.xx A*            B2OVER        75A  O 12  6COLOR(WHT)
     A*                                      OVRDTA
      * --> SLUTT IKKE OA VERSJON  <--
      * --> START OA VERSJON  <--
7.25 A                                 10 15'Varetekst'
7.xx A                                      COLOR(WHT)
7.25 A                                 10  6'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A            B2OVER        74A  H
7.2C A  70                             10 51'Varetekst'
7.2C A                                      COLOR(WHT)
7.2h A  71                             10 51'Sum netto    '
7.2C A                                      COLOR(WHT)
7.2h A  72                             10 51'Etter bonus  '
7.2h A                                      COLOR(WHT)
7.2h A N70                             10 65'Sum kost     '
7.2C A                                      COLOR(WHT)
7.2C A                                 10 83'Antall'
7.xx A                                      COLOR(WHT)
7.2C A*  70                             10 83'Antall'
7.2C A*                                      COLOR(WHT)
7.2C A* N70                             10 83'Sum netto'
7.2C A*                                      COLOR(WHT)
7.25 A                                 10 96'Enh'
7.xx A                                      COLOR(WHT)
7.2C A                                 10100'Salgspris'
7.2C A                                      COLOR(WHT)
7.2C A*  70                             10100'Salgspris'
7.2C A*                                      COLOR(WHT)
7.2C A* N70                             10100'Sum kost'
7.2C A*                                      COLOR(WHT)
7.25 A                                 10112'Rab'
7.xx A                                      COLOR(WHT)
7.2A A                                 10119'Kod'
7.xx A                                      COLOR(WHT)
7.2A A  70                             10123'Beh '
7.25 A                                      COLOR(WHT)
7.2A A N70                             10123'DG  '
7.25 A                                      COLOR(WHT)
      * --> SLUTT OA VERSJON  <--
     A          R B2CMD
     A*%%TS  SD  20060215  193639  HKO550      REL-V5R3M0  5722-WDS
7.2D A                                 25  2'F3=Avslutt F2=Tekstlin F5=Forny'
7.2D A                                      COLOR(BLU)
7.2D A N56                             25 34'F8=Skaffereg'
7.2D A                                      COLOR(BLU)
7.2D A                                 25 47'F1=DG-linje F6=DG-ordre'
7.2D A                                      COLOR(BLU)
7.2D A                                 25 71'F9=Lagerbeholdn. F10=Faste oppl.'
7.2D A                                      COLOR(BLU)
8.02 A  59                             25112'F19=Varer fra Excel'
7.30 A                                      COLOR(BLU)
7.f5 A  58                             25104'F7=Dep.'
7.f5 A                                      COLOR(BLU)
7.25 A                                 26  2'F11=Topp/bunntekst F13=Statistikk -
7.25 A                                      F14=Kundeinfo. F20=Til bestilling F-
7.25 A                                      21=Vare/pris'
     A                                      COLOR(BLU)
7.25 A N54                             26 84'F15=Saldo  F23=Kostpris'
     A                                      COLOR(BLU)
7.25 A  54                             26 84'F16=Ordreinfo F23=Kostpris'
     A                                      COLOR(BLU)
7.29 A  55                             26112'F17=Hent Budsj.'
7.29 A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051109  211441  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 9 30)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Informasjonsmelding '
     A                                      DSPATR(RI)
     A                                  5  2'Ordren/tilbudet inneholder'
     A                                  6  2'slettede varer.'
     A*
     A          R F1WIN
     A*%%TS  SD  20060310  102918  HKO550      REL-V5R3M0  5722-WDS
7.xx A                                      WINDOW(10 05 13 37)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Avslutning av ordre '
     A                                      DSPATR(RI)
     A  77                              4  2'Til bestilling. .'
     A  77        F1BEST         1Y 0B  4 20COLOR(YLW)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A N77                                  DSPATR(PR)
     A N77                                  DSPATR(ND)
     A                                      OVRDTA
     A  77                              4 23'1=Ja,0=Nei'
     A                                  6  2'Velg behandling .'
7.xx A            F1VALG         1A  B  6 20COLOR(YLW)
     A  31                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0050 *LIBL/QMFOFAK 32)
     A                                      OVRDTA
7.xx A                                  6 23'0=Oppdatering,'
7.xx A                                  7 23'1=Utskrift,'
     A                                  8 23'2=Utplukk'
     A  78                             10  2'Klar til plukking'
     A  78        F1PLUK         1Y 0B 10 20COLOR(YLW)
     A  33                                  ERRMSGID(VMA8200 *LIBL/QMFOFAK 33)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A N78                                  DSPATR(PR)
     A N78                                  DSPATR(ND)
     A                                      OVRDTA
     A  78                             10 23'1=Ja,0=Nei'
7.31 A  79                             11  2'Proffportal tilbud'
7.31 A  79        F1PROF         1Y 0B 11 21COLOR(YLW)
7.31 A                                      VALUES(0 1)
7.31 A                                      OVRDTA
7.31 A  79                             11 24'1=Ja,0=Nei'
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R F1MSG
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Plukk til bestilling '
     A                                      DSPATR(RI)
     A                                  4  2'Det er krav til depositum,   '
     A                                      COLOR(WHT)
     A                                  5  2'men det er ikke innbetalt.   '
     A                                      COLOR(WHT)
     A                                  6  2'Kan ikke bestille !          '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R F2WIN
     A*%%TS  SD  20080313  100004  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 11 51)
     A                                      CA03
     A                                      CA18
     A                                      PUTOVR
     A                                  2  2' Overskridelse av rabattgrense '
     A                                      DSPATR(RI)
     A                                  5  2'Kunden har overskredet rabattgrens-
     A                                      e for almenning'
     A                                  6  2'Før ytterliger registrering kan sk-
     A                                      je må grensen'
     A                                  7  2'utvides etter søknad'
     A                                  8  2'Overskridelsen er på kr.'
     A            F2OVER    R        O  8 27REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10 18'F18=Overstyring'
     A                                      COLOR(BLU)
     A          R G1WIN
     A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 60)
     A                                      CA03
     A                                      CA12
     A                                      CF06
     A                                      CA18
     A                                      PUTOVR
     A                                  2  2' Kredittgrense er overskredet '
     A                                      DSPATR(RI)
     A                                  5  2'Ordren er overskredet med . .'
     A            G1TOTL    R        O  5 32REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                  7  2'Skal ordren legges på venting'
     A            G1VALG    R        B  7 32REFFLD(FÅVALG)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 35'1=ja,0=nei'
     A                                 10  2'F3=Gå tilbake  F6=Slett ordren  F1-
     A                                      8=Godkjenn overskridelse'
     A                                      COLOR(BLU)
     A          R G2WIN
     A*%%TS  SD  20020623  204937  HKO         REL-V5R1M0  5722-WDS
6.NU A                                      WINDOW(*DFT 14 34)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette ordre '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenummer . . . . . .'
     A            G2NUMM    R        O  4 26REFFLD(FONUMM)
     A                                      OVRDTA
     A            G2NAVN    R        O  6  2REFFLD(FONAVN)
     A                                      OVRDTA
     A                                  8  2'Kommentar til slettingen:'
     A            G2KOM1    R        B  9  2REFFLD(FYKOM1)
     A  31                                  ERRMSGID(FMY0200 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            G2KOM2    R        B 10  2REFFLD(FYKOM2)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R G3WIN
     A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 60)
     A                                      CA03
     A                                      CA12
     A                                      CF06
     A                                      CA18
     A                                      PUTOVR
     A                                  2  2' Kredittgrense er låst '
     A                                      DSPATR(RI)
     A                                  5  2'Du kan ikke godkjenne overskridel-
     A                                      se'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R O1WIN
     A*%%TS  SD  20070328  105948  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 58)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
7.02 A*                                      RTNCSRLOC(&W_ARCDO1 &W_AFLDO1)
7.02 A*            W_ARCDO1      10A  H
7.02 A*            W_AFLDO1      10A  H
     A                                  2  2' Oppdatering av ordre - klar til p-
     A                                      lukking '
     A                                      DSPATR(RI)
7.2u A                                  4  2'Leveringsdato .'
7.2u A            O1LDAT         6Y 0B  4 18COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  33                                  ERRMSGID(FMO0257 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FMO0250 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.2u A                                  6  2'Informasjon . .'
7.2u A            O1INF1    R        B  6 18REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.2u A            O1INF2    R        B  7 18REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.2u A  57                              9  2'Plukkliste. . .'
7.2u A  57        O1PLUK    R        B  9 18REFFLD(FÅVALG)
7.2u A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.2u A                                      VALUES(0 1)
7.2u A                                      COLOR(YLW)
7.2u A                                      OVRDTA
7.2u A  57                              9 20'1=ja,0=nei'
7.2u A                                 13  2'F3=Gå tilbake'
7.2u A**                               13  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R R1WIN
     A*%%TS  SD  20020623  202929  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 36)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Grense for rabatt er ikke nådd '
     A                                      DSPATR(RI)
     A                                  4  2'Grense for rabatt .'
     A            R1RGRN    R        O  4 25REFFLD(FAAGRN)
     A                                      OVRDTA
     A                                  5  2'Netto ordreverdi. .'
     A            R1NETT    R        O  5 22REFFLD(FOTOTS)
     A                                      EDTWRD('       0 ,  ')
     A                                      OVRDTA
     A                                  7  2'Nullstill rabatt. .'
     A            R1VALG    R        B  7 22REFFLD(FÅVALG)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 26'1=ja,0=nei'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R T1WIN
     A*%%TS  SD  20060201  121529  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 11 42)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
7.02 A                                      RTNCSRLOC(&W_ARCDT1 &W_AFLDT1)
7.02 A            W_ARCDT1      10A  H
7.02 A            W_AFLDT1      10A  H
     A                                  2  2' Sett inn linje mellom eksisterend-
     A                                      e '
     A                                      DSPATR(RI)
     A                                  4  2'Opprett etter'
7.xx A            T1VALG    R        B  4 18REFFLD(FÅVALG)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 25'1=ja,0=nei'
7.xx A                                  6  2'Linjetype . . .'
7.xx A            T1LTYP    R        B  6 18REFFLD(FDLTYP)
     A  31                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Leveringstype +'
7.xx A            T1LETY    R        B  7 18REFFLD(FDLETY)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Vare. . . . . +'
7.xx A            T1VARE        24A  B  8 18COLOR(YLW)
     A  33                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A  35                                  ERRMSGID(VMV0020 *LIBL/QMFOFAK 35)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10 18'F4=Spørring'
     A                                      COLOR(BLU)
6.31 A          R F15WIN
6.31 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
6.31 A                                      WINDOW(*DFT 11 40)
6.31 A                                      CA03
6.31 A                                      CA12
6.31 A                                      PUTOVR
6.31 A                                  2  2' Kreditsjekk '
6.31 A                                      DSPATR(RI)
6.31 A            F15TX1        35A  O  5  2
6.31 A            F15TX2        35A  O  6  2
6.31 A            F15TX3        35A  O  7  2
6.31 A                                 10  2'F3=Gå tilbake'
6.31 A                                      COLOR(BLU)
6.31 A          R F15WI2
6.31 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
6.31 A                                      WINDOW(*DFT 11 50)
6.31 A                                      CA03
6.31 A                                      CA12
6.31 A                                      PUTOVR
6.31 A                                  2  2' Kreditsjekk '
6.31 A                                      DSPATR(RI)
6.31 A                                  4  2' Angi beløp som skal sjekkes'
6.31 A            F15BEL    R        B  5  8REFFLD(FDSUMS)
6.31 A                                      COLOR(YLW)
6.31 A                                 10  2'F3=Gå tilbake'
6.31 A                                      COLOR(BLU)
6.32 A          R F15WI3
6.32 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
6.32 A                                      WINDOW(*DFT 11 50)
6.32 A                                      CA03
6.32 A                                      CA12
6.32 A                                      PUTOVR
6.32 A                                  2  2' Kortkontroll'
6.32 A                                      DSPATR(RI)
6.32 A                                  4  2' Kortkoden på ordren kan '
6.32 A                                  5  2' ikke saldokontrolleres  '
6.32 A                                 10  2'F3=Gå tilbake'
6.32 A                                      COLOR(BLU)
9.xx A          R M1MSG1
9.xx A*%%TS  SD  20170201  135621  G0XEGASSKO  REL-V7R1M0  5770-WDS
9.xx A                                      WINDOW(*DFT 8 35)
9.xx A                                      CA03
9.xx A                                      CA12
9.xx A                                      PUTOVR
9.xx A                                  3  2'Du kan ikke plukke til       '
9.xx A                                      COLOR(WHT)
9.xx A                                  4  2'bestilling fra hovedordre!   '
9.xx A                                      COLOR(WHT)
9.xx A                                  5  2'                             '
9.xx A                                      COLOR(WHT)
9.xx A                                  6  2'                             '
9.xx A                                      COLOR(WHT)
9.xx A                                  7  2'F3=Gå tilbake'
9.xx A                                      COLOR(BLU)
Q.04 A          R Q4WIN
Q.04 A*%%TS  SD  20060201  121529  AMD550      REL-V5R3M0  5722-WDS
Q.04 A                                      WINDOW(*DFT 14 60)
Q.04 A                                      CA03
Q.04 A                                      CF04
Q.04 A                                      CA12
Q.04 A                                      PUTOVR
Q.04 A                                      RTNCSRLOC(&W_ARCDT1 &W_AFLDT1)
Q.04 A            W_ARCDT1      10A  H
Q.04 A            W_AFLDT1      10A  H
Q.04 A                                  2  2' Bytte kunde på eksisterende -
Q.04 A                                      ordre '
Q.04 A                                      DSPATR(RI)
Q.04 A                                  4  2'Odrenummer    '
Q.04 A                                      COLOR(WHT)
Q.04 A            Q4NUMM    R        O  4 18REFFLD(FONUMM)
Q.04 A                                      OVRDTA
Q.04 A            Q4NAVN    R        O  4 27REFFLD(FONAVN)
Q.04 A                                      OVRDTA
Q.04 A                                  6  2'Nytt kunde-nr +'
Q.04 A            Q4KUND    R        B  6 18REFFLD(FOKUND)
Q.04 A  31                                  ERRMSGID(FMO0200 *LIBL/QMFOFAK 31)
Q.04 A  32                                  ERRMSGID(FMO0220 *LIBL/QMFOFAK 32)
Q.04 A  33                                  ERRMSGID(FMO0230 *LIBL/QMFOFAK 33)
Q.04 A  34                                  ERRMSGID(FM01220 *LIBL/QMFOFAK 34)
Q.04 A  36                                  ERRMSG('Bestilling er knyttet til -
Q.04 A                                      ordren, kan ikke bytte kunde' 36)
Q.04 A                                      COLOR(YLW)
Q.04 A                                      OVRDTA
Q.04 A                                  7  2'Prosjekt. . . +'
Q.04 A            Q4KPRO    R        B  7 18REFFLD(FOKPRO)
Q.04 A  35                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 35)
Q.04 A                                      EDTCDE(Z)
Q.04 A                                      COLOR(YLW)
Q.04 A                                      OVRDTA
Q.04 A                                  9  2'Rekalkulere . .'
Q.04 A            Q4PRIS         1A  B  9 18COLOR(YLW)
Q.04 A                                      OVRDTA
Q.04 A                                  9 20'1/0'
Q.04 A                                 13  2'F3=Gå tilbake'
Q.04 A                                      COLOR(BLU)
Q.04 A                                 13 18'F4=Spørring'
Q.04 A                                      COLOR(BLU)
     A*
7.19 A*         R O1WIN
7.19 A          R O2WIN
7.19 A*%%TS  SD  20071030  091229  ASP_TMB     REL-V5R4M0  5722-WDS
7.19 A                                      WINDOW(*DFT 10 40)
7.19 A                                      CA03
7.19 A                                      CF04
7.19 A                                      CA12
7.19 A                                      RTNCSRLOC(&W_ARCDO2 &W_AFLDO2)
7.19 A                                      PUTOVR
7.19 A            W_ARCDO2      10A  H
7.19 A            W_AFLDO2      10A  H
7.19 A                                  1  1' Ekstra ordreinformasjon          -
7.19 A                                            '
7.19 A                                      DSPATR(RI)
7.19 A                                  9  1'Trykk enter for å gå tilbake.'
7.19 A                                      COLOR(BLU)
7.19 A                                  3  1'Totalvekt ordre (kg) . . .'
7.19 A            TOTKG          9   O  3 29
     A*
7.26 A          R S2WIN
7.26 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.26 A                                      WINDOW(*DFT 11 60)
7.26 A                                      CA03
7.26 A                                      CF04
7.26 A                                      CA12
7.26 A                                      PUTOVR
7.26 A                                  2  2' Bytte varenyummer '
7.26 A                                      DSPATR(RI)
7.26 A                                  4  2'Varenummer . . . . . . . . .'
7.26 A            S2VARE         8A  O  4 32
7.26 A                                      COLOR(YLW)
7.26 A                                      OVRDTA
7.26 A                                  6  2'Erstatt med varenummer . . +'
7.26 A            S2NYVA         8A  B  6 32
7.26 A  31                                  ERRMSGID(FMD0730 *LIBL/QMFOFAK 31)
7.26 A                                      COLOR(YLW)
7.26 A                                      OVRDTA
7.26 A                                 10  2'F3=Gå tilbake'
7.26 A                                      COLOR(BLU)
7.26 A                                 10 17'F4=Spørring'
7.26 A                                      COLOR(BLU)
7.fb A          R S1WIN
7.fb A*%%TS  SD  20180416  123235  ASP_XGR     REL-V7R1M0  5770-WDS
7.fc A                                      WINDOW(*DFT 12 60)
7.fb A                                      CA03
7.fC A N60                                  CF07
7.fb A                                      CA12
7.fb A                                      PUTOVR
7.fb A                                  2  2' Forskuddsbetaling'
7.fb A                                      DSPATR(RI)
7.fe A                                  3  2'Krav til depositum. . . . . .'
7.fe A            S1AKBS    R        O  3 32REFFLD(FOTOTS)
7.fe A                                      EDTWRD('       0 ,  ')
7.fe A                                      COLOR(YLW)
7.fe A                                      OVRDTA
7.fb A                                  4  2'Foreløpige ordretotaler . . .'
7.fb A            S1TOTA    R        O  4 32REFFLD(FOTOTS)
7.fb A                                      EDTWRD('       0 ,  ')
7.fb A                                      COLOR(YLW)
7.fb A                                      OVRDTA
7.fb A                                  5  2'Forhåndsbetaling 100 %    . .'
7.fb A            S1100P    R        B  5 32REFFLD(FÅVALG)
7.fb A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.fb A                                      VALUES(0 1)
7.fb A  31                                  ERRMSGID(FMO0760 *LIBL/QMFOFAK 31)
7.fb A                                      CHECK(ER)
7.fc A  60                                  DSPATR(PR)
7.fb A                                      COLOR(YLW)
7.fb A                                      OVRDTA
7.fb A                                  5 35'1=ja,0=nei'
7.fb A                                  6  2'Delbetaling . . . . . . . . .'
7.fa A            S1DEPP         2Y 0B  6 32COLOR(YLW)
7.fb A  33                                  ERRMSGID(FMO0770 *LIBL/QMFOFAK 33)
7.fb A                                      CHECK(ER)
7.fb A                                      CHECK(RB)
7.fc A  60                                  DSPATR(PR)
7.fb A                                      OVRDTA
7.fb A                                  7  2'Depositum   innkl. mva. . . .'
7.fb A            S1DEPO    R   11Y02B  7 32REFFLD(FOTOTS)
7.fb A                                      COLOR(YLW)
7.fb A  32                                  ERRMSGID(FMO0765 *LIBL/QMFOFAK 32)
7.fb A                                      CHECK(ER)
7.fb A                                      CHECK(RB)
7.fb A                                      EDTCDE(L)
7.fc A  60                                  DSPATR(PR)
7.fb A                                      OVRDTA
7.fb A                                  8  2'Beregn nytt depositumsbeløp .'
7.fb A            S1BERE    R        B  8 32REFFLD(FÅVALG)
7.fb A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.fb A                                      VALUES(0 1)
7.fb A                                      COLOR(YLW)
7.fc A  60                                  DSPATR(PR)
7.fb A                                      OVRDTA
7.fb A                                  8 35'1=ja,0=nei'
7.fc A                                  9  2'Betalt depositumsbeløp      .'
7.fc A            S1DEPB    R        O  9 32REFFLD(FOTOTS)
7.fc A                                      COLOR(YLW)
7.fc A                                      EDTCDE(L)
7.fc A                                 11  2'F3=Gå tilbake'
7.fC A                                      COLOR(BLU)
7.fc A N60                             11 17'F7=Slett depositum'
7.fc A                                      COLOR(BLU)
7.fb A          R S6WIN
7.fb A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.fb A                                      WINDOW(*DFT 11 60)
7.fb A                                      CA03
7.fb A                                      CA12
7.fb A                                      CF04
7.fb A                                      PUTOVR
7.fb A                                  2  2' Forskuddsbetaling'
7.fb A                                      DSPATR(RI)
7.fb A                                  4  2'Varenr. . . . . . . . . . . .'
7.fb A            S6VARE         8A  O  4 32
7.fb A                                      COLOR(YLW)
7.fb A                                      OVRDTA
7.fb A                                  6  2'Erstatt med varenr. . . . . +'
7.fb A            S6NYVA         8A  B  6 32
7.fb A  31                                  ERRMSGID(FMD0730 *LIBL/QMFOFAK 31)
7.fb A                                      COLOR(YLW)
7.fb A                                      OVRDTA
7.fb A                                 10  2'F3=Gå tilbake'
7.fb A                                      COLOR(BLU)
7.fb A                                 10 17'F4=Spørring'
7.fb A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
