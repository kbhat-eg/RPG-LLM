     A*%%TS  SD  20051031  162924  AMD550      REL-V5R3M0  5722-WDS
6.31  * 040914 BOF : test om lev. er sperret eller passiv
     A*%%EC
6.36  * 150218 NHO : sjekker om program er kallet tidligere i
6.36  *            : prosessen
7.xx A*     BSA  220116  Endringer relatert til ny GUI
7.03 A*     BSA  291119  Kun ordretype 'T' på tilbudskunde
7.06 A*K000 bsa  020920  Kan sette leveringsdato go forsendelsemåte uten
7.06 A*K000              å gå inn på bestillingen igjen.
7.07 A*K000 ask  110520    Lagt på fargekode styrt av kode "FARGE_SKAFF_SORTIMEN
     A*
8.09 A*K000 bsa  271025  Feilmelding om feil ordretype
     A*
     A*
     A*
     A*-------------------------------------------------------------------
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R A1MLD
     A*%%TS  SD  20051031  144647  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 8 34)
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
6.36 A N34                              2  2'Melding'
6.36 A N34                              3  2'Orderen er allerede satt i'
6.36 A N34                              4  2'bestilling og kan ikke bestilles'
6.36 A N34                              5  2'på nytt !!'
6.36 A  34                              2  2'Melding'
6.36 A  34                              3  2'Program vil feile.  Avslutt'
6.36 A  34                              4  2'ordren. Gå tilbake til startmeny '
6.36 A  34                              5  2'og inn på ordrebehandling.       '
6.36 A  34                              6  2'Deretter prøv på nytt.'
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R A1BLD
     A*%%TS  SD  20051031  162924  AMD550      REL-V5R3M0  5722-WDS
7.06 A                                      WINDOW(2 1 16 61)
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
7.xx A                                  1 50'FO734A1'
     A                                  2  2'Ordre-type. .'
     A                                      COLOR(WHT)
     A            A1FOTY    R        O  2 20REFFLD(FOOTYP)
     A                                      OVRDTA
     A                                      COLOR(WHT)
7.04aA            A1FTXT    R        O  2 25REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
7.04aA                                  2 59'(o)'
     A                                  3  2'Ordrenummer .'
     A                                      COLOR(WHT)
     A            A1NUMM    R        O  3 16REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
6.NU A            A1SUFF    R        O  3 25REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(WHT)
6.NU A            A1KUNA    R        O  3 28REFFLD(FOKUNA)
     A                                      OVRDTA
     A                                      COLOR(WHT)
7.04aA                                  3 59'(o)'
7.04aA                                  5  2'Leverandør. +'
     A            A1LDOR    R        B  5 16REFFLD(FOLDOR)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A  31                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 31)
     A  33                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
7.03aA                                  5 24'Navn'
     A            A1NAVN    R        O  5 29REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
7.03aA                                  6 24'Sted'
     A            A1STED    R        O  6 29REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  8  2'Varelinjer. . . .'
     A            A1VALG    R        B  8 21REFFLD(FÅSVA2)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      VALUES(0 1 2)
7.04aA                                  8 23'(r)'
7.04aA                                  8 27'0=Leverandør, 1=Spesifiser, 2=Alle'
     A                                 10  2'Leveringsadressen'
     A            A1TYPE    R        B 10 21REFFLD(FÅSVA2)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A                                      VALUES(0 1)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
7.04aA                                 10 23'(r)'
7.xx A                                 10 27'0=Via lager, 1=Direkte'
7.06 A                                 11  2'Til ordre-type.  +'
7.06 A            A1OTYP    R        B 11 21REFFLD(FOOTYP)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A  32                                  ERRMSGID(LMO0040 *LIBL/QMFLAGR 32)
7.03 A  68                                  ERRMSGID(FMO0662 *LIBL/QMFOFAK 68)
     A                                      COLOR(YLW)
7.06 A                                 12  2'Forsendelsesmåte +'
7.06 A            A1LMAT    R        B 12 21REFFLD(FOLMAT)
7.06 A                                      OVRDTA
7.06 A  34                                  ERRMSGID(LSO0180 *LIBL/QMFLAGR 34)
7.06 A                                      COLOR(YLW)
7.06 A            A1LMTX    R        B 12 25REFFLD(FOLMTX)
7.06 A                                      OVRDTA
7.06 A                                      COLOR(YLW)
7.06 A                                 13  2'Leveringsdato. . .'
7.06 A            A1LDAT         6Y 0B 13 21COLOR(YLW)
7.06 A                                      CHECK(RB)
7.06 A  35                                  ERRMSGID(LSO0020 *LIBL/QMFLAGR 35)
7.06 A                                      EDTWRD('  .  .  ')
7.06 A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.06 A                                 15 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1SFL                     SFL
     A*%%TS  SD  20020318  142841  MHA         REL-V5R1M0  5722-WDS
     A            D1LINE    R        H      REFFLD(FDLINE)
     A            D1LEVE         9Y 3B  8  2EDTWRD('      ,   ')
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            D1ANTA    R   -2 03O  8 13REFFLD(FDANTA)
     A                                      EDTWRD('      ,   ')
7.07 A  53                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
     A            D1ENH1    R        O  8 24REFFLD(FDENH1)
7.07 A  53                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
     A            D1VARE    R   -7   O  8 28REFFLD(FDVARE)
7.07 A  53                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
     A            D1LDOR    R        O  8 37REFFLD(FDLDOR)
7.07 A  53                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
     A            D1TEK1    R        O  8 44REFFLD(FDTEK1)
7.07 A  53                                  COLOR(RED)
7.07 A  54                                  COLOR(BLU)
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20020318  142841  MHA         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF03
     A                                      CF05
     A                                      CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_SVRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av enkeltlinjer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  6'Ordre:'
     A            D2NUMM    R        O  4 13REFFLD(FONUMM)
6.NU A            D2SUFF    R        O  4 22REFFLD(FOSUFF)
     A                                  4 28'Kundenr:'
     A            D2KUND    R        O  4 37REFFLD(FOKUND)
     A            D2KUNA    R        O  4 44REFFLD(FOKUNA)
     A                                  6  2'1=Plukke tekst'
     A                                      COLOR(BLU)
     A                                  5 28'Lev.nr.:'
     A            D2LDOR    R        O  5 37REFFLD(FOKUND)
     A            D2LENA    R        O  5 44REFFLD(FOKUNA)
     A                                  6 18'3=Kopier tekst'
     A                                      COLOR(BLU)
     A                                  7  6'Antall'
     A                                      COLOR(WHT)
     A                                  7 16'Bestilt'
     A                                      COLOR(WHT)
     A                                  7 24'Enh'
     A                                      COLOR(WHT)
     A                                  7 28'Varenr.'
     A                                      COLOR(WHT)
     A                                  7 37'Lev.  '
     A                                      COLOR(WHT)
     A                                  7 44'Varetekst'
     A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  20020312  205539  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 29'F8=Hent inn antall'
     A                                      COLOR(BLU)
     A          R H1BLD
     A*%%TS  SD  20051031  144340  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 10 35)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2' Ønskes utskriftsvalg ? '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A            H1VALG    R        H      REFFLD(FÅSVAR)
     A                                  6  2'Utskrift . . . . .'
     A            H1SVAR    R        B  6 21REFFLD(FÅSVAR)
     A                                      COLOR(YLW)
     A                                  6 25'1=Ja,0=Nei'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R L1BLD
     A*%%TS  SD  20020312  205806  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 32)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2' Resten til plukkliste ? '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  4  2'Oppgi ordretype. .'
     A            L1OTYP    R        B  4 21REFFLD(FOOTYP)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A  31                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA5040 *LIBL/QMFOFAK 32)
7.03 A  68                                  ERRMSGID(FMO0662 *LIBL/QMFOFAK 68)
     A                                      COLOR(YLW)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R M1BLD
     A*%%TS  SD  20020312  205806  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 32)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2' Bestillingsforslag er dannet '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                  4  2'Forslagsnr.  . . .'
     A            M1NUMM    R        O  4 21REFFLD(FONUMM)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A                                      COLOR(YLW)
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
8.09 A          R B18MSG3
8.09 A*%%TS  SD  20170201  135621  G0XEGASSKO  REL-V7R1M0  5770-WDS
8.09 A                                      WINDOW(*DFT 8 35)
8.09 A                                      CF03
8.09 A                                      CF12
8.09 A                                      PUTOVR
8.09 A                                  3  2'Du kan ikke plukke til       '
8.09 A                                      COLOR(WHT)
8.09 A                                  4  2'bestilling fra ordretypen !  '
8.09 A                                      COLOR(WHT)
8.09 A                                  5  2'                             '
8.09 A                                      COLOR(WHT)
8.09 A                                  6  2'                             '
8.09 A                                      COLOR(WHT)
8.09 A                                  7  2'F3=Gå tilbake'
8.09 A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
