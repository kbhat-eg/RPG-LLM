     A*%%TS  SD  20050531  094703  HKO530      REL-V5R2M0  5722-WDS
     A**
     A** 28.09.15 BOF hvis prisgriver er utgått på vare, så rød.
     A*
7.xx A*      130916 bsa Endringer relatert til ny GUI
7.01 A*K000  230321 bsa Takler utvidet skjermbilde
7.02 A*      200921 bof nytt hidden felt i subfile
     A*
8.01 A*K000  270223 bsa Legger ut varenummer og tekster
     A*
     A*
     A*
     A*-------------------------------------------------------------------------
     A*%%EC
7.01 A                                      DSPSIZ(24 80 *DS3 -
7.01 A                                             27 132 *WIDE)
     A                                      REF(*LIBL/JFRF)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050531  094703  HKO530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1LDOR    R        O  7  4REFFLD(JXLDOR)
6.33 A*  50                                  COLOR(RED)
     A            B1LNAV    R  -17   O  7 11REFFLD(JLNAVN)
6.33 A*  50                                  COLOR(RED)
6.33 A            B1NOBB    R        O  7 30REFFLD(JVNOBB)
6.33 A* 50                                  COLOR(RED)
     A            B1PRIS    R        O  7 39REFFLD(JXPRIS)
     A                                      EDTCDE(M)
6.33 A*  50                                  COLOR(RED)
     A            B1GDAT         6Y 0O  7 51EDTWRD('  .  .  ')
6.33 A*  50                                  COLOR(RED)
     A            B1CRDO    R        O  7 60REFFLD(JXCRDO)
     A            B1RKOD         1A  O  7 62
7.02 A            B1GDATI   R        H      REFFLD(JXGDAT)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050531  094703  HKO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      KEEP
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge prisgivende leverandør / pr-
     A                                      is '
     A                                      DSPATR(RI)
8.01 A                                  3  4'Nummer'
7.xx A                                      COLOR(WHT)
8.01 A                                  3 18'Varenr'
8.01 A                                      COLOR(WHT)
8.01 A                                  3 27'Varetekst'
8.01 A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Nummer'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 11'Navn              '
7.xx A                                      COLOR(WHT)
6.33 A                                  6 30'NOBB    '
6.33 A                                      COLOR(WHT)
7.xx A                                  6 39'Nobb-pris'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 51'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 60'C'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 62'R'
7.xx A                                      COLOR(WHT)
7.xx A**                                5  2'V  Nummer Navn                    -
7.xx A**                                       Nobb-pris    Dato   C R'
7.xx A**                                    COLOR(WHT)
8.01 A            B2LDOR    R        B  4  4REFFLD(JXLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  22                                  DSPATR(PC)
8.01 A            B1VARE         8A  O  4 18
8.01 A                                      COLOR(BLU)
8.01 A            B1TEK1    R        O  4 27REFFLD(JVTEK1)
8.01 A                                      COLOR(BLU)
8.01 A            B1TEK2    R        O  5 27REFFLD(JVTEK2)
8.01 A                                      COLOR(BLU)
     A*
     A          R B2CMD
     A*%%TS  SD  20000921  100323  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 17 62)
     A                                 16  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 78' '
