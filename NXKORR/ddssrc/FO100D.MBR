     A*%%TS  SD  20110510  081341  BOF620      REL-V7R1M0  5770-WDS
6.22 A*     NHO  20110316  Feilmelding dersom det tastes en int.ordre
6.22 A*                    på kunde som ikke har verdier i avg.fri og
6.22 A*                    spes.fakt.
6.30 A*     BOF  20140520  Fjernet indikator 58
6.31 A*     BSA  20140806  Omskrevet for å redusere bilder og taste-
6.31 A*                    trykk.
6.35 A*     BSA  20141014  Tilpasser skjermbildet til jacada automatikk
6.36 A*     BOF  20160424  Feilretting indikator på B1selg
6.37 A*     NHO  20180313  Ny feilmelding vedr kreditgrensen
6.38 A*     NHO  20180216  Feilmelding dersom ordredat <> enn 1 år
7.xx A*     BSA  210116    Endringer relatert til ny GUI
7.01 A*     BSA  140916    Markerer felt som er påkrevd
Q.01 A*     bsa  020419    Utvidet skjerm
7.14 A*     SST  20180914  Sperre Tilb.-kunde for annet enn otyp = 'T'
7.15 A*     BSA  150119    Feilmelding hvis prosjekt ikke er valgt
7.15 A*                    på kundekategorier hvor dette er krav.
7.30 A*     SST  180320    Sperre F-taster for brukere som ikke kan endre pakks.
7.33 A*     ASK  201120    Lagt inn søk mot 1881
7.35 A*     ASK  240620    Viser ekstern ordrereferanse
     A*-----------------------------------------------------------------------
     A*%%EC
Q.01 A                                      DSPSIZ(24 80 *DS3 -
Q.01 A                                             27 132 *WIDE)
Q.01 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20110510  081341  BOF620      REL-V7R1M0  5770-WDS
     A                                      CF01
6.38 A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF10
     A                                      CF11
     A                                      CF12
7.07 A                                      CF15
     A                                      CF21
     A                                      CF24
7.30 A                                      VLDCMDKEY(16)
     A                                      KEEP
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Registrering av tilbud/ordre'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO100B1'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
6.31 A**                                4  2'Lag skjerm-bunt'
6.31 A**          B1BIND    R        B  4 18REFFLD(FAABND)
6.31 A**                                    COLOR(YLW)
6.31 A**                                4 21'J=Ja,N=Nei'
     A            B2SAT1    R        O  4 50REFFLD(FONAVN)
     A  28                                  DSPATR(RI)
     A  28                                  COLOR(RED)
7.xx A                                  4  2'Ordre inf.kod +'
6.31 A            B1ITYP    R        B  4 18REFFLD(VAITYP)
     A  47                                  ERRMSGID(VMA8020 *LIBL/QMFOFAK 47)
     A                                      COLOR(YLW)
6.31 A            B1ITXT        28A  O  4 21
     A            B2SAT2    R        O  5 50REFFLD(FONAVN)
     A  28                                  DSPATR(RI)
     A  28                                  COLOR(RED)
6.31 A                                  5  2'Ordredato . . .'
6.31 A            B1BDAT         6Y 0B  5 18COLOR(YLW)
     A                                      CHECK(RB)
     A  32                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 32)
6.38 A  22                                  ERRMSGID(FM01195 *LIBL/QMFOFAK 22)
     A                                      EDTWRD('  .  .  ')
     A            B2SAT3    R        O  6 50REFFLD(FONAVN)
     A  28                                  DSPATR(RI)
     A  28                                  COLOR(RED)
6.31 A                                  6  2'Ordrenr . . . .'
6.31 A            B1ONUM    R        B  6 18REFFLD(FONUMM)
     A                                      DLTCHK
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  37                                  ERRMSGID(FMO0060 *LIBL/QMFOFAK 37)
     A  45                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 45)
6.NU A            B1OSUF    R        B  6 27REFFLD(FOSUFF)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  38                                  ERRMSGID(FMO0070 *LIBL/QMFOFAK 38)
7.xx A                                  7  2'Ordretype . . +'
6.31 A            B1OTYP    R        B  7 18REFFLD(FOOTYP)
     A  31                                  ERRMSGID(FMA0260 *LIBL/QMFOFAK 31)
     A  43                                  ERRMSGID(FMO0650 *LIBL/QMFOFAK 43)
     A  44                                  ERRMSGID(FMO0660 *LIBL/QMFOFAK 44)
     A  55                                  ERRMSGID(FMO0661 *LIBL/QMFOFAK 55)
     A  50                                  ERRMSGID(VMA5040 *LIBL/QMFOFAK 50)
9.14 A  68                                  ERRMSGID(FMO0662 *LIBL/QMFOFAK 68)
9.14 A  69                                  ERRMSGID(FMO0663 *LIBL/QMFOFAK 69)
     A  23                                  DSPATR(PC)
     A                                      COLOR(YLW)
7.35 A  63                              7 23'Referanse'
7.35 A  63        B1RNUM    R        B  7 33REFFLD(FONUMM)
7.25 A                                      COLOR(YLW)
7.25 A  61                                  DSPATR(PR)
7.25 A*  63        B1RSUF    R        B  7 47REFFLD(FOSUFF)
7.25 A*                                      COLOR(YLW)
7.25 A*  61                                  DSPATR(PR)
7.xx A                                  7 50'Telefonnr. . .'
6.31 A            B1TLFN        15A  O  7 66COLOR(YLW)
7.xx A                                  8  2'Leveringstype +'
6.31 A            B1LETY    R        B  8 18REFFLD(FOLETY)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  46                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 46)
     A  59                                  ERRMSGID(FMO0740 *LIBL/QMFOFAK 59)
7.28 A                                  8 23'Ekst.ref'
7.28 A            B1NREF        15A  O  8 33 COLOR(YLW)
7.xx A                                  8 50'Mobilnr  . . .'
6.31 A            B1MOBN        15A  O  8 66COLOR(YLW)
7.xx A                                  9  2'Kundenummer . +'
6.31 A            B1LKUN    R        B  9 18REFFLD(FOLKUN)
     A                                      DLTCHK
7.01 A                                      COLOR(TRQ)
     A                                      CHECK(RB)
     A  25                                  DSPATR(PC)
     A  39                                  ERRMSGID(FMO0200 *LIBL/QMFOFAK 39)
     A  40                                  ERRMSGID(FMO0210 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(FMO0220 *LIBL/QMFOFAK 41)
     A  48                                  ERRMSGID(FRK0300 *LIBL/QMFOFAK 48)
7.36 A  62                                  ERRMSGID(FRP0010 *LIBL/QMFOFAK 62)
6.31 A                                  9 26'(Lev.adresse)'
6.31 A            B1KUND    R        B  9 50REFFLD(FOKUND)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  9 60'(Fakt.adresse)'
6.31 A                                 10  2'Kundeprosjekt .'
6.31 A            B1KPRO    R        O 10 18REFFLD(FOKPRO)
     A                                      COLOR(YLW)
6.35 A                                 10 50'Lev.adr. OK?'
6.35 A            B1LAOK    R        B 10 63REFFLD(FOLAOK)
6.2h A  29                                  DSPATR(PC)
     A                                      COLOR(YLW)
6.35 A                                 10 66'(1/ )'
6.31 A                                 11  2'Navn. . . . . .'
6.31 A            B1NAVN    R        B 11 18REFFLD(FONAVN)
     A                                      CHECK(LC)
     A  27                                  DSPATR(PC)
7.01 A  74                                  COLOR(TRQ)
6.30 A**58                                  ERRMSGID(FMO0730 *LIBL/QMFOFAK 58)
7.01 A N74                                  COLOR(YLW)
6.31 A            B2NAVN    R        O 11 50REFFLD(FONAVN)
6.31 A                                 12  2'Gate. . . . . .'
6.31 A            B1ADR1    R        B 12 18REFFLD(FOADR1)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
6.31 A            B2GATE    R        O 12 50REFFLD(FOADR1)
6.31 A                                 13  2'Adresse . . . .'
6.31 A            B1ADR2    R        B 13 18REFFLD(FOADR2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
6.31 A            B2ADR2    R        O 13 50REFFLD(FOADR2)
6.31 A                                 14  2'Postadresse . .'
6.31 A            B1STED    R        B 14 18REFFLD(FOSTED)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
6.31 A            B2STED    R        O 14 50REFFLD(FOSTED)
7.01 A                                 15  2'Vår referanse  '
7.01 A            B1VREF    R        B 15 18REFFLD(FOVREF)
     A                                      CHECK(LC)
     A  56                                  ERRMSGID(FMO0720 *LIBL/QMFOFAK 56)
7.01 A N70                                  COLOR(YLW)
7.01 A  70                                  COLOR(TRQ)
6.31 A                                 15 50'Leveringsdato .'
7.01 A            B1LDAT         6Y 0B 15 66
     A                                      CHECK(RB)
     A  33                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMO0250 *LIBL/QMFOFAK 34)
7.26 A  53                                  ERRMSGID(FMO0257 *LIBL/QMFOFAK 53)
     A                                      EDTWRD('  .  .  ')
7.01 A  72                                  COLOR(TRQ)
7.01 A N72                                  COLOR(YLW)
7.xx A                                 16  2'Kundens ref.  +'
6.31 A            B1DREF    R        B 16 18REFFLD(FODREF)
     A                                      CHECK(LC)
     A  57                                  ERRMSGID(FMO0710 *LIBL/QMFOFAK 57)
7.01 A  71                                  COLOR(TRQ)
7.01 A N71                                  COLOR(YLW)
7.xx A                                 16 50'Oppfølgingsdato'
7.xx A            B1DDAT         6Y 0B 16 66
7.01 A* 73                                  COLOR(TRQ)
     A                                      COLOR(YLW)
     A  51                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 51)
     A  52                                  ERRMSGID(FMO0270 *LIBL/QMFOFAK 52)
     A  58                                  ERRMSGID(FMO0258 *LIBL/QMFOFAK 58)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
6.31 A                                 17  2'Rekvisisjonsnr.'
6.31 A            B1REKV    R        B 17 18REFFLD(FOREKV)
     A                                      CHECK(LC)
7.01 A  49                                  COLOR(TRQ)
7.01 A N49                                  COLOR(YLW)
     A  49                                  ERRMSGID(FMO0680 *LIBL/QMFOFAK 49)
6.31 A                                 17 50'Utgår dato. . .'
6.31 A            B1TDAT         6Y 0B 17 66COLOR(YLW)
     A                                      CHECK(RB)
     A  35                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FMO0260 *LIBL/QMFOFAK 36)
     A                                      EDTWRD('  .  .  ')
6.31 A                                 18  2'Mobilnr . . . .'
6.31 A            B1MOBI    R        B 18 18REFFLD(FOMOBI)
     A                                      COLOR(YLW)
6.31 A                                 18 50'Pakkseddel dato'
6.31 A            B1PADA         6Y 0O 18 66COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
7.xx A                                 19  2'Selger. . . . +'
6.31 A            B1SELG    R        B 19 18REFFLD(FOSELG)
6.36 A  26                                  ERRMSGID(FMA0150 *LIBL/QMFOFAK 26)
6.31 A                                      CHECK(RB)
7.01 A                                      COLOR(TRQ)
7.xx A                                 19 25'Lager .+'
7.xx A            B1LAGE    R        B 19 34REFFLD(FOLAGE)
6.31 A  42                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 42)
6.31 A  53                                  ERRMSGID(FMA0300 *LIBL/QMFOFAK 53)
7.10 A  60                                  ERRMSGID(FMA0310 *LIBL/QMFOFAK 60)
7.01 A                                      COLOR(TRQ)
6.31 A                                      CHECK(RB)
7.xx A                                 19 40'Avdeling .+'
7.xx A            B1AVDE    R        B 19 52REFFLD(FOAVDE)
6.31 A  24                                  ERRMSGID(FMA0140 *LIBL/QMFOFAK 24)
6.31 A                                      COLOR(YLW)
6.31 A                                      CHECK(RB)
7.xx A                                 20  2'Fors. måte. . +'
6.31 A            B1LMAT    R        B 20 18REFFLD(FOLMAT)
6.31 A  30                                  ERRMSGID(FMA0200 *LIBL/QMFOFAK 30)
7.01 A N75                                  COLOR(YLW)
7.01 A  75                                  COLOR(TRQ)
6.31 A                                      CHECK(RB)
6.31 A            B1LMTX    R        B 20 22REFFLD(FOLMTX)
6.31 A                                      CHECK(LC)
6.31 A                                      COLOR(YLW)
7.12 A  54                             20 50'Eks.ref.'
7.12 A  54        B1EREF        15   O 20 59
7.12 A  54                                  COLOR(YLW)
     A                                 21  2'Informasjon . .'
     A            B1INF1    R        B 21 18REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            B1INF2    R        B 21 31REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 22  2'F1=Kundeprosj'
     A                                      COLOR(BLU)
     A                                 22 17'F6=Opprett kunde'
     A                                      COLOR(BLU)
     A                                 22 36'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 22 51'F5=Oppdater'
     A                                      COLOR(BLU)
     A                                 22 64'F10=Ordreopplysn'
     A                                      COLOR(BLU)
     A                                 23  2'F11=Fri tekst'
     A                                      COLOR(BLU)
     A                                 23 17'F7=Opprett kprosj'
     A                                      COLOR(BLU)
7.07 A                                 23 36'F15=Lagre'
     A                                      COLOR(BLU)
     A                                 23 51'F18=Oppheve'
     A                                      COLOR(BLU)
     A                                 23 64'F21=Vare/Pris'
     A                                      COLOR(BLU)
7.13 A            B1PROS        22A  O 10 26COLOR(YLW)
     A          R C2WIN
     A*%%TS  SD  20070402  083836  AMD550      REL-V5R4M0  5722-WDS
6.35 A                                      WINDOW(2 3 16 57)
     A                                      CF03
     A                                      CF12
     A                                      CA18
     A                                      PUTOVR
     A                                  2  2' Ordren er under plukking '
     A                                      DSPATR(RI)
6.35 A                                  2 33'Ordrenr .'
6.35 A            C2NUMM    R        O  2 44REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
6.35 A            C2SUFF    R        O  2 54REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  4  2'Plukkes/plukket av.'
     A            C2PLNA        30A  O  4 22COLOR(YLW)
     A                                  6 20'Dato'
     A                                      COLOR(WHT)
     A                                  6 30'Kl'
     A                                      COLOR(WHT)
     A                                  6 36'Bruker'
     A                                      COLOR(WHT)
     A                                  6 47'Skjerm'
     A                                      COLOR(WHT)
     A                                  7  2'Sendt plukking'
     A            C2PLDA         6Y 0O  7 18EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A            C2PLTI         6Y 0O  7 27EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A            C2PLUS        10A  O  7 36COLOR(YLW)
     A            C2PLWS        10A  O  7 47COLOR(YLW)
     A                                  8  2'Under plukking'
     A            C2UPDA         6Y 0O  8 18EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A            C2UPTI         6Y 0O  8 27EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A            C2UPUS        10A  O  8 36COLOR(YLW)
     A            C2UPWS        10A  O  8 47COLOR(YLW)
     A                                  9  2'Ferdig plukket'
     A            C2FPDA         6Y 0O  9 18EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A            C2FPTI         6Y 0O  9 27EDTWRD('  :  :  ')
     A                                      COLOR(YLW)
     A            C2FPUS        10A  O  9 36COLOR(YLW)
     A            C2FPWS        10A  O  9 47COLOR(YLW)
     A                                 12  2'Ordren er under plukking, eller er-
     A                                       ferdig plukket.'
     A                                      COLOR(WHT)
     A                                 13  2'Bekreft at ordren likevel skal end-
     A                                      res ved å trykke F18.'
     A                                      COLOR(WHT)
     A                                 15  2'F3=Gå tilbake   F18=Bekreft endrin-
     A                                      g'
     A                                      COLOR(BLU)
7.33 A          R E2WIN
7.33 A                                      WINDOW(2 3 12 57)
7.33 A                                      CF03
7.33 A                                      CF12
7.33 A                                      CF06
7.33 A                                      PUTOVR
7.33 A                                  2  2'Søk på 1881?'
7.33 A                                  4  4'Kunden du søkte på finnes ikke i k-
7.33 A                                      underegisteret'
7.33 A                                  5  4'Vil du slå opp imot 1881 for å fin-
7.33 A                                      ne kunden?'
7.33 A                                  7  4'Søke på telefonnummer'
7.33 A            E2TLFN        15A  B  7 27COLOR(YLW)
7.33 A                                 10  2'F3=Nei   F6=Opprett kunde manuelt'
7.33 A                                      COLOR(BLU)
     A          R G2WIN
     A*%%TS  SD  20060215  201613  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 11 50)
     A                                      CF03
     A                                      CF12
     A                                      CA18
     A                                      PUTOVR
     A                                  2  2' Overskridelse av kredittgrense '
     A                                      DSPATR(RI)
     A                                  5  2'Kunden har overskredet kredittgren-
     A                                      se.'
     A                                  7  2'Dersom overskridelsen skal godjenn-
     A                                      es, trykk F18'
     A                                 10  2'F3=Gå tilbake   F18=Godkjenn overs-
     A                                      kridelse'
     A                                      COLOR(BLU)
     A          R G3WIN
     A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 60)
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A                                  2  2' Kredittgrense er låst '
     A                                      DSPATR(RI)
6.37 A*                                 5  2'Du kan ikke registrere videre '
6.37 A N65                              5  2'Du kan ikke registrere videre '
6.37 A  65                              5  2'Du kan kun vedlikeholde'
6.37 A  65                              6  2'tekstlinjer'
6.37 A  65                              8  2'Kontakt regnskap vedr utestående b-
     A                                      etalinger'
6.37 A*                                10  2'F3=Gå tilbake'
6.37 A N65                             10  2'F3=Gå tilbake'
6.37 A  65                             10  2'Trykk enter  '
     A                                      COLOR(BLU)
     A*
     A          R F2WIN
     A*%%TS  SD  20080313  083815  AMD550      REL-V5R4M0  5722-WDS
     A                                      CF03
     A                                      CF12
     A                                      CA18
     A                                      PUTOVR
     A                                      WINDOW(03 02 11 51)
     A                                  2  2' Overskridelse av rabattgrense '
     A                                      DSPATR(RI)
     A                                  5  2'Kunden har overskredet rabattgrens-
     A                                      e for almenning'
     A                                  6  2'Før ytterliger registrering kan sk-
     A                                      je må grensen'
     A                                  7  2'utvides etter søknad'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R F3WIN
     A*%%TS  SD  20080313  083815  AMD550      REL-V5R4M0  5722-WDS
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A                                      WINDOW(02 06 10 40)
     A                                  2  2'Avvik'
     A                                      DSPATR(RI)
     A                                  4  1'Lager og/eller avdeling avviker'
     A                                  6  2'Lager på ordre er satt til:'
     A            F3AVDE    R        O  7 33REFFLD(FOAVDE)
     A            F3LAGE    R        O  6 35REFFLD(FOLAGE)
     A                                  7  2'Avdeling på ordre er satt til:'
     A                                  9  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R F4WIN
     A*%%TS  SD  20080313  083815  AMD550      REL-V5R4M0  5722-WDS
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A                                      WINDOW(02 06 10 40)
     A                                  2  2'Varekunde'
     A                                      DSPATR(RI)
     A                                  5  1'Ikke samsvar mellom varekunde'
     A                                  6  1'og fakturakunde!'
     A                                  9  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
