     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
6.20 A* 6.20 180311  NHO  Begrense muligheter ved valg vedr EDI
6.24 A* 6.20 090511  BSA  Nytt valg for utplukk
6.26 A* 6.20 130312  BSA  Innfører lesemodus
6.NU A* 6.30 280512  BOF  Rettelser ihht. nummerutvidelse
6.30 A* 6.30 070814  BSA  Utvidet valg på filtrering
6.32 A* 6.30 040315  BSA  Melding hvis feil under innlesing
7.xx A* 7.00 200116  BSA  Endringer pga ny GUI
7.02 A* 7.00 140819  BSA  Utvidet skjermbilde og legger ut leveringsdato
7.05 A* 6.20 230816  BSA  Fakturaleverandør legges inn som filtervalg.
     A*
8.03 A* 8.00 060921  bsa  Innfører beløpskontroll
8.06 A* 8.00 270122  bsa  Innfører filter på status
8.11 A* 8.00 140525  bsa  Justert datofelt, da den tolker wizard i
8.11 A* 8.00              cloud feil.
     A*
     A*----------------------------------------------------------------------
     A*%%EC
7.02 A                                      DSPSIZ(27 132 *WIDE)
7.02 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20010528  155052  ASK         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 10 40)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A                                  2  2' Behandle EDI-meldinger        '
     A                                      DSPATR(RI)
7.XX A                                  4  2'Meldingstype . . .+'
     A            A1TYPE         4A  B  4 24OVRDTA
6.20 A  39                                  ERRMSGID(LMI0021 *LIBL/QMFLAGR 39)
6.20 A  40                                  ERRMSGID(LMI0022 *LIBL/QMFLAGR 40)
6.20 A  41                                  ERRMSGID(LMI0023 *LIBL/QMFLAGR 41)
6.20 A  42                                  ERRMSGID(LMI0024 *LIBL/QMFLAGR 42)
6.20 A  43                                  ERRMSGID(LMI0025 *LIBL/QMFLAGR 43)
     A                                  8  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
     A            B1TYPE    R        H      REFFLD(LITYPE)
     A            B1MNUM    R        H      REFFLD(LIMNUM)
     A            B1MLIN    R        H      REFFLD(LIMLIN)
     A            B1LDOR    R        H      REFFLD(LILDOR)
     A            B1VALG    R        B  7  2REFFLD(LÅVALG)
8.03 A                                      VALUES(0 1 3 4 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      COLOR(YLW)
     A            B1UTTY    R        O  7  4REFFLD(LITYPE)
     A            B1MDAT         6Y 0O  7  9EDTWRD('  .  .  ')
7.xx A            B1NUMM    R        O  7 18REFFLD(LINUMM)
     A            B1SUFF    R        O  7 27REFFLD(LISUFF)
     A            B1OTYP    R        O  7 30REFFLD(LOOTYP)
     A            B1LETY    R        O  7 33REFFLD(LÅLETY)
6.NU A            B1LEVR        19A  O  7 35
6.NU A            B1ONUM         8Y 0O  7 55EDTCDE(Z)
     A            B1FBEL        12Y 2O  7 64EDTWRD('        0 ,  ')
7.02 A            B1EDAT         6Y 0O  7 78EDTWRD('  .  .  ')
7.02 A            B1BDAT         6Y 0O  7 87EDTWRD('  .  .  ')
7.03 A            B1FDAT         6Y 0O  7 96EDTWRD('  .  .  ')
7.xx A            B1STAT         1A  O  7105
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  33                                  COLOR(GRN)
     A  34                                  COLOR(WHT)
7.03 A*            B1TSTA        15A  O  7 98
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140528  135857  BOF630      REL-V7R1M0  5770-WDS
7.02 A                                      SFLSIZ(0018)
7.02 A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF05
     A                                      CF07
     A                                      CF08
     A N79                                  CF09
     A N79                                  CA10
     A                                      CA12
     A                                      CA13
     A N79                                  CA14
     A N79                                  CF15
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      CSRLOC(W_XROW     W_XCOL)
     A                                      RTNCSRLOC(*WINDOW &W_XROW &W_XCOL)
     A                                      RTNCSRLOC(&W_ACTR &W_ACTF)
     A            W_CURN         5S 0H
     A            W_ACTR        10A  H
     A            W_ACTF        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_XROW         3S 0H
     A            W_XCOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.02 A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Behandling av EDI-meldinger'
     A                                      COLOR(WHT)
7.xx A                                  2 38'LI100B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.02 A                                  2118'Dato:'
     A                                      COLOR(WHT)
7.02 A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
7.02 A                                      ------------------------------------
7.02 A                                      ---------------------------'
     A                                      COLOR(WHT)
8.03 A N79                              4  2'1=Analyser antall/beløp'
8.03 A N79                                  COLOR(BLU)
8.03 A N79                              4 26'3=Kontroll'
     A N79                                  COLOR(BLU)
8.03 A N79                              4 37'4=Slett'
     A N79                                  COLOR(BLU)
8.03 A                                  4 45'5=Se på'
     A                                      COLOR(BLU)
8.03 A                                  4 53'6=Skriv ut'
     A                                      COLOR(BLU)
8.03 A N79                              4 64'7=Godkjenn'
     A N79                                  COLOR(BLU)
8.03 A N79                              4 76'8=Oppdater'
     A N79                                  COLOR(BLU)
8.03 A N79                              4 93'9=Finn best.'
     A N79                                  COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  4'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  9'Dato    '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 18'Bestnr  '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 27'Su'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 30'Ty'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 33'L'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 35'Leverandør         '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 55'Ordre   '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 64'Tot.ink.mva. '
7.xx A                                      COLOR(WHT)
7.02 A                                  6 78'Lev.dato'
7.02 A                                      COLOR(WHT)
7.02 A                                  6 87'Bes.dato'
7.02 A                                      COLOR(WHT)
7.03 A                                  6 96'Forfall'
7.03 A                                      COLOR(WHT)
7.02 A                                  6105'S'
7.xx A                                      COLOR(WHT)
7.03 A*                                  6 98'Status'
7.03 A*                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20041125  110702  GRO530      REL-V5R2M0  5722-WDS
7.02 A                                 25  2'F1=Filter'
     A                                      COLOR(BLU)
7.02 A                                 25 13'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.02 A                                 25 29'F5=Forny'
     A                                      COLOR(BLU)
7.02 A                                 25 40'F7=Vis'
     A                                      COLOR(BLU)
7.02 A                                 25 50'F8=Logg'
6.32 A                                      COLOR(BLU)
7.02 A N79                             25 60'F9=Hent meldinger'
6.26 A N79                                  COLOR(BLU)
7.02 A N79                             25 79'F10=Bestilling'
     A                                      COLOR(BLU)
7.02 A                                 25 96'F13=Statuskoder'
     A                                      COLOR(BLU)
7.02 A N79                             26  2'F14=Total rekontroll'
6.26 A N79                                  COLOR(BLU)
7.02 A N79                             26 25'F15=Opprydding'
6.26 A N79                                  COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20140528  140127  BOF630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 9 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette melding '
     A                                      DSPATR(RI)
     A                                  4  2'Tidspunkt :'
     A            B4MDAT         6Y 0O  4 15EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  5  2'Bestilling:'
     A            B4NUMM    R        O  5 15REFFLD(LINUMM)
     A                                      COLOR(YLW)
     A  30                              5 24'/'
     A  30        B4SUFF    R        O  5 26REFFLD(LISUFF)
     A                                      COLOR(YLW)
     A                                  6  2'Leverandør:'
     A            B4LEVR        30A  O  6 15COLOR(YLW)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20040401  082450  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 11 45)
     A                                  2  2' Manuelt godkjenne melding '
     A                                      DSPATR(RI)
     A                                  4  2'Tidspunkt :'
     A            B7MDAT         6Y 0O  4 14EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  5  2'Bestiling :'
     A            B7NUMM    R        O  5 14REFFLD(LINUMM)
     A                                      COLOR(YLW)
     A  30                              5 23'/'
     A  30        B7SUFF    R        O  5 25REFFLD(LISUFF)
     A                                  6  2'Leverandør:'
     A            B7LEVR        30A  O  6 14COLOR(YLW)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  8  2'Skrive avviksliste'
     A            B7UTSK         1A  B  8 21COLOR(YLW)
     A  31                                  ERRMSGID(LMI0020 *LIBL/QMFLAGR 31)
     A                                  8 24'1=Ja, 0=Nei'
     A          R B8WIN
     A*%%TS  SD  20020924  123604  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Oppdatere melding '
     A                                      DSPATR(RI)
     A                                  4  2'Tidspunkt :'
     A            B8MDAT         6Y 0O  4 14EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  5  2'Bestiling :'
     A            B8NUMM    R        O  5 14REFFLD(LINUMM)
     A                                      COLOR(YLW)
     A  30                              5 23'/'
     A  30        B8SUFF    R        O  5 25REFFLD(LISUFF)
     A                                  6  2'Leverandør:'
     A            B8LEVR        30A  O  6 14COLOR(YLW)
     A                                  8  2'Skal papirkopi skrives . .'
     A            B8UTSK         1A  B  8 29COLOR(YLW)
     A  31                                  ERRMSGID(LMI0010 *LIBL/QMFLAGR 31)
     A  35                                  ERRMSGID(LMI0030 *LIBL/QMFLAGR 35)
     A  36                                  ERRMSGID(LMI0031 *LIBL/QMFLAGR 36)
     A  37                                  ERRMSGID(LMI0040 *LIBL/QMFLAGR 37)
     A  38                                  ERRMSGID(LMI0041 *LIBL/QMFLAGR 38)
     A  32                                  ERRMSGID(LMI0043 *LIBL/QMFLAGR 33)
     A  33                                  ERRMSGID(LMI0042 *LIBL/QMFLAGR 32)
     A                                  8 32'1=Ja, 0=Nei'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20050523  130713  GRO530      REL-V5R2M0  5722-WDS
7.xx A                                      WINDOW(2 10 9 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Velg utskriftstype :'
     A                                      DSPATR(RI)
7.XX A*                                 1 29'LI100B9'
     A                                  5  2'Skriv dokumentkopi . .'
7.xx A*           B9DKOP         1A  B  5 25COLOR(YLW)
7.xx A            B9DKOP    R        B  5 25REFFLD(LÅVALG)
7.xx A                                      COLOR(YLW)
7.xx A                                  5 28'1/0'
7.xx A                                  6  2'Skriv avviksliste. . .'
7.xx A*           B9AVVK         1A  B  6 25COLOR(YLW)
7.xx A            B9AVVK    R        B  6 25REFFLD(LÅVALG)
7.xx A                                      COLOR(YLW)
7.xx A                                  6 28'1/0'
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.32 A          R B9WIN3
6.32 A*%%TS  SD  20081127  125855  ASP_MSX     REL-V5R4M0  5722-WDS
6.32 A                                      CA03
6.32 A                                      CA12
6.32 A                                      WINDOW(*DFT 13 45)
6.32 A                                  2  2' Feil i faktura innlesning!!!!!'
6.32 A                                      DSPATR(RI)
6.32 A                                      COLOR(RED)
6.32 A                                  5  2'En eller flere fakturaer feilet'
6.32 A                                  6  2'ved innlesing. Gå til loggen for'
6.32 A                                  7  2'å finne ut mer (F8)'
6.32 A                                 10  2'**** **** **** FEIL !!! **** **** -
6.32 A                                       ****'
6.32 A                                      COLOR(RED)
6.32 A                                 12  2'F3=Gå tilbake'
6.32 A                                      COLOR(BLU)
     A          R B13WIN
     A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
8.06 A                                      WINDOW(*DFT 17 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
8.06  *
     A                                  2  2' Statuskoder '
     A                                      DSPATR(RI)
8.06 A                                  3  2'A = Enhetsproblemer    '
8.06 A                                      COLOR(RED)
8.06 A                                  4  2'B = Beløpsfeil         '
8.06 A                                      COLOR(RED)
8.06 A                                  5  2'E = Feil ved innlesing.'
6.32 A                                      COLOR(RED)
8.06 A                                  6  2'1 = Feil i bestillingsnummer.'
     A                                      COLOR(RED)
8.06 A                                  7  2'2 = Sjekk leverandør'
     A                                      COLOR(RED)
8.06 A                                  8  2'3 = Avvik fra bestilling'
     A                                      COLOR(RED)
8.06 A                                  9  2'4 = Feil ordretype'
     A                                      COLOR(RED)
8.06 A                                 10  2'5 = Avvik totalsum'
     A                                      COLOR(RED)
8.06 A                                 12  2'7 = Slettemarkert'
     A                                      COLOR(BLU)
8.06 A                                 13  2'8 = Manuelt godkjent'
     A                                      COLOR(YLW)
8.06 A                                 14  2'9 = Oppdatert'
     A                                      COLOR(WHT)
8.06 A                                 16  2'F3=Tilbake'
     A                                      COLOR(BLU)
     A          R B14WIN
     A*%%TS  SD  20040422  215906  ASK         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      WINDOW(*DFT 9 35)
     A                                  2  2' Ny kontroll av alle meldinger'
     A                                      DSPATR(RI)
     A                                  5  2'Alle meldinger blir kontrollert.'
     A                                      COLOR(RED)
     A                                  6  2'Dette kan ta litt tid.'
     A                                      COLOR(RED)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B15WIN
     A*%%TS  SD  20061020  090707  GRO540      REL-V5R4M0  5722-WDS
6.12 A                                      WINDOW(*DFT 12 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Fysisk fjerning av meldinger :'
     A                                      DSPATR(RI)
6.12 A                                  4  2'Fjerne EDI meldinger. '
6.12 A            B15SL2         1A  B  4 25
6.12 A  32                                  ERRMSG('Kun B, S og G er gyldig')
6.12 A                                  4 28'B=Både S og G merkede'
6.12 A                                  5 28'S=Kun slettemerkede'
6.12 A                                  6 28'G=Kun godkjente'
     A                                  7  2'Til dato . . . . . . .'
8.11 A*           B15DAT         6Y 0B  7 26EDTCDE(Z)
8.11 A            B15DAT         6Y 0B  7 26
8.11 A                                      EDTWRD('  .  .  ')
8.11 A                                      CHECK(RB)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                  8  2'Meldinger frem til angitt dato'
     A                                      COLOR(RED)
     A                                  9  2'blir fysisk slettet'
     A                                      COLOR(RED)
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R H1WIN
     A*%%TS  SD  20050915  092234  GRO540      REL-V5R3M0  5722-WDS
7.xx A                                      WINDOW(*DFT 17 72)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Utvalgskriterier :'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                  4  2'Leverandørnr. +'
     A            H1SLDO         6Y 0B  4 18CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            H1LNAV        30A  O  4 26OVRDTA
     A                                  5  2'Leverandørnavn.'
     A            H1SNAV        20A  B  5 18COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Meldingsdato. .'
     A            H1SMDA         6Y 0B  6 18COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'Fakturanummer .'
     A            H1SFAK        20A  B  7 18COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Bestillingsnr..'
     A            H1SBES        20A  B  8 18COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Selger/innkj. .'
     A            H1INNK    R        B  9 18REFFLD(LOINNK)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            H1INAV        20A  O  9 26OVRDTA
6.30  *
7.xx A                                 10  2'Lager . . . . +'
6.30 A            H1LAGE    R        B 10 18REFFLD(LOLAGE)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(RB)
7.xx A**          H1LTXT         3A  O 10 21OVRDTA
7.xx A                                 10 23'+'
7.xx A            H2LAGE    R        B 10 25REFFLD(LOLAGE)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(RB)
7.xx A**          H2LTXT         3A  O 10 28OVRDTA
7.xx A                                 10 30'+'
6.30 A            H3LAGE    R        B 10 32REFFLD(LOLAGE)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(RB)
7.xx A**          H3LTXT         3A  O 10 35OVRDTA
7.xx A                                 10 37'+'
6.30 A            H4LAGE    R        B 10 39REFFLD(LOLAGE)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(RB)
7.xx A**          H4LTXT         3A  O 10 42OVRDTA
7.xx A                                 10 44'+'
6.30 A            H5LAGE    R        B 10 46REFFLD(LOLAGE)
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A                                      CHECK(RB)
7.xx A**          H5LTXT         3A  O 10 49OVRDTA
6.30 A                                 11  2'Varemotatt. . .'
6.30 A            H1VMOT         1A  B 11 18
6.30 A                                      OVRDTA
6.30 A                                      COLOR(YLW)
6.30 A  33                                  ERRMSG('Kun blank, 1,2 og 3 er gy+
6.30 A                                              ldige valg')
7.xx A                                 11 21'0=Ikke valgt,1=Mottatt,2=Ikke mott+
7.xx A                                      att, 3=Uten best.'
8.06  *
8.06 A                                 12  2'Statuskode. . .'
8.06 A            H1STAT         2A  B 12 18COLOR(YLW)
8.06 A                                      OVRDTA
8.06 A                                 12 21'A,B,E,1,2,3,4,5,7,8,9'
8.06  *
7.xx A                                 13  2'Direktelev. . .'
7.xx A            H1DIRK         1A  B 13 18
6.22 A                                      OVRDTA
6.22 A                                      COLOR(YLW)
6.22 A**                                    CHECK(RB)
6.22 A  32                                  ERRMSG('Kun blank og 1 er gyldig +
6.22 A                                              valg')
7.xx A                                 13 21'1=Kun'
7.xx A                                 14  2'Fakt/Kreditnota'
7.xx A            H1FAKR         1A  B 14 18
6.22 A                                      OVRDTA
6.22 A                                      COLOR(YLW)
6.22 A**                                    CHECK(RB)
6.22 A  33                                  ERRMSG('Kun blank og F/K er gyldi+
6.22 A                                              ge valg')
7.01 A                                 14 21'0=Ikke valgt, F=Faktura, K=Kreditn-
7.xx A                                      ota'
7.05 A                                 15  2'Fakturalev. . .'
7.05 A            H1FLDO         6Y 0B 15 18CHECK(RB)
7.05 A                                      EDTCDE(Z)
7.05 A                                      COLOR(YLW)
7.05 A                                      OVRDTA
7.05 A            H1FNAV        30A  O 15 26OVRDTA
7.xx A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A                                 16 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*
6.11 A          R H2WIN
6.11 A*%%TS  SD  20041126  120153  GRO530      REL-V5R2M0  5722-WDS
6.11 A                                      WINDOW(*DFT 9 45)
6.11 A                                      CA03
6.11 A                                      CA12
6.11 A                                  2  2' Visningsvalg   '
6.11 A                                      DSPATR(RI)
6.11 A                                  4  2'Velg visning:'
6.11 A                                  8  2'F3=Gå tilbake'
6.11 A                                      COLOR(BLU)
6.11 A            H2VISE         1   B  4 17COLOR(YLW)
7.01 A*                                 4 23'Blank er ubehandlede'
7.01 A                                  4 23'U = Ubehandlede'
6.11 A                                  5 23'S = Slettede '
6.11 A                                  6 23'G = Godkjente'
6.24 A                                  7 23'A = Alle'
6.11 A*
6.11 A          R ASUME                     ASSUME
6.11 A                                 24 79' '
