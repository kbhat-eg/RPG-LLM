     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
     A*
7.xx A*     BSA  180216    Endringer relatert til ny GUI
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  19980930  154338  HKO         REL-V4R2M0  5769-PW1
7.xx A            B1VALG    R        B 10  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 6 7 8 9 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
7.xx A            B1BINR    R        O 10  4REFFLD(SOBINR)
7.xx A            B1NUMM    R        O 10 13REFFLD(SONUMM)
7.xx A            B1SUFF    R        O 10 22REFFLD(SOSUFF)
7.xx A            B1OTYP    R        O 10 25REFFLD(SOOTYP)
7.xx A            B1FKDA         6Y 0O 10 28EDTCDE(Z)
7.xx A            B1KNAV        16A  O 10 35
7.xx A            B1NAVN        15A  O 10 52
7.xx A            B1FSUM    R        O 10 68REFFLD(SOFSUM)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080501  161452  KOB560      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
7.02 A*                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
7.02 A                                      CF21
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på fakturaregister'
     A                                      COLOR(WHT)
7.xx A                                  2 38'SO501B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde. .'
     A                                      COLOR(BLU)
     A            B2KUND    R        O  4 11REFFLD(SOKUND)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  4 66'År. . . .'
     A                                      COLOR(BLU)
     A            B2AARR    R        O  4 76REFFLD(SOAARR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  4 45'Ord.type.'
     A                                      COLOR(BLU)
7.xx A            B2OTY1    R        O  4 55REFFLD(SOOTYP)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.02 A            B2FSOK        35A  B  5 12COLOR(YLW)
7.02 A                                      OVRDTA
7.02 A  22                                  DSPATR(PC)
7.02 A            B2FSO2        25A  O  5 48COLOR(WHT)
7.02 A                                      OVRDTA
7.02 A                                  5  4'Frisøk'
7.02 A                                      COLOR(WHT)
7.02 A                                  5 75'(F21)'
7.02 A                                      COLOR(WHT)
7.02 A                                  6  4'Faktnr'
7.xx A                                      COLOR(WHT)
7.02 A                                  6 29'Ordre'
7.xx A                                      COLOR(WHT)
7.02 A                                  6 44'OT'
7.xx A                                      COLOR(WHT)
7.02 A                                  6 50'Dato'
7.xx A                                      COLOR(WHT)
7.02 A                                  7  4'Kunde'
7.xx A                                      COLOR(WHT)
7.02 A                                  7 29'Kundeprosjekt'
     A                                      COLOR(WHT)
7.02 A            B2BINR    R        B  6 12REFFLD(SOBINR)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.02 A            B2NUMM    R        B  6 35REFFLD(SONUMM)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.02 A            B2OTYP    R        B  6 47REFFLD(SOOTYP)
7.xx A                                      COLOR(YLW)
7.xx A                                      OVRDTA
7.02 A            B2FKDA         6Y 0B  6 55COLOR(YLW)
7.xx A                                      CHECK(RB)
7.xx A                                      EDTCDE(Z)
7.xx A                                      OVRDTA
7.02 A            B2KUNA        16A  B  7 12COLOR(YLW)
7.xx A                                      OVRDTA
7.02 A            B2NAVN        15A  B  7 47COLOR(YLW)
7.xx A                                      OVRDTA
7.xx A                                      CHECK(LC)
7.02 A                                  8  2'5=Vis faktura 6=Skriv kopi 8=Vis o-
7.02 A                                      rdrehode'
     A                                      COLOR(BLU)
7.02 A                                  8 45'7=Vis slettede ordreli. 9=Vis arki-
7.02 A                                      v'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V Faktnr   Ordre       OT  Dato  K-
7.xx A                                      unde            Kundeprosjekt     F-
     A                                      akturasum'
     A                                      COLOR(WHT)
     A*
     A          R B2CMD
     A*%%TS  SD  19980108  144330  HKO         REL-V3R7M0  5716-PW1
     A                                 23  2'F3=Gå tilbake F5=Forny'
     A                                      COLOR(BLU)
