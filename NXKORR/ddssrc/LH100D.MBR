     A*%%TS  SD  20140526  142221  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LH100D
     A* Beskrivelse . . : Bestillingsforslag, hovedbilde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120302 ASK  Nytt program
     A* 5.51  200508 TSV  Viser lager og utvidet filter med lager.
     A* 6.20  240310 BOF  Utvidet med best.forsl fra ordre mm.
     A* 6.20  161210 BOF  Utvidet med valg, se på ordre
     A* 6.21  140114 NHO  Feilmelding dersom lev.dato ikke er utfylt
6.NU A* 6.30  260514 NHO  utvidelse ihht. nummerserieutvidelse
6.31 A* 6.30  040914 BOF  endret logikk vedr. feilmeldinger
6.32 A* 6.30  280216 NHO  Endret alle linjenr for å få plass til fork-
6.32 A*                   laring på status 9  (H2WIN)
7.xx A* 7.xx  170316 bsa  Justeringer basert på ny GUI og regler
Q.01 A*       270319 bsa  Quick wins - bredere skjerm
Q.02 A*       270319 bsa  Quick wins - et valg for å bekrefte sletting
Q.02 A*                   av valgte bestillingsforslag.
     A*
7.fb A* 7.fb  050117 BOF  Linjer merkes rødt hvis fors.k. ikke betalt
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.03 A                                      DSPSIZ(27 132 *WIDE)
7.03 A*                                      DSPSIZ(24 80 *DS3 -
7.03 A*                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*  5.51 190508 TSV NYTT FELT LAGER I SUBFIL OG HEADING.
     A          R B1SFL                     SFL
     A*%%TS  SD  20140526  142221  BOF630      REL-V7R1M0  5770-WDS
7.03 A            B1VALG         2Y 0B  9  2CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A                                      VALUES(0 2 4 5 6 7 8 9 10 11 15 18)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.03 A            B1NUMM    R        O  9  5REFFLD(LHNUMM)
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
7.03 A            B1DATE         6Y 0O  9 12EDTWRD('  .  .  ')
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
7.03 A            B1LNAV        13A  O  9 21
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
     A            B1LSOR    R        H      REFFLD(LHLSOR)
     A            B1LDOR    R        H      REFFLD(LHLDOR)
7.03 A            B1SELG    R        O  9 35REFFLD(LHSELG)
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
7.03 A            B1ORNR    R        O  9 40REFFLD(LHORNR)
7.fb A  45                                  COLOR(RED)
7.fb A  46                                  COLOR(BLU)
7.03 A            B1LETY    R        O  9 49REFFLD(LHLETY)
7.03 A            B1LAGE         2Y 0O  9 51EDTCDE(Z)
7.03 A            B1BDAT         6Y 0O  9 54EDTWRD('  .  .  ')
7.03 A            B1STAT    R        O  9 63REFFLD(LHSTAT)
7.03 A            B1TBEL    R        O  9 65REFFLD(LHTBEL)
7.03 A                                      EDTCDE(L)
7.03 A            B1GBEL         6Y 0O  9 79EDTCDE(3)
7.03 A            B1DBEL         6Y 0O  9 86EDTCDE(L)
7.03 A            B1VEKT         6Y 0O  9 94
7.03 A                                      EDTCDE(3)
7.03 A            B1GVEK         6Y 0O  9101EDTCDE(3)
7.03 A            B1DVEK         6Y 0O  9109EDTCDE(L)
7.03 A            B1OPPR    R        O  9128REFFLD(LHOPPR)
7.03 A            B1PARA    R        H      REFFLD(MPBESK)
7.03 A            B1ORSU    R        H      REFFLD(LHORSU)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140526  142221  BOF630      REL-V7R1M0  5770-WDS
7.03 A                                      SFLSIZ(0018)
7.03 A                                      SFLPAG(0017)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CF08
     A                                      CA11
     A                                      CA12
     A                                      CA13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
7.03 A  15                                  SFLEND
7.03 A*  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_CURN         5S 0H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
7.03 A                                  1118'Kl. :'
7.03 A                                      COLOR(WHT)
7.03 A                                  1124TIME
7.03 A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bestillingsforslag'
     A                                      COLOR(WHT)
7.01 A                                  2 38'LH100T2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
7.03 A                                  2118'Dato:'
7.03 A                                      COLOR(WHT)
7.03 A                                  2124DATE
7.03 A                                      EDTCDE(Y)
7.03 A                                      COLOR(WHT)
7.03 A                                  3  1'-----------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ------------------------------------
7.03 A                                      ---------------------------'
7.03 A                                      COLOR(WHT)
7.xx A                                  4  5'Forslag'
7.xx A                                      COLOR(WHT)
7.01 A                                  4 29'Dato'
7.xx A                                      COLOR(WHT)
7.01 A                                  5  5'Lev.navn'
7.xx A                                      COLOR(WHT)
7.01 A                                  5 29'Selg'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 52'Totalvekt  :'
7.xx A                                      COLOR(WHT)
7.03 A            B2TVEK    R        O  4 65REFFLD(LHVEKT)
7.03 A                                      EDTCDE(3)
7.xx A                                  5 52'Totalverdi :'
7.xx A                                      COLOR(WHT)
7.xx A            B2TBEL    R        O  5 65REFFLD(LHTBEL)
7.xx A                                      EDTCDE(L)
7.xx A                                  6  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  6 11'4=Slett'
     A                                      COLOR(BLU)
7.03 A                                  6 19'5=Godkjenn status'
7.xx A                                      COLOR(BLU)
7.03 A                                  6 37'6=Utskrift'
     A                                      COLOR(BLU)
7.03 A                                  6 48'7=Endre linjer'
     A                                      COLOR(BLU)
7.03 A                                  6 63'8=Overfør til bestilling'
     A                                      COLOR(BLU)
7.03 A                                  6 88'9=Endre leverandør'
     A                                      COLOR(BLU)
7.03 A                                  6107'10=Slå sammen'
     A                                      COLOR(BLU)
7.03 A                                  6121'11=Splitt'
     A                                      COLOR(BLU)
7.03 A                                  7  2'15=Se på ordre'
     A                                      COLOR(BLU)
7.03 A                                  7 18'18=Overfør til leverandør'
     A                                      COLOR(BLU)
7.xx A**                                6 52'Totalverdi :'
7.xx A**                                    COLOR(WHT)
7.xx A**          B2TBEL    R        O  6 65REFFLD(LHTBEL)
7.xx A**                                    EDTCDE(L)
7.03 A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.03 A                                  8  5'Forslag'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 14' Dato '
7.xx A                                      COLOR(WHT)
7.03 A N51                              8 21'Lev.navn'
7.xx A                                      COLOR(WHT)
7.03 A  51                              8 21'Parm.navn  '
7.03 A                                      COLOR(WHT)
7.03 A                                  8 35'Selg'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 40' Ordre '
7.xx A                                      COLOR(WHT)
7.03 A                                  8 49'L'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 51'Lag'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 55'L.dato'
7.xx A                                      COLOR(WHT)
7.03 A                                  8 63'S'
7.xx A                                      COLOR(WHT)
7.03 A N51                              8 65'Ordreverdi'
7.xx A                                      COLOR(WHT)
7.03 A  51                              8 65'Tot.vekt'
7.03 A                                      COLOR(WHT)
7.03 A                                  8 79'GBeløp'
7.03 A                                      COLOR(WHT)
7.03 A                                  8 86'DBeløp'
7.03 A                                      COLOR(WHT)
7.03 A                                  8 94'Vekt'
7.03 A                                      COLOR(WHT)
7.03 A                                  8101'GVekt'
7.03 A                                      COLOR(WHT)
7.03 A                                  8109'DVekt'
7.03 A                                      COLOR(WHT)
7.03 A                                  8127'Opr'
     A                                      COLOR(WHT)
7.01 A            B2NUMM    R        B  4 14REFFLD(LHNUMM)
     A                                      DLTCHK
     A                                      OVRDTA
     A  31                                  ERRMSGID(LMH0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMH0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.01 A            B2DATE         6Y 0B  4 34OVRDTA
     A  31                                  ERRMSGID(LMH0020 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMH0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.01 A*                                     EDTCDE(Z)
7.01 A                                      EDTWRD('  .  .  ')
7.01 A            B2LSOR    R        B  5 14REFFLD(LHLSOR)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.10 A            B2SELG    R        B  5 34REFFLD(LHSELG)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A          R B2CMD
     A*%%TS  SD  20100426  142057  BOF560      REL-V5R4M0  5722-WDS
7.03 A                                 26 16'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.03 A                                 26 32'F5=Forny'
     A                                      COLOR(BLU)
7.03 A                                 26 44'F6=Opprett'
     A                                      COLOR(BLU)
7.03 A                                 26 57'F8=Fra håndterminal'
     A                                      COLOR(BLU)
7.03 A                                 26  2'F1=Filter'
     A                                      COLOR(BLU)
7.03 A                                 26 78'F11=Bytt Info'
     A                                      COLOR(BLU)
7.xx A*                                23 17'F13=Status'
7.xx A*                                     COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20020924  121046  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Slette bestillingsforslag '
     A                                      DSPATR(RI)
     A                                  4  2'Forslag nr. . . . . .'
     A            B4NUMM    R        O  4 24REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
Q.02 A          R Q4WIN
Q 02 A*%%TS  SD  20020924  121046  HKO         REL-V5R1M0  5722-WDS
Q.02 A                                      WINDOW(*DFT 8 35)
Q.02 A                                      CA03
Q.02 A                                      CA12
Q.02 A                                      PUTOVR
Q.02 A                                  2  2' Slette bestillingsforslag '
Q.02 A                                      DSPATR(RI)
Q.02 A                                  4  2'Bekreft sletting av alle '
Q.02 A                                  4 31'1/0'
Q.02 A            Q4VALG         1A  B  4 29
Q.02 A                                      OVRDTA
Q.02 A                                      COLOR(YLW)
Q.02 A                                  7  2'F3=Gå tilbake'
Q.02 A                                      COLOR(BLU)
     A          R B5WIN
     A*%%TS  SD  20040901  133205  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 9 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Endre status til 0 '
     A                                      DSPATR(RI)
     A                                  4  2'Forslag'
     A            B5NUMM    R        O  4 10REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Status '
     A            B5STAT    R        O  5 10REFFLD(LHSTAT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  6  2'Kun hvis detaljer har status 0'
     A                                      DSPATR(RI)
     A          R B6WIN
     A*%%TS  SD  20020924  121241  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 8 36)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Overføring av bestillingsforslag '
     A                                      DSPATR(RI)
     A                                  4  2'Fra . .'
     A            B6FNUM    R        B  4 10REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  4 19'Til . .'
     A            B6TNUM    R        B  4 27REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7WIN
     A*%%TS  SD  20100322  104039  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 19 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Vedlikehold av hodeopplysninger f-
     A                                      or best.forslag '
     A                                      DSPATR(RI)
     A                                  4  2'Bestillingsforslagsnummer :'
     A                                      COLOR(WHT)
     A            B7NUMM    R        O  4 31REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  5  2'Referanse . . . .'
     A            B7USER    R        B  5 21REFFLD(LHUSER)
     A                                      OVRDTA
     A                                      COLOR(YLW)
7.xx A                                  6  2'Ordretype . . . +'
     A            B7OTYP    R        B  6 21REFFLD(LHOTYP)
     A  31                                  ERRMSGID(LSO0010 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LM00300 *LIBL/QMFLAGR 33)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Ordrenr.  . . . .'
     A            B7ORNR    R        O  7 21REFFLD(LHORNR)
     A                                      OVRDTA
     A                                      COLOR(GRN)
7.xx A                                  8  2'Lager . . . . . +'
     A            B7LAGE    R        B  8 21REFFLD(LÅLAGE)
     A  32                                  ERRMSGID(LM00190 *LIBL/QMFLAGR 32)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B7LATX        30A  O  8 26OVRDTA
7.xx A                                  9  2'Avdeling. . . . +'
     A            B7AVDE    R        B  9 21REFFLD(LÅAVDE)
     A  36                                  ERRMSGID(LSO0210 *LIBL/QMFLAGR 36)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B7AVTX        30A  O  9 26OVRDTA
     A                                 11  2'Bestilings dato .'
     A            B7BDAT         6Y 0B 11 21
     A  34                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 12  2'Leverings dato  .'
     A            B7LDAT         6Y 0B 12 21
     A  35                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 35)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                 13  2'Grense Vekt/Beløp'
     A            B7GVEK    R        O 13 21REFFLD(LÅVEKT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            B7GBEL        11Y 2O 13 36OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                 14  2'Virkelig vekt . .'
     A            B7VEKT    R        O 14 21REFFLD(LÅVEKT)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                 15  2'Parametertekst'
     A            B7PTXT        30A  O 15 21OVRDTA
     A                                 16  2'Sats fra Parm.'
     A            B7SATS    R        O 16 21REFFLD(LHSATS)
     A                                      OVRDTA
     A                                 18  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
     A          R B8WIN
     A*%%TS  SD  20021213  100252  ASK         REL-V5R1M0  5722-WDS
     A                                      WINDOW(2 40 9 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Overføre bestillingsforslag '
     A                                      DSPATR(RI)
     A                                  4  2'Forslag nr. . . . . .'
     A            B8NUMM    R        O  4 24REFFLD(LHNUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  8  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B9WIN
     A*%%TS  SD  20040610  133338  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 10 35)
     A                                      CA03
     A                                      CF04
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Bytte av leverandør'
     A                                      DSPATR(RI)
     A                                  4  2'Forslag nr. . . . . .'
     A            B9NUMM    R        O  4 24REFFLD(LHNUMM)
     A                                  5  2'Fra leverandør  . . .'
     A            B9FLEV    R        O  5 24REFFLD(LHLDOR)
     A                                      OVRDTA
7.xx A                                  7  2'Til leverandør  . . +'
     A            B9TLEV    R        B  7 24REFFLD(LHLDOR)
     A  31                                  ERRMSGID(LMH0050 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B10WIN
     A*%%TS  SD  20040610  133338  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 10 35)
     A                                      CA03
     A                                      CF04
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Slå sammen to forslag'
     A                                      DSPATR(RI)
     A                                  4  2'Forslag nr. . . . . .'
     A            B10NUMM   R        O  4 24REFFLD(LHNUMM)
     A                                  5  2'Levernadør  . . . . .'
     A            B10LDOR   R        O  5 24REFFLD(LHLDOR)
     A                                      OVRDTA
     A                                  7  2'Slå sammen med  . . .'
     A            B10SMNR   R        B  7 24REFFLD(LHNUMM)
     A  31                                  ERRMSGID(LMH0060 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMH0070 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LMH0065 *LIBL/QMFLAGR 33)
     A  34                                  ERRMSGID(LMH0066 *LIBL/QMFLAGR 34)
     A  35                                  ERRMSGID(LMH0067 *LIBL/QMFLAGR 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B10LETY   R        H      REFFLD(LHLETY)
     A            B10OPPR   R        H      REFFLD(LHOPPR)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B11WIN
     A*%%TS  SD  20040611  132600  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 10 45)
     A                                      CA03
     A                                      CF04
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Splitte ut forslag'
     A                                      DSPATR(RI)
     A                                  4  2'Forslag nr. . . . . .'
     A            B11NUMM   R        O  4 24REFFLD(LHNUMM)
     A                                  5  2'Velg splitting  . . .'
     A            B11VALG        1A  B  5 24
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  5 26'C=cross-docket'
     A                                  6 26'V=Vare-leverandør'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B15WIN
     A*%%TS  SD  20100324  110328  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 10 55)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Overføre bestillingsforslag  pr. -
     A                                      leverandør'
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør  . . . . .'
     A            B15LDO    R        B  4 24REFFLD(LHLDOR)
     A  31                                  ERRMSGID(LMH0240 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMH0250 *LIBL/QMFLAGR 32)
     A  33                                  ERRMSGID(LMH0260 *LIBL/QMFLAGR 33)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A            B15NAV        20A  O  4 31OVRDTA
     A                                      COLOR(YLW)
     A                                  5  2'Antall for overføring'
     A            B15ANT    R        O  5 24REFFLD(LHLDOR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2'Kun Status 0 blir sendt'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A* 5.52 200508 TSV POP-H2WIN UTVIDET FILTER MED VALG AV LAGER
     A          R H1WIN
     A*%%TS  SD  20100324  091556  BOF560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4  2'Velg status  . .:'
     A            H1STAT    R        B  4 20REFFLD(LHSTAT)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 22'(kode)'
     A                                  4 35'Blank gir'
     A                                  5  2'Velg Selger  . .:'
     A            H1SELG    R        B  5 20REFFLD(LHSELG)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  5 35'alle'
7.xx A                                  6  2'Velg leverandør +'
     A            H1LDOR    R        B  6 20REFFLD(LHLDOR)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  7  2'Velg Ordretype  :'
     A            H1OTYP    R        B  7 20REFFLD(LHOTYP)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A**                                6 27'F4'
7.xx A                                  8  2'Velg Lager . . .+'
     A            H1LAGE         2Y 0B  8 20COLOR(YLW)
     A                                      RANGE(0 99)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A**                                8 27'F4'
     A                                  9  2'Opprinnelse  . .:'
     A            H1OPPR    R        B  9 20REFFLD(LHOPPR)
     A                                      VALUES('R' 'A' 'O')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9 23'R=RT  A=Automatisk'
     A                                  9 43'O=Ordre'
     A                                 10  2'Leveringsadresse:'
     A            H1LETY    R        B 10 20REFFLD(LHOPPR)
     A                                      VALUES(' ' '0' '1')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 10 23'0=Via lager  1=Direkte'
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 13 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R H2WIN
     A*%%TS  SD  20040602  092305  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CF04
     A                                  2  2'Forklaring på statuskoder'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  3  2'0 '
     A                                  3  5'Forslag er OK             '
     A                                  4  2'1 '
     A                                  4  5'Minst en vare er under bestillings-
     A                                      punkt.'
     A                                  5  2'2 '
     A                                  5  5'Totale vekt eller beløpsbetingelse-
     A                                      r er ikke nådd.'
     A                                  6  2'3 '
     A                                  6  5'Minst en vare er tatt med for å øk-
     A                                      e ordreverdien.'
     A                                  7  2'4 '
     A                                  7  5'Minst en vare har følgende status -
     A                                      :              '
     A                                  8  5'-Bestilt volum samsvarer ikke med -
     A                                      best.mengde.   '
     A                                  9  5'-Bestilt volum samsvarer ikke med -
     A                                      anbrekk.       '
     A                                 10  5'-Varen ligger allerede på annen be-
     A                                      stilling.      '
     A                                 11  2'5 '
     A                                 11  5'Minst en vare har cross docking   -
     A                                                     '
     A                                 12  2'6 '
     A                                 12  5'Minst en vare har annen leverandør-
     A                                      enn best.hode  '
     A                                 13  2'7 '
     A                                 13  5'En eller flere varer er utenfor Kj-
     A                                      ede Sortiment  '
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.32 A                                 14  2'9'
6.32 A                                 14  5'Ordrenr er blank, eller det er en -
6.32 A                                      tekstlinje'
