/*--------------------------------------------------------------------------------*/
/* Ref    Date  Sign  Description                                                 */
/* 8.01  041125 bsa   Endret navn på jobben                                       */
/*                                                                                */
/*                                                                                */
/*                                                                                */
/*--------------------------------------------------------------------------------*/
             pgm        parm(&figr)

             dcl        var(&figr)     type(*char) len(3)
             dcl        var(&mld1)     type(*char) len(50)
             dcl        var(&mld2)     type(*char) len(50)
             dcl        var(&svar)     type(*char) len(1)
             dcl        var(&in12)     type(*dec)  len(1 0)
             dcl        var(&figu)     type(*char) len(7)
             dcl        var(&fign)     type(*char) len(8)
             dcl        var(&figj)     type(*char) len(9)
             dcl        var(&sekn)     type(*dec)  len(4 0)

             chgvar     var(&figr) value(%upper(&figr))
             chgvar     var(&sekn)     value(1)
             chgvar     var(&figu)     value('ASP_'   *cat %upper(&figr))
             chgvar     var(&fign)     value('AUTOB'  *cat %upper(&figr))
/*8.01       chgvar     var(&figj)     value('AUTOB_' *cat %upper(&figr)) */
/*8.01*/     chgvar     var(&figj)     value(%upper(&figr) *cat '_AUTOB_')

/* Sjekker om subsystem allerede finns - hopper til generering av jobbkø */

 testsub:    strsbs     sbsd(aspgpl/autobest)
             monmsg     msgid(cpf1010) exec(do)
                goto       cmdlbl(jobq)
             enddo
             monmsg     msgid(cpf1013) exec(do)
                goto       cmdlbl(crtsub)
             enddo
             goto       cmdlbl(strsub)

/* Bygger subsystem, routingentry og klasse                               */

 crtsub:     crtsbsd    sbsd(aspgpl/autobest) pools((1 *base)) +
                          maxjobs(200) text('Subsystem for automatisk +
                          bestilling')
             monmsg     msgid(cpf1696)

             addrtge    sbsd(aspgpl/autobest) seqnbr(10) cmpval(*any) +
                          pgm(qsys/qcmd) cls(aspgpl/qbatch)
             monmsg     msgid(cpf0000)

             crtcls     cls(aspgpl/qbatch)
             monmsg     msgid(cpf0000)

 strsub:     strsbs     sbsd(aspgpl/autobest)
             monmsg     msgid(cpf1010)

/* Bygger jobbkø, jobbkøentry og jobbbeskrivelse pr.filgr                 */

 jobq:       crtjobq    jobq(aspgpl/&fign) text('Jobbkø for automatisk +
                          bestilling')
             monmsg     msgid(cpf3323) exec(do)
                goto       cmdlbl(endpgm)
             enddo

 jobqe:      addjobqe   sbsd(aspgpl/autobest) jobq(aspgpl/&fign) maxact(1) +
                          seqnbr(&sekn)
             monmsg     msgid(cpf1697) exec(do)
                chgvar     var(&sekn)     value(&sekn + 1)
                goto       cmdlbl(jobqe)
             enddo

             crtjobd    jobd(aspgpl/&fign) jobq(aspgpl/&fign) text('Jobb +
                          beskrivelse automatisk bestilling')
             monmsg     msgid(cpf1621)

/* Danner jobbplan jobb for denne filgruppen                            */

             addjobscde job(&figj) cmd(call   pgm(AABATCH) parm(&figr +
                          'LO745C' '1' )) frq(*weekly) scddate(*none) +
                          scdday(*mon *tue *wed *thu *fri *sat) +
                          scdtime(070000) jobd(aspgpl/&fign) user(&figu) +
                          text('automatisk bestilling')

 endpgm:     endpgm
