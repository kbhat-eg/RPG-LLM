
/*-------------------------------------------------------------------*/
/* Det spesielle med dette programmet er "dclprcopt  USRPRF(*OWNER)" */
/* Da blir alltid brukerprofil = *OWNER. Det betyr at den kjører med */
/* rettighetene til eieren av programmet. MEN det må gjøres manuelt, */
/* for som regel er det grupeprofilen som blir eier ved kompilering. */
/* Den har ikke de  nødvendige rettigheter til CRTUSRPRF/CHGUSRPRF   */
/* kommandoene. Velg en ASP_xxx bruker, da denne har alle rettigheter*/
/* 210725 bsa                                                        */
/*                                                                   */
/* 8.01 041225 bsa Passord kommer som parameter                      */
/* 8.02 151225 bsa Endre passord hvis brukerprofilen finnes fra før  */
/*                                                                   */
/*-------------------------------------------------------------------*/

/* bkv: SGR bruker ikke guppeprofil, og kan derfor ikke ha dette prg */

/*8.01*/     PGM        PARM(&USER &NAVN &MENY &milj &pasw)

             DCL        VAR(&GRPP) TYPE(*CHAR) LEN(10) VALUE(GRP_IMM)
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&UTST)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&NAVN)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&MENY)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&IPUS)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&A4S3)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBD)     TYPE(*CHAR) LEN(7)
             DCL        VAR(&MILJ)     TYPE(*CHAR) LEN(3)
             dcl        var(&grpusr)   type(*char) len(10)  value('ASP_xxx')
             dcl        var(&fgr)      type(*char) len(3)
/*8.01*/     dcl        var(&pasw)     type(*char) len(25)

/* Må kjøres med spesielle rettigheter                               */

             dclprcopt  USRPRF(*OWNER)


             MONMSG     MSGID(CPF2292) EXEC(GOTO MELDE)
             MONMSG     MSGID(CPF0000)

/* Hente inn fra LDA                                                 */

             RTVDTAARA  DTAARA(*LDA (995 1)) RTNVAR(&A4S3)
/*           RTVDTAARA  DTAARA(*LDA (1003 3)) RTNVAR(&MILJ)   */
             rtvdtaara  dtaara(*lda (931 3))  RTNVAR(&fgr)
             chgvar     var(%sst(&grpusr 5 3))  value(&fgr)
             rtvusrprf  usrprf(&grpusr) grpprf(&grpp)
/*           chgvar     var(%sst(&grpp 5 3))  value(&fgr)   */

/* Bygger variabel for jobb-beskrivelse                              */

             CHGVAR     VAR(&JOBD)            VALUE('JOBD   ')
             CHGVAR     VAR(%SST(&JOBD 5 3))  VALUE(&MILJ)

/* Dersom AS/400-miljø, settes startmeny                             */
/* til Main (Cl-program benyttes)                                    */

             IF         COND(&A4S3 *EQ ' ') THEN(DO)
             CHGVAR     VAR(&MENY)    VALUE('MAIN')
             ENDDO

/* Dersom Startmeny er blank, settes den                             */
/* til Main                                                          */

             IF         COND(&MENY *EQ ' ') THEN(DO)
             CHGVAR     VAR(&MENY)     VALUE('MAIN')
             ENDDO

             CHGVAR     VAR(&IPUS)     VALUE(IP *CAT &USER)

/* Sjekker om dette er en ASP_bruker                                 */

             CHGVAR     VAR(&UTST)     VALUE(%SST(&USER 1 3))
             IF         COND(&UTST *EQ 'ASP') THEN(DO)
             GOTO       CMDLBL(TAGASP)
             ENDDO

/* Sjekker om dette er en GRP_bruker                                 */

             CHGVAR     VAR(&UTST)     VALUE(%SST(&USER 1 3))
             IF         COND(&UTST *EQ 'GRP') THEN(DO)
             GOTO       CMDLBL(TAGGRP)
             ENDDO

/* Bygger bruker-profile ikke ASP-bruker                             */

             CRTUSRPRF  USRPRF(&USER) PASSWORD(&pasw) PWDEXP(*YES) USRCLS(*PGMR) +
                          INLPGM(ASPGPLK/&IPUS) INLMNU(&MENY) +
                          LMTCPB(*YES) TEXT(&NAVN) SPCAUT(*SPLCTL) +
                          SPCENV(*SYSVAL) PWDEXPITV(*SYSVAL) +
                          JOBD(ASPGPL/&JOBD) GRPPRF(&GRPP) +
                          OWNER(*GRPPRF) ATNPGM(*SYSVAL)
             MONMSG     MSGID(CPF2214) EXEC(DO)
/*8.02*/        CHGUSRPRF  USRPRF(&USER) PASSWORD(&pasw) PWDEXP(*YES) +
/*8.02*/                     USRCLS(*PGMR) +
                             INLPGM(ASPGPLK/&IPUS) INLMNU(&MENY) +
                             TEXT(&NAVN) JOBD(ASPGPL/&JOBD) GRPPRF(&GRPP) +
                             OWNER(*GRPPRF) ATNPGM(*SYSVAL) +
                             PWDEXPITV(*SYSVAL)
/*                 SPCAUT(*SPLCTL)                                            */
             ENDDO
             GOTO       CMDLBL(END)

/* Bygger bruker-profile ASP-bruker                                  */

 TAGGRP:

/* Bygger bruker-profile GRP-brukere                                 */

             CRTUSRPRF  USRPRF(&USER) STATUS(*ENABLED) USRCLS(*PGMR) +
                          INLPGM(ASPGPLK/&IPUS) INLMNU(*SIGNOFF) +
                          TEXT(&NAVN) SPCAUT(*NONE) SPCENV(*SYSVAL) +
                          ATNPGM(*SYSVAL)
             MONMSG     MSGID(CPF2214) EXEC(DO)
             CHGUSRPRF  USRPRF(&USER) STATUS(*ENABLED) +
                          INLPGM(ASPGPLK/&IPUS) INLMNU(*SIGNOFF) +
                          TEXT(&NAVN) GRPPRF(*NONE) +
                          ATNPGM(*SYSVAL)
             ENDDO
             GOTO       CMDLBL(END)
 TAGASP:

             CRTOUTQ    OUTQ(QGPL/ASP) TEXT('Utkø for ASP Brukere')
             MONMSG     MSGID(CPF3353)

             CRTUSRPRF  USRPRF(&USER) USRCLS(*SECOFR) +
                          INLPGM(ASPGPLK/&IPUS) INLMNU(&MENY) TEXT(&NAVN) +
                          SPCAUT(*ALLOBJ *AUDIT *IOSYSCFG *JOBCTL *SAVSYS +
                          *SECADM *SERVICE *SPLCTL) SPCENV(*SYSVAL) +
                          PWDEXPITV(*NOMAX) OUTQ(QGPL/ASP) +
                          JOBD(ASPGPL/&JOBD)  GRPPRF(&GRPP) OWNER(*GRPPRF) +
                          ATNPGM(*SYSVAL)
             MONMSG     MSGID(CPF2214) EXEC(DO)
                CHGUSRPRF  USRPRF(&USER) INLPGM(ASPGPLK/&IPUS) +
                             INLMNU(&MENY) TEXT(&NAVN) GRPPRF(&GRPP) +
                             OWNER(*GRPPRF) JOBD(ASPGPL/&JOBD) +
                             ATNPGM(*SYSVAL)
             ENDDO
             GOTO       CMDLBL(END)

/* Har ikke høy nok autoritet                                        */

 MELDE:      CALL       PGM(AA004C)

 END:        ENDPGM
