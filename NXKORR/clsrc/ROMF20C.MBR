             PGM
/* 9.01  Kunde kjøre denne summering fra 6.20                        */
/*7.01  21.08.20 bsa Feiul i filnavn                                 */
/*8.01  01.11.22 mst Ny rutine/programnavn for danne poster til Øko  */

             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(7) +
                          VALUE('/visma/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(31) +
                          VALUE('.lib/vismafak.file/vismafak.mbr')
             DCL        VAR(&nbr)      TYPE(*CHAR) LEN(6)
             DCL        VAR(&usr)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB)      TYPE(*CHAR) LEN(10)
             /* 8.01*/
             DCL        VAR(&type)     TYPE(*CHAR) LEN(1) /* 8.01*/
             DCL        VAR(&STAT)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&SMSG)     TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&OVRF)     TYPE(*CHAR) LEN(10) VALUE('VISMAFAKT ')

             DCLF       FILE(*LIBL/ROM00PF) /*  8.01*/
/* -> 8.01  Finner hvilken rutine som kjøres                            */
             CALL       PGM(ROM00C) PARM(&TYPE)
/* -< 8.01  Finner hvilken rutine som kjøres                            */
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RV01PF)
             CLRPFM     FILE(FOVIPF)
             CLRPFM     FILE(FOVUPF)

/*8.01*/     if         cond(&type *eq 'B')   then(do)
/* Omkjøring fra RLOG   */

             CPYF       FROMFILE(FOMKPF) TOFILE(FOVIPF) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))
             OVRDBF     FILE(FOVFL1) TOFILE(*LIBL/FOMKL1)
/*8.01*/     enddo
/*8.01*/     if         cond(&type *ne 'B')   then(do)

             CPYF       FROMFILE(FOVFPF) TOFILE(FOVIPF) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))
/*8.01*/     enddo


/* Kaller program for danning av transaksjoner                       */
             CALL       PGM(RV009R)
             CALL       PGM(RB020R)
             CALL       PGM(RB002R)


/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/VISMAFAK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(VISMAFAK) RCDLEN(800) +
                          TEXT('Posteringer til Visma')           +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RV01PF) +
                          TOFILE(*libl/VISMAFAK) FROMMBR(RV01PF) +
                           MBROPT(*REPLACE) FMTOPT(*NOCHK)

             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))

/* Starter kopiering til integret filsystem                          */

             CALL       PGM(LO762R) PARM(&FILN)

             CALL       PGM(AF700C) PARM(&OVRF &STAT &FILN &SMSG)

 END:        ENDPGM
