STRPGM:      PGM

/*-------------------------------------------------------------------------*/
/* Initiering av parametere                                                */
/* 8.01  25.01.22  bsa Sett flagg for aktiv før du får opp bildet om jobben*/
/* 8.01  25.01.22      skal startes. Blir du stående her uten å svare kan  */
/* 8.01  25.01.22      en risikere at andre starter jobben før du "kommer  */
/* 8.01  25.01.22      deg videre".                                        */
/* 8.02  01.12.22  mst Endret pga ny henting av økosystem                 */
/*                                                                         */
/*-------------------------------------------------------------------------*/

             DCL        VAR(&KODE)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&ONOF)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&TOMT)     TYPE(*CHAR) LEN(1)

             DCL        VAR(&VALG)  TYPE(*DEC)  LEN(2 0) VALUE(0)
             DCL        VAR(&NXDR)  TYPE(*DEC)  LEN(1 0) VALUE(0)

             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50) +
                          VALUE('Ingen poster for overføring +
                          ble funnet')

             MONMSG     MSGID(CPF2103) /*    Bibl finnes fra før     */
             MONMSG     MSGID(CPF2110) /*    Bibl ble ikke funnet    */


/* Har denne bruker lov til å oppdatere regnskap ?                   */

             CHGVAR     VAR(&KODE)     VALUE(11)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FB700R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 0) THEN(DO)
             CALL       PGM(AA004C)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Er fakturering igang fra annen skjerm ?                           */

             CHGVAR     VAR(&KODE)     VALUE(01)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA730R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 1) THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Kjøres oppdatering av regnskap allerede ?                         */

             CHGVAR     VAR(&KODE)     VALUE(02)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA730R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 1) THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Finnes det poster ?, hvis ikke, avslutt                           */

             CALL       PGM(FO795C)    PARM(&TOMT)
             IF         COND(&TOMT *EQ 'J') THEN(DO)
             CALL       PGM(AA005R)    PARM(&MLD1)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

/* Oppdater jobb-register med aktiv-flagg                            */
/*8.01*/
/*8.01*/     CHGVAR     VAR(&KODE)     VALUE(02)
/*8.01*/     CHGVAR     VAR(&ONOF)     VALUE(1)
/*8.01*/     CALL       PGM(FA720R)    PARM(&KODE &ONOF)

             CHGVAR     VAR(&MLD3) +
                        VALUE('Overføring til regnskap settes igang ')
             CHGVAR     VAR(&MLD4) +
                        VALUE('Bekreft at dette ønskes ! ')
             CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)


/*8.01       IF         COND(&IN12 *EQ 1 ) +                                */
/*8.01                    THEN(GOTO CMDLBL(ENDPGM))                         */
/*8.01*/     IF         COND(&IN12 *EQ 1) THEN(DO)
/*8.01*/                CHGVAR     VAR(&KODE)     VALUE(02)
/*8.01*/                CHGVAR     VAR(&ONOF)     VALUE(0)
/*8.01*/                CALL       PGM(FA720R)    PARM(&KODE &ONOF)
/*8.01*/     GOTO       CMDLBL(ENDPGM)
/*8.01*/     ENDDO

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
/*8.01       ELSE       CMD(GOTO CMDLBL(ENDPGM))                            */
/*8.01*/     ELSE       DO
/*8.01*/                CHGVAR     VAR(&KODE)     VALUE(02)
/*8.01*/                CHGVAR     VAR(&ONOF)     VALUE(0)
/*8.01*/                CALL       PGM(FA720R)    PARM(&KODE &ONOF)
/*8.01*/     GOTO       CMDLBL(ENDPGM)
/*8.01*/     ENDDO

VIDERE:


/* Oppdater jobb-register med aktiv-flagg                            */

             CHGVAR     VAR(&KODE)     VALUE(02)
             CHGVAR     VAR(&ONOF)     VALUE(1)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

             CHGDTAARA  DTAARA(*LDA (844 1)) VALUE('0')

             CALL       PGM(RF1TRANS)  PARM('FAKT')

/* Sjekker om Nexstep økonomi skal oppdateres                        */

 /* 8.02     CALL       PGM(RV015R)  PARM(&VALG &NXDR)           */
 /*8.02 */   CALL       PGM(RV015R)  PARM(&NXDR)

             IF         COND(&NXDR *EQ 0) THEN(DO)
             CALL       PGM(RF1JOURN)  PARM('FAKT')
             ENDDO

/* Oppdater jobb-register med jobb-ferdig-flagg                      */

AVSLUT:
             CHGVAR     VAR(&KODE)     VALUE(02)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

/* Avslutt program                                                   */

ENDPGM:      ENDPGM
