/*                  UniMicro    BUSINESS                                */
/*9.01  20.11.18 nho Tilpasning for B29, summering av transer   */
/*8.01  01.11.22 mst Ny rutine/programnavn for danne poster til Øko  */
             PGM

             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(10) +
                          VALUE('/UniMicro/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(31) +
                          VALUE('.lib/UniMLag.file/UniMlag.mbr')
 /* 8.01*/   DCL        VAR(&nbr)      TYPE(*CHAR) LEN(6)
 /* 8.01*/   DCL        VAR(&usr)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&JOB)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&type)     TYPE(*CHAR) LEN(1)
             DCLF       FILE(*LIBL/ROM00PF) /*  8.01*/
/* -> 8.01  Finner hvilken rutine som kjøres                            */
             CALL       PGM(ROM00C) PARM(&TYPE)
/* -< 8.01  Finner hvilken rutine som kjøres                            */
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RV03PF)
             CLRPFM     FILE(LOVIPF)
             CLRPFM     FILE(LOVUPF)

             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))

/*8.01*/     if         cond(&type *eq 'B')   then(do)
/* Omkjøring                                                         */

             CPYF       FROMFILE(LOMKPF) TOFILE(*libl/LOVIPF) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
             OVRDBF     FILE(LOVFL1) TOFILE(*LIBL/LOMKL1)
/*8.01*/     enddo
/*8.01*/     if         cond(&type *ne 'B')   then(do)

             CPYF       FROMFILE(LOVFPF) TOFILE(*libl/LOVIPF) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
/*8.01*/     enddo

/* Kaller program for danning av transaksjoner                       */

             CALL       PGM(RB023R)   /*  Summering  */

             CALL       PGM(RB056R)   /* Leverandører */
             CALL       PGM(RB052R)   /* Posteringer  */


/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/UniMLAG) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(UniMLag) RCDLEN(800) +
                          TEXT('Posteringer til UniMicro')           +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RV03PF) +
                          TOFILE(*libl/UniMLag) FROMMBR(RV03PF) +
                           MBROPT(*REPLACE) FMTOPT(*NOCHK)


             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))


/* Starter kopiering til integret filsystem                          */

             CALL       PGM(LO764R) PARM(&FILN)

   CHGVAR VAR(&FILN)    VALUE((%sst(&filn 1 7)) *cat '_' +
                         *cat (%sst(&FILN 10 6)) *cat '.txt')
   CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *tcat &FILN)
   CHGVAR VAR(&FILT) VALUE(&BAN3 *CAT &BIBL *CAT &BAN4)
             CPYTOSTMF  +
                          FROMMBR(&FILT) TOSTMF(&FILS) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT      OBJ(&FILS) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)


 END:        ENDPGM
