/*********************************************************************/
/*                                                                  */
/*_______________________________________________________________   */
/* ENDRINGER :                                                      */
/* DATO      INZ  UTFØRT                                            */
/* ---------------------------------------------------------------  */
/* 15.11.01  MHA  NYTT PROG                                         */
/* 25.10.13  MHA  Utvidet XNAVN til char 64                         */
/* 18.11.14  bof  Endret default til melding AA007R til J           */
/* 01.02.18  bof  Hvis LOGIQFAKT og status = *Blank, ingen melding  */
/* 22.02.18  bsa  Hvis ingen filer funnet, flagg melding på annen   */
/*                måte.                                             */
/* 23.08.19  bkv  Newlook tåler ikke popup uten enter wait          */
/* 07.09.23  bsa  Skal ikke vise kvittering, hvis overføring går    */
/* 07.09.23       bra. (8.01)                                       */
/* 02.07.25  bsa  Nullstiller innhenting før feilmelding sendes.    */
/* 02.07.25       (8.02)                                            */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*****************************************************************  */

             PGM        PARM(&NAVN &STATUS &XNAVN &SHWMSG)

             DCL        VAR(&NAVN)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&KONVIN)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&KONVUT)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&KONVINL)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&KONVUTL)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOST)     TYPE(*CHAR) LEN(20)
             DCL        VAR(&STATUS2)  TYPE(*CHAR) LEN(1)

             DCL        VAR(&XNAVN)    TYPE(*CHAR) LEN(64)
             DCL        VAR(&SHWMSG)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TSTBIT)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&JOBTYP)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBUSR)   TYPE(*CHAR) LEN(10)
/*              DCL        VAR(&CLIB)     TYPE(*CHAR) LEN(10)                 */

             DCL        VAR(&NXTFTP)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS3)  TYPE(*CHAR) LEN(1)

/*              DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(30)                 */
/*              DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(30)                 */
/*              DCL        VAR(&POSX)     TYPE(*DEC) LEN(2 0) VALUE(5)        */
/*              DCL        VAR(&POSY)     TYPE(*DEC) LEN(2 0) VALUE(5)        */

             DCL        VAR(&KOMM)     TYPE(*CHAR) LEN(80) VALUE(' ')
/*8.02*/     DCL        VAR(&kode)    type(*dec)  len(2 0)
/*8.02*/     DCL        VAR(&onof)    type(*dec)  len(1 0)

             RTVJOBA    USER(&JOBUSR)  TYPE(&JOBTYP)

             CHGVAR     VAR(&STATUS)   VALUE(' ')

LOCK:
/*  Sjekker om denne overføringen er opptatt, og setter den til  */
/*  opptatt hvis den ikke allerede er det. Hvis den er opptatt   */
/*  eller hvis noe går galt så avsluttes overføringen.           */
             CHGVAR     VAR(&STATUS2) VALUE('B')
             CALL       PGM(AF703R) PARM(&NAVN &STATUS2)
             IF         COND(&STATUS2 *eq 'L') THEN(DO)
             CHGVAR     VAR(&STATUS) VALUE(&STATUS2)
             GOTO       CMDLBL(SLUTT2)
             enddo
             IF         COND(&STATUS2 *eq 'N') THEN(DO)
             CHGVAR     VAR(&STATUS) VALUE(&STATUS2)
             GOTO       CMDLBL(SLUTT2)
             ENDDO

/* 23082019 bkv Newlook tåler ikke popup uten enter wait. Fjernet             */
/*              IF         COND(&SHWMSG *eq '1' *AND &JOBTYP *eq '1') THEN(DO)*/
/*              CHGVAR     VAR(&MLD3) VALUE('    Kjører FTP ' *BCAT &NAVN)    */
/*              CHGVAR     VAR(&MLD4) VALUE('    Vennligst vent......     ')  */
/*              CALL       PGM(AA006C) PARM(&POSX &POSY &MLD3 &MLD4)          */
/*              MONMSG     MSGID(CPF0000)                                     */
/*              ENDDO                                                         */

/*  Lage fil som inneholder loggfiler hvis den ikke allerede finnes */
             CRTSRCPF   FILE(*CURLIB/AFLOPF) RCDLEN(200) TEXT('FTP +
                          loggfiler') SIZE(*NOMAX)
             MONMSG     MSGID(CPF5813 CPF7302)
/*  Lage ny logg for denne overføringen hvis den ikke finnes */
             CLRPFM     FILE(*CURLIB/AFLOPF) MBR(&NAVN)
             MONMSG     MSGID(CPF3141) EXEC(DO)
             ADDPFM     FILE(*CURLIB/AFLOPF) MBR(&NAVN) +
                          TEXT('Loggfil for FTP overføring')
             CHGPFM     FILE(*CURLIB/AFLOPF) MBR(&NAVN) SRCTYPE(TXT)
             enddo

/*  Lage fil som inneholder parmfiler hvis den ikke allerede finnes */
             CRTSRCPF   FILE(*CURLIB/AFPAPF) RCDLEN(200) TEXT('FTP +
                          parmfiler') SIZE(*NOMAX)
             MONMSG     MSGID(CPF5813 CPF7302)
/*  Lage ny parm for denne overføringen hvis den ikke finnes */
             CLRPFM     FILE(*CURLIB/AFPAPF) MBR(&NAVN)
             MONMSG     MSGID(CPF3141) EXEC(DO)
             ADDPFM     FILE(*CURLIB/AFPAPF) MBR(&NAVN) +
                          TEXT('parmfil for FTP overføring')
             CHGPFM     FILE(*CURLIB/AFPAPF) MBR(&NAVN) SRCTYPE(TXT)
             enddo

/*  Lager fil for parameterfil for listing av filer hvis den ikke */
/*  finnes fra før.                                               */
             CRTSRCPF   FILE(*CURLIB/AFLSPF) RCDLEN(200) TEXT('FTP +
                          list-filer') SIZE(*NOMAX)
             MONMSG     MSGID(CPF5813 CPF7302)
/*  Lage ny parm for denne overføringen hvis den ikke finnes */
             CLRPFM     FILE(*CURLIB/AFLSPF) MBR(&NAVN)
             MONMSG     MSGID(CPF3141) EXEC(DO)
             ADDPFM     FILE(*CURLIB/AFLSPF) MBR(&NAVN) +
                          TEXT('Listfil for FTP overføring')
             CHGPFM     FILE(*CURLIB/AFLSPF) MBR(&NAVN) SRCTYPE(TXT)
             enddo
/*  Lager fil for lister av filer hvis den ikke                   */
/*  finnes fra før.                                               */
             CRTSRCPF   FILE(*CURLIB/AFLFPF) RCDLEN(200) TEXT('FTP +
                          fil-lister') SIZE(*NOMAX)
             MONMSG     MSGID(CPF5813 CPF7302)
/*  Lage ny mbr for denne overføringen hvis den ikke finnes */
             CLRPFM     FILE(*CURLIB/AFLFPF) MBR(&NAVN)
             MONMSG     MSGID(CPF3141) EXEC(DO)
             ADDPFM     FILE(*CURLIB/AFLFPF) MBR(&NAVN) +
                          TEXT('Filliste for FTP overføring')
             CHGPFM     FILE(*CURLIB/AFLFPF) MBR(&NAVN) SRCTYPE(TXT)
             enddo

             OVRDBF     FILE(AFPAPF) TOFILE(*CURLIB/AFPAPF) MBR(&NAVN)
             OVRDBF     FILE(AFLOPF) TOFILE(*CURLIB/AFLOPF) MBR(&NAVN)
             OVRDBF     FILE(AFLFPF) TOFILE(*CURLIB/AFLFPF) MBR(&NAVN)
             OVRDBF     FILE(AFLSPF) TOFILE(*CURLIB/AFLSPF) MBR(&NAVN)
/*                                                         */
/*  Kaller opp RPG som lager liste over filer ved GET      */
/*  Returnerer status = blank hvis det er er GET på gang.  */
             CALL       PGM(AF705R) PARM(&NAVN &STATUS &HOST)
             IF         COND(&STATUS *eq 'N') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
             IF         COND(&STATUS *eq 'A') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
/*  Hvis det er ønskelig med detaljert GET kommando....... */
             IF         COND(&STATUS *eq ' ') THEN(DO)
/*  Legger til dato og klokkeslett i loggfilen                      */
             CALL       PGM(AF711R)
/*  Kjører FTP som lister ut filer på host.                      */
             OVRDBF     FILE(INPUT) TOFILE(*CURLIB/AFLSPF) MBR(&NAVN)
             OVRDBF     FILE(OUTPUT) TOFILE(*CURLIB/AFLOPF) +
                          MBR(&NAVN)
             FTP        RMTSYS(&HOST)
             DLTOVR     FILE(INPUT)
             DLTOVR     FILE(OUTPUT)
/*  Kopierer liste til eget member.                              */
             CPYF       FROMFILE(*CURLIB/LSOUTPUT) +
                          TOFILE(*CURLIB/AFLFPF) FROMMBR(LSOUTPUT) +
                          TOMBR(&NAVN) MBROPT(*REPLACE) +
                          RCDFMT(*ONLY) FMTOPT(*CVTSRC)
             MONMSG     MSGID(CPF2817) EXEC(DO)
/* Hvis det ikke fantes noen filer i liste. Dvs, fra-member=*blank.. */
             ADDPFM     FILE(*CURLIB/AFLFPF) MBR(&NAVN)
             MONMSG     MSGID(CPF7306) EXEC(DO)
             CLRPFM     FILE(*CURLIB/AFLFPF) MBR(&NAVN)
             ENDDO
             ENDDO
/*  Kaller opp RPG som lager parameterfil for detaljert GET          */
             CHGVAR     VAR(&STATUS) VALUE(' ')
             CALL       PGM(AF706R) PARM(&NAVN &STATUS &KONVIN +
                                          &KONVINL &KONVUT &KONVUTL +
                                          &HOST &XNAVN)

/*  Sjekke om alt gikk bra med RPG. Hvis ikke så avslutt.           */
             IF         COND(&STATUS *eq 'N') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
             IF         COND(&STATUS *eq 'A') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO

             ENDDO
             ELSE       CMD(DO)
/*  Kaller opp RPG som lager parameterfil, og som returnerer host */
             CHGVAR     VAR(&STATUS) VALUE(' ')
             CALL       PGM(AF700R) PARM(&NAVN &STATUS &KONVIN +
                                          &KONVINL &KONVUT &KONVUTL +
                                          &HOST &XNAVN)

/*  Sjekke om alt gikk bra med RPG. Hvis ikke så avslutt.           */
             IF         COND(&STATUS *eq 'N') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
             IF         COND(&STATUS *eq 'A') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
/*  Legger til dato og klokkeslett i loggfilen                      */
             CALL       PGM(AF711R)
             ENDDO

/*  Gjennomfører FTP kall                           */
             OVRDBF     FILE(INPUT) TOFILE(*CURLIB/AFPAPF) MBR(&NAVN)
             OVRDBF     FILE(OUTPUT) TOFILE(*CURLIB/AFLOPF) +
                          MBR(&NAVN)
/*  Sjekker om det skal være med konverteringstabeller.             */
             IF         COND(&KONVUTL *NE '          ' *AND &KONVINL +
                          *NE '          ') THEN(DO)
             FTP        RMTSYS(&HOST) TBLFTPOUT(&KONVUTL/&KONVUT) +
                          TBLFTPIN(&KONVINL/&KONVIN)
             ENDDO
             ELSE       CMD(DO)
             FTP        RMTSYS(&HOST)
             ENDDO
             DLTOVR     FILE(INPUT)
             DLTOVR     FILE(OUTPUT)

/*  Sjekker loggen for å finne evt. feil           */
             CALL       PGM(AF704R) PARM(&NAVN &STATUS)
             CALL       PGM(AF720R) PARM(&NAVN &KOMM &STATUS)

/*  Hoppe ut hvis noe er galt                                             */
             IF         COND(&STATUS *eq 'E') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
             IF         COND(&STATUS *eq 'N') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO

/*  Kjører kopiering av filer inn til felles member (hvis GET og til-      */
/*  navn er spesifisert.                                                   */
             CHKOBJ     OBJ(*CURLIB/AFLFPF) OBJTYPE(*FILE) MBR(&NAVN)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             CALL       PGM(AF708R) PARM(&NAVN '          ' +
                          '          ' &STATUS)
             IF         COND(&STATUS *EQ 'E') THEN(CHGVAR +
                          VAR(&STATUS) VALUE(' '))

/* Sjekke om jobben skal sette i gang flere jobber...             */
             CHGVAR     VAR(&STATUS3) VALUE(' ')
             CHGVAR     VAR(&NXTFTP) VALUE('          ')
             CALL       PGM(AF710R) PARM(&NAVN &NXTFTP &STATUS3)
             IF         COND(&NXTFTP *NE '          ') THEN(DO)
             CALL       PGM(AF700C) PARM(&NXTFTP ' ' '          +
                          ' &SHWMSG)
             ENDDO

END:
             DLTOVR     FILE(AFPAPF)
             DLTOVR     FILE(AFLOPF)
             DLTOVR     FILE(AFLSPF)
             DLTOVR     FILE(AFLFPF)
/*  Frigjør overføringen ved å sette status til *blank.             */
             CHGVAR     VAR(&STATUS2) VALUE(' ')
             CALL       PGM(AF703R) PARM(&NAVN &STATUS2)
             IF         COND(&STATUS2 *eq 'N') THEN(DO)
             CHGVAR     VAR(&STATUS) VALUE(&STATUS2)
             ENDDO
SLUTT2:
/* Sjekker forskjellige feilsituasjoner, og returnerer passende beskjed */
/* til brukeren.                                                        */
             IF         COND(&SHWMSG *eq '1') THEN(DO)
             CHGVAR     VAR(&MLD1) VALUE('Overføring ' *BCAT &NAVN +
                          *TCAT ' fullført.')
             IF         COND(&STATUS *eq 'N') THEN(DO)
             CHGVAR     VAR(&MLD1) VALUE('Overføring ' *BCAT &NAVN +
                          *TCAT ' finnes ikke.')
             CALL       PGM(AA005R) PARM(&MLD1)
             GOTO       CMDLBL(SLUTT3)
             ENDDO
             IF         COND(&STATUS *eq 'L') THEN(DO)
             CHGVAR     VAR(&MLD1) VALUE('Overføring ' *BCAT &NAVN +
                          *TCAT ' allerede i gang.')
             CALL       PGM(AA005R) PARM(&MLD1)
             GOTO       CMDLBL(SLUTT3)
             ENDDO

/*6.30 Hvis inegn filer funnet, gi melding og avslutt                     */

/*6.30*/     IF         COND(&STATUS *eq 'F') THEN(DO)
/*8.02  Fjerne flagg om at rutine er aktiv                        */
/*8.02*/        chgvar     var(&kode)   value(07)
/*8.02*/        chgvar     var(&onof)   value(0)
/*8.02*/        call       pgm(FA720R)  parm(&kode &onof)
/*6.30*/     CHGVAR     VAR(&MLD1) VALUE('Ingen filer funnet ved +
/*6.30*/                overføring.')
/*6.30*/     CALL       PGM(AA005R) PARM(&MLD1)
/*6.30*/     GOTO       CMDLBL(SLUTT3)
/*6.30*/     ENDDO

             IF         COND(&STATUS *eq 'E') THEN(DO)
/*8.02  Fjerne flagg om at rutine er aktiv                        */
/*8.02*/        chgvar     var(&kode)   value(07)
/*8.02*/        chgvar     var(&onof)   value(0)
/*8.02*/        call       pgm(FA720R)  parm(&kode &onof)
             CHGVAR     VAR(&MLD1) VALUE('Noe gikk galt i overføring +
                          ' *BCAT &NAVN *TCAT '.')
             CHGVAR     VAR(&MLD2) VALUE('Vil du se logg?')
             CHGVAR     VAR(&TSTBIT) VALUE('N')
             CALL       PGM(AA007R) PARM(&MLD1 &MLD2 &TSTBIT 10)
             IF         COND(&TSTBIT *eq 'J') THEN(DO)
             CALL       PGM(AF701C) PARM(&NAVN)
             ENDDO
             GOTO       CMDLBL(SLUTT3)
             ENDDO
             IF         COND(&STATUS *eq 'A') THEN(DO)
/*8.02  Fjerne flagg om at rutine er aktiv                        */
/*8.02*/        chgvar     var(&kode)   value(07)
/*8.02*/        chgvar     var(&onof)   value(0)
/*8.02*/        call       pgm(FA720R)  parm(&kode &onof)
             CHGVAR     VAR(&MLD1) VALUE('Overføring ' *BCAT &NAVN +
                          *TCAT ' avbrutt.')
             CALL       PGM(AA005R) PARM(&MLD1)
             GOTO       CMDLBL(SLUTT3)
             ENDDO
             IF         COND(&NAVN *EQ 'LOGIQFAKT') THEN(DO) /*6.31*/
             IF         COND(&STATUS *EQ ' ') THEN(DO)       /*6.31*/
             GOTO       CMDLBL(SLUTT3)                       /*6.31*/
             ENDDO                                           /*6.31*/
             ENDDO
/*8.01*/     IF         COND(&STATUS *ne ' ') THEN(DO)
             CALL       PGM(AA005R) PARM(&MLD1)
/*8.01*/     ENDDO
             ENDDO

/* Avslutter programmet.                                                     */
SLUTT3:      ENDPGM
