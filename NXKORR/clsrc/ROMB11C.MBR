/*       UTLEGG  AV POSTER TIL Tripletex Butikk                     */
             PGM

             DCL        VAR(&FILN) TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(11) +
                          VALUE('/tritex/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(35) +
                          VALUE('.lib/tribut.file/tribut.mbr')
 /* 8.01*/   DCL        VAR(&nbr)      TYPE(*CHAR) LEN(6)
 /* 8.01*/   DCL        VAR(&usr)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&JOB)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&type)     TYPE(*CHAR) LEN(1)
             DCLF       FILE(*LIBL/ROM00PF) /*  8.01*/
/* -> 8.01  Finner hvilken rutine som kjøres                            */
             CALL       PGM(ROM00C) PARM(&TYPE)
/* -< 8.01  Finner hvilken rutine som kjøres                            */
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RM02PF)
             CLRPFM     FILE(BOVFUT)
             CLRPFM     FILE(BQVWPF)

/*8.01*/     if         cond(&type *eq 'B')   then(do)
/* Omkjøring  */
             CPYF       FROMFILE(BOMKPF) TOFILE(*libl/BQVWPF)  +
                          MBROPT(*ADD) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))
/*8.01*/     enddo
             if         cond(&type *ne 'B')   then(do)

             CPYF       FROMFILE(BOVFPF) TOFILE(*libl/BQVWPF)  +
                          MBROPT(*ADD) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))
/*8.01*/     enddo


/* Kaller program for danning av transaksjoner                       */

             CALL       PGM(RM003R)
             CALL       PGM(RM004R)
             CALL       PGM(RM002R)

/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/TRIBUT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(TRIBUT) RCDLEN(800) +
                          TEXT('Posteringer til Tripletex')     +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RM02PF) +
                          TOFILE(*libl/TRIBUT) FROMMBR(RM02PF) +
                           MBROPT(*REPLACE) FMTOPT(*NOCHK)

             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))

/* Starter kopiering til integret filsystem                          */

             CALL       PGM(LX763R) PARM(&FILN)
/* CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *bcat &FILN) */
   CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *tcat &FILN)
   CHGVAR VAR(&FILT) VALUE(&BAN3 *CAT &BIBL *CAT &BAN4)
             CPYTOSTMF  +
                          FROMMBR(&FILT) TOSTMF(&FILS) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT     OBJ(&FILS) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)


 END:        ENDPGM
