/*                  VISMA    BUSINESS                                */
/*9.01  20.11.18 nho Tilpasning for B29, summering av transer   */
/*8.01  01.11.22 mst Ny rutine/programnavn for danne poster til Øko  */
/*8.02  27.10.23 mst Endre  OVRDBF                                   */
             PGM

             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(7) +
                          VALUE('/visma/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(31) +
                          VALUE('.lib/vismalag.file/vismalag.mbr')
             DCL        VAR(&CO402_KEY) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CO402_V1)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&CO402_V2)  TYPE(*CHAR) LEN(2048)
             dcl        var(&fix)       type(*char) len(3)
             dcl        var(&fir)       type(*dec)  len(3 0) value(0)
             dcl        var(&lag)       type(*dec)  len(3 0) value(0)
             dcl        var(&filx)      type(*char) len(30)
             dcl        var(&i)         type(*dec)  len(3 0)
 /* 8.01*/   DCL        VAR(&nbr)      TYPE(*CHAR) LEN(6)
 /* 8.01*/   DCL        VAR(&usr)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&JOB)      TYPE(*CHAR) LEN(10)
 /* 8.01*/   DCL        VAR(&type)     TYPE(*CHAR) LEN(1)
             DCLF       FILE(*LIBL/ROM00PF) /*  8.01*/
/* -> 8.01  Finner hvilken rutine som kjøres                            */
             CALL       PGM(ROM00C) PARM(&TYPE)
/* -< 8.01  Finner hvilken rutine som kjøres                            */
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RV03PF)
             CLRPFM     FILE(LOVIPF)
             CLRPFM     FILE(LOVUPF)

             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))

/*8.01*/     if         cond(&type *eq 'B')   then(do)
/* Omkjøring                                                         */

             CPYF       FROMFILE(LOMKPF) TOFILE(*libl/LOVIPF) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
/*8.01*/     enddo
/*8.01*/     if         cond(&type *ne 'B')   then(do)

             CPYF       FROMFILE(LOVFPF) TOFILE(*libl/LOVIPF) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
/*8.01*/     enddo

/* Kaller program for danning av transaksjoner                       */

             CALL       PGM(RB023R)
             CALL       PGM(RB022R)

/*8.03*/       OVRDBF     FILE(LOVFL1) TOFILE(*LIBL/LOVIL1)
               CALL       PGM(RB226R)

/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/VISMALAG) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(VISMALAG) RCDLEN(800) +
                          TEXT('Posteringer til Visma')           +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RV03PF) +
                          TOFILE(*libl/VISMALAG) FROMMBR(RV03PF) +
                           MBROPT(*REPLACE) FMTOPT(*NOCHK)


             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))


/* Starter kopiering til integret filsystem                          */

             CALL       PGM(LO764R) PARM(&FILN)

             chgvar     var(&i)  value(%scan('.' &filn))
             if         cond(&i *gt 0)  then(do)
                chgvar     %sst(&filn &i 1)   value('_')
             enddo
/* Skal filen innehode en sufix?                                     */
             chgvar     var(&co402_key) value('EKSTERN_REGNSKAP_SUFIX')
             rtvdtaara  dtaara(*lda (944 3)) rtnvar(&fix)
             chgvar     var(&fir)   value(&fix)
             call       pgm(co402r) parm(&filg &fir &lag &co402_key +
                          &co402_v1 &co402_v2)

             chgvar     var(&filx) value(&filn)
             if         cond(&co402_v1 *eq '1') then(do)
                chgvar     var(&filx) value(&filn *tcat &co402_v2)
             enddo

             CHGVAR     VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *tcat +
                          &FILx)
             CHGVAR     VAR(&FILT) VALUE(&BAN3 *CAT &BIBL *CAT &BAN4)

             CPYTOSTMF  +
                          FROMMBR(&FILT) TOSTMF(&FILS) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT      OBJ(&FILS) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)


 END:        ENDPGM
