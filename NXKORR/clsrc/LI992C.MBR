             PGM        parm(&filg)
/*-------------------------------------------------------------------*/
/* Ref   Dato  Sgn Beskrivelse                                       */
/* 8.01 080925 bsa Endret navn på jobben som legges i jobbkalender.  */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/

             DCL        VAR(&filg)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&jobd)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&jobq)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&sbsd)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&job)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&user)     TYPE(*CHAR) LEN(10)

             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&bibl)     TYPE(*char) LEN(10)

             chgvar     var(&sbsd) value('ASBS' *cat &filg *cat 'EDI')
             chgvar     var(&jobq) value('JOBQ' *cat &filg *cat 'EO')
             chgvar     var(&jobd) value('JOBD' *cat &filg *cat 'EDI')
             chgvar     var(&bibl) value('ASKUND_' *cat &filg)
             chgvar     var(&user) value('ASP_' *cat &filg)


/* Sjekker om subsystem allerede er i gang og varsler                 */

 TESTSUB:    STRSBS     SBSD(aspgpl/&sbsd)

             MONMSG     MSGID(CPF1010) EXEC(DO)
             CHGVAR     VAR(&SVAR) VALUE(' ')
             CHGVAR     VAR(&MLD1) +
                        VALUE('Subsystem allerede i gang, vil bli   ')
             CHGVAR     VAR(&MLD2) +
                        VALUE('avsluttet og restartet. Er dette ok?')
             CALL       PGM(AA007R)    PARM(&MLD1 &MLD2 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))
             ENDDO

             MONMSG     MSGID(CPF1013) EXEC(DO)
             CRTSBSD    SBSD(aspgpl/&sbsd) POOLS((1 *BASE)) +
                          MAXJOBS(3) TEXT('Subsystem for EDI impo+
                          rt')
             GOTO CMDLBL(VIDERE)
             ENDDO
/*           MONMSG     MSGID(CPF1696)               */
/*           STRSBS     SBSD(aspgpl/&sbsd)           */

 VIDERE:     ENDSBS     SBS(&SBSD) OPTION(*IMMED)
             MONMSG     MSGID(CPF0000)
 /*          DLTSBSD    SBSD(&sbsd)             */
 /*          MONMSG     MSGID(CPF0000)          */

/* Lag jobbkø                                                       */

             DLTJOBQ    JOBQ(aspgpl/&jobq)
             MONMSG     MSGID(CPF0000)
             CRTJOBQ    JOBQ(aspgpl/&jobq) TEXT('Jobbkø for +
                          innhenting av EDI ordrebekreftelse')

/*           MONMSG     MSGID(CPF3323)                */
/*           DLTCLS     CLS(*LIBL/QBATCH)             */
/*           MONMSG     MSGID(CPF0000)                */
/*           CRTCLS     CLS(QGPL/QBATCH)              */

/* Legge jobbkø inn i subsystem                       */
             ADDJOBQE   SBSD(ASPGPL/&SBSD) JOBQ(ASPGPL/&JOBQ) +
                          MAXACT(1) SEQNBR(20)
             MONMSG     MSGID(CPF1535 CPF1697)

             ADDRTGE    SBSD(aspgpl/&sbsd) SEQNBR(20) +
                          CMPVAL(*ANY) PGM(QSYS/QCMD) +
                          CLS(*LIBL/QBATCH)
/* Lag jobbeskrivelse                                               */

             MONMSG     MSGID(CPF0000)
             CRTJOBD    JOBD(aspgpl/&jobd) JOBQ(aspgpl/&jobq) +
                          JOBPTY(4) TEXT('Jobb beskrivelse for +
                          innhenting av EDI')
             MONMSG     MSGID(CPF1621)

/* Legger jobben ut i jobbkalender                                    */

/*           CHGVAR     VAR(&JOB) VALUE('EDI_OB_' *CAT &filg)         */
/*8.01*/     CHGVAR     VAR(&JOB) VALUE(&filg *cat '_EDI_OB')
             ADDJOBSCDE JOB(&JOB) CMD(CALL PGM(AABATCH) PARM((&filg) +
                          ('LI700C_JOR') ('1'))) FRQ(*WEEKLY) +
                          SCDDATE(*NONE) SCDDAY(*ALL) +
                          SCDTIME(070200) JOBD(ASPGPL/&JOBD) +
                          JOBQ(ASPGPL/&JOBQ) USER(&USER) +
                          TEXT('Innhenting av EDI Ordrebekreftelse')

             STRSBS     SBSD(aspgpl/&sbsd)

 ENDPGM:     ENDPGM
