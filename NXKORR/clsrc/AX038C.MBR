
/*-------------------------------------------------------------------*/
/* Det spesielle med dette programmet er "dclprcopt  USRPRF(*OWNER)" */
/* Da blir alltid brukerprofil = *OWNER. Det betyr at den kjører med */
/* rettighetene til eieren av programmet. MEN det må gjøres manuelt, */
/* for som regel er det grupeprofilen som blir eier ved kompilering. */
/* Den har ikke de  nødvendige rettigheter til CRTUSRPRF/CHGUSRPRF   */
/* kommandoene. Velg en ASP_xxx bruker, da denne har alle rettigheter*/
/* 210725 bsa                                                        */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/
/* bkv: SGR bruker ikke guppeprofil, og kan derfor ikke ha dette prg */

             PGM        PARM(&USER)
             DCL        VAR(&JOBB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&IQUS)     TYPE(*CHAR) LEN(10) +
                                       VALUE(IQ)
             DCL        VAR(&TEXT)     TYPE(*CHAR) LEN(30) +
                                       VALUE('IQ-program for')
             DCL        VAR(&ASPB)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&ASP2)     TYPE(*CHAR) LEN(1)

/* Må kjøres med spesielle rettigheter                               */

             dclprcopt  USRPRF(*OWNER)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR))
             RTVJOBA    JOB(&JOBB)

/* Bygge opp variabler                                               */

             CHGVAR     VAR(%SST(&IQUS 03 8)) VALUE(&USER)
             CHGVAR     VAR(%SST(&IQUS 10 1)) VALUE('Q')
             CHGVAR     VAR(%SST(&TEXT 16 8)) VALUE(&USER)

/* Sjekke om dette er en ASP_ bruker                                 */

             CHGVAR     VAR(&ASPB)     VALUE(%SST(&USER 01 4))
             IF         COND(&ASPB *NE 'ASP_') THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO

/* Sjekke om dette er en ASP_ bruker, MEN ikke G eller T bruker      */
/*  feks skal ikke ASP_700G ha et IQ program                         */

             CHGVAR     VAR(&ASP2)     VALUE(%SST(&USER 08 1))
             IF         COND(&ASP2 *NE ' ') THEN(DO)
                GOTO       CMDLBL(END)
             ENDDO

/* Sjekk om IQ-program og kilde finnes fra før                       */

             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&IQUS) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(SJK_OBJ))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&IQUS)

 SJK_OBJ:    CHKOBJ     OBJ(ASPGPLK/&IQUS) OBJTYPE(*PGM)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(START))
             DLTPGM     PGM(ASPGPLK/&IQUS)
             MONMSG     MSGID(CPF2105)

/* Bygge opp Cl-member                                               */

 START:      ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&IQUS) +
                          TEXT(&TEXT) SRCTYPE(CLLE)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&IQUS)

/* Call til RPG-program                                              */

             CALL       PGM(AX038R) PARM(&USER)
             DLTOVR     FILE(QCLLESRC)

/* Create program                                                    */

             CRTBNDCL   PGM(ASPGPLK/&IQUS) SRCFILE(ASPGPLK/QCLLESRC) +
                          TEXT(&TEXT)

             GOTO       CMDLBL(END)
 ERR:        SNDPGMMSG  MSG('Det oppstod en feil, se melding +
                          i jobbloggen !!!') TOMSGQ(&JOBB)

 END:        ENDPGM
