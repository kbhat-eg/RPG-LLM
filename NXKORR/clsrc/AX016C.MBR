             PGM
             DCL        VAR(&JOBB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB1)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB2)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&SW12)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&LEVL)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&OKOK)     TYPE(*CHAR) LEN(2)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR))
             RTVJOBA    JOB(&JOBB)

/* Kontrollere brukertilgang                                         */

             CHGVAR     VAR(&LEVL)     VALUE('99')
             CHGVAR     VAR(&OKOK)     VALUE('  ')
             CALL       PGM(AX050C)    PARM(&LEVL &OKOK)
             IF         COND(&OKOK *NE 'OK') THEN(DO)
             CALL       PGM(AA004C)
             GOTO       CMDLBL(END)
             ENDDO

/* Ta vare på det databibliotek du står i.                           */

             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&LIB1)

/* Call til valg av file-gruppe.                                     */

             CALL       PGM(AA001R)    PARM(&USER)

/* Hente inn fra LDA                                                 */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&LIB2)
             RTVDTAARA  DTAARA(*LDA (984 1))  RTNVAR(&SW12)

/* Endre biblioteksliste                                             */

             IF         COND(&LIB2 *NE ' ') THEN(DO)
             CHGCURLIB  CURLIB(&LIB2)
             ENDDO

/* Call til program for vedlikehold                                  */
/* av skjerm-register i den file-gruppe                              */
/* som er valgt.                                                     */

             IF         COND(&SW12 *NE '1') THEN(DO)
             CALL       PGM(AX016R)
             ENDDO

/* Legge tilbake opprinnelig biblioteksliste                         */

             IF         COND(&LIB1 *NE ' ') THEN(DO)
             CHGCURLIB  CURLIB(&LIB1)
             CHGDTAARA  DTAARA(*LDA (934 10)) VALUE(&LIB1)
             ENDDO

             GOTO       CMDLBL(END)
 ERR:        SNDPGMMSG  MSG('Det oppstod en feil, se melding +
                          i jobbloggen !!!') TOMSGQ(&JOBB)

 END:        ENDPGM
