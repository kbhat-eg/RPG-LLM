     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX500R
      * Beskrivelse . . : Vare-pris, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210900 HKO  Nytt program
      *       310505 HKO  Viser cross-docking
6.30  *       160714 bof  utvidet nøkkel med fra dato
6.31  *       280915 bof  Hvis vare-leverandør utgått, vis som rød
6.32  *       270117 bof  Utgåtte varer skal ikke vises.
6.33  *       121118 bof  Utvidet med nobbnr pga. trelast-sortiment
6.33  *                   Skal heller vise utgåtte priser
7.xx  * 7.00  130916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  230321 bsa  Takler brede skjermbilder.
7.02  * 7.00  200921 bof  utvidet parm med valgt prisdato
      *
8.01  *K0000  270223 bsa  Legger ut varenummer og varetekst.
8.02  *K0000  210519 bsa  Filtrerer på prioritert leverandør.
8.03  *K0000  080925 bof  Forhindre at gamle priser uten til dato vises
8.04  *K0000  031125 bof  Feil-retting 8.03 må ha med leverandør i sql
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.32 fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
8.01 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fjx500d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(jxvare)
     d p_ldor          s                   like(jxldor)
     d p_lnav          s                   like(jlnavn)
     d p_pris          s                   like(jxpris)
7.02 d p_gdat          s                   like(jxgdat)
8.02 d p_irec          s             80
8.02 d p_orec          s             80
8.02  *
8.02  * Datastruktur parameterliste -inn
8.02  *
8.02 d                 ds
8.02 d hirec                         80
8.02 d  hilage                        2  0 overlay(hirec)
8.02 d  hivare                       15    overlay(hirec:3)
8.02  *
8.02  * Datastruktur parameterliste -ut
8.02  *
8.02 d                 ds
8.02 d horec                         80
8.02 d  hopr1l                        6  0 overlay(horec)
8.02 d  hopr2l                        6  0 overlay(horec:7)
8.02 d  hopr3l                        6  0 overlay(horec:13)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvprl1_ldor     s                   like(jxldor)
     d jlevl1_ldor     s                   like(jlldor)
6.32 d jvlelr_lldo     s                   like(jvlldo)
6.32 d jvlelr_lvnr     s                   like(jvlvnr)
8.01 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
6.33 d b_logi          s              1    inz(*off)
8.02 d u_pril          s              1    inz(*off)
      *
     d w_vare          s                   like(jxvare)
6.31 d w_udat          s                   like(jxodat)
8.03 d w_gdat_max      s                   like(jxodat)
6.33 d w_nobb          s              8  0
6.33 d w_anta          s              4  0
6.33 d w_firm          s              3  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
8.02 d c_pril          s             30    inz('BRUK_PRIORITERT_LEVERANDØR_F8')
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
8.02 d l_lage               1001   1002  0
8.02  *
8.02  * Definering av eksterne prg
8.02  *
8.02   dcl-s co402_verdi1  char(1);
8.02   dcl-s co402_verdi2  char(2048);
8.02   DCL-PR CO402R EXTPGM('CO402R');
8.02     p_filgrp            char(3)     const;
8.02     p_firm              packed(3:0) const;
8.02     p_lager             packed(2:0) const;
8.02     p_nokkel            char(50)    const;
8.02     p_verdi1            char(1);
8.02     p_verdi2            char(2048);
8.02   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
8.02 c                   when      *inkg
8.02 c                   exsr      vis_prio
8.02 c                   exsr      forny
8.02 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jvprl1_ldor = b1ldor
     c     jvprl1_key    setll     jvprl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jvprl1_ldor = b2ldor
     c     jvprl1_key    setll     jvprl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_ldor = b1ldor
     c                   eval      p_lnav = b1lnav
     c                   eval      p_pris = b1pris
7.02 c                   eval      p_gdat = b1gdati
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvprl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jxvare <> w_vare
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
8.02  * Hvis vi kjører på sjekk med prioritert leverandør
8.02 c                   if        u_pril = *on
8.02  *
8.02 c                   if        *in40 = *on
8.02 c                   if        hopr1l <> 0 and
8.02 c                             hopr1l <> jxldor and
8.02 c                             hopr2l <> 0 and
8.02 c                             hopr2l <> jxldor and
8.02 c                             hopr3l <> 0 and
8.02 c                             hopr3l <> jxldor
8.02 c                   iter
8.02 c                   endif
8.02 c                   endif
8.02  *
8.02 c                   endif
6.32  *
6.32  * sjekker om vare-leverandør er ugfått
6.32 c                   eval      jvlelr_lvnr = jxvare
6.32 c                   eval      jvlelr_lldo = jxldor
6.32 c     jvlelr_key    chain     jvlelr
6.32 c                   if        %found(jvlelr)  and
6.32 c                             jvluda <> *loval   and
6.32 c                             jvluda < w_udat
6.32 c                   iter
6.32 c                   endif
6.32 c                   if        not %found(jvlelr)
6.32 c                   iter
6.32 c                   endif
6.33 c                   eval      *in50 = *off
6.33 c                   if        jxtdat <> *loval and
6.33 c                             jxtdat < w_udat
6.33 c                   iter
6.33 c                   endif
8.03  * pris er gammel og mangler tildato, skal ikke vises
8.03 c/exec sql
8.03 c+ select max(jxgdat) into :w_gdat_max
8.04 c+ from jvprpf where jxvare = :p_vare  and jxldor = :jxldor
8.04 c+ and jxtdat = '0001-01-01'
8.03 c/end-exec
8.03 c                   if        jxtdat = *loval and
8.03 c                             jxgdat < w_udat and
8.03 c                             jxgdat < w_gdat_max
8.03 c                   iter
8.03 c                   endif
      *
6.33 c                   eval      w_nobb = 0
6.33  * hvis logivare - hent nobb-nr fra undervarens nobbnr
6.33 c                   if        b_logi = *on
6.33 c/exec sql
6.33 c+ select jvquno into :w_nobb
6.33 c+ from jvklpf where jvqfir = :w_firm and
6.33 c+                   jvqvnr = :p_vare  and
6.33 c+                   jvqldo = :jxldor
6.33 c/end-exec
6.33 c                   else
6.33 c/exec sql
6.33 c+ select jvnobb into :w_nobb
6.33 c+ from jvarpf where jvvare = :p_vare
6.33 c/end-exec
6.33 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   eval      b1ldor = jxldor                                           .
6.33 c                   eval      b1nobb = w_nobb                                           .
     c                   eval      b1pris = jxpris                                           .
     c                   eval      b1crdo = jxcrdo
     c                   eval      b1gdat = 0
7.02 c                   eval      b1gdati= *loval
      *
     c                   if        jxgdat <> *loval
     c     *dmy          move      jxgdat        b1gdat
7.02 c                   eval      b1gdati = jxgdat
     c                   endif
      *
      * Henter leverandørnavn
      *
     c                   eval      jlevl1_ldor = jxldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1lnav = jlnavn
     c                   else
     c                   eval      b1lnav = *blank
     c                   endif
      *
      * Merker rader som er opprettet lokalt
      *
     c                   if        jxrsts = 1
     c                   eval      b1rkod = 'L'
     c                   else
     c                   eval      b1rkod = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jvprl1_key    setgt     jvprl1
     c                   readp     jvprl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jvprl1
      *
     c                   if        %eof or
     c                             jxvare <> w_vare
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   enddo
      *
     c                   if        %eof
     c     jvprl1_key    setll     jvprl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for vise eller ikke vise prioriterte leverandører
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     vis_prio      begsr
8.02  *
8.02 c                   if        u_pril = *on
8.02  *
8.02 c                   if        *in40 = *on
7.02 c                   eval      *in40 = *off
8.02 c                   else
8.02 c                   eval      *in40 = *on
8.02 c                   endif
8.02  *
8.02 c                   eval      b1ldor = 0
8.02 c                   eval      w_fcrn = 0
8.02  *
8.02 c                   endif
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_ldor
     c                   parm                    p_lnav
     c                   parm                    p_pris
7.02 c                   parm                    p_gdat
      *
      * Key for les på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.39  *
6.39  * Key for les på vare-leverandør
6.39  *
6.39 c     jvlelr_key    klist
6.39 c                   kfld                    jvlelr_lvnr
6.39 c                   kfld                    jvlelr_lldo
8.01  *
8.01  * Key for oppslag på vare
8.01  *
8.01 c     jvarl1_key    klist
8.01 c                   kfld                    jvarl1_vare
      *
      *
      * Henter vare fra parameter
      *
     c                   eval      w_vare = p_vare
6.33 c                   eval      w_firm = l_firm
8.02  *
8.02  * Sjekk om tilpasning for bruk av prioriterte leverandører skal benyttes
8.02  *
8.02      *in40 = *off;
8.02      u_pril = *off;
8.02      Callp co402r(l_filg:w_firm:0:c_pril:
8.02                   co402_verdi1:co402_verdi2);
8.02      if co402_verdi1 = '1';
8.02         u_pril = *on;
8.02         *in40 = *on;
8.02      endif;
8.02  *
6.33  * sjekke om det er trelast-vare
6.33 c/exec sql
6.33 c+ select COUNT(*) into :w_anta from jvkhpf
6.33 c+ where jvkfir = :w_firm and jvkvnr = :p_vare
6.33 c/end-exec
6.33 c                   eval      b_logi = *off
6.33 c                   if        w_anta > 0
6.33 c                   eval      b_logi = *on
6.33 c                   endif
      *
8.01 c                   eval      b1vare = *blanks
8.01 c                   eval      b1tek1 = *blanks
8.01 c                   eval      b1tek2 = *blanks
8.01 c                   eval      jvarl1_vare = p_vare
8.01 c     jvarl1_key    chain     jvarl1
8.01 c                   if        %found
8.01 c                   eval      b1vare = jvvare
8.01 c                   eval      b1tek1 = jvtek1
8.01 c                   eval      b1tek2 = jvtek2
8.01 c                   endif
      *
      * Posisjonerer i databasen, og oppretter subfile
      *
6.31 c                   time                    w_udat
     c                   eval      *in22 = *on
8.02  *
8.02  * Hent evnt prioriterte leverandører
8.02  *
8.02 c                   if        u_pril = *on
8.02 c                   eval      hilage = l_lage
8.02 c                   eval      hivare = p_vare
8.02 c                   eval      p_irec = hirec
8.02 c                   call      'JV164R'
8.02 c                   parm                    p_irec
8.02 c                   parm                    p_orec
8.02 c                   eval      horec = p_orec
8.02 c                   endif
      *
     c     jvprl1_key    setll     jvprl1                                                    .
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
