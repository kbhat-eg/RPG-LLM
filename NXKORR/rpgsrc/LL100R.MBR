     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LL100R
      * Beskrivelse . . : Lager, vedlikehold
      *
 7.01 * Spesialversjon. : Overføre lagerdata til annen vare
 7.01 * Tilpasset for . : Gipling
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.30  *       090904 bø   Utvidet subfile med min.lager,b.mengde,l.tid
5.51  * 5.51  190508 tsv  Vise/redigere abc-kode på lager
5.60  *       050408 bø   Lagerenhet fra vare-register
5.61  *       080808 bø   Kode for om abc kode er låst
5.70  * PRGR  211008 bø   Kode for varetype
5.62  *       050109 bø   Justert skriv til sp.progn. ved kopiering
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  201109 bsa  Viser feil tekst i vindy ved sletting av lagerposter
6.12  * 6.10  050210 bsa  Gir mulighet for filtrering på lager
6.13  * 6.10  041010 bof  Gir melding hvis vtyp endres fra L->S
6.13  * 6.10              Danner også justering av lager til 0
6.14  * 6.10  111110 bof  Sjekker mot et lager vedr. endr. av varetype
6.15  * 6.10  100311 bof  hvis L-->S, så nullstill hittil i år tall
6.16  * 6.10  060411 AMD  Lagt inn korreksjon for å unngå timestamp
      *                   melding dersom dato var 0001-01-01
6.20  * 6.20  221110 bof  Utvidet med leverandør (sentrallager-løsning)
6.21  * 6.20  231110 bof  Utvidet med utgått dato pr. lager
6.22  * 6.20  051210 bof  Hindre feilm. etter spørring på vare
6.23  * 6.20  070211 nho  Byttet ut fil mpspxx med vlagxx
6.24  * 6.20  260511 bsa  Utvider filtermuligheter.
6.25  * 6.20  270511 bsa  Overgang til program med beholdning
6.26  * 6.20  270511 bsa  Varsel og logg ved endring av varetype S->L
6.27  * 6.20  030611 bof  Lagt ut vtyp i subfile + start på varenr
6.27  * 6.20              ved kall fra andre program
6.28  * 6.20  110811 bsa  Utvider filtermuligheter. Lagerplass og ABC kode (telling)
6.29  * 6.20  101111 bof  Generell opprydding i programmet
6.2a  * 6.20  220212 bof  Tatt bort mulighet for sletting
6.2b  * 6.20  010312 bsa  Hvis leverandør er angitt, starter søk på leverandør
6.2c  * 6.20  070312 bsa  Rydder i filerfunksjon
6.2d  * 6.20  080312 bof  Rettet 6.2b - skulle vært å hente lev.navn
6.2e  * 6.20  080312 bof  Viser også hoved.lev.nr, litt mer testing.
6.2f  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
6.2g  * 6.20  241013 nho  Får ikke opprette varetype dersom varetype
      *                   er D eller T i vvarpf
6.2h  * 6.20  200115 bsa  Sjekker at til lager er gyldig ved kopiering
6.nu  * 6.30  050614 bof  Endring iforb. nummerutvidelse
6.31  * 6.30  040615 bsa  Endret ledetekst på skjermbildet
6.32  * 6.30  230615 bof  ved utfylling av prio.lev, test om pris evt.distr.
6.33  * 6.30  230615 bof  6.2g utgår, D og T skal også ligge i VLAGPF
6.34  * 6.30  080715 ask  Legt inn beskrivelse på lager i C2 bilde
6.35  * 6.30  241116 bof  Lev.nr filter skal gjelde mot levnr på lager
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       191116 bof  Overføre lagerdata til annen vare
7.01  *                   Spesial fra Gipling.
7.02  *       200217 bof  Skal ikke ha test på prio.lev. 6.32
7.02  *                   Spesial fra Gipling.
7.03  *       250917 bof  Utvidet med prioritert leverandør VLBLDO
7.03  *                   Spesial fra Gipling.
7.04  *       301018 bsa  Endret ledeteksten på Bestillingspunkt og mengde.
7.04  *                   Spesial fra Gipling.
7.07  * (7.04)210317 bof  Utvidet med fler lager i filter
7.08  * (7.01)  220416 ask  Lager fra pålogget bruker og brukes i filter
      *
8.00  * 8.00  200922 sst  Overgang til vedlikehold av MalProff-transer
8.01  * 8.00  171022 bsa  Nye felter med RDS informasjon
8.02  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.03  *       270525 bof  Switch-styre om pålogget bruker skal brukes i filter
8.04  *K0000  100725 bsa  Når du velger prioritert leverandør, kun velg blandt
8.04  *K0000              de som gir pris på varen.
8.05  *K0000  100725 bsa  Sjekk at prioritert leverandør er prisgiver på varen.
8.05  *K0000              Styres av switch.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlagl2    if   e           k disk    rename(vlagpfr:vlagl2r)
     fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.32 fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
6.32 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
6.13 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.2b frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.2h fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
7.08 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
8.01 fvla2iu    uf a e           k disk    rename(vla2st:vla2iur)
8.01 fvla2i1    if   e           k disk    rename(vla2st:vla2i1r)
      *
     fll100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
7.04  *
7.04  * Definering av array's
7.04  *
7.04 d a_txt           s             30    dim(4) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.04 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
6.13  *
6.13  * Datastruktur for lageroppdatering
6.13  *
6.13 d                 ds
6.nu d hlrec                        128
6.13 d  hlvare                       15    overlay(hlrec)
6.13 d  hllage                        2  0 overlay(hlrec:16)
6.13 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
6.13 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
6.13 d  hlotyp                        2    overlay(hlrec:36)
6.13 d  hlltyp                        2    overlay(hlrec:38)
6.13 d  hlanta                       11  3 overlay(hlrec:40)
6.13 d  hlkopr                        9  2 overlay(hlrec:51)
6.13 d  hlrefr                       20    overlay(hlrec:60)
6.13 d  hlsyst                        1    overlay(hlrec:80)
6.13 d  hlaltk                        2    overlay(hlrec:81)
6.13 d  hlrest                        1    overlay(hlrec:83)
6.13 d  hltype                        1    overlay(hlrec:84)
6.13 d  hlenhe                        3    overlay(hlrec:85)
6.13 d  hlinlr                        1    overlay(hlrec:88)
6.13 d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
     d vlagl2_lage     s                   like(vllage)
     d vlaglr_vare     s                   like(vlvare)
     d vlaglr_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_vare     s                   like(vvvare)
6.32 d vvprl1_vare     s                   like(vpvare)
6.32 d vvprl1_ldor     s                   like(vpldor)
6.32 d lldil1_ldor     s                   like(lpldor)
5.70 d ra30l1_dkod     s                   like(redkod)
6.2b d rlevl1_levr     s                   like(rllevr)
6.2h d ra10l1_jkod     s                   like(rajkod)
7.08 d ausrl1_user     s                   like(abuser)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.13 d b_lagr          s              1    inz(*off)
6.13 d b_endr_vtyp     s              1    inz(*off)
6.13 d b_inlr          s              1    inz(*off)
7.07 d b_lage          s              1    inz(*off)
      *
     d w_firm          s                   like(vlfirm)
     d w_lage          s                   like(vllage)
     d w_loka          s                   like(vlplas)
     d w_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
     d w_ltek          s             30
     d w_lbes          s             30
     d w_dtxt          s                   like(redtxt)
      *
7.01 d w_mld1          s             50
7.01 d w_plas          s                   like(vlplas)
7.01 d w_pla1          s                   like(vlpla1)
7.01 d w_pla2          s                   like(vlpla2)
7.01 d w_mini          s                   like(vlmini)
7.01 d w_maks          s                   like(vlmaks)
7.01 d w_bmin          s                   like(vlbmin)
7.01 d w_bpun          s                   like(vlbpun)
7.01 d w_bmen          s                   like(vlbmen)
7.01 d w_ltid          s                   like(vlltid)
7.01 d w_kabc          s                   like(vlkabc)
7.01 d w_labc          s                   like(vllabc)
7.01 d w_vtyp          s                   like(vlvtyp)
7.01 d w_tabc          s                   like(vltabc)
7.03 d w_bldo          s                   like(vlbldo)
8.00  *
8.00 d w_tilgko        s             10
8.00 d w_rutine        s             10
8.00 d w_adv           s              3
8.00 d w_status        s             10
      *
     d s_vare          s                   like(vlvare)
     d s_lage          s                   like(vllage)
     d s_stda          s                   like(c2stda)
6.23 d s_sldo          s                   like(vlbldo)
6.13 d s_vtyp          s                   like(vlvtyp)
6.21 d s_suda          s                   like(c2suda)
     d w_stat          s              1
6.24 d w_ttxt          s             35
6.24 d w_odat          s               d   datfmt(*iso)
6.25 d p_vare          s                   like(vlvare)
6.27 d p_stva          s                   like(vvvare)
      *
7.04   dcl-s co402_verdi1  char(1);
7.04   dcl-s co402_verdi2  char(2048);
7.04   DCL-PR CO402R EXTPGM('CO402R');
7.04     p_filgrp            char(3)     const;
7.04     p_firm              packed(3:0) const;
7.04     p_lager             packed(2:0) const;
7.04     p_nokkel            char(50)    const;
7.04     p_verdi1            char(1);
7.04     p_verdi2            char(2048);
7.04   END-PR;
7.04 d u_bpkt          s              1    inz(*off)
8.03 d u_803           s              1    inz(*off)
8.05 d b_prislev       s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
8.00  *
8.00  * Sjekk om denne bruker har tilgang til denne F-tast
8.00  *
8.00 c                   if        *in16 = *on
8.00 c                   select
8.00 c                   when      *inka
8.00 c                   eval      w_adv    = 'F01'
8.00 c                   when      *inkf
8.00 c                   eval      w_adv    = 'F06'
8.00 c                   when      *inkh
8.00 c                   eval      w_adv    = 'F08'
8.00 c                   other
8.00 c                   goto      f_tast_ok
8.00 c                   endsl
8.00 c                   eval      w_tilgko = l_user
8.00 c                   eval      w_rutine = 'LL100R'
8.00 c                   eval      w_status = *blank
8.00 c                   call      'AD005R'
8.00 c                   parm                    w_tilgko
8.00 c                   parm                    w_rutine
8.00 c                   parm                    w_adv
8.00 c                   parm                    w_status
8.00 c                   if        w_status = 'Sperret'
8.00 c                   goto      b2tagb
8.00 c                   endif
8.00 c                   endif
8.00 c     F_tast_ok     tag
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sp_input
6.22 c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
8.00 c                   when      *inkh
8.00 c                   call      'RMP020R'
8.00 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blank or
     c                             b2lage <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vlagl2_lage = b1lage
     c     vlagl2_key    setll     vlagl2
     c                   other
     c                   eval      vlagl1_vare = b1vare
     c     vlagl1_key    setll     vlagl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vlagl1_vare = b2vare
     c     vlagl1_key    setll     vlagl1
     c                   goto      end_pos
     c                   endif
      *
      * Lager
      *
     c                   if        b2lage <> *blank
     c                   eval      w_seqe = 'B'
     c                   if        %len(%trim(b2lage)) = 1
     c                   eval      b2lage = '0' + %trim(b2lage)
     c                   endif
     c                   move      b2lage        vlagl2_lage
     c                   move      b2lage        vlagl2_lage
     c     vlagl2_key    setll     vlagl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = *blank
     c                   eval      b2lage = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vare = b1vare
     c                   eval      k1lage = b1lage
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.2a  *
6.2a  * Slett
6.2a  *
6.2a c*                  if        b1valg = 4
6.2a c*                  eval      d1vare = b1vare
6.2a c*                  eval      d1lage = b1lage
6.2a c*                  exsr      xd1win
6.2a c*                  eval      b1valg = 0
6.2a c*                  update    b1sfl
6.2a c*                  iter
6.2a c*                  endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b1vare
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.25  *
6.25  * Beholdninger
6.25  *
6.25 c                   if        b1valg = 9
6.25 c                   eval      p_vare = b1vare
6.25 c                   call      'VL750C'
6.25 c                   parm                    w_firm
6.25 c                   parm                    p_vare
6.25 c                   eval      b1valg = 0
6.25 c                   update    b1sfl
6.25 c                   iter
6.25 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
     c                   move      b2lage        c1lage
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1vare = *blank and
     c                             c1lage = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekk om gyldige verdier
      *
     c                   eval      w_vare = c1vare
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vlaglr_vare = c1vare
     c                   eval      vlaglr_lage = c1lage
     c     vlaglr_key    chain     vlaglr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
6.33 c*                  eval      *in45 = *off
6.33 c*                  eval      b_feil = *off
6.33 c*                  eval      vvarl1_vare = c1vare
6.33 c*    vvarl1_key    chain     vvarl1
6.33 c*                  if        %found
6.33 c*                  eval      w_tek1 = vvtek1
6.33 c*                  if        vvvtyp = 'D' or
6.33 c*                            vvvtyp = 'T'
6.33 c*                  eval      b_feil = *on
6.33 c*                  eval      *in45 = '1'
6.33 c*                  goto      c1taga
6.33 c*                  endif
6.33 c*                  endif
6.33  *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1vare = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1lage = 0
     c                   eval      c1ltek = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2vare = *blank
     c                   eval      b2lage = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
6.13 c                   eval      s_vtyp = *blank
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
7.04  *
7.04 c                   if        u_bpkt = *off
7.04 c                   eval      c2bptx = a_txt(1)
7.04 c                   eval      c2bmtx = a_txt(2)
7.04 c                   else
7.04 c                   eval      c2bptx = a_txt(3)
7.04 c                   eval      c2bmtx = a_txt(4)
7.04 c                   endif
      *
     c                   eval      vlaglr_vare = c1vare
     c                   eval      vlaglr_lage = c1lage
     c     vlaglr_key    chain     vlaglr
      *
     c                   if        %found
6.34 c                   eval      ra10l1_jkod = c1lage
6.34 c     ra10l1_key    chain     ra10l1
6.34 c                   if        %found
6.34 c                   eval      c2ltek = rajtxt
6.34 c                   else
6.34 c                   eval      c2ltek = *blank
6.34 c                   endif
     c                   eval      c2plas = vlplas
     c                   eval      c2pla1 = vlpla1
     c                   eval      c2pla2 = vlpla2
     c                   eval      c2mini = vlmini
     c                   eval      c2maks = vlmaks
     c                   eval      c2bmin = vlbmin
     c                   eval      c2bpun = vlbpun
     c                   eval      c2bmen = vlbmen
     c                   eval      c2ltid = vlltid
5.51 c                   eval      c2abck = vlkabc
5.61 c                   eval      c2labc = vllabc
5.70 c                   eval      c2vtyp = vlvtyp
6.13 c                   eval      s_vtyp = vlvtyp
5.61 c                   eval      c2abct = vltabc
6.16 c                   eval      c2stda = 0
6.23 c                   if        vlstda <> *loval
6.23 c     *dmy          move      vlstda        c2stda
     c                   else
     c                   eval      c2stda = 0
     c                   endif
6.23 c                   if        vludat <> *loval
6.23 c     *dmy          move      vludat        c2suda
6.21 c                   else
6.21 c                   eval      c2suda = 0
6.21 c                   endif
6.23 c                   eval      c2sldo = vlbldo
6.29 c                   if        c2sldo <> 0
6.2d c                   eval      rlevl1_levr = c2sldo
5.2d c     rlevl1_key    chain     rlevl1
5.2d c                   if        %found(rlevl1)
5.2d c                   eval      c2lnav = rlnavn
5.2d c                   else
6.2d c                   eval      c2lnav = *blank
6.2d c                   endif
6.29 c                   else
6.29 c                   eval      c2lnav = *blank
6.29 c                   endif
6.2f c                   if        vlfeda <> *loval
6.2f c     *dmy          move      vlfeda        c2edat
6.2f c                   else
6.2f c                   eval      c2edat = 0
6.2f c                   endif
6.2f c                   eval      c2eusr = vlfeus
     c                   else
6.34 c                   eval      c2ltek = c1ltek
     c                   eval      c2plas = *blank
6.29 c                   eval      c2pla1 = *blank
6.29 c                   eval      c2pla2 = *blank
     c                   eval      c2enhl = *blank
     c                   eval      c2mini = 0
     c                   eval      c2maks = 0
     c                   eval      c2bmin = 0
     c                   eval      c2bpun = 0
     c                   eval      c2bmen = 0
     c                   eval      c2ltid = 0
     c                   eval      c2stda = 0
     c                   eval      c2labc = 0
6.29 c                   eval      c2abck = *blank
6.29 c                   eval      c2abct = *blank
6.29 c                   eval      c2vtyp = *blank
6.29 c                   eval      c2stda = 0
6.29 c                   eval      c2suda = 0
6.29 c                   eval      c2sldo = 0
6.29 c                   eval      c2lnav = *blank
6.29 c                   eval      c2edat = 0
6.29 c                   eval      c2eusr = *blank
     c                   endif
     c                   eval      s_stda =  c2stda
6.21 c                   eval      s_suda =  c2suda
6.21 c                   eval      s_sldo =  c2sldo
      *
8.01 c                   eval      c22txt = *blanks
8.01 c                   eval      c22rle = 0
8.01 c                   eval      c22gin = '0'
8.01 c     vlaglr_key    chain     vla2i1
8.01 c                   if        %found
8.01 c                   eval      c22rle = vl2rle
8.01 c                   eval      c22gin= %char(vl2gin)
8.01 c                   if        c22rle <> 0
8.01 c                   eval      rlevl1_levr = vl2rle
8.01 c     rlevl1_key    chain     rlevl1
8.01 c                   if        %found
8.01 c                   eval      c22txt = rlnavn
8.01 c                   else
8.01 c                   eval      c22txt = *blanks
8.01 c                   endif
8.01 c                   endif
8.01 c                   endif
      *
     c                   eval      w_vare = c1vare
     c                   exsr      les_vare
     c                   eval      c2tek1 = w_tek1
5.60 c                   eval      c2enhl = vvenhl
6.2d c                   eval      c2hlev = vvldor
6.2d c                   eval      rlevl1_levr = c2hlev
5.2d c     rlevl1_key    chain     rlevl1
5.2d c                   if        %found(rlevl1)
5.2d c                   movel     rlnavn        c2hlna
5.2d c                   else
6.2d c                   eval      c2hlna = *blank
6.2d c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vare = k1vare
     c                   eval      c1lage = k1lage
5.62 c                   eval      s_stda =  0
6.21 c                   eval      s_suda =  0
6.21 c                   eval      s_sldo =  0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
6.33 c*                  eval      *in45 = *off
6.33 c*                  eval      b_feil = *off
6.33 c*                  eval      vvarl1_vare = w_vare
6.33 c*    vvarl1_key    chain     vvarl1
6.33 c*                  if        %found
6.33 c*                  eval      w_tek1 = vvtek1
6.33 c*                  if        vvvtyp = 'D' or
6.33 c*                            vvvtyp = 'T'
6.33 c*                  eval      b_feil = *on
6.33 c*                  eval      *in45 = '1'
6.33 c*                  goto      c2taga
6.33 c*                  endif
6.33 c*                  endif
6.33  *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2PLAS'
     c                   call      'MD510R'
     c                   parm                    w_firm
     c                   parm                    c1lage
     c                   parm                    c2plas
     c                   parm                    w_lbes
     c                   goto      c2taga
     c                   endif
6.29 c                   if        *inkd   and
6.29 c                             w_afld = 'C2SLDO'
     c                   eval      w_vare = c1vare
8.04 c*                  call      'RL500R'
8.04 c*                  parm                    w_firm
8.04 c*                  parm                    c2sldo
8.04 c*                  parm                    c2lnav
8.04 c                   call      'VP507R'
8.04 c                   parm                    w_firm
8.04 c                   parm                    w_vare
8.04 c                   parm                    c2sldo
8.04 c                   parm                    c2lnav
6.29 c                   goto      c2taga
6.29 c                   endif
8.01  *
8.01  *  Spørring leverandør 2
8.01  *
8.01 c                   if        w_afld = 'C22RLE'
8.01 c                   call      'RL500R'
8.01 c                   parm                    w_firm
8.01 c                   parm                    c22rle
8.01 c                   parm                    c22txt
8.01 c                   goto      c2taga
8.01 c                   endif
      *
     c                   if        w_afld = 'C2PLA1'
     c                   call      'MD510R'
     c                   parm                    w_firm
     c                   parm                    c1lage
     c                   parm                    c2pla1
     c                   parm                    w_lbes
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2PLA2'
     c                   call      'MD510R'
     c                   parm                    w_firm
     c                   parm                    c1lage
     c                   parm                    c2pla2
     c                   parm                    w_lbes
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * validering
      *
     c                   if        c2stda <> 0
     c     *dmy          test(d)                 c2stda                 31
     c                   if        *in31 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2plas <> *blank
     c                   call      'MD710R'
     c                   parm                    w_stat
     c                   parm                    c1lage
     c                   parm                    c2plas
     c                   parm                    w_lbes
     c                   if        w_stat <> *blank
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2pla1 <> *blank
     c                   call      'MD710R'
     c                   parm                    w_stat
     c                   parm                    c1lage
     c                   parm                    c2pla1
     c                   parm                    w_lbes
     c                   if        w_stat <> *blank
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2pla2 <> *blank
     c                   call      'MD710R'
     c                   parm                    w_stat
     c                   parm                    c1lage
     c                   parm                    c2pla2
     c                   parm                    w_lbes
     c                   if        w_stat <> *blank
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
6.20  *
6.20 c                   if        c2sldo <> 0
6.20 c                   call      'RS760R'
6.20 c                   parm                    w_stat
6.20 c                   parm                    c2sldo
6.20 c                   parm                    c2lnav
6.20 c                   if        w_stat <> *blank
6.20 c                   eval      *in35 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
8.05 c                   if        b_prislev = *on
8.05 c                   exsr      sjekk_pris
8.05 c                   if        *in37 = *on
8.05 c                   goto      c2taga
8.05 c                   endif
8.05 c                   endif
6.20 c                   endif
6.21 c                   if        c2suda <> 0
6.21 c     *dmy          test(d)                 c2suda                 36
6.21 c                   if        *in36 = *on
6.21 c                   goto      c2taga
6.21 c                   endif
6.21 c                   endif
6.13  * Dersom varen endrer varetype fra lagervare, og varen har aktiv
6.13  * lagerbeholdning, må brukeren bekrefte endring av varetype.
6.13  *
6.13 c                   if        faalag = 1 and
6.13 c                             s_vtyp <> *blank and
6.13 c                             s_vtyp <> c2vtyp and
6.13 c                             s_vtyp =  'L'
6.13 c                   eval      b_inlr = *off
6.14 c                   call      'LL766R'
6.13 c                   parm                    w_firm
6.13 c                   parm                    w_vare
6.14 c                   parm                    c1lage
6.13 c                   parm                    b_inlr
6.13 c                   parm                    b_lagr
6.13 c                   if        b_lagr = *on
6.13 c                   exsr      xc3msg
6.13 c                   if        b_endr_vtyp = *off
      * her ble det ingen endring, type settes tilbake,ellers oppdatering
6.13 c                   eval      c2vtyp = s_vtyp
6.13 c                   goto      c2taga
6.13 c                   endif
6.13 c                   endif
6.13 c                   endif
6.26  * Dersom varen endrer varetype til lagervare,
6.26  * må brukeren bekrefte endring av varetype.
6.26  *
6.26 c                   if        s_vtyp <> *blank and
6.26 c                             s_vtyp <> c2vtyp and
6.26 c                             s_vtyp =  'S'
6.26 c                   exsr      xc4msg
6.26 c                   if        b_endr_vtyp = *off
6.26  * her ble det ingen endring, type settes tilbake,ellers oppdatering
6.26 c                   eval      c2vtyp = s_vtyp
6.26 c                   goto      c2taga
6.26 c                   endif
6.26 c                   endif
8.01  *
8.01 c                   if        c22gin <> '0' and
8.01 c                             c22gin <> '1'
8.01 c                   eval      *in38 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  *
8.01 c                   if        c22rle <> 0
8.01 c                   eval      rlevl1_levr = c22rle
8.01 c     rlevl1_key    chain     rlevl1
8.01 c                   if        not %found(rlevl1)
8.01 c                   eval      *in39 = *on
8.01 c                   eval      c22txt = *blanks
8.01 c                   goto      c2taga
8.01 c                   else
8.01 c                   eval      c22txt = rlnavn
8.01 c                   endif
8.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vlaglur
      *
     c                   eval      vlaglu_vare = c1vare
     c                   eval      vlaglu_lage = c1lage
     c     vlaglu_key    chain     vlaglur
      *
     c                   eval      vlplas = c2plas
     c                   eval      vlpla1 = c2pla1
     c                   eval      vlpla2 = c2pla2
     c                   eval      vlmini = c2mini
     c                   eval      vlmaks = c2maks
     c                   eval      vlbmin = c2bmin
     c                   eval      vlbpun = c2bpun
     c                   eval      vlbmen = c2bmen
     c                   eval      vlltid = c2ltid
5.51 c                   eval      vlkabc = c2abck
5.61 c                   eval      vllabc = c2labc
5.70 c                   eval      vlvtyp = c2vtyp
5.51 c                   eval      vltabc = c2abct
6.29 c                   if        c2stda <> 0
6.29 c     *dmy          move      c2stda        vlstda
6.29 c                   else
6.29 c                   eval      vlstda = *loval
6.29 c                   endif
6.29 c                   eval      vlbldo = c2sldo
6.29 c                   if        c2suda <> 0
6.29 c     *dmy          move      c2suda        vludat
6.29 c                   else
6.29 c                   eval      vludat = *loval
6.29 c                   endif
6.29 c                   time                    vledat
6.29 c                   time                    vletim
6.29 c                   eval      vleusr = l_user
6.2f c                   time                    vlfeda
6.2f c                   time                    vlfeti
6.2f c                   eval      vlfeus = l_user
      *
     c                   if        %found
     c                   update    vlaglur
     c                   else
     c                   eval      vlfirm = w_firm
     c                   eval      vlvare = c1vare
     c                   eval      vllage = c1lage
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
     c                   write     vlaglur
     c                   endif
      *
8.01 c     vlaglu_key    chain     vla2iu
      *
8.01 c                   if        not %found
8.01 c                   clear                   vla2iur
8.01 c                   eval      vl2fir = w_firm
8.01 c                   eval      vl2var = vlaglu_vare
8.01 c                   eval      vl2lag = vlaglu_lage
8.01 c                   time                    vl2oda
8.01 c                   time                    vl2oti
8.01 c                   eval      vl2ous = l_user
8.01 c                   endif
8.01  *
8.01 c                   time                    vl2eda
8.01 c                   time                    vl2eti
8.01 c                   eval      vl2eus = l_user
8.01  *
8.01 c                   eval      vl2gin = %int(c22gin)
8.01 c                   eval      vl2rle = c22rle
8.01  *
8.01 c                   if        %found
8.01 c                   update    vla2iur
8.01 c                   else
8.01 c                   write     vla2iur
8.01 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1mini = vlmini
     c                   eval      b1bmen = vlbmen
     c                   eval      b1ltid = vlltid
6.29 c                   eval      b1vtyp = vlvtyp
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  * Subrutine for å bekrefte bytting av varetype
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  *
6.13 c     xc3msg        begsr
6.13  *
6.13 c                   eval      b_endr_vtyp = *off
6.14 c                   eval      c3vtyp = c2vtyp
6.13  *
6.13 c     c3tagm        tag
6.13  *
6.13 c                   exfmt     c3msg
6.13  *
6.13 c                   select
6.13 c                   when      *inkc or *inkl
6.13 c                   leavesr
6.13 c                   when      *inkm
6.13 c                   call      'LL502R'
6.13 c                   parm                    w_firm
6.13 c                   parm                    w_vare
6.13 c                   goto      c3tagm
6.13 c                   when      *inkx
6.13 c                   exsr      oppd_lager
6.13 c                   eval      b_endr_vtyp = *on
6.13 c                   leavesr
6.13 c                   endsl
6.13  *
6.13 c                   endsr
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  * Subrutine for å bekrefte bytting av varetype  S->L
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  *
6.26 c     xc4msg        begsr
6.26  *
6.26 c                   eval      b_endr_vtyp = *off
6.26  *
6.26 c     c4tagm        tag
6.26  *
6.26 c                   exfmt     c4msg
6.26  *
6.26 c                   select
6.26 c                   when      *inkc or *inkl
6.26 c                   leavesr
6.26 c                   when      *inkx
6.26 c                   exsr      logg_lager
6.26 c                   eval      b_endr_vtyp = *on
6.26 c                   leavesr
6.26 c                   endsl
6.26  *
6.26 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av filter-bildet (H1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
6.2c c                   if        h1ldor = 0
6.2c c                   eval      h1lnav = *blank
6.2c c                   endif
      *
6.2c c                   if        h1lage = 0
7.07 c*                  eval      h1ltek = *blank
6.2c c                   endif
      *
     c     h1taga        tag
     c                   exfmt     h1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_h1win
     c                   endif
      *
     c                   if        *inkd
     c                   exsr      sp_input
     c                   goto      h1taga
     c                   endif
      *
     c     end_h1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_vare = k1vare
     c                   eval      s_lage = k1lage
      *
      * Fyller kopierings-bildet
      *
     c                   eval      k1fvar = k1vare
     c                   eval      k1flag = k1lage
      *
     c                   eval      w_vare = k1vare
     c                   exsr      les_vare
     c                   eval      k1ftek = w_tek1
     c                   eval      k1tek1 = *blank
     c                   eval      k1ltek = *blank
     c                   eval      k1lage = 0
7.01 c                   eval      k1ovef = 0
      *
     c                   eval      vlaglr_vare = k1vare
     c                   eval      vlaglr_lage = k1lage
     c     vlaglr_key    chain     vlaglr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_k1win
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      k1taga
     c                   endsl
      *
      * Sjekk om gyldige verdier
      *
     c                   eval      w_vare = k1vare
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
6.2h  *
6.2h  * Sjekk om gyldige verdier
6.2h  *
6.2h c                   eval      ra10l1_jkod = k1lage
6.2h c     ra10l1_key    chain     ra10l1
6.2h c                   if        not %found
6.2h c                   eval      *in32 = *on
6.2h c                   goto      k1taga
6.2h c                   endif
7.01  *
7.01  * Hvis overfør utfylt så, kun overfør lagerdata, ikke dann ny
7.01 c                   if        k1ovef = 1
7.01  * sjekk at vare-lager finnes
7.01 c                   eval      vlaglr_vare = k1vare
7.01 c                   eval      vlaglr_lage = k1lage
7.01 c     vlaglr_key    chain     vlaglr
7.01 c                   if        not %found(vlaglr)
7.01 c                   eval      w_mld1 = 'Vare / Lger finnes  +
7.01 c                                       ikke, kan ikke overfø+
7.01 c                                       re info. '
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
7.01 c                   goto      k1taga
7.01 c                   endif
7.01 c                   exsr      overf_info
7.01 c                   leavesr
7.01 c                   endif
      *
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vlaglr_vare = k1vare
     c                   eval      vlaglr_lage = k1lage
     c     vlaglr_key    chain     vlaglr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1vare = s_vare
     c                   eval      c1lage = s_lage
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vlagl2
     c                   other
     c                   read      vlagl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vlfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vlagl2_lage = vllage
     c                   other
     c                   eval      vlagl1_vare = vlvare
     c                   endsl
      *
      * Sjekk mot vareregister
      *
     c                   eval      w_vare = vlvare
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   iter
     c                   endif
      *
      * Uaktuell leverandør?
      *
6.35  * sjekker først mot lev.på lager
     c                   if        h1ldor <> 0 and
6.35 c                             vlbldo <> 0 and
6.35 c                             h1ldor <> vlbldo
     c                   iter
     c                   endif
6.35  * sjekker så evt. mot varens leverandør
6.35 c                   if        h1ldor <> 0 and
6.35 c                             vlbldo =  0 and
6.35 c                             h1ldor <> vvldor
6.35 c                   iter
6.35 c                   endif
6.12  *
6.12  * Uaktuelt lager ?
6.12  *
7.07 c*                  if        h1lage <> 0 and
7.04 c                   if        h1lage <> 0  or
7.07 c                             h1lag2 <> 0  or
7.07 c                             h1lag3 <> 0  or
6.12 c*                            h1lage <> vllage
7.07 c                             h1lag4 <> 0  or
7.07 c                             h1lag5 <> 0
7.07 c                   eval      b_lage = *off
7.07 c                   if        h1lage <> 0 and
7.07 c                             h1lage =  vllage
7.07 c                   eval      b_lage = *on
7.07 c                   endif
7.07 c                   if        h1lag2 <> 0 and
7.07 c                             h1lag2 =  vllage
7.07 c                   eval      b_lage = *on
7.07 c                   endif
7.07 c                   if        h1lag3 <> 0 and
7.07 c                             h1lag3 =  vllage
7.07 c                   eval      b_lage = *on
7.07 c                   endif
7.07 c                   if        h1lag4 <> 0 and
7.07 c                             h1lag4 =  vllage
7.07 c                   eval      b_lage = *on
7.07 c                   endif
7.07 c                   if        h1lag5 <> 0 and
7.07 c                             h1lag5 =  vllage
7.07 c                   eval      b_lage = *on
7.07 c                   endif
7.07 c                   if        b_lage = *off
6.12 c                   iter
7.07 c                   endif
6.12 c                   endif
6.24  *
6.24  * Varetype
6.24  *
6.24 c                   if        h1vtyp <> *blank and
6.24 c                             h1vtyp <> vlvtyp
6.24 c                   iter
6.24 c                   endif
6.24  *
6.24  * ABC kode (prognose)
6.24  *
6.24 c                   if        h1kabc <> *blank and
6.24 c                             h1kabc <> vlkabc
6.24 c                   iter
6.24 c                   endif
6.24  *
6.24  * Opprettet dato
6.24  *
6.24 c                   if        h1odat <> 0
6.24 c     *dmy          move      h1odat        w_odat
6.24 c                   if        w_odat <> vlodat
6.24 c                   iter
6.24 c                   endif
6.24 c                   endif
6.28  *
6.28  * ABC kode (telling)
6.28  *
6.28 c                   if        h1tabc <> *blank and
6.28 c                             h1tabc <> vltabc
6.28 c                   iter
6.28 c                   endif
6.28  *
6.28  * Lagerplass
6.28  *
6.28 c                   if        h1plas <> *blank and
6.28 c                             h1plas <> vlplas
6.28 c                   iter
6.28 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = vlvare
     c                   eval      b1lage = vllage
6.27 c                   eval      b1vtyp = vlvtyp
      *
     c                   movel     w_tek1        b1tek1
     c                   eval      b1mini = vlmini
     c                   eval      b1bmen = vlbmen
     c                   eval      b1ltid = vlltid
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vlagl2_key    setgt     vlagl2
     c                   readp     vlagl2
     c                   other
     c     vlagl1_key    setgt     vlagl1
     c                   readp     vlagl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vlagl2
     c                   other
     c                   readp     vlagl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vlfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vlagl2_lage = vllage
     c                   other
     c                   eval      vlagl1_vare = vlvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vlagl2_key    setll     vlagl2
     c                   other
     c     vlagl1_key    setll     vlagl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne varebeskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      w_tek1 = vvtek1
     c                   else
     c                   eval      b_feil = *on
     c                   eval      w_tek1 = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på varenummer
      *
     c                   if        w_arcd = 'B2CTL' and
     c                             w_afld = 'B2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_arcd = 'C1BLD' and
     c                             w_afld = 'C1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      c1vare = w_vare
     c                   eval      c1tek1 = w_tek1
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_arcd = 'K1WIN' and
     c                             w_afld = 'K1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   eval      k1tek1 = w_tek1
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        w_arcd = 'B2CTL' and
     c                             w_afld = 'B2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltek
     c                   move      w_lage        b2lage
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_arcd = 'C1BLD' and
     c                             w_afld = 'C1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltek
     c                   eval      c1lage = w_lage
     c                   eval      c1ltek = w_ltek
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_arcd = 'K1WIN' and
     c                             w_afld = 'K1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltek
     c                   eval      k1lage = w_lage
     c                   eval      k1ltek = w_ltek
     c                   goto      sp_end
     c                   endif
6.12  *
6.12 c                   if        w_arcd = 'H1WIN' and
6.12 c                             w_afld = 'H1LAGE'
6.12 c                   call      'RA510R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_lage
6.12 c                   parm                    w_ltek
6.12 c                   eval      h1lage = w_lage
7.07 c*                  eval      h1ltek = w_ltek
     c                   goto      sp_end
     c                   endif
7.07  *
7.07  *
7.07 c                   if        w_arcd = 'H1WIN' and
7.07 c                             w_afld = 'H1LAG2'
7.07 c                   call      'RA510R'
7.07 c                   parm                    w_firm
7.07 c                   parm                    w_lage
7.07 c                   parm                    w_ltek
7.07 c                   eval      h1lag2 = w_lage
7.07 c                   goto      sp_end
7.07 c                   endif
7.07  *
7.07 c                   if        w_arcd = 'H1WIN' and
7.07 c                             w_afld = 'H1LAG3'
7.07 c                   call      'RA510R'
7.07 c                   parm                    w_firm
7.07 c                   parm                    w_lage
7.07 c                   parm                    w_ltek
7.07 c                   eval      h1lag3 = w_lage
7.07 c                   goto      sp_end
7.07 c                   endif
7.07  *
7.07 c                   if        w_arcd = 'H1WIN' and
7.07 c                             w_afld = 'H1LAG4'
7.07 c                   call      'RA510R'
7.07 c                   parm                    w_firm
7.07 c                   parm                    w_lage
7.07 c                   parm                    w_ltek
7.07 c                   eval      h1lag4 = w_lage
7.07 c                   goto      sp_end
7.07 c                   endif
7.07  *
7.07 c                   if        w_arcd = 'H1WIN' and
7.07 c                             w_afld = 'H1LAG5'
7.07 c                   call      'RA510R'
7.07 c                   parm                    w_firm
7.07 c                   parm                    w_lage
7.07 c                   parm                    w_ltek
7.07 c                   eval      h1lag5 = w_lage
7.07 c                   goto      sp_end
7.07 c                   endif
      *
      *  Spørring leverandør
      *
     c                   if        w_arcd = 'H1WIN' and
     c                             w_afld = 'H1LDOR'
     C                   call      'RL500R'
     C                   parm                    w_firm
     C                   parm                    h1ldor
     C                   parm                    h1lnav
     c                   goto      sp_end
     c                   endif
      *
6.24  * Spørring på varetype
6.24 c                   if        w_arcd = 'H1WIN' and
6.24 c                             w_afld = 'H1VTYP'
6.24 c                   call      'VA570R'
6.24 c                   parm                    w_firm
6.24 c                   parm                    h1vtyp
6.24 c                   parm                    w_ttxt
6.24 c                   eval      h1lage = w_lage
6.24 c                   goto      sp_end
6.24 c                   endif
6.28  * Lagerplass
6.24 c                   if        w_arcd = 'H1WIN' and
6.28 c                             w_afld = 'H1PLAS'
6.28 c                   if        h1lage <> 0
6.28 c                   call      'MD510R'
6.28 c                   parm                    w_firm
6.28 c                   parm                    h1lage
6.28 c                   parm                    h1plas
6.28 c                   parm                    w_lbes
6.28 c                   goto      sp_end
6.28 c                   else
6.28 c                   call      'MD511R'
6.28 c                   parm                    w_firm
6.28 c                   parm                    h1lage
6.28 c                   parm                    h1plas
6.28 c                   parm                    w_lbes
6.28 c                   goto      sp_end
6.28 c                   endif
6.28 c                   endif
      *
     c     sp_end        endsr
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  * Subrutine for å hente å oppdatere lager
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  *
6.13 c     oppd_lager    begsr
6.13  *
6.15  * må også sette alle hittil i år tall til 0, ellers vil
6.15  * i mange tilfeller betraktes som aktiv og vareregister
6.15  *
6.15 c                   eval      vlaglu_vare = c1vare
6.15 c                   eval      vlaglu_lage = c1lage
6.15 c     vlaglu_key    chain     vlaglur
6.15 c                   if        %found(vlaglu)
6.15 c                   eval      vlbbes = 0
6.15 c                   eval      vloord = 0
6.15 c                   eval      vlpakk = 0
6.15 c                   eval      vlinng = 0
6.15 c                   eval      vlutta = 0
6.15 c                   eval      vltell = 0
6.15 c                   eval      vlsalg = 0
6.15 c                   time                    vledat
6.15 c                   time                    vletim
6.15 c                   eval      vleusr = l_user
6.15 c                   update    vlaglur
6.15 c                   endif
6.13  *
6.13 c                   move      w_vare        hlvare
6.13 c                   move      c1lage        hllage
6.13 c                   move      c2enhl        hlenhe
6.13 c                   time                    hldato
6.13 c                   time                    hltime
6.13 c                   eval      hlotyp  = *blank
6.13 c                   eval      hlltyp  = *blank
6.13  *
6.13 c                   eval      hlanta = 0
6.13 c                   eval      hlkopr = 0
6.13  *
6.13 c                   eval      hlrefr = 'Vtyp endret L--> S '
6.13 c                   move      'L'           hlsyst
6.13 c                   eval      hlaltk = 'J '
6.13 c                   eval      hlrest = *blank
6.13 c                   eval      hlonum = 0
6.13 c                   eval      hlolin = 0
6.13  *
6.13  *
6.13 c                   eval      w_stat = *blank
6.13  *
6.13 c                   call      'VL001R'
6.13 c                   parm                    w_stat
6.13 c                   parm                    hlrec
6.13  *
6.13 c     end_oppd      endsr
      *
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  * Subrutine for å hente å logge endring på lager
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  *
6.26 c     logg_lager    begsr
6.26  *
6.26 c                   move      w_vare        hlvare
6.26 c                   move      c1lage        hllage
6.26 c                   move      c2enhl        hlenhe
6.26 c                   time                    hldato
6.26 c                   time                    hltime
6.26 c                   eval      hlotyp  = *blank
6.26 c                   eval      hlltyp  = *blank
6.26  *
6.26 c                   eval      hlanta = 0
6.26 c                   eval      hlkopr = 0
6.26  *
6.26 c                   eval      hlrefr = 'Vtyp endret S--> L '
6.26 c                   move      'L'           hlsyst
6.26 c                   eval      hlaltk = 'SL'
6.26 c                   eval      hlrest = *blank
6.26 c                   eval      hlonum = 0
6.26 c                   eval      hlolin = 0
6.26  *
6.26 c                   eval      w_stat = *blank
6.26  * Siden 'S' varer ikke lager poster i historisk register, lager vi postering
6.26  * manulet herfra. Ingen god løsning.
6.26 c                   call      'VL001R'
6.26 c                   parm                    w_stat
6.26 c                   parm                    hlrec
6.26  *
6.26 c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om leverandør har pris evt. er i distribusjon
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pris    begsr
6.32  *
6.32 c                   eval       *in37 = *off
6.32  *
6.32 c                   eval      vvprl1_vare = vvvare
6.32 c                   eval      vvprl1_ldor = c2sldo
6.32 c     vvprl1_key    chain     vvprl1
6.32 c                   if        %found(vvprl1)
6.32 c                   leavesr
6.32 c                   endif
      *
6.32 c                   eval      lldil1_ldor = c2sldo
6.32 c     lldil1_key    chain     lldil1
6.32 c                   if        %found(lldil1)
6.32 c                   leavesr
6.32 c                   endif
6.32 c                   eval       *in37 = *on
6.32  *
6.32 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for initiering av program
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     overf_info    begsr
7.01  *
7.01  * les fra lager
7.01 c                   eval      vlaglr_vare = k1fvar
7.01 c                   eval      vlaglr_lage = k1flag
7.01 c     vlaglr_key    chain     vlaglr
7.01 c                   if        %found(vlaglr)
7.01 c                   eval      w_plas = vlplas
7.01 c                   eval      w_pla1 = vlpla1
7.01 c                   eval      w_pla2 = vlpla2
7.01 c                   eval      w_mini = vlmini
7.01 c                   eval      w_maks = vlmaks
7.01 c                   eval      w_bmin = vlbmin
7.01 c                   eval      w_bpun = vlbpun
7.01 c                   eval      w_bmen = vlbmen
7.01 c                   eval      w_ltid = vlltid
7.01 c                   eval      w_kabc = vlkabc
7.01 c                   eval      w_labc = vllabc
7.01 c                   eval      w_vtyp = vlvtyp
7.01 c                   eval      w_tabc = vltabc
7.03 c                   eval      w_bldo = vlbldo
7.01  * oppdaterer vare
7.01 c                   eval      vlaglu_vare = k1vare
7.01 c                   eval      vlaglu_lage = k1lage
7.01 c     vlaglu_key    chain     vlaglu
7.01 c                   if        %found(vlaglu)
7.01 c                   eval      vlplas = w_plas
7.01 c                   eval      vlpla1 = w_pla1
7.01 c                   eval      vlpla2 = w_pla2
7.01 c                   eval      vlmini = w_mini
7.01 c                   eval      vlmaks = w_maks
7.01 c                   eval      vlbmin = w_bmin
7.01 c                   eval      vlbpun = w_bpun
7.01 c                   eval      vlbmen = w_bmen
7.01 c                   eval      vlltid = w_ltid
7.01 c                   eval      vlkabc = w_kabc
7.01 c                   eval      vllabc = w_labc
7.01 c                   eval      vlvtyp = w_vtyp
7.01 c                   eval      vltabc = w_tabc
7.03 c                   eval      vlbldo = w_bldo
7.01 c                   time                    vledat
7.01 c                   time                    vletim
7.01 c                   eval      vleusr = l_user
7.01 c                   update    vlaglur
7.01 c                   eval      b_forn = *on
7.01 c                   endif
7.01 c                   endif
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
6.27 c     *entry        plist
6.27 c                   parm                    p_stva
      *
      * Key for posisjonering på lager via varenummer
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for posisjonering på lager via lagernummer
      *
     c     vlagl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl2_lage
      *
      * Key for les på lager
      *
     c     vlaglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
     c                   kfld                    vlaglr_lage
      *
      * Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.32  *
6.32  *  Key for les av VARE-pris
6.32  *
6.32 c     vvprl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vvprl1_vare
6.32 c                   kfld                    vvprl1_ldor
6.32  *
6.32  *  Key for les av lev.distribusjon
6.32  *
6.32 c     lldil1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    lldil1_ldor
5.70  *
5.70  * Key for les av prisgruppe kode
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
6.13  *
6.13  * Key for oppslag på kode/status
6.13  *
6.13 c     fstsl1_key    klist
6.13 c                   kfld                    w_firm
6.20  *
6.2d  * Key for les av prisgruppe kode
6.2d  *
6.2d c     rlevl1_key    klist
6.2d c                   kfld                    w_firm
6.2d c                   kfld                    rlevl1_levr
6.2h  *
6.2h  * Key for les av lagerkoder
6.2h  *
6.2h c     ra10l1_key    klist
6.2h c                   kfld                    w_firm
6.2h c                   kfld                    ra10l1_jkod
      *
7.08  *
7.08  * Key for brukerregister
7.08  *
7.08 c     ausrl1_key    klist
7.08 c                   kfld                    ausrl1_user
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
6.13  *
6.13  * Oppslag på kode/status
6.13  *
6.13 c     fstsl1_key    chain     fstsl1
8.03  *
8.03  * Endring 8.04 Hvis lager fra bruker skal styre lager på filter, så ON
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'BRUKER_STYRER_LAGER':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_803 = *on;
8.03   endif;
7.08  *
7.08  * Slå opp bruker-register
7.08  *
8.03 c                   if        u_803 = *on
8.03 c                   eval      ausrl1_user = l_user
7.08 c     ausrl1_key    chain     ausrl1
7.08 c                   eval      h1lage = ablagr
8.03 c                   else
8.03 c                   eval      h1lage = 0
8.03 c                   ENDIF
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vlagl1_vare = p_stva
     c     vlagl1_key    setll     vlagl1
      *
     c                   exsr      crt_subfile
7.04  *
7.04  * Endring 7.04 Styre ledetekster til bestillingspkt og mengde
7.04  *
7.04   CallP CO402R(l_filg:w_firm:0:'LEDETEKSTER_BESTILLINGSPKT':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     u_bpkt = *on;
7.04   endif;
8.05  *
8.05  * Endring 8.05 Sjekk om prioritert leverandør har pris     de
8.05  *
8.05   CallP CO402R(l_filg:w_firm:0:'SJEKK_PRIOLEV_PRISGIVER':
8.05                    co402_verdi1:co402_verdi2);
8.05   if co402_verdi1 = '1';
8.05     b_prislev = *on;
8.05   endif;
      *
     c                   endsr
**     Ledetekster
Bestillingspunkt . . . . . . .
Bestillingsmengde. . . . . . .
Hyllemin . . . . . . . . . . .
Hyllemax . . . . . . . . . . .
