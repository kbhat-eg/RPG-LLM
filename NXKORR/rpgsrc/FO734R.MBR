     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO734R
      * Beskrivelse . . : Plukke/Vise ordrenummer for bestilling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.11 200100 AMD  Programmet kunne gå i loop dersom alle linjer
      *                   var bestilt på en ordre, og dette var siste ordre
      *                   i systemet. Dette er korrigert.
      *                -  I tillegg er det foretatt endringer som gjør at
      *                   dersom alle linjer på en leverandør er bestilt og
      *                   det ligger flere på en annen leverandør, kommer
      *                   leverandørnummer på neste ikke bestilte linje opp
      *                   automatisk.
      *                -  Når alle linjer er bestilt, avsluttes programmet
      *                   direkte uten å spørre om "resten til plukkliste"
      *                -  Velges 18 for bestilling fra hovedprogrammet, og
      *                   alle linjer er bestilt, skjer det ingen ting.
      *                -  Ordrelinjeregister blir nå oppdatert med
      *                   bestillingsnummer.
      *                -  Varenummer ble lagt ut feil i skjermbildet.
      * R3.12 270100 AMD  Det er lagt inn følgende nye funksjonalitet:
      *                -  Dersom det ikke er registrert antall til best.
      *                   avsluttes programmet og går tilbake nytt utvalg.
      *                -  Bildet som spør om resten skal kjøres til plukk-
      *                   liste kommer bare forutsatt at det har vært fore-
      *                   tatt en bestilling og det er flere linjer igjen.
      *                -  Dersom det kun ligger igjen tekstlinjer, vil ikke
      *                   bestillingsvalg kunne benyttes.
      * R3.13 270100 AMD  Dersom det først var bestilt deler av en ordre, og
      *                   det etterpå ble valgt alle linjer (valg 2) ble alt
      *                   bestilt pånytt, også de linjer som var bestilt før
      *                -  Tekstlinjer kom ikke med når alle  linjer var
      *                   valgt (valg 2)
      * R3.14 100200 AMD  Programmet er skrevet om til ILE, ingen anmerkning
      * R3.15 220200 ASK  Legger ut varegrupper og LVR-kode til bestilling.
      * R3.16 220300 AMD  Leveringstype er tatt med i datastruktur hlrec
      *                   I tillegg legges det ut referanse på 10 posisjoner
      * R3.17 080600 ASK  Sjekker om bestnr. er brukt før, legg ut melding.
      * R3.18 131000 AMD  Tar med over Nobbnummer fra varelinje.
      * R3.31 020201 AMD  Priser skal ikke hentes inn på nytt ved generering
      *                   av bestilling. (Se beskrivelse i AAINF330)
      * R5.01 261102 AMD  Avdeling legges nå ut i bestillingshode
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
5.32  * R5.32 310304 ASK  Lagt inn faktura adresse hvis vareadresse
5.32  *                   er blank ved direktelevering.
5.40  * R5.40 080605 AMD  Ordretype hentes nå fra ordretyperegister
      *                   (OT for motsvarende system og ikke fra status)
5.51  * R5.51 261005 AMD  Lagt inn mer automatisering av plukkerutinen
5.52  * R5.52 271005 BOF  Istedet for utskr./mail her -> overg. bestilling
5.53  * R5.53 281005 BOF  Lagt på linjereferanser til best. på o.detalje
      * R5.54 311005 HKO  Lagt inn test på om ordren kan bestilles
5.55  * R5.55 130607 AMD  Kom opp med feil ordretype dersom noe skulle
      *                   kjøres til plukkliste (Se kommentar inne i
      *                   programmet også på 5.55)
5.60  * R5.60 201207 bof  Innkjøps.pris fra skaffevare til bestilling. Samt
5.60  *                   danne summer til bestillingshode
5.61  * R5.61 010208 bof  Teste om system på ordretype = 0 (dvs. ordre)Samt
5.70  * PRGR  231008 sst  Prisgruppe
5.62  * R5.62 060309 AMD  Gjort om logikk for hvordan kostpriser og
5.62  *                   innkjøpspriser legges fra ordre til bestilling
5.62  *                   (se logg for 06.03.09)
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  131109 BSA  Lagt tilbake logikk for kalkulering av innkjøpspris.
6.11  *                   Tidligere løsning (ref.5.62) forutsetter at innkjøpspris
6.11  *                   hentes inn på ordren. Dette er ikke tilfelle. Henter
6.11  *                   derfor inn igjen innkjøpspris.
6.12  *  6.10  110610 bof Justert i utvidelse av parm VL001R
6.13  *  6.10  030111 bof Hvis skaffevare og innkjøpspris = 0, så settes den lik kostpris
6.14  *  6.10  030111 bof Skal bruke varetype fra vare-lager
6.15  *  6.10  060612 bof Lagt på momskode fra leverandør
6.16  *  6.10  020812 bof Lagt på en clear før skriv til best.head/line
6.17  *  6.10  060712 bsa Må huske å ta med GTIN nummer. (NEB)
6.20  *  6.20  011210 bof sjekke om lev.på vare er har sentrallager-lev.nr
6.20  *  6.20  261110 bof Ordretype hentes fra lev. hvis denne er spesifisert
6.20  *                   Feltet ligger på lev.distribsjon pr. lager
6.23  * R6.20  220310 bof  Utvidet at man man danne best.forslag eller bestilling
6.21  * R6.20  161210 bof  Best.forslag-skal legge ut leveringsdato i b.dato
6.22  * R6.20  110511 bof  Parameterstyre om best. eller best.forslag, VAOSKA kan
      *                    overstyre hvis best.forslag
6.24  * R6.20  190511 bof  Utvidet vl710r med utg.dato og lev.nr
6.25  * R6.20  010411 bof  Tar med lev.varenr til best.forslag
6.26  * R6.20  071111 bof  Legger ut inntastet ordretype til best.forslag også
6.27  * R6.20  120112 bsa  Utvidet VP753R med kampanje
6.28  * R6.20  210312 bof  Justert logikk vedr ordretype BI
6.29  * R6.20  110113 bof  Best.forslag - skal ha dagens dato i best.dato
6.30  * R6.20  100613 sst  Ta med prosjektnummer fra ordre til bestilling
6.2A  *  6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.2A  *                    legg av verdi på bestillingsforslag.
6.2B  *  6.20  300114 bof  Lagt inn eksakt nøkkel før les av best.f.hode
6.2c  *  6.20  020914 bof  LOLENA skal tas fra RLALFA
6.2d  *  6.20  260914 bof  test om best.f. innkj.pris skal være kostpris
6.2e  *  6.20  111214 bof  Legger på bet.bet. fra leverandør
6.2f  *  6.20  260116 nho  Tester mot VOTYL1 før FSTSPF
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * R6.30 040914  bof Sjekk om lev. er sperret eller passiv
6.32  * R6.30 041114  bsa Legger inn mvakode på linjenivå på bestilling
6.33  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.34  * 6.30  221216 sst  Justert vedr mvakode på bestilling
6.35  * 6.30  270917 nho  Forsendelsesmåte ble ikke overf. til best.
6.36  * 6.30  180418 bof  Mer søk etter ordretype fra LLDIPF
6.37  * 6.30  050116 nho  Hvis intern bestilling og vi kommer fra
6.37  *                   ordre, sjekk om vi allerede har LO500R i
6.37  *                   program stack (rekursivt kall).
6.38  * 6.30  060718 nho  Rettet feil flytt til ldproj som er alfa
6.39  * 6.30  180118 ask  Justert vedr mvakode på bestilling for utl. lev.
6.3a  * 6.30  301018 bof  Utvidet med trelast sortiment
6.3b  * 6.30  030119 bsa  Endret feilmelding til å kalle AA006 - jit Gui
7.xx  * 7.00  220116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  120220 bkv  Skal hente innk.pris også v/vtyp = S (Gipling)        ok-7.05
7.02  * 7.00  120220 bkv  Hvis PN legg *H (faak98) i info-type (Gipling)
      *                 (denne er fjernet pga ikke mulig med PN og oakk = 1)
7.03  * 7.00  140220 bkv  Lagt på ledetekst pga Newlook fikk problem med levna  ok-7.03a
7.04  * 7.00  190220 bkv  Rettelser pga Newlook                                 ok-7.04a
7.04s *K0000  290519 sst  Egen kontering på dir.lev varer ved prosjekt          ok-7.04
7.05  *K0000  150920 bsa  Innført ny parameter for om det skal lages
7.05  *K0000              bestillingsforslag, valg 2. Må testes i kombinasjon
7.05  *K0000              med ny tabell.
      *_________________________________________________________________________________
NB!!! *                   Her er det en del avvik mellom denne versjonene og askorr700a
      *_________________________________________________________________________________
7.06  *       110520 ask  Lagt inn fargestyring basert på kode i sortimentfeltet på VVARPF
7.06  *                   Dette er switch-styrt med koden kode "FARGE_SKAFF_SORTIMENT"
7.06  *                   Alternativt settes farge på skaffevarer
7.06b *K000   020920 bsa  Legger ut forsendelsesmåte og leveringsdato for       ok-7.06
7.06b *K000               å slippe gå inn på bestillingen og sette disse.       ok-7.06
7.06b *K000               Sparer tastetrykk. (NXS-5305)                         ok-7.06
7.07  *K000   111220 bsa  Henter forsendelsesmåte fra leverandør hvis den       ok-7.07
7.07  *K000               finnes.                                               ok-7.07
7.08  *K000   270121 sst  Hente default valg fra nx                             ok-7.08
7.09  *K0000  050221 bsa  Selger på bestillingsforslag må også henter fra
7.09  *K0000              leverandør ved kode 1. Ref 7.05
7.10  *K000   091117 sst  Ikke ta med prosejkt/underprosjekt fra ordre
7.11  *K000   110518 bsa  Avdeling på bestillingen styres fra lager, ikke fra
7.11  *                   ordrehodet.
7.12  *K000   140918 sst  Kun ordretype 'T' på tilbudskunde
      *
7.fb  *K000   170419 bof  Forskuddsbetalig og depositum
      *
8.01  *K0000  230322 bsa  Hvis første linje er tekst, blir det feil.
8.02  *800    010422 ask  Legger inn adresse fra eksternlager dersom dette er valgt
8.02  *                   Dette erstatter tidligere endring som var merket med 8.02
8.03  *800    271022 bsa  Legg også ut referanse på tilleggsregister.
8.03  *800                Brukes ved utsendelse av EDI bestilling fra
8.03  *800                ny EDI modul (NGN).
8.04  *800    031122 ask  Lagt inn referanse til bestilling på ordre da dette manglet i standard
8.05  *800    211122 ask  Endret nøkkelbegrep på eksdternt lager fra N(2 0) til A(20)
8.06  *800    090323 ask  Fjernet leveringsadresse fra eksternlager (8.02)
8.07  *800    111223 bsa  Legg ut lager på utplukkstabell.
8.08  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.09  *800    271025 bsa  Sjekker om ordretypen kan plukkes til bestiling.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
5.32 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
7.fb ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
3.17 flohel1    if a e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf a e           k disk    rename(lohepfr:lohelur)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplllu    uf a e           k disk    rename(fpllpfr:fplllur)
5.60 flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
6.11 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.20 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
6.23 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.23 flfdtl6    if   e           k disk    rename(lfdtpfr:lfdtl6r)
6.23 flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
6.23 flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
6.32 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
7.05 fmpldi1    if   e           k disk    rename(mpldst:mpldi1r)
7.11 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
8.03 flotii2    uf a e           k disk    rename(lotist:lotii2r)
      *
     ffo734d    cf   e             workstn sfile(d1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      *  Lager - koderegister
     d/COPY QCPYLESRC,RA10PFDS
      *
      * Definering av parametere
      *
     d p_plor          s              1
     d p_firm          s              3  0
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
5.70 d p_vtyp          s              1
6.37 d r_prog          s             10
6.37 d r_stat          s              1
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
7.01 d l_filg                931    933
     d l_fnav                951    980
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fplll1_numm     s                   like(fklonr)
     d fplll1_suff     s                   like(fklsuf)
     d fplll1_line     s                   like(fkllin)
     d fplolu_numm     s                   like(fkponr)
     d fplolu_suff     s                   like(fkpsuf)
3.17 d lohel1_numm     s                   like(lonumm)
3.17 d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d votyl1_otyp     s                   like(vaotyp)
     d rlevl1_levr     s                   like(rllevr)
5.32 d rkunl1_kund     s                   like(rkkund)
5.60 d lodtlr_line     s                   like(ldline)
6.11 d vvarl1_vare     s                   like(fdvare)
6.20 d lldil1_ldor     s                   like(lpldor)
6.20 d lldil1_lage     s                   like(lplage)
6.23 d lfhelu_numm     s                   like(lhnumm)
6.23 d lfdtl6_ornr     s                   like(ldornu)
6.23 d lfdtl6_orsu     s                   like(ldorsu)
6.23 d lfdtl6_orli     s                   like(ldorli)
6.23 d vvenl1_vare     s                   like(vevare)
6.23 d vvenl1_enhe     s                   like(veenhe)
6.32 d vmval1_mkod     s                   like(vamkod)
7.05 d mpldi1_dlev     s                   like(mldlev)
7.05 d mpldi1_dlag     s                   like(mldlag)
7.11 d ra10l1_jkod     s                   like(rajkod)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_tell     s                   like(sktell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_lety          s              1  0
     d w_navn          s                   like(rlnavn)
     d w_lage          s              2  0
8.05 d w_ekst          s             20
8.02 d w_dist          s              2  0
     d w_text          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_feil          s              1
     d w_firm          s              3  0
     d w_pnra          s              4
     d w_test          s              1  0
     d w_anta          s             13  3
     d w_rest          s              1  0
     d w_enor          s              1
     d w_fast          s             10
     d w_fell          s              6
     d w_ipor          s              1
     d w_kode          s              1
     d w_skaf          s              1
6.nu d w_numm          s              8  0
     d w_eann          s             13  0
     d w_regr          s              1  0
     d w_retu          s              1
     d w_type          s              2
     d w_stat          s              1
     d w_sted          s                   like(lonavn)
     d w_adr1          s                   like(loadr1)
     d w_adr2          s                   like(loadr2)
6.nu d w_lrec          s            128
3.17 d w_rnum_alf      s              6
3.17 d w_knum_alf      s              6
3.17 d w_mld1          s             50
5.52 d w_lpro          s              1
5.60 d w_totk          s             11  2
5.60 d w_toti          s             11  2
5.60 d w_totr          s             11  2
6.33 d w_numa          s              8
5.02 d w_lina          s              4
6.11 d w_kpny          s                   like(ldkpny)
6.11 d w_prgr          s                   like(fdprgr)
6.14 d w_vtyp          s                   like(vvvtyp)
6.20 d w_sldo          s                   like(fdldor)
6.20 d w_ldor          s                   like(fdldor)
6.24 d w_udat          s               d   datfmt(*iso)
6.20 d b_inlr          s              1    inz(*off)
6.23 d w_tbel          s                   like(lhtbel)
6.23 d w_tvkt          s             11  3
6.23 d w_bestf         s                   like(lfnumm)
6.3a d w_lv_nobb       s                   like(vvnobb)
6.3a d w_lv_modn       s                   like(vvnmod)
6.3a d w_lv_lldo       s                   like(vvldor)
6.3a d w_lv_llva       s                   like(vvlvar)
6.3b d w_mld01         s             30
6.3b d w_mld02         s             30
6.3b d w_ctli          s              2  0
6.3b d w_ctpo          s              3  0
7.06bd w_retk          s              1
7.08 d w_valg_vare     s              1    inz('0')
7.12 d w_tilb_kund     s                   like(rkkund)
      *
6.28 d s_ldor          s                   like(fdldor)
6.23  *
6.23 d b_bfor          s              1    inz(*off)
6.23 d b_line          s              1    inz(*off)
6.23 d b_besf          s              1    inz(*off)
7.05 d b_slev          s              1    inz(*off)
7.fb d b_feil          s              1    inz(*off)
8.09 d b_otyp          s              1    inz(*off)
7.05 d s_bfor          s              1    inz(*off)
6.27 d w_kamp          s              5
7.fb d w_sumi          s             11  2
7.fb d w_sumu          s             11  2
7.04sd u_kont_prosj    s              1    inz(*off)                            Hentes fra NX
7.06 d u_fsor          s              1    inz(*off)                            Fargekode fra sortim
7.06 d u_fskv          s              1    inz(*off)                            Farge på skaffvare
7.06 d w_fso1          s             10
7.06 d w_fso2          s             10
7.06 d w_sort          s             10
      *
7.00  * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
7.10 d u_s710          s              1    inz(*off)
7.11 d u_s711          s              1    inz(*off)
7.12 d u_s712          s              1    inz(*off)
7.02 d* u_702           s              1    inz(*off)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * A1 Behandler plukking av ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom ordre ikke finnes eller dersom behandlingskoden
      * til ordretypen er forskjellig fra 1
      *
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        vaoakk <> 1
     c                   goto      avslutt
     c                   endif
8.09  *
8.09 c                   if        b_otyp = *on
8.09 c                   eval      w_stat = *blanks
8.09 c                   call      'FO728R'
8.09 c                   parm                    footyp
8.09 c                   parm                    w_stat
8.09 c                   if        w_stat = '1'
8.09 c                   exfmt     b18msg3
8.09 c                   goto      avslutt
8.09 c                   endif
8.09 c                   endif
8.09  *
7.05 c*                  if        faabfs = 1
7.05 c                   if        faabfs > 0
6.22 c                   eval      b_bfor = *on
6.22 c                   if        vaoska = 1
6.22 c                   eval      b_bfor = *off
6.22 c                   endif
6.22 c                   else
6.22 c                   eval      b_bfor = *off
6.22 c                   endif
      *
      * Viser melding og avbryter dersom orderen allerede er satt i
      * bestilling
      *
3.13 c                   call      'FO722R'
3.13 c                   parm                    w_retu
3.13 c                   parm                    w_firm
6.NU c                   parm                    p_numm
3.13 c                   parm                    p_suff
3.13  *
3.13 c                   if        w_retu = '8'
     c                   exfmt     a1mld
     c                   goto      avslutt
3.13 c                   endif
6.37  *
6.37  * Hvis det er snakk om intern bestilling, sjekk om vi
6.37  * vi allerede har LO500R i program stack. Gi feilmelding
6.37  *
6.37 c                   eval      r_stat = *blanks
6.37 c                   eval      r_prog = 'LO500R'
6.37 c                   eval      *in34 = *off
6.37 c                   call      'AP650R'
6.37 c                   parm                    r_prog
6.37 c                   parm                    r_stat
6.37  * Hvis programmet allerede finnes i stack, kan vi ikke kalle det på nytt
6.37 c                   if        r_stat = '1'
6.3b c**                 eval      *in34 = *on
6.3b c**                 exfmt     a1mld
6.3b c                   eval      w_ctli = 12
6.3b c                   eval      w_ctpo = 42
6.3b c                   eval      w_mld01 = 'Program vil feile. Gå ut til'
6.3b c                   eval      w_mld02 = 'meny, og prøv igjen '
6.3b  *
6.3b c                   call      'AA006C'
6.3b c                   parm                    w_ctli
6.3b c                   parm                    w_ctpo
6.3b c                   parm                    w_mld01
6.3b c                   parm                    w_mld02
6.3b  *
6.37 c                   goto      avslutt
6.37 c                   endif
      *
     c     a1taga        tag
      *
      * Oppdater Ordre-hode-register, Ordren er til behandling
      *
     c     fohel1_key    chain     fohelur
     c                   eval      fokode = 3
7.02  ** Legg inn hold kode hvis ordretype = PN
7.02 c*                   if        ((footyp = 'PN') and (u_702=*on))
7.02 c*                   eval      foityp = faak98
7.02 c*                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
      * Legger inn opplysningene i skjermbildefeltene
      *
     c                   eval      a1numm = fonumm
     c                   eval      a1suff = fosuff
     c                   eval      a1foty = footyp
     c                   eval      a1kuna = fokuna
     c                   eval      a1ftxt = vaotxt
      *
     c                   eval      a1ldor = *zero
     c                   eval      a1valg = *zero
7.06bc                   eval      a1ldat = 0
      *
5.51  *
5.51 c                   if        folety = 1
5.51 c                   eval      a1type = 1
5.51 c                   else
5.51 c                   eval      a1type = 0
5.51 c                   endif
      *
     c                   exsr      hntlev
7.07  *
7.07 c                   if        a1ldor <> 0
7.07 c                   eval      rlevl1_levr = a1ldor
7.07 c     rlevl1_key    chain     rlevl1r
7.07 c                   if        %found
7.07 c                   eval      a1lmat = rlform
7.07  * Finner evnt tekst
7.06bc                   exsr      sbform
7.07 c                   endif
7.07 c                   endif
7.07  *
5.40 c                   if        vaomot <> *blank
5.40 c                   eval      a1otyp = vaomot
5.40 c                   else
6.20  *
6.20  * sjekker om det er lagt alt. ordretype på leverandøren (distr)
6.20  *
6.36 c                   eval      a1otyp = *blank
6.20 c                   eval      lldil1_ldor = a1ldor
6.20 c                   move      folage        lldil1_lage
6.20 c     lldil1_key    chain     lldil1
6.36 c                   if        %found(lldil1)
6.36 c                   if        lpotyp <> *blank
6.20 c                   eval      a1otyp = lpotyp
6.36 c                   else
6.36 c                   eval      a1otyp = faaot7
6.36 c                   endif
6.36 c                   else
6.36 c                   eval      lldil1_lage = *blank
6.36 c     lldil1_key    chain     lldil1
6.36 c                   if        %found(lldil1)  and
6.36 c                             lpotyp <> *blank
5.40 c                   eval      a1otyp = lpotyp
6.36 c                   else
6.36 c                   eval      a1otyp = faaot7
6.36 c                   endif
5.40 c                   endif
5.40 c                   endif
6.28 c                   eval      s_ldor = a1ldor
      *
      * Det er ingen flere linjer som kan bestilles
      *
     c                   if        w_test = 0
     c                   eval      p_plor = '9'
     c                   goto      a1tag9
     c                   endif
      *
      * Send Alt
      *
     c                   write     a1bld
      *
      * Bare data
      *
     c     a1tagb        tag
      *
     c                   eval      a1navn = *blank
     c                   eval      a1sted = *blank
     c                   eval      rlevl1_levr = a1ldor
     c     rlevl1_key    chain     rlevl1r                            90
     c                   if        *in90 = *off
     c                   move      rlnavn        a1navn
     c                   movel     rlsted        a1sted
     c                   endif
      *
      *
     c                   exfmt     a1bld
      *
      * F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   eval      p_plor = '9'
     c                   goto      a1tag9
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   if        w_arcd = 'A1BLD'
      *
      * Spørring i leverandør-register
      *
     c                   if        w_afld = 'A1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    a1ldor
     c                   parm                    w_navn
     c                   goto      a1tagb
     c                   endif
      *
      * Spørring i ordretype-register
      *
     c                   if        w_afld = 'A1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    a1otyp
      *
     c                   goto      a1tagb
     c                   endif
7.06b *
7.06b * Spørring på forsendelsesmåte
7.06b *
7.06bc                   if        w_afld = 'A1LMAT'
7.06bc                   call      'RA533R'
7.06bc                   parm                    w_firm
7.06bc                   parm                    a1lmat
7.06bc                   parm                    a1lmtx
7.06b *
7.06bc                   goto      a1tagb
7.06bc                   endif
      *
     c                   endif
     c                   endif
      *
      * Sjekk, Finnes leverandør i Leverandør-register
      *
     c                   eval      rlevl1_levr = a1ldor
     c     rlevl1_key    chain     rlevl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   goto      a1tagb
     c                   endif
6.31  * hvis sperret eller passiv - feilmelding.
6.31 c                   if        rlpass = 'J' or
6.31 c                             rlsprk = 'J'
6.31 c                   eval      *in33 = *on
6.31 c                   goto      a1tagb
6.31 c                   endif
6.28  *
6.28  * Hvis endring av leverandør må det sjekkes om
6.28  * om det er lagt alt. ordretype på leverandøren (distr)
6.28  *
6.28 c                   if        s_ldor <> a1ldor
6.36 c                   eval      a1otyp = *blank
6.28 c                   eval      lldil1_ldor = a1ldor
6.28 c                   move      folage        lldil1_lage
6.28 c     lldil1_key    chain     lldil1
6.36 c                   if        %found(lldil1)
6.36 c                   if        lpotyp <> *blank
6.28 c                   eval      a1otyp = lpotyp
6.28 c                   else
6.2f c***                eval      votyl1_otyp = a1otyp
6.2f c                   eval      votyl1_otyp = footyp
6.2f c     votyl1_key    chain     votyl1r                            90
6.2f c                   if        *in90 = '0'
6.28 c                   eval      a1otyp = vaomot
6.2f c                   else
6.28 c                   eval      a1otyp = faaot7
6.28 c                   endif
6.2f c                   endif
6.36 c                   else
6.36 c                   eval      lldil1_lage = *blank
6.36 c     lldil1_key    chain     lldil1
6.36 c                   if        %found(lldil1)  and
6.36 c                             lpotyp <> *blank
5.40 c                   eval      a1otyp = lpotyp
6.36 c                   else
6.36 c                   eval      a1otyp = faaot7
6.36 c                   endif
6.36 c                   endif
      *
6.28 c                   eval      s_ldor = a1ldor
6.28 c                   goto      a1tagb
6.28 c                   endif
      *
      * Sjekk,  Finnes ordretype i Ordretype-register
      *
     c                   eval      votyl1_otyp = a1otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in32 = *on
     c                   goto      a1tagb
     c                   endif
     c                   if        vaosys <> 1
     c                   eval      *in32 = *on
     c                   goto      a1tagb
     c                   endif
     c                   if        vaoakk <> 1
     c                   eval      *in32 = *on
     c                   goto      a1tagb
     c                   endif
7.06b *
7.06b * Sjekk, Finnes leveringsmåte ??
7.06b *
7.06bc                   if        a1lmat <> 0
7.06bc                   exsr      sbform
7.06bc                   if        w_retk <> *blank
7.06bc                   eval      *in34 = *on
7.06bc                   goto      a1tagb
7.06bc                   endif
7.06bc                   endif
7.06b *
7.06b * Gyldig leveringsdato ?
7.06b *
7.06bc                   if        a1ldat <> 0
7.06bc     *dmy          test(d)                 a1ldat                 35
7.06bc                   if        *in35 = *on
7.06bc                   goto      a1tagb
7.06bc                   endif
7.06bc                   endif
7.12  *
7.12  * 1  MGR.  Ikke lov med annet enn ordretype 'T' på tilbudskunde
7.12  *
7.12 c                   if            u_s712 = *on
7.12 c                   if            fokund = w_tilb_kund
7.12 c                             and a1otyp <> 'T'
7.12 c                   eval      *in68 = *on
7.12 c                   goto      a1tagb
7.12 c                   endif
7.12 c                   endif
      *
      * Utvalg/linjer er behandlet, Legg inn verdi i feltet
      *
     c                   eval      w_regr = 1
      *
      * Subrutinen for plukking av varelinjer kalles opp
      *
     c                   if        a1valg = 0
     c                   exsr      hntlll
     c                   exsr      xd1bld
     c                   exsr      sjekk_ant
     c                   endif
      *
     c                   if        a1valg = 1
     c                   exsr      xd1bld
     c                   exsr      sjekk_ant
     c                   endif
      *
      * Dersom det ikke er valgt alle linjer,
      * og antall fortsatt er 0, skal det returneres til startbildet
      *
     c                   if        a1valg =  0 or
     c                             a1valg =  1
     c                   if        w_anta =  0
     c                   eval      w_test =  0
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      * Subrutinen for avsluttning av denne leverandør kalles opp
      *
6.23  * Parameterstyrt om man skal danne best.forslag eller bestilling
7.05  * Utvidet med delvis start med bestillingsforslag. (kode 2 - FAABFS)
6.23  *
7.05 c                   eval      s_bfor = b_bfor
7.05 c                   eval      b_slev = *off
7.05 c                   if        faabfs = 2 and
7.05 c                             b_bfor = *on
7.05 c                   eval      b_slev = *on
7.05 c                   eval      mpldi1_dlev = a1ldor
7.05 c                   eval      mpldi1_dlag = folage
7.05 c     mpldi1_key    chain     mpldi1
7.05 c                   if        not %found
7.05 c                   eval      mpldi1_dlag = 0
7.05 c     mpldi1_key    chain     mpldi1
7.05 c                   if        not %found
7.05 c                   eval      b_bfor = *off
7.05 c                   eval      b_slev = *off
7.05 c                   endif
7.05 c                   endif
7.05 c                   endif
6.23  *
6.23 c                   if        b_bfor = *on
6.23 c                   exsr      xm1win
6.23 c                   else
     c                   exsr      xh1win
6.23 c                   endif
      *
7.05 c                   eval      b_bfor = s_bfor
      *
      * Hopp til start for valg av neste leverandør
      *
     c                   goto      a1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutter behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag9        tag
5.60  *
5.60  * Oppdaterer bestilling med total-summer
5.60  *
5.60 c                   exsr      oppdat_best
      *
      * Oppdaterer ordre-register med at ordren er ferdig behandlet
      *
     c                   if        w_regr = 0
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90  = *off
     c                   eval      fokode = *zero
7.02  ** Legg inn hold kode hvis ordretype = PN
7.02 c*                   if        ((footyp = 'PN') and (u_702=*on))
7.02 c*                   eval      foityp = faak98
7.02 c*                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
     c                   goto      avslutt
     c                   endif
      *
      * Kaller opp subrutine for generering av plukkliste
      *
     c                   if        w_rest = 1
     c                   if        w_test = 1
     c                   exsr      xl1win
     c                   endif
     c                   endif
      *
      * Oppdater ORDRE-HODE-REGISTER, Ordren er ferdigbehandlet
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   eval      fokode = *zero
     c                   if        *inkc = *off
     c                   if        l1otyp <> *blank
     c                   eval      fokode = 2
7.02  ** Legg inn hold kode hvis ordretype = PN
7.02 c*                   if        ((footyp = 'PN') and (u_702=*on))
7.02 c*                   eval      foityp = faak98
7.02 c*                   endif
     c                   endif
     c                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Avslutt dersom det ikke var flere linjer igjen
      * som ikke var bestilt
      *
     c                   if        w_test = 0
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk,  Valg=1, Kall opp subprogram for utskrift
      *
     c                   if        *inkc = *off
     c                   if        l1otyp <> *blank
     c                   call      'FO600R'
     c                   parm                    w_ipor
     c                   parm                    w_firm
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
     c                   endif
     c                   endif
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle bilde for plukking av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xd1bld        begsr
      *
      * Hent opplysninger fra Ordre-hode-register
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   eval      d2numm = fonumm
     c                   eval      d2suff = fosuff
     c                   eval      d2kund = fokund
     c                   eval      d2kuna = fokuna
      *
      * Hent opplysninger fra Ordre-linje-register
      *
     c                   eval      fodtl1_line = *zero
     c                   eval      fplll1_line = *zero
     c     fodtl1_key    setll     fodtl1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Legg over leverandørnummer og navn
      *
     c                   eval      d2ldor = a1ldor
     c                   eval      d2lena = a1navn
      *
      * Send Alt
      *
     c     d2taga        tag
     c                   write     d2cmd
      *
      * Bare data
      *
     c     d2tagb        tag
     c                   exsr      dsp_subfile
      *
      * F3=Avslutt
      *
     c     *inkc         cabeq     *on           xd1slt
      *
      * F5=Forny
      *
     c                   if        *inke = *on
     c                   exsr      forny
     c                   goto      d2tagb
     c                   endif
      *
      * F8=Hent inn antall automatisk
      *
     c                   if        *inkh = *on
     c                   exsr      hentin
     c                   goto      d2tagb
     c                   endif
      *
      * Roll
      *
     c                   if        *in10 = *on
     c                   exsr      crt_subfile
     c                   goto      d2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av subfile (Alle linjer behandles hver gang)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w  = w_srrn_w + 1
     c     w_srrn_w      chain     d1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
      * Sjekk alle linjer
      *
     c                   eval      fplll1_line = d1line
     c                   exsr      xd7win
      *
     c                   enddo
      *
     c                   exsr      forny
     c                   goto      d2tagb
      *
      * Avslutter behandlingen av utplukk/resting
      *
     c     xd1slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D7 Behandle bilde for Oppdatering av plukk-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xd7win        begsr
      *
      * Benytter Z-ADD på antall inn i nytt felt fordi feltene
      * ikke er like store, benytter kortere feltlengde i bildet
      *
     c     fplll1_key    chain     fplllur                            92
      *
      * Antall er ikke tastet inn, hopp ut, Slett evnt. record
      *
     c                   if        d1leve = *zero
     c                   if        *in92  = *off
     c                   delete    fplllur
     c                   endif
     c                   goto      xd7slt
     c                   endif
      *
      * Legg inn verdier i Utplukk-linje-register
      *
     c                   eval      fklant = d1leve
      *
     c                   if        *in92 = *off
     c                   update    fplllur
     c                   else
     c                   eval      fklfir = w_firm
     c                   eval      fklonr = fplll1_numm
     c                   eval      fklsuf = fplll1_suff
     c                   eval      fkllin = fplll1_line
     c                   write     fplllur
     c                   endif
      *
      * Oppdater subfile
      *
     c     xd7slt        tag
      *
     c                   update    d1sfl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * H1 Behandle bilde for danning av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xh1win        begsr
7.fb  *
7.fb  * sjekker om depositumskrav
7.fb c                   exsr      sjekk_depo
7.fb c                   if        b_feil = *on
7.fb c                   leavesr
7.fb c                   endif
      *
5.51 c                   eval      h1valg = 1
5,51 c                   eval      h1svar = 1
      *
      * Send Alt
      *
5.52 c*                  write     h1bld
5.52 c*                  exfmt     h1bld
      *
      * F3=Handteres lik enter uten valg
      *
5.52 c*                  if        *inkc  = *on
5.52 c*                  eval      h1valg = *zero
5.52 c*                  eval      h1svar = *zero
5.52 c*                  endif
      *
      * Henter inn neste ledige bestillingsnummer
      *
     c                   exsr      hntnum
     c                   exsr      olrest
      *
     c                   if        h1valg = 1
     c                   exsr      ohrest
      *
      * Det settes nå en kode som sier at bestilling er generert,
      * noe som er en forutsetning for at bilde for :
      * 'Resten til plukkliste' skal komme opp.
      *
     c                   eval      w_rest = 1
      *
      * Beregning av priser, summer og totaler
      *
      * Statuskode = 0  gir beregning av nye summer og totaler
      * Statuskode = 1  gir henting av nye priser/rabatter samt beregning
      *
3.31 c                   eval      w_enor = ' '
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   endif
      *
      * Sjekk,  Valg=1, Kall opp subprogram for utskrift
      *
5.52 c*                  if        h1svar = 1
5.52 c*                  call      'LO600R'
5.52 c*                  parm                    w_ipor
5.52 c*                  parm                    w_firm
5.52 c*                  parm                    lohelu_numm
5.52 c*                  parm                    lohelu_suff
5.52 c*                  parm                    h1svar
5.52 c*                  endif
5.52  *
5.52  * Erstattet utskriftsvalg med å hoppe direkte over til bestilling
5.52  *
5.52 c                   eval      w_lpro = 'L'
5.52 c                   call      'LO500R '
5.52 c                   parm                    lohelu_numm
5.52 c                   parm                    w_lpro
      *
     c     xh1slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L1 Behandle bilde for danning av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xl1win        begsr
5.55  *
5.55  * NB! Her kom det opp forslag om ordretype IP når det ble valgt
5.55  *     å kjøre resten til plukkliste. Det var nok fordi siste
5.55  *     oppslag mot ordretyperegister var ordretype B og neste
5.55  *     ordretype for B gjerne er IP.  Men mange benytter ikke
5.55  *     PL som neste ordretype for O.  I stedet ligger P som neste
5.55  *     ordretype for O.  Derfor kommer P opp som forslag og det er
5.55  *     jo slett ingen god løsning.  Jeg endrer derfor dette inntil
5.55  *     videre slik at jeg legger ut samme ordretype som ordren har
5.55  *     Dersom noen benytter PL må denne i så fall overstyres, men
5.55  *     vi unngår i hvertfall å få kjørt Pakkseddel.
5.55  *     Dersom denne funksjonen skal leve videre, bør det kanskje
5.55  *     legges opp en engen kode for dette i ordretyperegister ?
5.55  *
5.55  *                  eval      votyl1_otyp = footyp
5.55  *    votyl1_key    chain     votyl1r
      *
5.55  ***>               eval      l1otyp = vaonxt
5.55 c                   eval      l1otyp = footyp
      *
      * Send Alt
      *
     c     l1taga        tag
     c                   write     l1bld
     c                   exfmt     l1bld
      *
      * F3=Handteres lik enter uten valg
      *
     c     *inkc         cabeq     *on           xl1slt
      *
      * Hopper ut dersom ordretype er blank
      *
     c     l1otyp        cabeq     *blank        xl1slt
      *
      * validerer
      *
     c                   eval      votyl1_otyp = l1otyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found(votyl1)
     c                   eval      *in31 = *on
     c                   goto      l1taga
     c                   endif
      *
     c                   if        vaosys <> 0
     c                   eval      *in32 = *on
     c                   goto      l1taga
     c                   endif
7.12  *
7.12  * 1  MGR.  Ikke lov med annet enn ordretype 'T' på tilbudskunde
7.12  *
7.12 c                   if            u_s712 = *on
7.12 c                   if            fokund = w_tilb_kund
7.12 c                             and l1otyp <> 'T'
7.12 c                   eval      *in68 = *on
7.12 c                   goto      a1taga
7.12 c                   endif
7.12 c                   endif
      *
      *
      * Legger ut resten til plukk-liste
      *
      * Oppdater Utplukk-param-register med ny ordre
      *
     c     fplolu_key    chain     fplolur                            90
     c                   if        *in90 = *on
     c                   eval      fkpfir = w_firm
     c                   eval      fkponr = fplolu_numm
     c                   eval      fkpsuf = fplolu_suff
     c                   eval      fkpoty = l1otyp
     c                   eval      fkpwsd = l_wsid
     c                   eval      fkpusr = l_user
     c                   eval      fkpalt = *zero
     c                   eval      fkpdir = *zero
     c                   eval      fkpres = 1
     c                   eval      fkpkol = *zero
     c                   eval      fkpnet = *zero
     c                   eval      fkpbru = *zero
     c                   eval      fkplmt = *blank
     c                   eval      fkpfpr = *zero
     c                   eval      fkpfte = *blank
     c                   eval      fkpepr = *zero
     c                   eval      fkpete = *blank
8.07 c                   eval      fkplag = folage
     c                   write     fplolur
     c                   endif
      *
      * Kaller opp subrutine for ulegging av varelinjer
      *
     c                   exsr      okrest
      *
     c     xl1slt        endsr
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Lag bestillingsforslag
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     xm1win        begsr
6.23  *
6.23 c                   eval      h1valg = 1
6.23 c                   eval      h1svar = 1
6.23  *
6.23  * Henter neste bestillingsforslag-nummer
6.23  *
6.23 c                   eval      w_kode = '0'
6.23 c                   eval      w_fell = laafor
6.23 c                   eval      w_type = *blank
6.23 c                   eval      w_fast = *blank
6.23  *
6.23 c                   call      'AS100R'
6.23 c                   parm                    w_kode
6.23 c                   parm                    w_firm
6.23 c                   parm                    w_fell
6.23 c                   parm                    w_type
6.23 c                   parm                    w_fast
6.nu c                   parm                    w_numm
      *
6.nu c                   eval      w_bestf = w_numm
6.23  *
6.23  * Legg ut bestillingslinjer og hode
6.23  *
6.23 c                   exsr      bes_lin_for
6.23  *
6.23 c                   if        b_line = *on
6.23 c                   exsr      bes_hode_for
6.23  * legger ut linje med at best.forslag er dannet
6.23 c                   eval      m1numm = w_bestf
6.23 c                   write     m1bld
6.23 c                   exfmt     m1bld
6.23 c                   endif
6.23  *
6.23  * noe som er en forutsetning for at bilde for :
6.23  * 'Resten til plukkliste' skal komme opp.
6.23  *
6.23 c                   eval      w_rest = 1
6.23  *
6.23 c     end_best_for  endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Legger ut ordre-linjer til Bestillingsforsllag
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     bes_lin_for   begsr
6.23  *
6.23  * Leser inn poster fra Ordre-linje-register
6.23  *
6.23 c     fodtl1_ke2    setll     fodtl1r
6.23 c     fodtl1_ke2    reade     fodtl1r
6.23  *
6.23 c                   dow       not %eof
6.23  *
6.23  * Hent opplysninger fra Utplukk-linje-register (rest/Tekst-linje)
6.23  *
6.23 c                   eval      fplll1_line = fdline
6.23 c     fplll1_key    chain     fplllur                            92
6.23 c                   if        *in92 = *off
6.23 c                   delete    fplllur
6.23 c                   endif
6.23  *
6.23  * Dersom alle linjer skal plukkes,
6.23  * legges hele antallet inn som antall plukket
6.23  * dersom linjen ikke finnes i Ordre-plukk-linje-register
6.23  *
6.23 c                   if        *in92  = *on
6.23 c                   eval      fklant = *zero
6.23  *
6.23 c                   if        a1valg = 2
6.23 c                   eval      fklant = fdanta
6.23 c                   endif
6.23  *
6.23 c                   if        a1valg = 2
6.23 c                   if        fdvare = *blank
6.23 c                   eval      fklant = 1
6.23 c                   endif
6.23 c                   endif
6.23 c*
6.23 c                   endif
6.23  *
6.23  * Antall ikke oppgitt ?
6.23  *
6.23 c                   if        fdvare <> *blank
6.23 c                   if        fklant =  *zero
6.23 c                   goto      lin_les_for
6.23 c                   endif
6.23 c                   endif
6.23  *
6.23  * Tekstlinje hvor antall ikke er oppgitt og det
6.23  * ikke er valgt alle linjer  a1valg = 2
6.23  *
6.23 c                   if        a1valg <> 2
6.23 c                   if        fdvare = *blank
6.23 c                   if        fklant = *zero
6.23 c                   goto      lin_les_for
6.23 c                   endif
6.23 c                   endif
6.23 c                   endif
6.23  *
6.23  * Tekstlinje hvor antall ikke er oppgitt og det
6.23  *
6.23 c*                  if        fdvare = *blank
6.23 c*                  if        fdanta = *zero
6.23 c*                  goto      lin_les_for
6.23 c*                  endif
6.23 c*                  endif
6.23  *
6.23  * Linje er allerede bestilt
6.23  *
6.23 c                   if        fdantb <> 0
6.23 c                   goto      lin_les_for
6.23 c                   endif
6.23 c                   exsr      sjekk_bestf
6.23 c                   if        b_besf  = *on
6.23 c                   goto      lin_les_for
6.23 c                   endif
6.23  *
6.23  * Ny record til Bestilling-linje-register
6.23  *
6.23 c                   clear                   lfdtlur
6.23 c                   eval      lffirm = w_firm
6.23 c                   eval      lfnumm = w_bestf
6.23 c                   eval      lfline = fdline
6.23  *
6.23 c                   eval      lfltyp = fdltyp
6.23 c                   eval      lfldor = a1ldor
6.23 c                   eval      lflage = fdlage
6.23 c                   eval      lfogrp = fdogrp
6.23 c                   eval      lfhgrp = fdhgrp
6.23 c                   eval      lfugrp = fdugrp
6.25 c                   eval      lflvar = fdlvar
6.23 c                   eval      lfvare = fdvare
6.23 c                   eval      lfenh1 = fdenh1
6.23 c                   eval      lftek1 = fdtek1
6.23 c                   eval      lftek2 = fdtek2
6.23  *
6.23  * Henter vekt på enhet
6.23 c                   eval      vevekt = 0
6.23 c                   eval      vvenl1_vare = lfvare
6.23 c                   eval      vvenl1_enhe = lfenh1
6.23 c     vvenl1_key    chain     vvenl1
6.23 c                   if        %found(vvenl1)
6.23 c                   eval      lfvekt = vevekt
6.23 c                   endif
6.23  *
6.23 c                   eval      lfornr = fdnumm
6.23 c                   eval      lforsu = fdsuff
6.23 c                   eval      lforli = fdline
6.23 c                   eval      lflety = fdlety
6.23 c                   eval      lfldat = fdldat
6.23  *
6.23 c                   eval      lfanta = fdanta
6.2A  * Bruker innkjøpspris hvis finnes.
6.2A c**                 eval      lflbel = fdkopr
6.2d c                   if        laaenl = 1
5.6d c                   eval      ldipny = ldkpny
6.2d c                   else
6.2A c                   eval      lflbel = fdinpr
6.2d c                   endif
6.2A c                   if        lflbel = 0
6.2A c                   eval      lflbel = fdkopr
6.2A c                   endif
6.23  *
6.23 c                   write     lfdtlur
6.23  *
6.23 c                   eval      b_line = *on
6.23  *
6.23  * Les neste post
6.23  *
6.23 c     lin_les_for   tag
6.23 c     fodtl1_ke2    reade     fodtl1r
6.23 c                   enddo
6.23  *
6.23 c                   endsr
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Legger ut bestillings hode forslag
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     bes_hode_for  begsr
6.23  *
6.23 c                   clear                   lfhelur
6.23  *
6.2b c                   eval      lfhelu_numm = w_bestF
6.23 c     lfhelu_key    chain     lfhelur
6.23  *
6.23 c                   if        not %found
6.23 c                   eval      lhfirm = w_firm
6.23 c                   eval      lhnumm = w_bestF
6.23 c                   eval      lhornr = fonumm
6.23 c                   eval      lhorsu = fosuff
6.26 c                   eval      lhotyp = a1otyp
6.23 c                   eval      lhlage = folage
6.23 c                   eval      lhavde = foavde
7.11  * Hent inn avdeling fra lager, settes forslag fra avdeling fra lager
7.11 c                   if        u_s711 = *on
7.11 c                   eval      ra10l1_jkod = folage
7.11 c     ra10l1_key    chain     ra10l1
7.11 c                   if        %found
7.11 c                   eval      lhavde = rajavd
7.11 c                   endif
7.11 c                   endif
7.11  *
7.05  *
6.23 c                   eval      lhselg = foselg
7.09 c**                 if        b_slev = *on and
7.09 c                   if        b_bfor = *on and
7.05 c                             rlsaks <> 0
7.05 c                   eval      lhselg = rlsaks
7.05 c                   endif
7.05  *
6.29 c*                  if        foldat <> *loval
6.29 c*                  eval      lhbdat = foldat
6.29 c*                  else
6.29 c                   time                    lhbdat
6.29 c*                  endif
6.23 c                   eval      lhldat = foldat
6.23 c                   eval      lhwsid = l_wsid
6.23 c                   eval      lhuser = l_user
6.23 c                   eval      lhoppr = 'O'
6.23 c                   eval      lhldor = a1ldor
6.23 c                   eval      lhlety = a1type
6.23 c                   movel     rlalfa        lhlsor
6.23  *
6.23 c                   time                    lhdate
6.23 c                   time                    lhtime
6.23  *
6.23 c                   if        lhbdat = *loval
6.23 c                   time                    lhbdat
6.23 c                   endif
6.23 c                   eval      lhornr = p_numm
6.23 c                   eval      lhorsu = p_suff
6.23 c                   eval      w_tbel = 0
6.23 c                   eval      w_tvkt = 0
6.23 c                   eval      w_stat = *blank
6.23 c                   call      'LH714R '
6.23 c                   parm                    w_bestf
6.23 c                   parm                    w_stat
6.23 c                   parm                    w_tbel
6.23 c                   parm                    w_tvkt
6.23 c                   eval      lhvekt = w_tvkt
6.23 c                   eval      lhtbel = w_tbel
6.23 c                   eval      lhstat = w_stat
6.23  *
6.23 c                   write     lfhelur
6.23 c                   endif
6.23  *
6.23 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter/Oppdatering av Nummer-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = laabrk
     c                   eval      w_type = a1otyp
     c                   eval      w_fast = l_wsid
     c                   eval      w_numm = *zero
      *
      * Kaller opp subprogram for henting av nummer fra Nummer-register
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      lohelu_numm = w_numm
     c                   eval      lohelu_suff = *zero
     c                   eval      lodtlu_numm = w_numm
     c                   eval      lodtlu_suff = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om det er registrert noe i antall til bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ant     begsr
      *
     c                   eval      w_anta = *zero
     c     fplll1_ke2    setll     fplll1
     c     fplll1_ke2    reade     fplll1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      w_anta = (w_anta + fklant)
      *
     c     fplll1_ke2    reade     fplll1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Henter neste leverandør                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     hntlev        begsr
      *
      * Leser poster fra Ordre-linje-register
      *
     c                   eval      w_test = *zero
     c     fodtl1_ke2    setll     fodtl1r
     c     fodtl1_ke2    reade     fodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
      * Er linje allerede bestillt ?
      *
     c                   if        fdantb <> 0
     c                   goto      next01_les
     c                   endif
8.01  *
8.01  * Hopp videre hvis det ikke er varenummer
8.01  *
8.01 c                   if        %trim(fdvare) = *blanks
8.01 c                   goto      next01_les
8.01 c                   endif
      *
6.23 c                   exsr      sjekk_bestf
6.23 c                   if        b_besf  = *on
6.23 c                   goto      next01_les
6.23 c                   endif
      *
     c                   if        fdanta <> 0
     c                   eval      w_test =1
     c                   endif
      *
      * Finner Leverandør
6.20 c                   call      'VL721R '
6.20 c                   parm                    w_firm
6.20 c                   parm                    fdvare
6.20 c                   parm                    fdlage
6.20 c                   parm                    w_sldo
6.20 c                   parm                    b_inlr
6.20 c                   if        w_sldo <> 0
6.20 c                   eval      a1ldor = w_sldo
6.20 c                   leave
6.20 c                   endif
      *
     c                   if        fdldor <> *zero
     c                   eval      a1ldor = fdldor
     c                   leave
     c                   endif
      *
      * Les neste post
      *
     c     next01_les    tag
     c     fodtl1_ke2    reade     fodtl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn varelinjer for denne leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     hntlll        begsr
      *
      * Leser inn en record fra Ordre-linje-register
      *
     c     fodtl1_ke2    setll     fodtl1r
     c     fodtl1_ke2    reade     fodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
      * Finner Leverandør
      *
6.20 c                   call      'VL721R '
6.20 c                   parm                    w_firm
6.20 c                   parm                    fdvare
6.20 c                   parm                    fdlage
6.20 c                   parm                    w_sldo
6.20 c                   parm                    b_inlr
6.20 c                   if        w_sldo <> 0
6.20 c                   eval      w_ldor = w_sldo
6.20 c                   else
6.20 c                   eval      w_ldor = fdldor
6.20 c                   endif
6.20 c                   if        w_ldor = a1ldor
      *
      *
      * Benytter Z-ADD på antall inn i nytt felt fordi feltene
      * ikke er like store, benytter kortere feltlengde i bildet
      *
     c                   eval      fplll1_line = fdline
     c     fplll1_key    chain     fplllur                            92
     c                   if        *in92 = *on
     c                   eval      fklfir = w_firm
     c                   eval      fklonr = fplll1_numm
     c                   eval      fklsuf = fplll1_suff
     c                   eval      fkllin = fplll1_line
     c                   eval      fklant = fdanta
     c                   write     fplllur
     c                   endif
     c                   endif
      *
     c     fodtl1_ke2    reade     fodtl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-linjer til Bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     olrest        begsr
3.17  *
3.17  * Sjekker om bestilling er brukt - legger evt. ut feilmelding
3.17  *
3.17 c                   eval      lohel1_numm = lohelu_numm
3.17 c                   eval      lohel1_suff = 0
3.17 c     lohelu_key    chain     lohel1                             90
3.17 c                   if        *in90 = *off
3.17 c                   move      lohel1_numm   w_knum_alf
3.17 c                   move      w_numm        w_rnum_alf
3.17 c                   eval      w_mld1 = 'Feil i nummertildeling - Avbryt!' +
3.17 c                                      ' R=' + w_rnum_alf + ' K=' +
3.17 c                                       w_knum_alf
3.17 c                   call      'AA005R'
3.17 c                   parm                    w_mld1
3.17 c                   goto      avslutt
3.17 c                   endif
      *
      * Leser inn poster fra Ordre-linje-register
      *
     c     fodtl1_ke2    setll     fodtl1r
     c     fodtl1_ke2    reade     fodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
      * Hent opplysninger fra Utplukk-linje-register (rest/Tekst-linje)
      *
     c                   eval      fplll1_line = fdline
     c     fplll1_key    chain     fplllur                            92
     c                   if        *in92 = *off
     c                   delete    fplllur
     c                   endif
      *
      * Dersom alle linjer skal plukkes,
      * legges hele antallet inn som antall plukket
      * dersom linjen ikke finnes i Ordre-plukk-linje-register
      *
     c                   if        *in92  = *on
     c                   eval      fklant = *zero
      *
     c                   if        a1valg = 2
     c                   eval      fklant = fdanta
     c                   endif
      *
     c                   if        a1valg = 2
     c                   if        fdvare = *blank
     c                   eval      fklant = 1
     c                   endif
     c                   endif
     c*
     c                   endif
      *
      * Skal bestilling dannes ?   h1valg = 1
      *
     c                   if        h1valg = *zero
     c                   goto      next02_les
     c                   endif
      *
      * Antall ikke oppgitt ?
      *
     c                   if        fdvare <> *blank
     c                   if        fklant =  *zero
     c                   goto      next02_les
     c                   endif
     c                   endif
      *
      * Tekstlinje hvor antall ikke er oppgitt og det
      * ikke er valgt alle linjer  a1valg = 2
      *
     c                   if        a1valg <> 2
     c                   if        fdvare = *blank
     c                   if        fklant = *zero
     c                   goto      next02_les
     c                   endif
     c                   endif
     c                   endif
      *
      * Linje er allerede bestilt
      *
     c                   if        fdantb <> 0
     c                   goto      next02_les
     c                   endif
6.23 c                   exsr      sjekk_bestf
6.23 c                   if        b_besf  = *on
6.23 c                   goto      next02_les
6.23 c                   endif
      *
      * Ny record til Bestilling-linje-register
      *
6.16 c                   clear                   lodtlur
     c                   eval      lodtlu_line = fdline
     c     lodtlu_key    chain     lodtlur                            91
     c                   if        *in91 = *on
      *
     c                   eval      ldfirm = w_firm
     c                   eval      ldnumm = lodtlu_numm
     c                   eval      ldsuff = lodtlu_suff
     c                   eval      ldline = lodtlu_line
      *
     c                   eval      ldltyp = fdltyp
3.16 c                   eval      ldlety = fdlety
     c                   eval      ldvare = fdvare
     c                   eval      ldenh1 = fdenh1
     c                   eval      ldlage = fdlage
     c                   eval      ldldor = a1ldor
     c                   eval      ldldat = *loval
     c                   eval      ldavde = *zero
     c                   eval      ldkont = *zero
     c                   eval      ldspes = *zero
6.38 c***                eval      ldproj = *zero
6.38 c                   eval      ldproj = *blank
     c                   eval      ldakti = *blank
     c                   eval      ldlvar = fdlvar
3.18 c                   eval      ldnobb = fdnobb
6.17 c                   eval      ldeann = fdeann
     c                   eval      ldtek1 = fdtek1
     c                   eval      ldtek2 = fdtek2
     c                   eval      ldogrp = fdogrp
     c                   eval      ldhgrp = fdhgrp
     c                   eval      ldugrp = fdugrp
     c                   eval      ldlvre = fdlvre
     c                   eval      ldipny = *zero
     c                   eval      ldkpny = *zero
     c                   eval      ldrab1 = *zero
     c                   eval      ldrab2 = *zero
      *
     c                   eval      ldornu = fdnumm
     c                   eval      ldorsu = fdsuff
     c                   eval      ldorli = fdline
      *
     c                   eval      ldotyp = a1otyp
      *
7.04s *  Egen kontering på direktelev varer når prosjekt
7.04sc                   if        u_kont_prosj = *on
7.04sc                   if        fdlety = 1
7.04sc                             and foproj <> *blank
7.04sc                   eval      ldlety = 9
7.04sc                   endif
7.04sc                   endif
      *
     c                   eval      ldanta = fklant
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Etterfølgende logikk er fjernet fra programmet
5.62  * og erstattet med en ny og enklere logikk
5.62  * (Koden er slettet i denne source men finnes i std. V5.60)
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Ny logikk er lagt inn
5.62  *
5.62 c                   eval      ldkpny = fdkopr
5.62 c                   eval      ldipny = fdinpr
5.62  *
6.11 c**                 if        ldipny = *zero
6.11 c**                 eval      ldipny = ldkpny
6.11 c**                 endif
5.62  *
5.62 c                   if        laaenl = 1
5.62 c                   eval      ldipny = ldkpny
6.11 c                   else
6.11 c                   if        ldipny = *zero
6.11  *
6.11  * Finner innkjøpspris fra register dersom den skal overstyre.
6.11  * Gjelder bare lagervarer.
6.11  *
6.11 c                   eval      vvarl1_vare = fdvare
6.11 c     vvarl1_key    chain     vvarl1r                            90
6.11  *
6.14 c                   if        *in90 = *off
6.14  * finner varetype
6.14 c                   call      'VL710R  '
6.14 c                   parm                    w_firm
6.14 c                   parm                    fdvare
6.14 c                   parm                    fdlage
6.14 c                   parm                    w_vtyp
6.24 c                   parm                    w_udat
6.24 c                   parm                    w_ldor
6.24 c                   parm                    b_inlr
6.3a c                   parm                    w_lv_nobb
6.3a c                   parm                    w_lv_modn
6.3a c                   parm                    w_lv_lldo
6.3a c                   parm                    w_lv_llva
      *
7.01 c                   if        w_vtyp = 'L' or ((u_701 = *on) and
7.01 c                             (w_vtyp = 'S'))
6.11 c                   eval      w_lety = 0
6.11 c                   eval      w_prgr = fdprgr
6.27 c                   eval      w_kamp = lokamp
6.11  *
6.11 c                   call      'VP753R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    fdvare
6.11 c                   parm                    w_prgr
6.11 c                   parm                    fdenh1
6.11 c                   parm                    w_lety
6.11 c                   parm                    w_date
6.11 c                   parm                    w_kpny
6.11 c                   parm                    ldipny
6.27 c                   parm                    w_kamp
6.14 c                   endif
6.14 c                   endif
6.13 c                   if        ldipny = *zero
6.13 c                   eval      ldipny = ldkpny
6.13 c                   endif
6.13 c                   endif
5.62 c                   endif
5.62  *
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
5.60 c                   eval      ldsumk = ldkpny * ldanta
5.60 c                   eval      ldsumi = ldipny * ldanta
      *
6.32  * Vi må sette mvakode på linja
      *
6.32 c                   eval      ldomva = '1'
6.34 c                   if        rlmkod = 1
6.32 c                   eval      ldomva = '0'
6.32 c                   goto      endtes
6.32 c                   endif
6.39 c                   if        rlmkod = 2
6.39 c                   eval      ldomva = 'C'
6.39 c                   endif
6.32  * Sjekk evnt overstyring fra varen
6.32 c                   eval      vvarl1_vare = fdvare
6.32 c     vvarl1_key    chain     vvarl1r
6.32 c                   if        %found
6.32 c                   eval      vmval1_mkod = vvkmva
6.32 c     vmval1_key    chain     vmval1
6.32 c                   if        %found
6.32 c                   eval      ldomva = vamkla
6.32 c                   else
6.32 c                   select
6.32 c                   when      vvkmva = 1
6.32 c                   eval      ldomva = '0'
6.32 c                   when      vvkmva = 2
6.32 c                   eval      ldomva = 'I'
6.32 c                   when      vvkmva = 3
6.32 c                   eval      ldomva = 'K'
6.32 c                   endsl
6.32 c                   endif
6.32 c                   endif
6.32  *
6.32 c     endtes        tag
      *
      * Kaller opp subrutine for oppdatering av lager
      * Trekker ut antall for vedlikeholdt linje
      *
     c                   if        laalag <> *zero
     c                   exsr      opplag
     c                   endif
      *
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   endif
      *
      * Oppdaterer ordre-linje-register med nytt antall bestilt
      *
     c                   eval      fodtl1_line = fdline
     c     fodtl1_key    chain     fodtlur                            91
     c                   if        *in91  = *off
     c                   eval      fdantb = fklant
     c                   eval      fdbenr = lohelu_numm
5.53 c                   eval      fdbsuf = lohelu_suff
5.53 c                   eval      fdblin = lodtlu_line
      *
     c                   if        fdvare = *blank
     c                   if        fklant = 3
     c                   eval      fdantb = 0
     c                   eval      fdbenr = 0
     c                   eval      fdbsuf = 0
     c                   eval      fdblin = 0
     c                   endif
     c                   endif
     c*
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
      * Les neste post
      *
     c     next02_les    tag
     c     fodtl1_ke2    reade     fodtl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-heading for resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ohrest        begsr
6.16 c                   clear                   lohelur
      *
     c     fohel1_key    chain     fohel1r                            90
     c     lohelu_key    chain     lohelur                            90
      *
     c                   eval      loline = *zero
     c                   eval      lolage = folage
5.01 c                   eval      loavde = foavde
7.11  * Hent inn avdeling fra lager, settes default fra ordrehodet
7.11 c                   if        u_s711 = *on
7.11 c                   eval      ra10l1_jkod = folage
7.11 c     ra10l1_key    chain     ra10l1
7.11 c                   if        %found
7.11 c                   eval      loavde = rajavd
7.11 c                   endif
7.11 c                   endif
7.11  *
     c                   eval      loinnk = foselg
     c                   eval      lobinr = *zero
     c                   eval      lokref = *zero
     c                   eval      lobdat = w_date
     c                   eval      loldat = foldat
7.06bc                   if        a1ldat <> 0
7.06bc     *dmy          move      a1ldat        loldat
7.06bc                   endif
     c                   eval      lofkda = *loval
     c                   eval      loffda = *loval
      *
     c                   eval      lootyp = a1otyp
     c                   eval      loldor = a1ldor
6.2c c                   movel     rlalfa        lolena
     c                   eval      lovref = fovref
     c                   eval      lodref = *blank
     c                   eval      lorekv = *blank
6.15 c                   eval      lomkod = rlmkod
6.2e c                   eval      lobetb = rlbetb
6.30 c                   eval      loproj = foproj
6.30 c                   eval      loarbo = foproj
7.10 c                   if        u_s710 = *on
7.10 c                   eval      loproj = *blank
7.10 c                   eval      loarbo = *blank
7.10 c                   endif
6.35 c                   eval      lolmat = rlform
7.06bc                   eval      lolmat = rlform
7.06bc                   eval      lolmat = a1lmat
7.06bc                   eval      lolmtx = a1lmtx
      *
     c                   eval      loornr = p_numm
     c                   eval      loorsu = p_suff
      *
     c                   eval      lowsid = l_wsid
     c                   eval      louser = l_user
     c                   eval      lodate = w_date
     c                   eval      lotime = w_time
      *
     c                   if        a1type = 0
     c                   exsr      hntltx
     c                   else
5.32 c                   if        fonavn <> *blank
     c                   movel     fonavn        lonavn
     c                   move      foadr1        loadr1
     c                   move      foadr2        loadr2
     c                   move      fosted        losted
5.32 c                   else
5.32 c                   exsr      hntfad
5.32 c                   movel     rknavn        lonavn
5.32 c                   move      rkgate        loadr1
5.32 c                   move      rkadr2        loadr2
5.32 c                   move      rksted        losted
     c                   endif
5.32 c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      lofirm = w_firm
     c                   eval      lonumm = lohelu_numm
     c                   eval      losuff = lohelu_suff
     c                   eval      lokode = *zero
6.10 c                   time                    lodate
6.10 c                   time                    lotime
6.10 c                   eval      louser = l_user
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   write     lohelur
8.03  *
8.03  * Legg ut post på tilleggsregister
8.03  *
8.03 c     lohelu_key    chain     lotii2r
8.03 c                   if        not %found
8.03  *
8.03 c                   if        folety = 1
8.03 c                   eval      lotko2 = 'B'
8.03 c                   else
8.03 c                   eval      lotko2 = 'L'
8.03 c                   endif
8.03  *
8.03 c                   eval      lotfir = w_firm
8.03 c                   eval      lotnum = lohelu_numm
8.03 c                   eval      lotsuf = lohelu_suff
8.03  *
8.03 c                   time                    loteda
8.03 c                   time                    loteti
8.03 c                   eval      loteus = l_user
8.03  *
8.03 c                   write     lotii2r
8.03 c                   endif
8.04  *
8.04  *  Oppdaterer referanse til bestilling på ordrehode
8.04  *
8.04 c     fohel1_key    chain     fohelur                            90
8.04 c                   if        *in90  = *off
8.04 c                   eval      fokode = *zero
8.04 c                   eval      fobenr = lohelu_numm
8.04 c                   eval      fobsuf = lohelu_suff
8.04 c                   time                    foedat
8.04 c                   time                    foetim
8.04 c                   eval      foeusr = l_user
8.04 c                   update    fohelur
8.04 c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-linjer til plukk-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     okrest        begsr
      *
      * Leser inn poster fra Ordre-linje-register
      *
     c     fodtl1_ke2    setll     fodtl1r
     c     fodtl1_ke2    reade     fodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
      * Oppdatere utplukk med aktuelle linjer
      *
     c                   if        fdantb = *zero
      *
     c                   eval      fplll1_line = fdline
     c     fplll1_key    chain     fplllur                            92
     c                   if        *in92  = *on
     c                   eval      fklfir = w_firm
     c                   eval      fklonr = fplll1_numm
     c                   eval      fklsuf = fplll1_suff
     c                   eval      fkllin = fplll1_line
     c                   eval      fklant = fdanta
     c                   write     fplllur
     c                   else
     c                   eval      fklant = fdanta
     c                   update    fplllur
     c                   endif
      *
     c                   endif
      *
     c     fodtl1_ke2    reade     fodtl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = ldvare
5.31 c                   eval      hllage = ldlage
     c                   eval      hlenhe = ldenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = a1otyp
     c                   eval      hlltyp = ldltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
3.16 c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk/Hent tekst for lager                        RS710R
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntltx        begsr
      *
     c                   eval      w_lage = lolage
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_text
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    w_feil
      *
     c                   movel     w_text        lonavn
     c                   move      w_adrs        loadr1
     c                   move      *blank        loadr2
     c                   move      w_ponr        w_pnra
     c                   eval      losted = %trim(w_pnra + ' ' + w_pste)
      *
      * Sjekk om logistikkpunkt er registrert
      *
     c                   exsr      hent_logp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter logistikkpunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logp     begsr
      *
     c                   eval      w_skaf = 'S'
8.06 c                   eval      w_ekst = ' '
8.06 c*                  exsr      sjekk_ekst
8.02 c                   if        w_ekst <> ' '
8.02 c                   eval      w_stat  = ' '
8.02 c                   goto      upd_log
8.02 c                   endif
      *
     c     les_log       tag
     c                   call      'MJ500R'
     c                   parm                    w_stat
     c                   parm                    lolage
     c                   parm                    loavde
     c                   parm                    w_skaf
     c                   parm                    w_eann
     c                   parm                    w_sted
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_pste
      *
8.02 c     upd_log       tag
     c                   if        w_stat = *blank
     c                   eval      lonavn = w_sted
     c                   eval      loadr1 = w_adr1
     c                   eval      loadr2 = w_adr2
     c                   eval      losted = w_pste
     c                   endif
      *
     c     end_log       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om leveringsadresse skal overstyres fra eksternlager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ekst    begsr
8.02
8.02     exec SQL
8.02       select rkrela into :w_ekst
8.02         from ra36st
8.02         where rkrfir = :w_firm
8.02           and rkrfsk = :folmat
8.02           and rkrsla = :folage;
8.02
8.02        if sqlcod < 0 or
8.02        sqlcod = 100;
8.05          w_ekst = ' ';
8.02        endif;
8.02
8.05     if w_ekst <> ' ';
8.02      exec SQL
8.02        select rkenav, rkead1, rkead2,rkeste
8.02          into :w_sted, :w_adr1, :w_adr2, :w_pste
8.02         from ra37st
8.02         where rkefir = :w_firm
8.02           and rkeela = :w_ekst;
8.02
8.02        if sqlcod < 0 or
8.02        sqlcod = 100;
8.05          w_ekst = ' ';
8.02        endif;
8.02     endif;
8.02
     c                   endsr
5.32  *
5.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.32  * Sjekk/Hent fakturaadresse på kunde
5.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.32  *
5.32 c     hntfad        begsr
5.32  *
5.32 c                   eval      rkunl1_kund = fokund
5.32 c     rkunl1_key    chain     rkunl1r
5.32 c                   if        not %found
5.32 c                   eval      rknavn = *blank
5.32 c                   eval      rkgate = *blank
5.32 c                   eval      rkadr2 = *blank
5.32 c                   eval      rksted = *blank
5.32 c                   endif
5.32  *
5.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fornyer subfilen (dvs. loader subfilen pånytt)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     d1sfl
     c                   endif
      *
     c                   eval      fodtl1_line = 0
      *
     c     fodtl1_key    setll     fodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Display subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     d2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tøm subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
     c                   eval      *in14 = *on
     c                   write     d2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
     c                   eval      w_spge = w_spge + 14
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fodtl1                                 15
     c                   if        *in15 = *off
     c                   if        fdnumm <> fodtl1_numm
     c                   eval      *in15 = *on
     c                   endif
     c                   endif
     c                   if        *in15 = *off
     c                   if        fdsuff <> fodtl1_suff
     c                   eval      *in15 = *on
     c                   endif
     c                   endif
      *
     c     *in15         cabeq     *on           xend02
      *
     c                   if        fdantb <> *zero
     c                   iter
     c                   endif
6.23 c                   exsr      sjekk_bestf
6.23 c                   if        b_besf  = *on
6.23 c                   iter
6.23 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      fplll1_line = fdline
     c     fplll1_key    chain     fplll1                             95
     c                   if        *in95 = *on
     c                   eval      d1leve = *zero
     c                   endif
     c                   if        *in95 = *off
     c                   eval      d1leve = fklant
     c                   endif
      *
     c                   eval      d1line = fdline
     c                   eval      d1anta = fdanta
6.20 c                   call      'VL721R '
6.20 c                   parm                    w_firm
6.20 c                   parm                    fdvare
6.20 c                   parm                    fdlage
6.20 c                   parm                    w_sldo
6.20 c                   parm                    b_inlr
6.20 c                   if        w_sldo <> 0
6.20 c                   eval      d1ldor = w_sldo
6.20 c                   else
     c                   eval      d1ldor = fdldor
6.20 c                   endif
     c                   movel     fdvare        d1vare
     c                   move      fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
7.06  *
7.06  * Sette farge
7.06  *
7.06 c/exec sql
7.06 c+                  select vvsort
7.06 c+                  into :w_sort
7.06 c+                  from vvarpf
7.06 c+                  where vvfirm = :w_firm and vvvare = :fdvare
7.06 c/end-exec
7.06 c                   if        u_fsor = *on
7.06 c                   eval      *in53  = *off
7.06 c                   eval      *in54  = *off
7.06 c                   if        w_sort = w_fso1
7.06 c                   eval      *in53 = *on
7.06 c                   endif
7.06 c                   if        w_sort = w_fso2
7.06 c                   eval      *in54 = *on
7.06 c                   endif
7.06 c                   else
7.06 c                   if        u_fskv = *on
7.06 c                   eval      *in54 = *off
7.06 c                   if        fdlvre = 1
7.06 c                   eval      *in54 = *on
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
     c                   write     d1sfl
     c                   enddo
      *
     c     xend02        tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_svrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent inn antall automatisk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hentin        begsr
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w  = w_srrn_w + 1
     c     w_srrn_w      chain     d1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   eval      d1leve    = d1anta
     c                   if        d1leve    = 0
     c                   eval      d1leve    = 1
     c                   endif
      *
     c                   update    d1sfl
     c                   enddo
      *
     c                   endsr
5.60  *
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Oppdaterer bestilling
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     oppdat_best   begsr
5.60  *
5.60  * Henter ny bestillings-total
5.60  *
5.60 c                   exsr      hent_total
5.60  *
5.60  * Oppdaterer bestilling-hode med ny saldo
5.60  *
5.60 c     lohelu_key    chain     lohelur
5.60  *
5.60 c                   if        %found(lohelu)
5.60 c                   eval      lototi = w_toti
5.60 c                   eval      lototr = w_totr
5.60 c                   eval      lototk = w_totk
5.60  *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
5.60 c                   update    lohelur
5.60 c                   endif
5.60  *
5.60 c                   endsr
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine for henting av bestillingstotal
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     hent_total    begsr
5.60  *
5.60 c                   eval      w_totk = 0
5.60 c                   eval      w_toti = 0
5.60 c                   eval      w_totr = 0
5.60  *
5.60 c     lodtlr_key    setll     lodtlr
5.60 c     lodtlr_key    reade     lodtlr
5.60 c                   dow       not %eof
5.60 c                   eval      w_totk = w_totk + ldsumk
5.60 c                   eval      w_toti = w_toti + ldsumi
5.60 c                   eval      w_totr = w_totr + ldsumr
5.60 c     lodtlr_key    reade     lodtlr
5.60 c                   enddo
5.60  *
5.60 c                   endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for henting av bestillingstotal
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     sjekk_bestf   begsr
6.23  *
6.23 c                   eval      b_besf  = *off
6.23 c                   eval      lfdtl6_ornr = fdnumm
6.23 c                   eval      lfdtl6_orsu = fdsuff
6.23 c                   eval      lfdtl6_orli = fdline
6.23 c     lfdtl6_key    chain     lfdtl6
6.23 c                   if        %found(lfdtl6)
6.23 c                   eval      b_besf  = *on
6.23 c                   endif
6.23  *
6.63 c                   endsr
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om depositumsregler følges              s
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
7.fb c                   eval      b_feil = *off
7.fb  *
7.fb  * hvis krav til beløp, sjekk først om ordre er større enn dette
7.fb c                   if        faak11 = 1 and
7.fb c                             faakbs <> 0
7.fb  * Beregn ordretotal
7.fb c                   eval      w_sumi = 0
7.fb c                   eval      w_sumu = 0
7.fb c                   call      'FO704R '
7.fb c                   parm                    w_firm
7.fb c                   parm                    p_numm
7.fb c                   parm                    p_suff
7.fb c                   parm                    w_sumi
7.fb c                   parm                    w_sumu
7.fb c                   if        w_sumi < faakbs
7.fb c                   leavesr
7.fb c                   endif
7.fb c                   endif
7.fb  *
7.fb  * Sjekker om depositum er betalt
7.fb  * 2 muligheter, hvis man kjører depositumsløsning og kode kontant-kode satt, sjekk eller at
7.fb  * man ikke kjører depositumsløsning
7.fb c                   if        faak11 = 0 or
7.fb c                             (faak11 = 1 and
7.fb c                              faak12 = 1)
7.fb c                   eval      fohel1_numm = p_numm
7.fb c                   eval      fohel1_suff = p_suff
7.fb c     fohel1_key    chain     fohel1
7.fb c                   if        %found(fohel1)
7.fb c                   eval      sdepl1_numm = fonumm
7.fb c                   eval      sdepl1_kund = fokund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1
7.fb c                   if        %found(sdepl1)
7.fb c                   if        skdepo > 0 and
7.fb c                             skdepo > skdepb
7.fb c                   eval      b_feil  = *on
7.fb c                   eval      w_mld1 = 'Depositum er ikke betalt på ordren'
7.fb c                   call      'AA005R'
7.fb c                   parm                    w_mld1
7.fb c                   leavesr
7.fb c                   endif
7.fb c                   else
7.fb c                   if        faak12 = 1
7.fb  * sjekk om kunde er kontantkunde
7.fb  *
7.fb c                   eval      rkunl1_kund = fokund
7.fb c     rkunl1_key    chain     rkunl1
7.fb c                   if        %found(rkunl1) and
7.fb c                             rkkres = 2
7.fb c                   eval      b_feil  = *on
7.fb c                   eval      w_mld1 = 'Depositum er ikke betalt på ordren'
7.fb c                   call      'AA005R'
7.fb c                   parm                    w_mld1
7.fb c                   leavesr
7.fb c                   endif
7.fb  *  eller har BM-kort eller Santander
7.fb c                   if        fopsuf = 5 or fopsuf = 4
7.fb c                   eval      b_feil  = *on
7.fb c                   eval      w_mld1 = 'Depositum er ikke betalt på ordren'
7.fb c                   call      'AA005R'
7.fb c                   parm                    w_mld1
7.fb c                   leavesr
7.fb c                   endif
7.fb c                   endif
7.fb c                   endif
7.fb c                   endif
7.fb c                   endif
7.fb  *
7.fb c                   endsr
      *
7.06b * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06b *  Sjekk/Hent tekst for forsendelsesmåte
7.06b * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06b *
7.06bc     sbform        begsr
7.06b *
7.06bc                   eval      a1lmtx = *blank
7.06b *
7.06bc                   call      'RS731R'
7.06bc                   parm                    w_retk
7.06bc                   parm                    a1lmat
7.06bc                   parm                    a1lmtx
7.06b *
7.06bc                   endsr
7.06b *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
      *
     c                   eval      w_spge = 0
     c                   eval      w_srrn = 0
      *
     c                   eval      w_regr = *zero
     c                   eval      w_enor = *blank
     c                   eval      w_ipor = *blank
     c                   eval      p_plor = *blank
      *
     c                   eval      w_kode = *blank
     c                   eval      w_fell = *blank
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
      * Key for file : Leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
5.32  * Key for file : KUNDE-REGISTER
5.32  *
5.32 c     rkunl1_key    klist
5.32 c                   kfld                    w_firm
5.32 c                   kfld                    rkunl1_kund
      *
      * Key for file : Status-koder Ofak
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Status-koder Lager
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Ordre-type-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : Ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : Ordre-linje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : Ordre-linje-register les 2
      *
     c     fodtl1_ke2    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
3.17  *
3.17  * Key for lese : Ordre-hode-register, for bestilling
3.17  *
3.17 c     lohel1_key    klist
3.17 c                   kfld                    w_firm
3.17 c                   kfld                    lohel1_numm
3.17 c                   kfld                    lohel1_suff
      *
      * Key for file : Ordre-hode-register, for bestilling
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for file : Ordre-linje-register, for bestilling
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for file : Utplukk-param-register
      *
     c     fplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplolu_numm
     c                   kfld                    fplolu_suff
      *
      * Key for file : Utplukk-linje-register les
      *
     c     fplll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplll1_numm
     c                   kfld                    fplll1_suff
     c                   kfld                    fplll1_line
      *
      * Key for file : Utplukk-linje-register les 2
      *
     c     fplll1_ke2    klist
     c                   kfld                    w_firm
     c                   kfld                    fplll1_numm
     c                   kfld                    fplll1_suff
      *
5.60  *
5.60 c     lodtlr_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    lodtlu_numm
5.60 c                   kfld                    lodtlu_suff
6.11  *
6.11  * Key for file : Vare-register
6.11  *
6.11 c     vvarl1_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vvarl1_vare
      *
6.20  *
6.20  * Key for les av leverandør distribusjon
6.20  *
6.20 c     lldil1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lldil1_ldor
6.20 c                   kfld                    lldil1_lage
6.23  *
6.23  *  Key for oppdatering av FORSLAG-HODE
6.23  *
6.23 c     lfhelu_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    lfhelu_numm
6.23  *
6.23  *  Key for les av best.forsl.linjer pr. ordre
6.23  *
6.23 c     lfdtl6_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    lfdtl6_ornr
6.23 c                   kfld                    lfdtl6_orsu
6.23 c                   kfld                    lfdtl6_orli
6.23  *
6.23  *  Key for les av enhetsregister
6.23  *
6.23 c     vvenl1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvenl1_vare
6.23 c                   kfld                    vvenl1_enhe
6.32  *
6.32  * Key for oppdat av mva kode
6.32  *
6.32 c     vmval1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vmval1_mkod
7.05  *
7.05  *  Key for les av lev. for bestillingsforslag
7.05  *
7.05 c     mpldi1_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    mpldi1_dlev
7.05 c                   kfld                    mpldi1_dlag
7.11  *
7.11  * Key for oppdat av lager
7.11  *
7.11 c     ra10l1_key    klist
7.11 c                   kfld                    w_firm
7.11 c                   kfld                    ra10l1_jkod
7.fb  *
7.fb  *  Key for les  av depositum
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
7.fb  *
7.fb  * Key for oppslag på ofak-koderegister sideregister
7.fb  *
7.fb c     fst2l1_key    klist
7.fb c                   kfld                    w_firm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_plor
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FO734R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
7.02  **
7.02  ** Endring 7.02
7.02  **
7.02   //CallP CO402R(l_filg:w_firm:0:'FO734R_702':
7.02   //                 co402_verdi1:co402_verdi2);
7.02   //if co402_verdi1 = '1';
7.02   //  u_702 = *on;
7.02   //endif;
7.06  *
7.06  * Test om fargekode fra sortimentskode   7.06
7.06  *
7.06   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFF_SORTIMENT':
7.06                    co402_verdi1:co402_verdi2);
7.06   if co402_verdi1 = '1';
7.06     u_fsor = *on;
7.06     w_fso1 = %subst(co402_verdi2:3:%scan('B=':co402_verdi2) - 3);
7.06     w_fso2 = %subst(co402_verdi2:%scan('B=':co402_verdi2) + 2:10);
7.06   endif;
7.06  *
7.06  * Skal skaffevarer ha farger
7.06  *
7.06   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFFEVARE':
7.06                    co402_verdi1:co402_verdi2);
7.06   if co402_verdi1 = '1';
7.06     u_fskv = *on;
7.06   endif;
      *
      * Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = *zero
      *
     c                   eval      fplolu_numm = p_numm
     c                   eval      fplolu_suff = p_suff
      *
     c                   eval      fplll1_numm = p_numm
     c                   eval      fplll1_suff = p_suff
     c                   eval      fplll1_line = *zero
      *
      * Henter opplysninger fra Ofak-status-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
7.fb c     fst2l1_key    chain     fst2l1
      *
      * Henter opplysninger fra Lager-status-register
      *
     c     lstsl1_key    chain     lstsl1r                            60
      *
      * Legger inn dagens dato
      *
     c                   time                    w_date
     c                   time                    w_time
7.10  *
7.10  * Ikke ta med prosjekt og underprosjekt fra ordre
7.10  *
7.10   CallP CO402R(l_filg:w_firm:0:'FO734_710':
7.10                    co402_verdi1:co402_verdi2);
7.10   if co402_verdi1 = '1';
7.10     u_s710 = *on;
7.10   endif;
7.11  *
7.11  * Avdeling styres fra lager og ikke fra ordrehodet
7.11  *
7.11   CallP CO402R(l_filg:w_firm:0:'FO734_711':
7.11                    co402_verdi1:co402_verdi2);
7.11   if co402_verdi1 = '1';
7.11     u_s711 = *on;
7.11   endif;
7.12  *
7.12  * Ikke lov med annet enn ordretype 'T' på tilbudskunde
7.12  *
7.12   CallP CO402R(l_filg:w_firm:0:'BARE_OT_T_V_TILBUDSKUNDE':
7.12                    co402_verdi1:co402_verdi2);
7.12   if co402_verdi1 = '1';
7.12     u_s712 = *on;
7.12     w_tilb_kund = %dec(co402_verdi2:6:0);
7.12   endif;
7.04s *
7.04s * Egen kontering på direktelev varer når prosjekt
7.04s *
7.04s  CallP CO402R(l_filg:w_firm:0:'KTO_INNKJ_PROSJ':
7.04s                   co402_verdi1:co402_verdi2);
7.04s  if co402_verdi1 = '1';
7.04s    u_kont_prosj = *on;
7.04s  endif;
7.08  *
7.08  * Valg vedr avslutning bestilling
7.08  *
7.08   CallP CO402R(l_filg:w_firm:0:'ORDRE_BEST_VALG_VARELINJER':
7.08                    co402_verdi1:co402_verdi2);
7.08   if co402_verdi1 = '1';
7.08     w_valg_vare = %subst(co402_verdi2:1:1);
7.08   endif;
8.09  *
8.09  * Skal vi sjekke om ordrteypen kan plukke til bestilling ?
8.09  *
8.09   CallP CO402R(l_filg:w_firm:0:'SO_SPERRET_BESTILLING':
8.09                    co402_verdi1:co402_verdi2);
8.09   if co402_verdi1 = '1';
8.09     b_otyp = *on;
8.09   endif;
      *
      *
     c                   endsr
