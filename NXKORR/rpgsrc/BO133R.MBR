     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BO133R
      * Beskrivelse . . : Oppsett av dagsoppgjør på batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *K0000  040925 bsa  Nytt program
      *
      *
8.00  *K00 140621 sst NXS-9050 Kjøre dagsoppgjør fra Jobbplan
8.01  *K00 040923 sst NXS-17211 Justert rutine for sperring for kjøring fra andr
8.02  *K00 281123 sst NXS-17211 Styre valg F6 vedr aut. dags.oppgj fra switch
8.03  *K00 100624 bsa     Skille dagsoppgjør og oppdatering til økonomi i egne
8.03  *K00 100624         parametre.
8.04  *K00 110624 bsa     Hente inn informasjon om oppsett.
8.05  *K00 110624 bsa     Kan ikke kjøre oppdatering av økonomi sammen med
8.05  *K00 110624 bsa     dagsoppgjør hvis de har NX økonomi.
8.06  *K00 040925 bsa     Mulighet for å legge inn 2 spm og svar
8.07  *K00 090925 bsa     Legger ut flere felter for default oppsett
8.08  *K00 100925 bsa     Legger ut flere felter for default oppsett
8.09  *K00 101025 bsa     Oppdaterer verdier for automatisk dagsoppgjør
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.00 fbdoplu    uf a e           k disk    rename(bdoppfr:bdoplur)
8.05 fxtill1    if   e           k disk    rename(xtilpfr:xtill1r)
     fbo133d    cf   e             workstn
      *
     d                 ds
     d ldprec                       256
     d  ldotal                        4    overlay(ldprec)
     d  ldot01                        2    overlay(ldprec:5)
     d  ldot02                        2    overlay(ldprec:7)
     d  ldot03                        2    overlay(ldprec:9)
     d  ldot04                        2    overlay(ldprec:11)
     d  ldot05                        2    overlay(ldprec:13)
     d  ldot06                        2    overlay(ldprec:15)
     d  ldot07                        2    overlay(ldprec:17)
     d  ldot08                        2    overlay(ldprec:19)
     d  ldot09                        2    overlay(ldprec:21)
     d  ldot10                        2    overlay(ldprec:23)
     d  ldwsgr                        6    overlay(ldprec:25)
     d  ldwsal                       10    overlay(ldprec:31)
     d  ldws01                       10    overlay(ldprec:41)
     d  ldws02                       10    overlay(ldprec:51)
     d  ldws03                       10    overlay(ldprec:61)
     d  ldws04                       10    overlay(ldprec:71)
     d  ldws05                       10    overlay(ldprec:81)
     d  ldws06                       10    overlay(ldprec:91)
     d  ldws07                       10    overlay(ldprec:101)
     d  ldws08                       10    overlay(ldprec:111)
     d  ldws09                       10    overlay(ldprec:121)
     d  ldws10                       10    overlay(ldprec:131)
     d  ldavst                        1  0 overlay(ldprec:142)
     d  lddato                       10d   datfmt(*iso)
     d                                     overlay(ldprec:143)
     d  ldperi                        6  0 overlay(ldprec:153)
     d  lddekn                        1  0 overlay(ldprec:159)
     d  ldvdag                        2  0 overlay(ldprec:160)
     d  lddeta                        1  0 overlay(ldprec:162)
     d  ldtota                        1  0 overlay(ldprec:163)
     d  ldfrad                       10d   datfmt(*iso)
     d                                     overlay(ldprec:164)
     d  ldtild                       10d   datfmt(*iso)
     d                                     overlay(ldprec:174)
     d  ldaarr                        4  0 overlay(ldprec:184)
     d  ldlavs                        8  0 overlay(ldprec:188)
     d  ldldet                        6  0 overlay(ldprec:196)
     d  ldtime                        6  0 overlay(ldprec:202)
     d  ldkred                        1  0 overlay(ldprec:208)
8.00 d  ldjobp                        1    overlay(ldprec:209)
8.03 d  ldokoo                        1    overlay(ldprec:210)
      *
      * Definering av local data area
      *
     d lda            uds
     d l_prtf                800    809
     d l_chgv                844    844
7.02 d l_user                911    920
7.02 d l_filg                931    933
     d l_sfir                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(bopfir)
7.01 d bdoplu_sgr      s                   like(bopsgr)
8.05 d xtill1_regi     s                      like(xtregi)
8.05 d xtill1_felt     s                      like(xtfelt)
8.05 d xtill1_xkey     s                      like(xtxkey)
      *
      * Definering av variable
      *
8.01 d w_in12          s              1
8.01 d w_type          s              1
8.01 d w_mld1          s             50
8.01 d w_mld2          s             50
8.01 d w_mld3          s             50
8.01 d w_svar          s              1
8.02 d w_prog          s             50
      *
6.22  *
6.22  * Firmastyrt validering av felt
6.22  *
6.22 d u_wsgr          s              1    inz(*off)
7.02 d u_702           s              1    inz(*off)
      *
7.02   dcl-s co402_verdi1  char(1);
7.02   dcl-s co402_verdi2  char(2048);
7.02   DCL-PR CO402R EXTPGM('CO402R');
7.02     p_filgrp            char(3)     const;
7.02     p_firm              packed(3:0) const;
7.02     p_lager             packed(2:0) const;
7.02     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.02     p_verdi2            char(2048);
7.02   END-PR;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05  * Tilleggsregister
8.05  *
8.05 c                   eval      xtill1_regi = 'FSTSPF'
8.05 c                   eval      xtill1_felt = 'OKOSYS'
8.05 c                   eval      xtill1_xkey = *blanks
8.05 c     xtill1_key    chain     xtill1
8.05 c                   if        not %found
8.05 c                   eval      xtnumm = 99
8.05 c                   endif
8.01  *  Sjekk om dagsoppgjør er satt opp til å kjøres fra jobbplan
8.01  *
8.01 c                   eval      bdoplu_sgr  = *blank
8.01 c     bdoplu_key    chain     bdoplu
8.01 c                   if        %found(bdoplu)
8.01 c                   eval      ldprec = boprec
8.03 c                   eval      b1okoo = ldokoo
8.03 c                   eval      b1jobp = ldjobp
8.07 c                   eval      b1avst = ldavst
8.07 c                   eval      b1dekn = lddekn
8.07 c                   eval      b1deta = lddeta
8.07 c                   eval      b1tota = ldtota
8.08 c                   eval      b1kred = 0
8.01 c                   if        ldjobp = 'J'
8.01  *
8.01 c                   call      'AP700C'
8.01 c                   parm                    w_type
8.01 c                   if        w_type = '1'
8.01  *
8.01 c                   eval      w_mld1 = 'Dagsoppgjør er satt opp til å kjør-
8.01 c                             es automatisk'
8.06  *
8.06 c                   call      'AA005R'
8.06 c                   parm                    w_mld1
8.01 c                   endif
8.01  *
8.01 c                   endif
8.01 c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
     c                   move      *off          *in21
      *
      * F3=Avslutt
      *
8.01 c                   if        *inkc = *on
8.01 c                   goto      xslutt
8.01 c                   endif
      *
      * F12=Annuler
      *
     c     *inkl         cabeq     *on           xslutt
8.09  *
8.09  * Må oppdatere oppsett for automatisk dagsoppgjør, hvis finnes. NB Ikke hvis F6,
8.09  * og nytt oppsett.
8.09  *
8.09 c                   if        *inkf = *off
8.09 c                   eval      bdoplu_sgr  = *blank
8.09 c     bdoplu_key    chain     bdoplu
8.09 c                   if        %found
8.09 c                   eval      ldotal = '*ALL'
8.09 c                   eval      ldwsal = '*ALL'
8.09 c                   eval      lddekn = b1dekn
8.09 c                   eval      lddeta = b1deta
8.09 c                   eval      ldtota = b1tota
8.09 c                   eval      ldavst = b1avst
8.09 c                   eval      ldokoo = b1okoo
8.09 c                   eval      ldjobp = b1jobp
8.09 c                   eval      ldtime = b1time
8.09 c                   eval      ldkred = b1kred
8.09 c                   eval      ldlavs = 0
8.09 c                   eval      ldldet = 0
8.09 c                   eval      ldperi = 0
8.09 c                   move      *loval        ldfrad
8.09 c                   move      *loval        lddato
8.09 c                   move      *hival        ldtild
8.09 c                   eval      ldaarr = 2001
8.09 c                   eval      boprec = ldprec
8.09 c                   time                    bopeda
8.09 c                   time                    bopeti
8.09 c                   eval      bopeus = l_user
8.09 c                   update    bdoplur
8.09 c                   endif
8.09 c                   endif
8.03  *
8.03  * F6=Skal ikke være lov hvis ikke daglig kjøring fra jobbplan.
8.03  *
8.03 c                   if        *inkf = *on and
8.03 c                             b1jobp <> 'J'
8.03 c                   eval      w_mld1 = 'F6 er ikke gyldig uten å vel+
8.03 c                                       ge aut. dagsoppgjør'
8.03 c                   call      'AA005R'
8.03 c                   parm                    w_mld1
8.03 c                   goto      b1taga
8.03 c                   endif
8.00  *
8.00  * F6=Legge jobb på jobbplan for kjøring daglig
8.00  *
8.00 c                   if        *inkf = *on
8.02  *  Har firmaet tilgang til denne løsningen?
8.02 c                   if        *in55 = *off
8.02  *
8.02  *  Sjekk om firma har spesialiteter på noen av de berørte programmer
8.02  *
8.02 c                   Call      'BO129C'
8.02 c                   parm                    w_prog
8.02 c                   if        w_prog <> *blank
8.02 c                   eval      w_mld1 = 'Derre har spesialversjoner +
8.02 c                                     på følgende programmer:'
8.02 c                   eval      w_mld2 = w_prog
8.02 c                   eval      w_mld3 = 'Disse må tilpasses før +
8.02 c                                       denne løsningen startes'
8.02 c                   call      'AA031R'
8.02 c                   parm                    w_mld1
8.02 c                   parm                    w_mld2
8.02 c                   parm                    w_mld3
8.02  *
8.02 c                   eval      w_mld1 = 'Dersom nevnte programmer +
8.02 c                                       allerede er tilpasset'
8.02 c                   eval      w_mld2 = 'svar >J< og løsningen +
8.02 c                                       settes i drift'
8.02 c                   call      'AA007R'
8.02 c                   parm                    w_mld1
8.02 c                   parm                    w_mld2
8.02 c                   parm                    w_svar
8.02 c                   parm                    w_in12
8.02 c                   if        w_svar <> 'J'
8.02 c                   goto      b1taga
8.02 c                   endif
8.02 c                   endif
8.02  *
8.07 c                   eval      ldotal = '*ALL'
8.07 c                   eval      ldwsal = '*ALL'
8.07 c                   eval      lddekn = b1dekn
8.07 c                   eval      lddeta = b1deta
8.07 c                   eval      ldtota = b1tota
8.07 c                   eval      ldavst = b1avst
8.04 c                   eval      ldokoo = b1okoo
8.04 c                   eval      ldjobp = b1jobp
8.07 c                   eval      ldtime = b1time
8.08 c                   eval      ldkred = b1kred
8.08 c                   eval      ldlavs = 0
8.08 c                   eval      ldldet = 0
8.08 c                   eval      ldperi = 0
8.08 c                   move      *loval        ldfrad
8.08 c                   move      *loval        lddato
8.08 c                   move      *hival        ldtild
8.08 c                   eval      ldaarr = 2001
8.02  *
8.00 c                   eval      bdoplu_sgr  = *blank
8.00 c     bdoplu_key    chain     bdoplu
8.00 c                   eval      boprec = ldprec
8.00 c                   time                    bopeda
8.00 c                   time                    bopeti
8.00 c                   eval      bopeus = l_user
8.00 c                   if        %found(bdoplu)
8.00 c                   update    bdoplur
8.00 c                   else
8.00 c                   eval      bopfir = l_sfir
8.00 c                   eval      bopsgr = *blank
8.00 c                   time                    bopoda
8.00 c                   time                    bopoti
8.00 c                   eval      bopous = l_user
8.00 c                   write     bdoplur
8.00 c                   endif
      *
      * Lag jobb i jobbkalender.
      *
     c                   call      'BO133C'
     c                   parm                    ldprec
      *
8.02 c                   else
8.02 c                   eval      w_mld1 = 'Dette firma har ikke tilgang til +
8.02 c                                       denne funksjonen'
8.02 c                   eval      w_mld2 = 'Kontakt EG for avtale'
8.02 c                   call      'AA007R'
8.02 c                   parm                    w_mld1
8.02 c                   parm                    w_mld2
8.02 c                   parm                    w_svar
8.02 c                   parm                    w_in12
8.02 c                   goto      xslutt
8.02 c                   endif
8.02  *
8.00 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
7.01  *
7.01  * Key for file : Dagsoppgjør paramter
7.01  *
7.01 c     bdoplu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    bdoplu_sgr
8.05  *
8.05 c     xtill1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    xtill1_regi
8.05 c                   kfld                    xtill1_felt
8.05 c                   kfld                    xtill1_xkey
8.02  *
8.02  * Endring 8.02Styre kjøreing fra Jobbplan fra switch
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'D_Oppgj_Batch':
8.02                    co402_verdi1:co402_verdi2);
8.02         *in55 = *on;
8.02   if co402_verdi1 = '1';
8.02         *in55 = *off;
8.02   endif;
      *
     c                   endsr
      *
