     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIOKO
      * Program . . . . : RN003R
      * Beskrivelse . . : Leser linjer fra LOVF til NAV transaksjonsfil
      *
      * Spesialversjon. : OBS!!! Det finnes et "søsterprgram" RN013R
      *                   Endringer må også gjøres der
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       300714 ask  Nytt program
7.00  *       150221 ask  Lagt inn kode for transtype FAKT/KRED
7.01  *       250221 ask  Lagt inn betalingsbetingelse
8.01  * 800   190521 ask  Lagt inn firma i SQL mot SISTPF
8.02  * 800   210921 ask  Lagt "posting date" for å overstyre periode
8.03  *       290921 ask  Dropper overføring av poster med bilagssum = 0
8.04  *       130122 ask  Lagt inn endring på bokføringsdato ved overstyrt periode
      *                   Legger inn 28 i posteringsdato på valgt periode
8.05  *       050522 ask  Lagt inn switchstyring på funksjon i 8.04
8.06  *       080922 ask  Lagt inn switchstyring på remitteringsflagg (Poweroffice)
8.07  *       071025 ask  Lagt inn mulighet for kreditt referanse på reskontroposter
      *                   ved faktura leverandør. NXS-21858
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     flovfl1    if   e           k disk    rename(lovfpfr:lovfl1r)
     fiinkst    o    e           k disk    rename(iinkst:iinkstr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
8.05 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d lovfl1_peri     s                   like(lgperi)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
7.00 d w_sign          s              1
7.00 d w_otyp          s              2
7.00 d w_type          s              4
7.00 d w_year          s              4  0
7.01 d w_betb          s              2  0
8.04 d w_bilm          s              2  0
8.04 d w_perm          s              2  0
8.04 d w_pery          s              2  0
8.04 d w_bdat          s               d   datfmt(*iso)
8.04 d w_data          s             10
8.04 d w_pera          s              2
8.04 d w_peya          s              4
8.04 d b_omkj          s              1    inz(*off)
8.05  *
8.05  * Rutine for å hente switch informasjon
8.05  *
8.05 d u_bokd          s              1    inz(*off)
8.06 d u_remf          s              1    inz(*off)
8.07 d u_kref          s              1    inz(*off)
8.05  *
8.05   dcl-s co402_verdi1  char(1);
8.05   dcl-s co402_verdi2  char(2048);
8.05   DCL-PR CO402R EXTPGM('CO402R');
8.05     p_filgrp            char(3)     const;
8.05     p_firm              packed(3:0) const;
8.05     p_lager             packed(2:0) const;
8.05     p_nokkel            char(50)    const;
8.05     p_verdi1            char(1);
8.05     p_verdi2            char(2048);
8.05   END-PR;

     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra LOVFL1 og skriver til IINKST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      lovfl1_peri = 0
     c     lovfl1_key    setll     lovfl1
     c                   read      lovfl1
     c                   dow       not %eof
8.03  *
8.03  * Sjekker om "0" - bilag
8.03  *
8.03 c                   if        lgbelp = 0
8.03 c                   goto      les_neste
8.03 c                   endif
7.00  *
7.00 c                   exsr      finn_type
8.02  *
8.02 c                   exsr      finn_bdat
7.00 c                   eval      iitype = w_type
7.01 c                   eval      iibetb = w_betb
      *
8.07 c                   if        u_kref = *on
8.07 c                   exsr      finn_ref
8.07 c                   endif
8.07  *
     c                   eval      iifirm = lgfirm
     c                   eval      iibiln = lgbiln
     c                   eval      iibdat = lgbdat
     c                   eval      iifdat = lgfdat
     c                   eval      iibilk = lgbilk
     c                   eval      iitext = lgtext
     c                   eval      iidavd = lgdavd
     c                   eval      iidkto = lgdkto
     c                   eval      iidsps = lgdsps
     c                   eval      iidmom = lgdmom
     c                   eval      iicavd = lgcavd
     c                   eval      iickto = lgckto
     c                   eval      iicsps = lgcsps
     c                   eval      iicmom = lgcmom
     c                   if        lgckto > 0
     c                   eval      iibelp = lgbelp * -1
     c                   else
     c                   eval      iibelp = lgbelp
     c                   endif
     c                   eval      iikund = lgkund
     c                   eval      iikref = lgkref
     c                   eval      iikidi = lgkidi
     c                   eval      iivalk = lgvalk
     c                   eval      iivalb = lgvalb
     c                   eval      iiproj = lgproj
     c                   eval      iiakti = lgakti
     c                   eval      iisfak = lgsfak
8.02 c                   eval      iiboda = w_bdat
     c                   eval      iiodat = w_date
     c                   eval      iiotim = w_time
     c                   eval      iiousr = l_user
      *
8.06 c                   if        u_remf = *on
8.06 c                   eval      iiremi = 'NEI'
8.06 c                   else
8.06 c                   eval      iiremi = 'JA '
8.06 c                   endif
8.06  *
     c                   write     iinkstr
      *
8.03 c     les_neste     tag
     c                   read      lovfl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
7.00  *
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å hente transaksjonstype og betalingsbetingelse
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.00  *
7.00 c     finn_type     begsr
7.00  *
8.04 c                   eval      b_omkj = *off
8.04 c                   eval      w_year = %subdt(lgbdat:*years)
8.04 c                   eval      w_pery = 0
8.04  *
8.04 c     ny_periaa     tag

       /FREE
          w_sign = ' ';
          w_otyp = '  ';
          w_type = '    ';
          w_betb = 0;

          exec sql
            select shotyp, shbetb
            into   :w_otyp, :w_betb
            from   sihepf
            where  shfirm = :w_firm and shbinr = :lgbiln and shaarr = :w_year
               fetch first 1 rows only;

          exec sql
            select vaokre
            into :w_sign
            from votypf
             where vaofir = :w_firm and vaotyp = :w_otyp
                       fetch first 1 rows only;

         if w_sign = '-';
           w_type = 'KRED';
          else;
           w_type = 'FAKT';
         endif;
      /END-FREE

8.00  *
8.04  * Sjekker om funn på år fra bilagsdato - hvis ikke, prøv med år fra periode
8.04  *
8.04 c                   if        b_omkj = *off
8.04 c                   if        w_otyp = '  ' or
8.04 c                             w_betb = 0
8.04 c                   move      lgperi        w_pery
8.04 c                   eval      w_year = 2000 + w_pery
8.04 c                   eval      b_omkj = *on
8.04 c                   goto      ny_periaa
8.04 c                   endif
8.04 c                   endif
7.00  *
7.00  *
7.00 c     end_type      endsr

8.02  *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for å beregne bokføringsdato
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     finn_bdat     begsr
8.05  *
8.05 c                   if        u_bokd = *off
8.05 c                   eval      w_bdat = lgbdat
8.05 c                   goto      end_bdat
8.05 c                   endif
8.02  *
8.02 c                   movel     lgperi        w_perm
8.02 c                   eval      w_bilm = %subdt(lgbdat:*m)
8.02 c                   if        w_perm = w_bilm
8.02 c                   eval      w_bdat = lgbdat
8.02 c                   else
8.03 c                   eval      w_data = %char(lgbdat)
8.03 c                   eval      w_pera = %editc(w_perm : 'X')
8.04 c                   eval      w_peya = %editc(w_year : 'X')
8.03 c                   eval      w_data = %replace(w_pera:w_data:6)
8.04 c                   eval      w_data = %replace(w_peya:w_data:1)
8.03 c                   eval      w_data = %replace('01':w_data:9)
8.03 c                   eval      w_bdat = %date(w_data:*iso)
8.02 c                   endif
8.02  *
8.02 c     end_bdat      endsr
8.07  *
8.07  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.07  * Subrutine for å finne reskontro referanse på fakturakunde
8.07  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.07  *
8.07 c     finn_ref      begsr
8.07  *
8.07 c                   if        lgckto > 9999 and
8.07 c                             lgckto <> lgkund and
8.07 c                             lgbilk > 10
8.07 c                   eval      lgkref = lgbiln
8.07 c                   endif
8.07 c                   if        lgdkto > 9999 and
8.07 c                             lgdkto <> lgkund and
8.07 c                             lgbilk > 10
8.07 c                   eval      lgkref = lgbiln
8.07 c                   endif
7.07  *
7.07 c     end_ref       endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for initiering av program
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     *inzsr        begsr
      *
      * Key for posisjonering på posteringsfil
      *
     c     lovfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lovfl1_peri
6.21  *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
8.05  *
8.05  * Skal bokføringsdato overstyres ved periodeavvik
8.05  *
8.05   CallP CO402R(l_filg:w_firm:0:'POSTDAT_EKSTOKO':
8.05                   co402_verdi1:co402_verdi2);
8.05     if co402_verdi1 = '1';
8.05         u_bokd = *on;
8.05     endif;
8.06  *
8.06  * Skal remitteringsflagg slås av
8.06  *
8.06   CallP CO402R(l_filg:w_firm:0:'POWOFF_REM_AV':
8.06                   co402_verdi1:co402_verdi2);
8.06     if co402_verdi1 = '1';
8.06         u_remf = *on;
8.06     endif;
8.07  *
8.07  * Skal kreditt referanse legges på
8.07  *
8.07   CallP CO402R(l_filg:w_firm:0:'BC_INKREF':
8.07                   co402_verdi1:co402_verdi2);
8.07     if co402_verdi1 = '1';
8.07         u_kref = *on;
8.07     endif;
6.21  *
6.21 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
