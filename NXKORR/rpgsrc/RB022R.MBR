     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB012R
      * Beskrivelse . . : Leser linjer fra LOVF til kommaseparert til
      *                   (Business)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.22  * V6.20 051113 nho  Avstand mellom felter
6.23  * V6.20 141014 nho  Test på firma
6.30  * V6.30 131218 nho  Legger ut avdeling
9.01  *       211118 nho  Kunde har konvertering på bilagskode
9.02  * V6.20 030419 nho  Val.dato skal være lik bilagsdato
9.03  *       100220 nho  Konvertering på bilag
7.01  * V7.00 100220 nho  Henter inn konvertering av bilagskode
7.02  * V7.00 130220 nho  Henter inn firm_begin
7.03  *       120521 mst  Henter avvikende momskode, hvis den finnes
7.04  *       311023 mst  Legg ut KID på fil, sjekk LOVFPF først
7.05  *       311023 mst  Leverandørens fakturanummer er for kort
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     flovupf    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
7.01 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
7.03 frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
      *
      *
     frv03pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(lgfdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d tell            s              1  0
7.05 d*nbsfak          s              7  0
7.05 d nbsfak          s             15
     d f1momd          s                   like(lgdmom)
     d f1momc          s                   like(lgcmom)
6.30 d f1avde          s                   like(lgcavd)
     d zfbelp          s             12  4
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
7.01 d xxbilk          s              5  0
7.01 d rb10l1_gkod     s                   like(raakod)
7.03 d rb00l1_gkod     s                   like(r00kod)
7.03 d w_type          s             10
7.04 d w_kid           s                   like(lgkidi)
      *
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
7.01  * Key for posisjonering på bilagskoder
7.01  *
7.01 c     rb10l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rb10l1_gkod
7.01  *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
7.03  * Key for posisjonering på koder (moms)
7.03  *
7.03 c     rb00l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    w_type
7.03 c                   kfld                    rb00l1_gkod
      *
      *
6.23  *
6.23 c                   if        l_firm <> lgfirm
6.23 c                   goto      slutt
6.23 c                   endif
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
7.02 c                   read      rb10l1                                 90
     c                   eval      rvdata = *blank
7.02 c***                eval      rvdata = %trim('@FIRM_BEGIN(20063)')
7.02 c                   eval      rvdata = %trim('@FIRM_BEGIN(') +
7.02 c                                      %trim(rafirb) +
7.02 c                                      %trim(')')
     c                   write     rv03pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(3)')
     c                   write     rv03pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaBnd(Descr,ValDT)')
     c                   write     rv03pfr
      *
     c                   eval      rvdata = *blank
      *
      *
      * Dagense dato
     c                   time                    valdat
     c                   movel     valdat        d_fdati
     c                   movel     d_fa          nval08
     c                   movel     d_fn          nval04
     c                   move      d_fd          nval04
     c                   move      nval04        nval08
6.22 c                   eval      rvdata = %trim('"KREDITSYSTEM" "') +
     c                                      %char(nval08) +
     c                                      %trim('"')
     c                   write     rv03pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaVo(VoNo,VoDt,') +
     c                                           ('ValDt,Votp,Txt,') +
     c                                           ('DbAcNo,DbTxcd,CrAcNo,') +
     c                                           ('CrTxCd,InvoNo,Am,R1,') +
     c                                           ('DueDt,Cid,R2)')
     c                   write     rv03pfr
      *
     c                   endif
      *
      ***********************************************************
     c
     c* Blanker felt
     c                   eval      d_dati  = *blanks
7.05 c*                  eval      nbsfak = *zeros
7.05 c                   eval      nbsfak = *blanks
     c                   movel     lgsfak        nbsfak
      *
     c                   move      lgbdat        d_dati
      *
      * Forfallsdato
     c                   movel     lgfdat        f_fdati
     c                   movel     f_fa          nfor08
     c                   movel     f_fn          nfor04
     c                   move      f_fd          nfor04
     c                   move      nfor04        nfor08
      *
      *
      * Bilagsdato
     c                   movel     lgbdat        b_fdati
     c                   movel     b_fa          nbil08
     c                   movel     b_fn          nbil04
     c                   move      b_fd          nbil04
     c                   move      nbil04        nbil08
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   z-add     lgbelp        zfbelp
      * Legger ut detaljer fra bovf
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
      *
7.04 c                   if        lgkidi <> *blanks
7.04 c                   move      lgkidi        w_kid
7.04 c                   else
     c                   exsr      control_init
7.04 c                   move      sokidd        w_kid
7.04 c                   endif
7.04  *
7.01  * Sjekker om bilagstype skal konverteres
7.01 c                   eval      xxbilk = lgbilk
7.01 c                   eval      rb10l1_gkod = lgbilk
7.01 c     rb10l1_key    chain     rb10l1
7.01 c                   if        %found
7.01 c                   eval      xxbilk = rankod
7.01 c                   endif
7.01  *
      *
     c                   if        lgdkto <> *zeros and
     c                             lgdkto >=  1000 and
     c                             lgdkto <=  9999
     c                   eval      f1momd = lgdmom
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momd        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c*                  movel     r00nko        f1momd
7.03 c                   move      r00nko        f1momd
7.03 c                   endif
     c                   endif
      *
     c                   if        lgckto <> *zeros and
     c                             lgckto >=  1000 and
     c                             lgckto <=  9999
     c                   eval      f1momc = lgcmom
     c                   eval      f1momd = '0'
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momc        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c*                  movel     r00nko        f1momc
7.03 c                   move      r00nko        f1momc
7.03 c                   endif
     c                   endif
      *
     c*
6.30  * Legger ut avdleing
6.30 c                   eval      f1avde = *zeros
6.30  *
6.30 c                   if        lgcavd <> *zeros
6.30 c                   eval      f1avde = lgcavd
6.30 c                   endif
6.30  *
6.30 c                   if        lgdavd <> *zeros
6.30 c                   eval      f1avde = lgdavd
6.30 c                   endif
     c**********
     c                   eval      rvdata = *blank
      * Start
     c                   eval      rvdata = %trim('"') +
      * Bilagsnr.
     c                                     %char(lgbiln)     + '" "' +
      * Bil.dato
     c                                     %char(nbil08)     + '" "' +
      * Val.dat fra heading
9.02 c**                                   %char(nval08)    + '" "' +
9.02 c                                     %char(nbil08)     + '" "' +
      * Bil.type
9.01 c**                                   %char(lgbilk)     + '" "' +
9.01 c                                     %char(xxbilk)     + '" "' +
      * Bil.tekst
     c                                     %trim(lgtext)     + '" "' +
      * Deb.konto
     c                                     %char(lgdkto)     + '" "' +
      * Deb.mva kode
     c                                     %trim(f1momd)     + '" "' +
      *
      * Kredkonto
     c                                     %char(lgckto)     + '" "' +
      * Kredit mva kode
     c                                     %trim(f1momc)     + '" "' +
      * Fakturanr (Bilagsnummer)
     c*                                    %char(lgbiln)     + '" "' +
7.05 c*                                    %char(nbsfak)     + '" "' +
7.05 c                                     %trim(nbsfak)     + '" "' +
      *
      * Beløp på linje
     c                                     %char(zfbelp)     + '" "' +
      * Avdeling?? Kan prosjekt legges her?
6.30 c*                                    %trim('" "') +
6.30 c                                     %char(f1avde)    + '" "' +
      * Forfallsdato
     c                                     %char(nfor08)    + '" "' +
      * Kid nr ???????
7.04 c*                                    %trim(sokidd)    + '" "' +
7.04 c                                     %trim(w_kid)     + '" "' +
      * Prosjekt
     c                                     %trim(lgproj)    + '"'
      *
     c                   write     rv03pfr
      *
      *
6.23 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = lgfirm
     c                   eval      w_binr = lgbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
