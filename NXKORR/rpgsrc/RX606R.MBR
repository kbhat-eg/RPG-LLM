     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : RX606R
      * Beskrivelse . . : Eksport av kundeprosjekt informasjon til excel
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240925 bsa  Nytt program
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprlr    if   e           k disk    rename(fkprpfr:fkprlrr)
     frx02st    uf a e           k disk    rename(rx02st:rx02str)
      *
      * Definering av parametre
      *
     d p_parm          s            256
      *
      * Parameter overføringer
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
     d l_lage               1001   1002  0
      *
      * Definering av variabler i nøkler
      *
     d fkprlr_kund     s                   like(flkund)
     d fkprlr_kpro     s                   like(flkpro)
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       512
     d  d_firm                        3  0 overlay(d_list)
     d  d_kund                        6  0 overlay(d_list:4)
     d  d_kpro                        6  0 overlay(d_list:10)
     d  d_txt1                       60    overlay(d_list:36)
     d  d_txt2                       60    overlay(d_list:96)
     d  d_txt3                       60    overlay(d_list:156)
     d  d_emal                       60    overlay(d_list:216)
     d  d_subj                       60    overlay(d_list:276)
     d  d_avse                       60    overlay(d_list:336)
     d  d_dumm                        1    overlay(d_list:512) inz('*')
      *
     d                 ds
     d d_frie                         5
     d  d_fri1                        1    overlay(d_frie)
     d  d_fri2                        1    overlay(d_frie:2)
     d  d_fri3                        1    overlay(d_frie:3)
     d  d_fri4                        1    overlay(d_frie:4)
     d  d_fri5                        1    overlay(d_frie:5)
      *
      * Definering av variable
      *
     d w_firm          s                   like(flfirm)
     d w_aar           s              4  0
     d w_excel_url     s            100
     d w_numm          s              8  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_lnvn          s             30
     d w_btxt          s             30
     d w_read          s              1
      *
     d p_excel_url     s            100
      *
     d u_excl          s              1    inz(*off)
      *
      * Eksternt program
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fkprlr_kund = d_kund
     c                   eval      fkprlr_kpro = d_kpro
     c     fkprlr_key    setll     fkprlr
     c                   select
     c                   when      w_read = '1'
     c                   read      fkprlr
     c                   when      w_read = '2'
     c     fkprlr_key2   reade     fkprlr
     c                   when      w_read = '1'
     c     fkprlr_key    reade     fkprlr
     c                   endsl
     c                   dow       not  %eof(fkprlr)
      *
     c                   exsr      add_line
      *
     c                   select
     c                   when      w_read = '1'
     c                   read      fkprlr
     c                   when      w_read = '2'
     c     fkprlr_key2   reade     fkprlr
     c                   when      w_read = '1'
     c     fkprlr_key    reade     fkprlr
     c                   endsl
     c                   enddo
      *
      *  Kaller opp program for exccel eksport
      *
     c                   if        u_excl = *on
     c                   eval      p_excel_url = w_excel_url
     c                   call      'RX606C'
     c                   parm                    p_excel_url
     c                   parm                    w_numm
     c                   endif
      *
9.xx c**                 if        1 = 2
      *
     c/exec sql
     c+ DELETE FROM RX02ST WHERE R02IDE = :w_numm
     c/end-exec
       exec sql commit;
      *
9.xx c**                 endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine legge ut kundepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     add_line      begsr
      *
     c                   eval      r02ide = w_numm
     c                   eval      r02fir = w_firm
     c                   eval      r02kun = flkund
     c                   eval      r02nxp = flkpro
     c                   eval      r02slg = flselg
     c                   eval      r02fda = flfdat
     c                   eval      r02tda = fltdat
     c                   eval      r02nav = flnavn
     c                   eval      r02ad1 = fladr1
     c                   eval      r02ad2 = fladr2
     c                   eval      r02pnr = flponr
     c                   eval      r02kpr = flkprs
     c                   eval      r02tla = fltlfa
     c                   eval      r02tlp = fltlfp
     c                   eval      r02tlm = fltlfm
     c                   eval      r02ema = flmail
     c                   eval      r02epr = flepro
     c                   eval      r02fty = flfaty
     c                   eval      r02kjo = flkjor
     c                   eval      r02kjn = flkjnr
     c                   eval      r02obu = flboku
     c                   eval      r02ork = florka
     c                   eval      r02okn = flokun
     c                   eval      r02opr = flokpr
     c                   eval      r02opa = flopda
     c                   eval      r02pri = flpriv
     c                   eval      r02pro = flproj
     c                   eval      r02bdo = flbdok
     c                   eval      r02cbo = flcobu
     c                   eval      r02mko = flmkod
     c                   eval      r02bet = flbetb
     c                   eval      r02ppa = flprpa
     c                   eval      r02net = flneto
     c                   eval      r02ekg = flekgb
     c                   eval      r02sek = flsekv
     c                   eval      r02sam = flsamf
     c                   eval      r02fag = flfagb
     c                   eval      r02fku = flfkun
      *
     c                   write     rx02str
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for oppslag på kunde
      *
     c     fkprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlr_kund
     c                   kfld                    fkprlr_kpro
      *
      * Key for oppslag på kunde
      *
     c     fkprlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlr_kund
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_firm = 0
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Henter parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      d_list = p_parm
      * Sjekk hvilke parametere som er utfylt.
     c                   eval      w_read = *blanks
     c                   if        d_kund = 0 and
     c                             d_kpro = 0
     c                   eval      w_read = '1'
     c                   endif
     c                   if        w_read = *blanks
     c                   if        d_kund <> 0 and
     c                             d_kpro = 0
     c                   eval      w_read = '2'
     c                   endif
     c                   endif
     c                   if        w_read = *blanks
     c                   if        d_kund <> 0 and
     c                             d_kpro <> 0
     c                   eval      w_read = '3'
     c                   endif
     c                   endif
      *
      * OBS!!  Felles for alle filgrupper og firma
      *
       CallP CO402R('   ':0:0:'NX_EXCEL_URL':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_excl = *on;
         w_excel_url = co402_verdi2;
       endif;
      *
     c                   endsr
