      /TITLE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GS110R                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           260324  Nytt program                             bsa  *
8.01  *K0000      270324  Oppdaterer ikke hvis det ikke er lest    bsa  *
8.01  *K0000              inn noen poster.                         bsa  *
8.02  *K0000      020724  Justert innlesing.                       bsa  *
8.03  *K0000      010425  Endret litt på behandling av dato        bsa  *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Bruk av indikatorer                                            *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgseppf    uf   e           k disk    rename(gseppfr:gseppfp)
     fgatrpf    o  a e             disk
     f                                     rename(gatrpfr:gatrpfp)
     fgavtl2    if   e           k disk    rename(gavtpfr:gavtl2r)
     fgavtlu    uf   e           k disk    rename(gavtpfr:gavtlur)
      *
      * Nøkler
      *
     d gavtlu_bgir     s                   like(gcbgir)
      *
      *
      *  Generell datastruktur
      *
     d                 ds
     d dssepa                       160
     d  dsreko                       80    overlay(dssepa:1)
      *
     d                 ds
     d d_dato                        10
     d  d_aar                         4    overlay(d_dato:1)
     d  d_mnd                         2    overlay(d_dato:6)
     d  d_dag                         2    overlay(d_dato:9)
      *
     d                 ds
     d w_dato                         8  0
     d  w_aar                         4  0 overlay(w_dato:1)
     d  w_mnd                         2  0 overlay(w_dato:5)
     d  w_dag                         2  0 overlay(w_dato:7)
      *
     d                 ds
     d d_time                         8
     d  d_tim                         2    overlay(d_time:1)
     d  d_min                         2    overlay(d_time:4)
     d  d_sek                         2    overlay(d_time:7)
     d                 ds
     d w_time                         6  0
     d  w_tim                         2  0 overlay(w_time:1)
     d  w_min                         2  0 overlay(w_time:3)
     d  w_sek                         2  0 overlay(w_time:5)
      *
      *  Generell datastruktur for LAGRING AV FOREGÅENDE LESTE POST:
      *
     d                 ds
     d dsprev                       160
      *
      *  Henter firmanavn i fra LDA:
      *
     d                uds
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d c_hed           c                   'xml version='
     d*c_dat           c                   '<CreDtTm>'
     d c_dat           c                   'CreDtTm'
     d w_type          s              3
     d w_lopn          s              5  0
     d w_giro          s             11  0
     d w_posi          s              4  0
     d w_text          s            160
     d*w_dato          s             10
     d*w_time          s              8
     d i               s              6  0
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDSHR, LANGID=NOR
     C/End-Exec
      *
      * Behandler transaksjoner fra SEPA retur
      *
     c                   eval      w_type = 'SP2'
     c                   eval      i = 0
      *
     c                   read      gseppf
     c                   dow       not %eof
      *
      * Behandler transaksjoner fra Mottaks og avregninsgreturer
      *
     c                   eval      dssepa = gsqsep
     c                   eval      w_text = gsqsep
     c                   eval      w_posi = 0
      *
     c                   select
     c                   when      %scan(c_hed:dssepa) > 1
     c                   goto      no_trans
     c                   when      %scan(c_dat:dssepa) > 1
     c                   eval      w_posi = %scan(c_dat:dssepa)
8.02 c*                  eval      w_text = %subst(dssepa:w_posi+8:160-23)
8.02 c                   eval      w_text = %subst(dssepa:w_posi+8)
     c                   eval      w_posi = %scan('T':w_text)
     c                   eval      d_dato = %trim(%subst(w_text:1:w_posi-1))
     c                   eval      w_aar = %dec(d_aar:4:0)
     c                   eval      w_mnd = %dec(d_mnd:2:0)
     c                   eval      w_dag = %dec(d_dag:2:0)
     c                   eval      w_text = %trim(%subst(w_text:12:160-w_posi))
     c                   eval      w_posi = %scan('.':w_text)
8.03 c                   if        w_posi > 0
     c                   eval      d_time = %trim(%subst(w_text:1:w_posi-1))
8.03 c                   else
8.03 c                   eval      d_time = %trim(w_text)
8.03 c                   endif
     c                   eval      w_tim = %dec(d_tim:2:0)
     c                   eval      w_min = %dec(d_min:2:0)
     c                   eval      w_sek = %dec(d_sek:2:0)
     c                   endsl
      *
     c                   eval      gbgiro = w_giro
     c                   eval      gblØpn = w_lopn
     c                   eval      gbreco = dsreko
     c                   eval      gbtype = w_type
     c                   eval      i = i + 1
     c                   eval      gbtell = i
     c                   if        %trim(d_dato) = *blanks
     c                   eval      gbdato = 0
     c                   else
     c                   eval      gbdato = w_dato
     c                   endif
     c                   if        %trim(d_time) = *blanks
     c                   eval      gbklok = 0
     c                   else
     c                   eval      gbklok = w_time
     c                   endif
     c*                  time                    gbklok
     c                   eval      gbfgrp = l_filg
     c                   eval      gbfirm = l_firm
      *
     c                   write     gatrpfp
      *
     c     no_trans      tag
     c                   delete    gseppf
      *
     c                   read      gseppf
     c                   enddo
      *
8.01 c                   if        i > 0
      *
      * Oppdater med dato og tid på linjer som ikke har det fra før
      *
     c/exec sql
     c+ update gatrpf set gbdato = :w_dato,gbklok = :w_time
     c+   where gblØpn = :w_lopn
     c/end-exec
      *
8.01 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste:
      *
     c*    *entry        plist
     c*                  parm                    w_arow
     c*                  parm                    w_acol
      *
      * Key til bruk på avtaleregister - GAVTL2, hente gironummer:
      *
     c     key03         klist
     c                   kfld                    l_filg
     c                   kfld                    l_firm
      *
      * Key til bruk på avtaleregister - GAVTLU, oppdatere løpenummer
      *
     c     gavtlu_key    klist
     c                   kfld                    gavtlu_bgir
      *
     c     key03         setll     gavtl2
     c     key03         reade     gavtl2
      *                                                        .
     c                   eval      w_giro = 0
      *                                                        .
     c                   if        not %eof
     c                   if        gcbgir <> 0
     c                   eval      w_giro = gcbgir
     c                   else
     c                   eval      w_giro = gcpgir
     c                   endif
     c                   endif
      *
     c                   if        gcskto <> 0
     c                   eval      w_giro = gcskto
     c                   endif
      *
     c                   eval      w_lopn = gclØpn + 1
     c                   eval      gavtlu_bgir = gcbgir
     c     gavtlu_key    chain     gavtlu
     c                   if        %found
     c                   eval      gclØpn = w_lopn
     c                   update    gavtlur
     c                   endif
      *
     c                   endsr
      *
