     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : NXCLOUD                                             *
      * Program . . . . : RMP010R                                             *
      * Beskrivelse . . : Innlesning av lagerbevegelser fra MalProff          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R8.00 sst 280622  Nytt Program                                        *
8.01  * R8.00 sst 050722  Nullstille Ordrenummer og linje                     *
ssd   *       sst 170822  Foreløig tatt ut noen felter som ikke er i filen.
8.02  *       bof 191125  Det må test på om GTIN = 0, dette skal gi feil.
      *   * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fmpinput   up   f  256        disk
     f*                                    commit(COMFLAG)
     f*                                    infsr(FileErr)
     fmjlol2    if   e           k disk    rename(mjlopfr:mjlol2r)
     fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     frmpilu    uf a e           k disk    rename(rmpipfr:rmpilur)
     frmpflu    uf a e           k disk    rename(rmpfpfr:rmpflur)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     frax9l1    if   e           k disk    rename(rax9pfr:rax9l1r)
      *
     d digit           c                   '0123456789 '
      *
     d                uds
      *
     d l_MalP                  1      2
      *
     d l_wsid                901    910
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_user                911    920
      *
      * Lagerbevegelser fra Mal Proff
      *
     d                 ds
     d wwdato                         8  0
     d  wwaar                         4  0 overlay(wwdato)
     d   wwaar2                       2  0 overlay(wwaar:3)
     d  wwmnd                         2  0 overlay(wwdato:5)
     d  wwdag                         2  0 overlay(wwdato:7)
8.00  *
8.00  * Lageroppdatering
8.00  *
8.00 d                 ds
8.00 d hlrec                        128
8.00 d  hlvare                       15    overlay(hlrec)
8.00 d  hllage                        2  0 overlay(hlrec:16)
8.00 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
8.00 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
8.00 d  hlotyp                        2    overlay(hlrec:36)
8.00 d  hlltyp                        2    overlay(hlrec:38)
8.00 d  hlanta                       11  3 overlay(hlrec:40)
8.00 d  hlkopr                        9  2 overlay(hlrec:51)
8.00 d  hlrefr                       20    overlay(hlrec:60)
8.00 d  hlsyst                        1    overlay(hlrec:80)
8.00 d  hlaltk                        2    overlay(hlrec:81)
8.00 d  hlrest                        1    overlay(hlrec:83)
8.00 d  hltype                        1    overlay(hlrec:84)
8.00 d  hlenhe                        3    overlay(hlrec:85)
8.00 d  hlinlr                        1    overlay(hlrec:88)
8.00 d  hllety                        1    overlay(hlrec:89)
8.00 d  hlonum                        8  0 overlay(hlrec:90)
8.00 d  hlolin                        4  0 overlay(hlrec:98)
8.00 d  w_feil                       10    overlay(hlrec:102)
      *
7.06 d i               s              5  0
7.06 d w_stat          s             10
7.06 d w_fil           s             60
7.06 d w_prog          s             10    inz('RMP010R')
7.06 d w_sekv          s             03    inz('001')
7.06 d w_text          s            200
7.06 d w_jobb          s             10    inz('RMPGETIFS')
7.06 d w_rec           s            256
      *
7.06 d d_gln           s             14
7.06 d d_ean           s             14
7.06 d d_ant           s             11
7.06 d d_avd           s             30
7.06 d d_onr           s              9
7.06 d d_ref           s             30
7.06 d d_dat           s             10
7.06 d d_fda           s             10
7.06 d d_tim           s              8
7.06 d d_fnr           s             10
7.06 d d_kun           s             30
7.06 d d_knr           s              8
7.06 d d_lin           s              4
7.06 d w_ant           s             11  3
7.06 d w_lag           s              4  0
7.06 d w_var           s             15
7.06 d w_enh           s              4
7.06 d w_oty           s              2    inz('MP')
7.06 d w_ean           s             14  0
7.06 d w_gln           s             14  0
7.06 d w_firm          s              3  0
      *
7.08 d w_rec1          s             50
7.08 d w_rec2          s             50
7.08 d w_rec3          s             50
7.08 d w_subj          s            100
7.08 d w_txt1          s            100
7.08 d w_txt2          s            100
7.08 d w_txt3          s            100
7.08 d w_txt4          s            100
7.08 d w_avsl          s            100
7.08 d w_filn          s            256
7.08 d w_fnav          s             50
7.08 d w_mime          s            150
7.15 d w_bcc           s              1
8.00 d comflag         s              1n
      *
7.15 d s_gln           s                   like(d_gln)
7.15 d s_lin           s                   like(d_lin)
7.15 d s_onr           s                   like(d_onr)
      *
     d v_stat          s              1    inz(' ')
     d p_stat          s              8
     d p_errt          s            120
7.11  *
8.01 d rmpilu_fir      s                   like(rmpfir)
8.01 d rmpilu_lag      s                   like(rmplag)
8.01 d rmpilu_var      s                   like(rmpvar)
8.01 d rmpilu_kda      s                   like(rmpkda)
8.01 d rmpilu_kti      s                   like(rmpkti)
8.01 d rmpilu_sek      s                   like(rmpsek)
8.01 d rmpflu_fir      s                   like(rmffir)
8.01 d rmpflu_kda      s                   like(rmfkda)
8.01 d rmpflu_kti      s                   like(rmfkti)
8.01 d rmpflu_tel      s                   like(rmftel)
8.01 d ra09l1_ikod     s                   like(raikod)
7.11  *
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
     impinput   ns  01
     i                                  1  256  rec
      *
     c                   eval      i = 256
     c                   dow       i > 1
     c                             and %subst(rec:i:1) = ' '
     c                   eval      i = i - 1
     c                   enddo
      *
     c                   eval      w_rec = rec
     c                   eval      %subst(rec:i+1:10) = ';;;;;;;;;;'
     c                   eval      w_text = *blank
      *  GLN-nummer
     c                   eval      i = %scan(';':rec)
     c                   eval      d_gln  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Hopper ut dersom 1. siffer i GLN nr <> 0-9
     c                   if        %subst(d_gln:1:1) < '0'
     c                             or %subst(d_gln:1:1) > '9'
     c                   goto      slutt
     c                   endif
      *
      *  Dato
     c                   eval      i = %scan(';':rec)
     c                   eval      d_dat  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Tid
     c                   eval      i = %scan(';':rec)
     c                   eval      d_tim  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Linjenr
     c                   eval      i = %scan(';':rec)
     c                   eval      d_lin  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  EAN-nummer  Gtin
     c                   eval      i = %scan(';':rec)
     c                   eval      d_ean  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Antall
     c                   eval      i = %scan(';':rec)
     c                   eval      d_ant  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
     c                   eval      i = %scan('-':d_ant)
     c                   if        i > 0
     c                   eval      %subst(d_ant:i:1) = '0'
     c                   eval      w_ant = %dec(d_ant:11:3) * -1
     c                   else
     c                   eval      w_ant = %dec(d_ant:11:3)
     c                   endif
      *  Ordrenr
     c                   eval      i = %scan(';':rec)
     c                   eval      d_onr  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Avdeling    Navn
     c                   eval      i = %scan(';':rec)
     c                   eval      d_avd  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Kundenr.   MalProff
     c                   eval      i = %scan(';':rec)
     c                   eval      d_knr  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Kundenavn  MalProff
     c                   eval      i = %scan(';':rec)
     c                   eval      d_kun  = %trim(%subst(rec:1:i-1))
     c                   eval      rec  = %subst(rec:i+1:256-i)
      *  Vår referanse
     c*                  eval      i = %scan(';':rec)
     c*                  eval      d_ref  = %trim(%subst(rec:1:i-1))
     c*                  eval      rec  = %subst(rec:i+1:256-i)
      *  FakturaNr
     c*                  eval      i = %scan(';':rec)
     c*                  eval      d_fda  = %trim(%subst(rec:1:i-1))
     c*                  eval      rec  = %subst(rec:i+1:256-i)
      *  FakturaDato
     c*                  eval      i = %scan(';':rec)
     c*                  eval      d_fnr  = %trim(%subst(rec:1:i-1))
     c*                  eval      rec  = %subst(rec:i+1:256-i)
      *
      * ________________________________________________________________________
      *   Test av div. felter
      * ________________________________________________________________________
      *    Dato
     c     *eur          test(d)                 d_dat                  41
     c                   if        *in41 = *on
     c                   eval      w_text = 'Feil i datofelt for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(' Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       ' ' + %trim('InputFil: ') +
     c                                      %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
      *    Tid
     c     *hms          test(t)                 d_tim                  41
     c                   if        *in41 = *on
     c                   eval      w_text = 'Feil i tid_felt for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                      ' ' + %trim('InputFil: ') +
     c                                      %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
      *
      *  Finn Lagernr via MJLOL2
      *
     c                   if        %check(digit:d_gln) = 0
     c                   eval      w_gln = %dec(d_gln:14:0)
     c                   else
     c                   eval      w_text = 'Feil i GLN_nr. for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       %trim(',   Ikke numerisk') +
     c                                       ' ' + %trim('i InputFil: ') +
     c                                       %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
     c     mjlol2_key    chain     mjlol2
     c                   if        not %found(mjlol2)
     c                   eval      w_text = 'Ukjent GLN_nr. for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       %trim(', Finnes ikke i MJLOPF') +
     c                                       ' ' + %trim('InputFil: ') +
     c                                       %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
     c                   eval      w_lag = mjlage
      *
      *  Finn Varenr og enhet via vvepl2
      *
     c                   if        %check(digit:d_ean) = 0
     c                   eval      w_ean = %dec(d_ean:14:0)
     c                   else
     c                   eval      w_text = 'Feil i GTIN_nr. for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       %trim(', Er ikke numerisk') +
     c                                       ' ' + %trim('i InputFil: ') +
     c                                       %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
8.02 c                   if        w_ean = 0
8.02 c                   eval      w_text = 'Feil i GTIN_nr. for Ordre: ' +
8.02 c                                       %trim(d_onr)  +
8.02 c                                       %trim(', GLN_nr: ') +
8.02 c                                       %trim(d_gln)  +
8.02 c                                       %trim(', Linjenr: ') +
8.02 c                                       %trim(d_lin) +
8.02 c                                       %trim(', GLN = 0') +
8.02 c                                       ' ' + %trim('i InputFil: ') +
8.02 c                                       %trim(w_fil)
8.02 c                   exsr      feil
8.02 c                   goto      slutt
8.02 c                   endif
     c     vvepl2_key    chain     vvepl2
     c                   if        not %found(vvepl2)
     c                   eval      w_text = 'Ukjent GTIN_nr. for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       %trim(', Finnes ikke i VVEPPF') +
     c                                       ' ' + %trim('InputFil: ') +
     c                                       %trim(w_fil)
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
     c                   eval      w_var = vepvar
     c                   eval      w_enh = vepenh
      *
      *   Midlertidig oppdatering av tilleggsopplysninger fra MalProff
      *
     c                   eval      rmpilu_fir = 999
     c                   eval      rmpilu_lag = 99
     c                   eval      rmpilu_var = 'MIDLERTIDIG   '
     c                   eval      rmpilu_kda = *loval
     c                   eval      rmpilu_kti = *loval
     c                   eval      rmpilu_sek = *zero
     c     rmpilu_key    chain     rmpilu
     c                   eval      rmponr = %dec(d_onr:8:0)
     c                   eval      rmplin = *zero
ssd  c                   eval      rmpknr = %dec(d_knr:8:0)
ssd  c*                  eval      rmpfda = %date(d_fda)
     c                   eval      rmpkun = d_kun
ssd  c                   eval      rmplin = %dec(d_lin:4:0)
     c                   eval      rmpref = d_ref
ssd  c                   eval      rmpdat = %date(d_dat:*eur)
ssd  c                   eval      rmptim = %time(d_tim:*hms)
     c                   eval      rmpavd = d_avd
     c                   if        not %found(rmpilu)
     c                   eval      rmpfir = 999
     c                   eval      rmplag = 99
     c                   eval      rmpvar = 'MIDLERTIDIG   '
     c                   eval      rmpkda = *loval
     c                   eval      rmpkti = *loval
     c                   eval      rmpsek = *zero
     c                   write     rmpilur
     c                   else
     c                   update    rmpilur
     c                   endif
      *
      *   Oppdater lager med VL001R som foreløpig er laget av VL001R
      *
     c                   eval      hlvare = w_var
     c                   eval      hllage = w_lag
     c                   time                    hldato
     c                   time                    hltime
     c                   eval      hlotyp = w_oty
     c                   eval      hlltyp = *blank
     c                   eval      hlanta = w_ant
     c                   eval      hlkopr = *zero
     c                   eval      hlrefr = 'MP:' + d_onr
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   eval      hltype = *blank
     c                   eval      hlenhe = w_enh
     c                   eval      hlinlr = *blank
     c                   eval      hllety = *blank
8.01 c                   eval      hlonum = *zero
8.01 c                   eval      hlolin = *zero
8.01  *
8.01 c                   eval      l_MalP = 'MP'
      *
     c                   call      'VL001R'
     c                   parm                    v_stat
     c                   parm                    hlrec
     c*                  parm                    w_feil
     c*                  parm                    COMFLAG
     c*                  parm                    p_stat
     c*                  parm                    p_errt
      *
     c**                 if        p_stat <> ' '
     c                   if        v_stat <> '0'
     c**                 eval      w_stat = 'Feil'
     c                   eval      w_text = 'Feil i lager oppdat. for Ordre: ' +
     c                                       %trim(d_onr)  +
     c                                       %trim(', GLN_nr: ') +
     c                                       %trim(d_gln)  +
     c                                       %trim(', Linjenr: ') +
     c                                       %trim(d_lin) +
     c                                       %trim(',  Vare: ') +
     c                                       %trim(hlvare) +
     c                                       %trim(',  Lager: ') +
     c                                       %trim(%char(hllage)  +
     c                                       %trim(', Feil: ') +
     c                                       %trim(w_feil))
     c                   exsr      feil
     c                   goto      slutt
     c                   endif
      *
     c                   goto      slutt
      *
     c     slutt1        tag
     c*                  eval      *inlr = *on
     c     slutt         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = 'DBERR'
     c                   eval      p_errt = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
     c                   goto      slutt1
      *
     c                   endsr
      *
      * ________________________________________________________________________
      *
     c     Feil          begsr
     c                   call      'RMP97C'
     c                   parm                    w_jobb
     c                   parm                    w_prog
     c                   parm                    w_sekv
     c                   parm                    w_text
     c**                 eval      w_stat = 'Feil'
      *
      *  Legg ut i feilregister dersom linja ikke er lagt ut før
     c                   if        d_gln <> s_gln
     c                             or d_lin <> s_lin
     c                             or d_onr <> s_onr
     c                   eval      s_onr = d_onr
     c                   eval      s_gln = d_gln
     c                   eval      s_lin = d_lin
      *
     c                   eval      rmpflu_fir = l_firm
     c                   time                    rmpflu_kda
     c                   time                    rmpflu_kti
     c                   eval      rmpflu_tel = *zero
     c     rmpflu_key    chain     rmpflu
     c                   dow       %found(rmpflu)
     c                   eval      rmpflu_tel = rmpflu_tel + 1
     c     rmpflu_key    chain     rmpflu
     c                   enddo
     c                   eval      rmffir = l_firm
     c                   time                    rmfkda
     c                   time                    rmfkti
     c                   eval      rmftel = rmpflu_tel
     c                   eval      rmfrec = w_rec
     c                   time                    rmfeda
     c                   time                    rmfeti
     c                   eval      rmfeus = 'RMP010R'
     c                   write     rmpflur
     c                   endif
      *
      *  Sende mail til alle som er merket for dette vedr feilen.
      *        Finnes avdeling på lager?  Ja.
      *        Kode på selger sier at denne skal ha mail (v/vareoppdatering) ogs
      *             Denne ligger i RAX9 og heter RAIKO1/01
      * finner mailadresser
     c     rax9l1_key    setll     rax9l1
     c     rax9l1_key    reade     rax9l1
     c                   dow       not %eof
      *
     c                   if        raik01 = 1
     c                   eval      ra09l1_ikod = raikod
     c     ra09l1_key    chain     ra09l1
     c                   if        %found(ra09l1) and
     c                             raiema <> *Blank
      *
     c                   if        w_rec1 = *blank
     c                   eval      w_rec1 = raiema
     c                   else
     c                   if        w_rec2 = *blank
     c                   eval      w_rec2 = raiema
     c                   else
     c                   if        w_rec3 = *blank
     c                   eval      w_rec3 = raiema
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c     rax9l1_key    reade     rax9l1
     c                   enddo

      *
     c                   eval      w_subj = 'Lager oppdatering fra MalProff'
     c                   eval      w_txt1 = w_text
      *
     c                   eval      w_txt2 = %subst(w_text:101:100)
     c                   eval      w_txt3 = *blank
     c                   eval      w_txt4 = 'Med vennlig hilsen'
     c*                  eval      w_filn = '/asp/'+l_filg+'/MalProff/' +
     c*                            %trim(w_fil)
     c                   eval      w_filn = *blank
     c*                  eval      w_fnav = 'Lageroppdatering_feil' +
     c*                                      %char(%date) +   '.xlsx'
     c                   eval      w_fnav = *blank
     c                   eval      w_mime = 'application/vnd.openxmlformats-'
     c                   eval      w_mime = %trim(w_mime)+'officedocument.'
     c                   eval      w_mime = %trim(w_mime)+'spreadsheetml.sheet'
     c                   eval      w_bcc  = *blank
     c                   call      'AF728R'
     c                   parm                    w_rec1
     c                   parm                    w_rec2
     c                   parm                    w_rec3
     c                   parm                    w_subj
     c                   parm                    w_txt1
     c                   parm                    w_txt2
     c                   parm                    w_txt3
     c                   parm                    w_txt4
     c                   parm                    w_avsl
     c                   parm                    w_filn
     c                   parm                    w_fnav
     c                   parm                    w_mime
     c                   parm                    w_bcc
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
     c     *entry        plist
     c                   parm                    w_stat
     c                   parm                    w_fil
     c                   parm                    COMFLAG
     c                   parm                    p_stat
6.20 c                   parm                    p_errt
      *
     c                   eval      w_stat = 'Ok'
      *
      * Legge inn firma
      *
     c                   move      l_firm        w_firm
      *
      * Key for å finne lagernr
      *
     c     mjlol2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_gln
      *
      * Key for å finne varenr og lagernr
      *
     c     vvepl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ean
5.62  *
5.62  * Key for midlertidig oppdatering av MalProff tilleggsopplsyninger
5.62  *
5.62 c     rmpilu_key    klist
5.62 c                   kfld                    rmpilu_fir
5.62 c                   kfld                    rmpilu_lag
5.62 c                   kfld                    rmpilu_var
5.62 c                   kfld                    rmpilu_kda
5.62 c                   kfld                    rmpilu_kti
5.62 c                   kfld                    rmpilu_sek
5.62  *
      * Key for utlegg av feilrecords
      *
     c     rmpflu_key    klist
     c                   kfld                    rmpflu_fir
     c                   kfld                    rmpflu_kda
     c                   kfld                    rmpflu_kti
     c                   kfld                    rmpflu_tel
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på selger sidereg
      *
     c     rax9l1_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
      *
