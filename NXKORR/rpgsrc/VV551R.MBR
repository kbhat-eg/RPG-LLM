     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV551R
      * Beskrivelse . . : Vare, spørring på vare/pris - detaljer/kalkuler
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290498 HKO  Nytt program
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       261103 HKO  Viser utgår dato
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       020104 HKO  Henter parameter-verdier på et tidligere tidspunkt
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
      *       301104 HKO  Endret parameter ved kall til VP716R
5.70  * PRGR  311008 bof  Utvidet med prisgruppe /varetype fra lager
5.71  * PRGR  160509 bof  Får med lager fra kallende program
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  290212 bof  Justert hente pris ihht. lev.nr på lager
6.23  * 6.20  080312 bof  Utvidet parm. til vp716r
6.24  * 6.20  170215 nho  Blanker rabatt etter første vis
6.25  * 6.20  120615 bsa  Reverserer rettelse 6.24, da dette nullstiller
6.nu  * 6.30  160614 bof  Endret ihht. nummerutvidelse
6.30  * 6.30  080814 bof  Utvidet med nobb erstattet av fra VA
6.31  * 6.30  271014 bsa  Innført F tast for visning av NOBB info. Erstatter
6.31  *                   koding i jacada.
6.32  * 6.30  020715 bof  Hvis annen prisgiver, så hvis denne info i bildet.
6.33  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
7.xx  * 7.00  110117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.0x  * 7.00  020719 bsa  Fiks ved kalkulering av rabatt
7.01  * 6.30  240616 bsa  Lagt inn F tast for visning av VA info.
7.02  * 6.30  040618 bsa  Lagt inn F tast for varekort
7.03  * 7.00  261119 bof  Feilretting vedr. tidspkt på kampanje
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *       170323 bof  Skal kalle VP726R i stedet for VP716R
      *
8.05  *K0000  110925 bof  Hvis endr. kunde og storkunde så kalle VP726R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.30 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.33 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvv551d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
5.70 d l_lagr               1001   1002  0
      *
      * Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d d_orec                        80
     d  d_ltyp                        1  0 overlay(d_orec)
     d  d_kpri                        1    overlay(d_orec:2)
     d  d_sapr                        9  2 overlay(d_orec:12)
     d  d_rab1                        5  2 overlay(d_orec:30)
     d  d_rab2                        5  2 overlay(d_orec:35)
8.01 d  hoprgr                        2    overlay(d_orec:71)
      *
      * Definering av parameter
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
     d p_lety          s                   like(vylety)
5.71 d p_lage          s              2  0
6.31 d p_nobb          s              8
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguhgr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vltpl1_lety     s                   like(vylety)
     d fusrl1_user     s                   like(fbuser)
6.30 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
6.20 d w_uuda          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
5.50 d w_mv2f          s             10  9
5.50 d w_mv2p          s              6  2
5.50 d w_mv2t          s              5  4
     d w_kode          s              1  0
     d w_enhe          s              3
     d w_spny          s              9  2
     d w_kopr          s              9  2
     d w_sapr          s              9  2
     d w_s1pr          s              9  2
     d w_s2pr          s              9  2
     d w_s3pr          s              9  2
     d w_b2pr          s              9  2
     d w_b3pr          s              9  2
     d w_omr2          s             12  6
     d w_omr3          s             12  6
     d w_pda1          s               d   datfmt(*iso)
     d w_pda2          s               d   datfmt(*iso)
     d w_pda3          s               d   datfmt(*iso)
     d w_tfda          s               d   datfmt(*iso)
     d w_tfti          s               t   timfmt(*iso)
     d w_ttda          s               d   datfmt(*iso)
     d w_ttti          s               t   timfmt(*iso)
     d w_enin          s              3
     d w_enps          s              3
     d w_pspr          s              9  2
     d w_time_6        s              6  0
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_avde          s              5  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
6.21 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.33 d w_lv_nobb       s                   like(vvlnob)
6.33 d w_lv_modn       s                   like(vvlmod)
6.33 d w_lv_lldo       s                   like(vvlnle)
6.33 d w_lv_llva       s                   like(vvllva)
8.05 d w_prgr_y        s                   like(hiprgr)
8.05 d w_lage_y        s                   like(rklagr)
      *
     d s_anta          s             11  3
     d s_enhe          s              3
     d s_sapr          s              9  2
     d s_saim          s              9  2
     d s_raba          s              5  2
      *
     d w_firm          s                   like(vvfirm)
     d w_ltxt          s                   like(vybesk)
      *
     d s_kund          s                   like(flkund)
     d s_kpro          s                   like(flkpro)
     d s_lety          s                   like(vylety)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    p_vare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
      *
6.30  * henter info fra VA
6.33 c                   if        w_lv_nobb > 0
6.33 c                   movel     w_lv_nobb     jvarl1_vare
6.33 c                   else
6.30 c                   eval      jvarl1_vare = vvvare
6.33 c                   endif
6.30 c     jvarl1_key    chain     jvarl1
6.30 c                   if        %found(jvarl1)
6.30 c                   eval      c2enob = jvenob
6.30 c                   else
6.30 c                   eval      c2enob = 0
6.30 c                   endif
      *
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.32 c                   call      'VE722R'
6.11 c                   parm                    w_firm
6.32 c                   parm                    w_ldor
6.11 c                   parm                    p_vare
6.11 c                   parm                    vvenhe
6.11 c                   parm                    w_eann
6.11 c                   parm                    c2lvar
6.11 c                   parm                    w_east
5.70  *
      *
     c                   eval      c2vare = vvvare
6.33 c                   if        w_lv_nobb > 0
6.33 c                   eval      c2nobb = w_lv_nobb
6.33 c                   eval      c2nmod = w_lv_modn
6.33 c                   else
     c                   eval      c2nobb = vvnobb
     c                   eval      c2nmod = vvnmod
6.33 c                   endif
     c                   eval      c2tek1 = vvtek1
     c                   eval      c2tek2 = vvtek2
     c                   eval      c2ogrp = vvogrp
     c                   eval      c2hgrp = vvhgrp
     c                   eval      c2ugrp = vvugrp
6.33 c*                  eval      c2nmod = vvnmod
6.32 c                   eval      c2ldor = w_ldor
6.32 c*                  eval      c2lvar = vvlvar
6.11 c                   eval      c2eann = w_eann
     c                   eval      c2udat = 0
     c                   eval      c2kpri = vvkpri
5.70 c                   eval      c2prgr = w_prgr
5.70 c                   eval      c2vtyp = w_vtyp
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_uuda
6.20 c                   parm                    b_inlr
6.20 c                   if        w_uuda <> *loval
6.20 c     *dmy          move      w_uuda        c2udat
     c                   endif
      *
      * Kaller program for henting av pris-informasjon på tre enheter
      *
8.02 c                   call      'VP726R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
6.23 c                   parm                    p_lage
     c                   parm                    c2lety
     c                   parm                    w_udat
     c                   parm                    w_utim
     c                   parm                    c2enh1
     c                   parm                    w_s1pr
     c                   parm                    c2bupr
     c                   parm                    c2kopr
     c                   parm                    c2inpr
     c                   parm                    w_pda1
     c                   parm                    c2enh2
     c                   parm                    w_s2pr
     c                   parm                    w_b2pr
     c                   parm                    w_omr2
     c                   parm                    w_pda2
     c                   parm                    c2enh3
     c                   parm                    w_s3pr
     c                   parm                    w_b3pr
     c                   parm                    w_omr3
     c                   parm                    w_pda3
     c                   parm                    w_tfda
     c                   parm                    w_tfti
     c                   parm                    w_ttda
     c                   parm                    w_ttti
     c                   parm                    c2kamp
     c                   parm                    c2t1pr
     c                   parm                    c2kpti
     c                   parm                    c2t2pr
     c                   parm                    c2t3pr
     c                   parm                    w_enin
     c                   parm                    w_enps
     c                   parm                    w_pspr
      *
      * Salgspris
      *
     c                   eval      c2sagm = w_s1pr
      *
      * Tilbudsperiode
      *
     c                   if        w_tfda <> *loval
     c     *dmy          move      w_tfda        c2tfda
     c                   endif
      *
     c                   if        w_tfti <> *loval
     c     *hms          move      w_tfti        w_time_6
     c                   movel     w_time_6      c2tfti
     c                   endif
      *
     c                   if        w_ttda <> *loval
     c     *dmy          move      w_ttda        c2ttda
     c                   endif
      *
     c                   if        w_ttti <> *loval
     c     *hms          move      w_ttti        w_time_6
     c                   movel     w_time_6      c2ttti
     c                   endif
      *
      * Henter undergruppe-beskrivelse
      *
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2utxt = vgutxt
     c                   else
     c                   eval      c2utxt = *blank
     c                   endif
      *
      * Henter leverandørnavn
      *
     c                   eval      rlevl1_levr = c2ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c2lnav = rlnavn
     c                   else
     c                   eval      c2lnav = *blank
     c                   endif
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.50  *
5.50  * Finner hvilken mva-sats
5.50  * som skal benyttes (for halv sats)
5.50  *
5.50 c                   call      'FÅ706R'
5.50 c                   parm                    w_firm
5.50 c                   parm                    w_udat
5.50 c                   parm                    w_mv2p
5.50 c                   parm                    w_mv2t
5.50 c                   parm                    w_mv2f
      *
      * Beregner tilbudspris inkl. moms
      *
     c                   eval      w_kode = 4
      *
     c                   if        c2t1pr <> 0
5.50  *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2t1im = c2t1pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2t1im = c2t1pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2t1im
     c                   endif
      *
     c                   if        c2t2pr <> 0
5.50  *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2t2im = c2t2pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2t2im = c2t2pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2t2im
     c                   endif
      *
     c                   if        c2t3pr <> 0
5.50  *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2t3im = c2t3pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2t3im = c2t3pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2t3im
     c                   endif
      *
      * Henter kunde-/kundeprosjekt-informasjon, salgspriser og rabatt
      *
     c                   exsr      ny_kunde
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
6.24 c**6.25             eval      c2raba = 0
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or
     c                             *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      c2taga
     c                   endsl
7.02  *
7.02  * F07=Vis Varekort
7.02  *
7.02 c                   if        *inkg = *on
7.02 c                   exsr      vis_vkort
7.02 c                   goto      c2taga
7.02 c                   endif
7.01  *
7.01  * F10=Vis VA info
7.01  *
7.01 c                   if        *inkj = *on
7.01 c                   exsr      vis_VA
7.01 c                   goto      c2taga
7.01 c                   endif
6.31  *
6.31  * F18=NOBB informasjon
6.31  *
6.31 c                   if        *inks = *on
6.31 c                   exsr      vis_nobb
6.31 c                   goto      c2taga
6.35 c                   endif
      *
      * Valider input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c2taga
     c                   endif
      *
      * Tar vare på valgt kunde, kundeprosjekt og leveringstype
      *
     c                   eval      p_kund = c2kund
     c                   eval      p_kpro = c2kpro
     c                   eval      p_lety = c2lety
      *
      * Dersom F5 eller dersom det er foretatt endringer i kunde, kunde-
      * prosjekt eller leveringstype kalles subrutine for å hente
      * hente priser for kunde
      *
     c                   if        *inke   = *on or
     c                             c2kund <> s_kund or
     c                             c2kpro <> s_kpro or
     c                             c2lety <> s_lety
     c                   exsr      ny_kunde
     c                   goto      c2taga
     c                   endif
      *
      * Kalkulerer
      *
     c                   exsr      kalkulator
      *
      * F8=Vis produktinformasjon
      *
     c                   if        *inkh = *on
     c                   call      'FO704C'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   goto      c2taga
     c                   endif
      *
      * F9=Vise beholdningstall for vare
      *
     c                   if        *inki = *on
     c                   exsr      beholdning
     c                   goto      c2taga
     c                   endif
      *
      * F23=Vis innkjøpspris/kostpris/DG
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *off
     c                   exsr      vis_kost
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   goto      c2taga
     c                   endif
      *
     c                   goto      c2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente variabel informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_kunde      begsr
      *
     c                   eval      c2knav = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2s1pr = 0
     c                   eval      c2s2pr = 0
     c                   eval      c2s3pr = 0
      *
     c                   if        c2kund <> 0
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      c2knav = rknavn
     c                   endif
     c                   endif
8.05  *
8.05  * henter ny pris for valgt kunde
8.05  * tas fra når storkunde er på plass i standrd
8.05 c*                   if        rk2sto = 1
8.05 c*                   eval      w_prgr_y = w_prgr_sk
8.05 c*                   eval      c2prgr  = w_prgr_sk
8.05 c*                   eval      w_lage_y = w_stla
8.05 c*                   else
8.05 c                   eval      w_prgr_y = w_prgr
8.05 c                   eval      c2prgr  = w_prgr
8.05 c                   eval      w_lage_y = p_lage
8.05 c*                   ENDIF
8.05  *
8.05 c                   call      'VP726R'
8.05 c                   parm                    w_firm
8.05 c                   parm                    vvvare
8.05 c                   parm                    w_prgr_y
8.05 c                   parm                    w_lage_y
8.05 c                   parm                    c2lety
8.05 c                   parm                    w_udat
8.05 c                   parm                    w_utim
8.05 c                   parm                    c2enh1
8.05 c                   parm                    w_s1pr
8.05 c                   parm                    c2bupr
8.05 c                   parm                    c2kopr
8.05 c                   parm                    c2inpr
8.05 c                   parm                    w_pda1
8.05 c                   parm                    c2enh2
8.05 c                   parm                    w_s2pr
8.05 c                   parm                    w_b2pr
8.05 c                   parm                    w_omr2
8.05 c                   parm                    w_pda2
8.05 c                   parm                    c2enh3
8.05 c                   parm                    w_s3pr
8.05 c                   parm                    w_b3pr
8.05 c                   parm                    w_omr3
8.05 c                   parm                    w_pda3
8.05 c                   parm                    w_tfda
8.05 c                   parm                    w_tfti
8.05 c                   parm                    w_ttda
8.05 c                   parm                    w_ttti
8.05 c                   parm                    c2kamp
8.05 c                   parm                    c2t1pr
8.05 c                   parm                    c2kpti
8.05 c                   parm                    c2t2pr
8.05 c                   parm                    c2t3pr
8.05 c                   parm                    w_enin
8.05 c                   parm                    w_enps
8.05 c                   parm                    w_pspr
8.05  *
8.05  *
8.05  * Salgspris
8.05  *
8.05 c                   eval      c2sagm = w_s1pr
      *
     c                   if        c2kund <> 0 and
     c                             c2kpro <> 0
     c                   eval      fkprl1_kund = c2kund
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      c2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Henter kundepris og rabatt
      *
     c                   if        c2enh1 <> *blank
     c                   eval      w_enhe = c2enh1
     c                   exsr      nettopris
     c                   eval      c2s1pr = w_spny
     c                   eval      c2let1 = d_ltyp
     c                   eval      c2kpr1 = d_kpri
     c                   endif
      *
     c                   if        c2enh2 <> *blank
     c                   eval      w_enhe = c2enh2
     c                   exsr      nettopris
     c                   eval      c2s2pr = w_spny
     c                   eval      c2let2 = d_ltyp
     c                   eval      c2kpr2 = d_kpri
     c                   endif
      *
     c                   if        c2enh3 <> *blank
     c                   eval      w_enhe = c2enh3
     c                   exsr      nettopris
     c                   eval      c2s3pr = w_spny
     c                   eval      c2let3 = d_ltyp
     c                   eval      c2kpr3 = d_kpri
     c                   endif
      *
      * Fyller kalkulator-linje
      *
     c                   eval      c2anta = 1
     c                   eval      c2enhe = c2enh1
     c                   eval      c2sapr = c2s1pr
     c                   if        w_s1pr <> 0
     c                   eval(h)   c2raba = 100 * (w_s1pr - c2s1pr) / w_s1pr
     c                   endif
      *
      * Kalkulerer antall
      *
     c                   exsr      hent_antall
      *
      * Kalkulerer salgspriser inkl. mva og DG
      *
     c                   exsr      hent_info
     c                   eval      c2saim = c2s1im
      *
      * Tar vare på initieringsverdier
      *
     c                   eval      s_kund = c2kund
     c                   eval      s_kpro = c2kpro
     c                   eval      s_lety = c2lety
     c                   eval      s_anta = c2anta
     c                   eval      s_enhe = c2enhe
     c                   eval      s_sapr = c2sapr
     c                   eval      s_saim = c2saim
     c                   eval      s_raba = c2raba
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter nettopris og rabatt for kunde/kundeprosjekt for valgt enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nettopris     begsr
      *
     c                   eval      w_spny = 0
      *
      * Kaller prisberegningsprogram for w_enhe og finner salgspris
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = c2kund
     c                   eval      hikpro = c2kpro
     c                   eval      hivare = vvvare
     c                   eval      hilety = c2lety
     c                   eval      hienhe = w_enhe
     c                   eval      hidato = w_udat
     c                   eval      hitime = w_utim
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *blank
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
6.22 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *blank
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    d_orec
      *
     c                   eval      w_spny = d_sapr
      *
     c                   if        d_rab1 > 0
     c                   eval(h)   w_spny = w_spny - (w_spny * d_rab1 / 100)
     c                   endif
     c                   if        d_rab2 > 0
     c                   eval(h)   w_spny = w_spny - (w_spny * d_rab2 / 100)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer og henter antall mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_antall   begsr
      *
     c                   if        c2enhe <> *blank
      *
     c                   eval      c2ant1 = 0
     c                   eval      c2ant2 = 0
     c                   eval      c2ant3 = 0
      *
     c                   select
     c                   when      c2enhe = c2enh1
     c                   eval      c2ant1 = c2anta
     c                   if        w_omr2 <> 0
     c                   eval(h)   c2ant2 = c2anta / w_omr2
     c                   endif
     c                   if        w_omr3 <> 0
     c                   eval(h)   c2ant3 = c2anta / w_omr3
     c                   endif
     c                   when      c2enhe = c2enh2
     c                   eval(h)   c2ant1 = c2anta * w_omr2
     c                   eval      c2ant2 = c2anta
     c                   if        w_omr3 <> 0
     c                   eval(h)   c2ant3 = c2ant1 / w_omr3
     c                   endif
     c                   when      c2enhe = c2enh3
     c                   eval(h)   c2ant1 = c2anta * w_omr3
     c                   if        w_omr2 <> 0
     c                   eval(h)   c2ant2 = c2ant1 / w_omr2
     c                   endif
     c                   eval      c2ant3 = c2anta
     c                   endsl
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulator
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkulator    begsr
      *
     c                   eval      c2s1pr = 0
     c                   eval      c2s2pr = 0
     c                   eval      c2s3pr = 0
      *
     c                   if        c2anta = 0
     c                   eval      c2anta = 1
     c                   endif
      *
     c                   if        c2enhe = *blank
     c                   eval      c2enhe = c2enh1
     c                   endif
      *
      * Kalkulerer antall
      *
     c                   exsr      hent_antall
      *
      * Behandling ved forandring av antall, enhet eller rabatt
      *
     c                   if        c2anta <> s_anta or
     c                             c2enhe <> s_enhe or
     c                             c2raba <> s_raba or
     c                             c2sapr =  0 and
     c                             c2saim =  0
      *
     c                   eval      w_sapr = 0
      *
     c                   if        c2raba = 0
     c***                if        c2raba = 0 or
     c***                          c2enhe <> s_enhe and
     c***                          c2raba = s_raba
     c***                eval      c2raba = 0
     c                   eval      w_enhe = c2enhe
     c                   exsr      nettopris
     c                   eval      w_sapr = w_spny
     c                   else
     c                   select
     c                   when      c2enhe = c2enh1
     c                   eval      w_sapr = w_s1pr
     c                   when      c2enhe = c2enh2
     c                   eval      w_sapr = w_s2pr
     c                   when      c2enhe = c2enh3
     c                   eval      w_sapr = w_s3pr
     c                   endsl
     c                   endif
      *
     c                   eval      c2sapr = w_sapr * c2anta
     c                   eval(h)   c2sapr = c2sapr - (c2sapr * c2raba/100)
      *
     c                   eval      c2saim = 0
      *
     c                   if        c2sapr <> 0
5.50  *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2saim = c2sapr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2saim = c2sapr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2saim
     c                   endif
      *
     c                   goto      end_kalk
      *
     c                   endif
      *
      * Behandling ved forandring av salgspris eksl. mva
      *
     c                   if        c2sapr <> 0 and
     c                             c2sapr <> s_sapr or
     c                             c2saim = 0
      *
     c                   eval      c2saim = 0
      *
     c                   if        c2sapr <> 0
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2saim = c2sapr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2saim = c2sapr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2saim
     c                   endif
      *
     c                   goto      end_kalk
      *
     c                   endif
      *
      * Behandling ved forandring av salgspris inkl. mva
      *
     c                   if        c2saim <> 0 and
     c                             c2saim <> s_saim or
     c                             c2sapr = 0
      *
     c                   eval(h)   c2sapr = c2saim * w_mvaf
      *
     c                   goto      end_kalk
      *
     c                   endif
      *
     c     end_kalk      tag
      *
      * Kalkulerer rabatt
      *
     c                   eval(h)   w_sapr = c2sapr / c2anta
      *
     c                   select
     c                   when      c2enhe = c2enh1
7.0x c                   if        w_s1pr <> 0
     c                   eval(h)   c2raba = (w_s1pr - w_sapr) * 100 / w_s1pr
7.0x c                   endif
     c                   when      c2enhe = c2enh2
7.0x c                   if        w_s2pr <> 0
     c                   eval(h)   c2raba = (w_s2pr - w_sapr) * 100 / w_s2pr
7.0x c                   endif
     c                   when      c2enhe = c2enh3
7.0x c                   if        w_s3pr <> 0
     c                   eval(h)   c2raba = (w_s3pr - w_sapr) * 100 / w_s3pr
7.0x c                   endif
     c                   endsl
      *
      * Kalkulerer enhetspriser alle enheter
      *
     c                   eval      c2s1pr = w_s1pr - (w_s1pr * c2raba / 100)
     c                   eval      c2s2pr = w_s2pr - (w_s2pr * c2raba / 100)
     c                   eval      c2s3pr = w_s3pr - (w_s3pr * c2raba / 100)
      *
      * Kalkulerer salgspris inkl. mva og DG
      *
     c                   exsr      hent_info
      *
      * Tar vare på variable
      *
     c                   eval      s_anta = c2anta
     c                   eval      s_enhe = c2enhe
     c                   eval      s_sapr = c2sapr
     c                   eval      s_saim = c2saim
     c                   eval      s_raba = c2raba
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer og henter salgspriser inkl. mva og dekningsgrad (DG)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c2s1im = 0
     c                   eval      c2s2im = 0
     c                   eval      c2s3im = 0
     c                   eval      c2dekn = 0
      *
      * Kalkulerer priser inkl. moms
      *
     c                   if        c2s1pr <> 0
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2s1im = c2s1pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2s1im = c2s1pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2s1im
     c                   endif
      *
     c                   if        c2s2pr <> 0
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2s2im = c2s2pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2s2im = c2s2pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2s2im
     c                   endif
      *
     c                   if        c2s3pr <> 0
5.50  *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      c2s3im = c2s3pr * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      c2s3im = c2s3pr * w_mv2t
5.50 c                   endsl
5.50  *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    c2s3im
     c                   endif
      *
      * Kalkulerer DG
      *
     c                   eval      w_sapr = c2sapr / c2anta
      *
     c                   eval      w_kopr = 0
      *
     c                   if        w_udat >= w_tfda and
     c                             w_udat <= w_ttda
7.03 c**                  if        w_tfti = *loval or
7.03 c**                            w_tfti <> *loval and
7.03 c**                            w_utim >= w_tfti and
7.03 c**                            w_utim <= w_ttti
      *
7.03 c                   if        w_udat <> w_tfda or
7.03 c                             w_udat = w_tfda and
7.03 c                             w_utim >= w_tfti
7.03 c                   if        w_udat <> w_ttda or
7.03 c                             w_udat = w_ttda and
7.03 c                             w_utim <= w_ttti
     c                   eval      w_kopr = c2kpti
     c                   endif
7.03 c                   endif
     c                   endif
      *
     c                   if        w_kopr = 0
     c                   eval      w_kopr = c2kopr
     c                   endif
      *
     c                   select
     c                   when      c2enhe = c2enh2
     c                   eval(h)   w_kopr = w_kopr * w_omr2
     c                   when      c2enhe = c2enh3
     c                   eval(h)   w_kopr = w_kopr * w_omr3
     c                   endsl
      *
     c                   if        w_sapr <> 0
     c                   select
     c                   when      (100 - (w_kopr * 100 / w_sapr)) < -999,99
     c                   eval      c2dekn = -999,99
     c                   when      (100 - (w_kopr * 100 / w_sapr)) > 999,99
     c                   eval      c2dekn = 999,99
     c                   other
     c                   eval(h)   c2dekn = 100 - (w_kopr * 100 / w_sapr)
     c                   endsl
     c                   endif
      *
     c     end_dg        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'C2KUND'
      *
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c2kund
     c                   parm                    c2knav
      *
     c                   if        c2kund <> s_kund
     c                   eval      c2kpro = 0
     c                   eval      c2pnav = *blank
     c                   eval      c2anta = 0
     c                   eval      c2enhe = *blank
     c                   eval      c2sapr = 0
     c                   eval      c2saim = 0
     c                   eval      c2raba = 0
     c                   eval      c2s1pr = 0
     c                   eval      c2s2pr = 0
     c                   eval      c2s3pr = 0
     c                   eval      c2s1im = 0
     c                   eval      c2s2im = 0
     c                   eval      c2s3im = 0
     c                   endif
      *
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'C2KPRO'
      *
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    c2kund
     c                   parm                    c2kpro
     c                   parm                    c2knav
     c                   parm                    c2pnav
      *
     c                   if        c2kund <> s_kund or
     c                             c2kpro <> s_kpro
     c                   eval      c2anta = 0
     c                   eval      c2enhe = *blank
     c                   eval      c2sapr = 0
     c                   eval      c2saim = 0
     c                   eval      c2raba = 0
     c                   eval      c2s1pr = 0
     c                   eval      c2s2pr = 0
     c                   eval      c2s3pr = 0
     c                   eval      c2s1im = 0
     c                   eval      c2s2im = 0
     c                   eval      c2s3im = 0
     c                   endif
      *
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'C2LETY'
      *
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c2lety
     c                   parm                    w_ltxt
      *
     c                   if        c2lety <> s_lety
     c                   eval      c2anta = 0
     c                   eval      c2enhe = *blank
     c                   eval      c2sapr = 0
     c                   eval      c2saim = 0
     c                   eval      c2raba = 0
     c                   eval      c2s1pr = 0
     c                   eval      c2s2pr = 0
     c                   eval      c2s3pr = 0
     c                   eval      c2s1im = 0
     c                   eval      c2s2im = 0
     c                   eval      c2s3im = 0
     c                   endif
      *
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å validere input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Kunde og kundeprosjekt
      *
     c                   if        c2kund = 0
     c                   eval      c2kpro = 0
     c                   endif
      *
     c                   if        c2kund <> 0
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *on
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        c2kpro <> 0
     c                   eval      fkprl1_kund = c2kund
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Enhet
      *
     c                   if        c2enhe <> *blank
     c                   if        c2enhe <> c2enh1 and
     c                             c2enhe <> c2enh2 and
     c                             c2enhe <> c2enh3
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Antall, enhet og rabatt kan ikke endres samtidig med priser
      *
     c                   if        c2anta <> s_anta or
     c                             c2enhe <> s_enhe or
     c                             c2raba <> s_raba
     c                   if        c2sapr <> 0 and
     c                             c2sapr <> s_sapr or
     c                             c2saim <> 0 and
     c                             c2saim <> s_saim
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Salgspris eksl.mva og inkl.mva kan ikke endres samtidig
      *
     c                   if        c2sapr <> 0 and
     c                             c2sapr <> s_sapr
     c                   if        c2saim <> 0 and
     c                             c2saim <> s_saim
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise beholdnigstall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beholdning    begsr
      *
     c     fstsl1_key    chain     fstsl1                             91
     c                   if        *in91 = *off and
     c                             faalag = 0
     c                   eval      *in40 = *on
     c                   goto      end_beholdn
     c                   endif
      *
     c                   call      'VL750C'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    vvtek1
      *
     c     end_beholdn   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise innkjøpspris, kostpris og DG
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_kost      begsr
      *
      * Innkøpspris og kostpris skal kun vises dersom bruker har tillatelse
      * til dette
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1                             91
     c                   if        *in91 = *on or
     c                             fbko02 = 0
     c                   eval      *in41 = *on
     c                   goto      end_kost
     c                   endif
      *
     c                   eval      *in80 = *on
      *
     c     end_kost      endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     vis_nobb      begsr
6.31 c
6.31 c                   eval      p_nobb = %char(c2nobb)
6.31 c                   call      'AP633R'
6.31 c                   parm                    p_nobb
6.31  *
6.31 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Viser VA info
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     vis_VA        begsr
7.01  *
7.01 c                   call      'JV145R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    p_vare
7.01 c                   parm                    c2prgr
7.01 c                   parm                    p_lety
7.01 c                   parm                    w_udat
7.01 c                   parm                    c2ldor
7.01  *
7.01 c                   endsr
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Viser varekort
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     vis_vkort     begsr
7.02  *
7.02 c                   call      'VV510R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    p_vare
7.02  *
7.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_lety
5.71 c                   parm                    p_lage
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag mot undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag mot leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag mot kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag mot kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag mot leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag mot bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på OFAK-kode
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
6.30  *
6.30  * Key for oppslag på vare i VA
6.30  *
6.30 c     jvarl1_key    klist
6.30 c                   kfld                    jvarl1_vare
6.30  *
      *
      *
      * Henter firma, kunde, kundeprosjekt og leveringstype fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      c2kund = p_kund
     c                   eval      c2kpro = p_kpro
     c                   eval      c2lety = p_lety
      *
      * Finner udate
      *
     c                   time                    w_udat
     c                   time                    w_utim
      *
5.71 c                   eval      w_lage = p_lage
5.71 c                   eval      w_avde = 0
      *
5.71  * Finner prisgruppe
5.71  *
5.71 c                   call      'VL712R  '
5.71 c                   parm                    w_firm
5.71 c                   parm                    w_lage
5.71 c                   parm                    w_avde
5.71 c                   parm                    w_prgr
      *
     c                   endsr
