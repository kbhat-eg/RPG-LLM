      /TITLE  ASOFAK Faktura, spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : SO501R
      * Beskrivelse . . : Faktura, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
      * 3.11  071100 AMD  Korreksjon på time-stamp-melding
      * 5.30  131104 KOB  Lagt inn kundenr i call til SO510R
5.60  * R5.60 250408 KOB  Lagt inn ny funksjon for å vise arkivpost
5.60  * R5.60 010508 KOB  Lagt inn ny funksjon for å vise slettede varelinjer
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
      *
6.20  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.20  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.21  * R6.20     030113  Endret metode for oppslag mot arkiv            bsa  *
6.22  * R6.20     040113  Endret oppbygging av refno                     bsa  *
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
7.xx  * 7.00  180216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  161018 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  280319 bkv  Frisøk
      *
8.01  *K0000  271025 bsa  Justert sortering ved bruk av søk.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohelr    if   e           k disk    rename(sohepfr:sohelrr)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
     fsohel4    if   e           k disk    rename(sohepfr:sohel4r)
     fsohel5    if   e           k disk    rename(sohepfr:sohel5r)
     fsohela    if   e           k disk    rename(sohepfr:sohelar)
     fsohel6    if   e           k disk    rename(sohepfr:sohel6r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fso501d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s                   like(soaarr)
     d p_otyp          s                   like(sootyp)
     d p_kund          s                   like(sokund)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sohelr_binr     s                   like(sobinr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel3_numm     s                   like(sonumm)
     d sohel4_otyp     s                   like(sootyp)
     d sohel5_fkda     s                   like(sofkda)
     d sohela_kuna     s                   like(sokuna)
     d sohela_navn     s                   like(sonavn)
     d sohel6_navn     s                   like(sonavn)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
5.60  *
5.60  * Definering av parametre MultiArcive
5.60  *
6.21 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
5.60 d*w_modu*pdf      s             10    inz('MSA     ')
5.60 d w_disp          s              1    inz('1')
5.60 d w_tilg          s              1
5.60 d w_pgrp          s              2
5.60 d w_aara          s              4
6.nu d w_bila          s              8
5.60 d w_afir          s              3
6.21 d w_refn          s             50
7.01 d w_len           s              3  0
7.02 d b_open_sok      s              1    inz(*off)
7.02 d sqlquery        s           4096
7.02 d sqlwhere        s           2096
7.02 d sql_fri_s       s             50
7.02 d sql_fri_sl      s             50
7.02 d x               s              1  0
7.02 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
7.02 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
7.02 d w_ste1          s             35
7.02 d w_ste2          s             35
7.02 d w_ste3          s             35
7.02 d w_ste4          s             35
7.02 d w_ste5          s             35
7.02 d w_fsok          s                   like(b2fsok)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
7.02  *
7.02  * F21=Hent søk
7.02  *
7.02 c                   when      *inkv
7.02 c                   eval      b2fsok = w_fsok
7.02 c                   eval      *in22 = *on
7.02 c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2binr <> 0 or
     c                             b2numm <> 0 or
     c                             b2otyp <> *blank or
     c                             b2fkda <> 0 or
     c                             b2kuna <> *blank or
     c                             b2navn <> *blank or
7.02 c                             b2fsok <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c                   eval      b2fsok = *blank
7.02 c                   eval      b2fso2 = *blank
7.02 c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'O'
     c     sohel3_key    setll     sohel3
     c                   when      w_seqe = 'T'
8.01 c                   eval      b_open_sok = *on
8.01 c*    sohel4_key    setll     sohel4
     c                   when      w_seqe = 'D'
     c     sohel5_key    setll     sohel5
     c                   when      w_seqe = 'K'
     c     sohela_key    setll     sohela
     c                   when      w_seqe = 'A'
     c     sohel6_key    setll     sohel6
     c                   other
     c     sohel1_key    setll     sohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på fakturanummer
      *
     c                   if        b2binr <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = ' '
     c                   eval      sohel1_binr = b2binr
     c                   eval      sohel1_numm = b2numm
     c     sohel1_key    setll     sohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'O'
     c                   eval      sohel3_numm = b2numm
     c     sohel3_key    setll     sohel3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordretype
      *
     c                   if        b2otyp <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'T'
8.01 c                   eval      b_open_sok = *on
8.01 c*                  eval      sohel4_otyp = b2otyp
8.01 c*    sohel4_key    setll     sohel4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på fakturadato
      *
     c                   if        b2fkda <> 0
7.02 c                   eval      b2fso2 = *blank
      *
     c                   eval      w_seqe = 'D'
     c                   eval      sohel5_fkda = *loval
      *
     c     *dmy          test(d)                 b2fkda                 90
     c                   if        *in90 = *off
     c     *dmy          move      b2fkda        sohel5_fkda
     c                   endif
      *
     c     sohel5_key    setll     sohel5
     c                   goto      end_pos
      *
     c                   endif
      *
      * Posisjoner startverdi på alfasøk kunde
      *
     c                   if        b2kuna <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'K'
     c                   eval      sohela_kuna = b2kuna
     c                   eval      sohela_navn = b2navn
     c     sohela_key    setll     sohela
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vareadresse
      *
     c                   if        b2navn <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'A'
     c                   eval      sohel6_navn = b2navn
     c     sohel6_key    setll     sohel6
     c                   goto      end_pos
     c                   endif
7.02  *
7.02  * Posisjoner startverdi på frisøk
7.02  *
7.02 c                   if        b2fsok <> *blank
7.02 c                   eval      b2fso2 = b2fsok
7.02 c                   eval      w_fsok = b2fsok
7.02 c                   eval      b_open_sok = *on
7.02 c                   eval      w_seqe = 'S'
7.02 c                   goto      end_pos
7.02 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2binr = 0
     c                   eval      b2numm = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2fkda = 0
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
7.02 c                   eval      b2fsok = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis faktura
      *
     c                   if        b1valg = 5
     c                   call      'SO510R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
V5.30c                   parm                    p_kund
     c                   parm                    b1binr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv fakturakopi
      *
     c                   if        b1valg = 6
     c                   call      'FO681R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1binr
     c                   parm                    b1otyp
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
5.60  *
5.60  * Vis slettede ordrelinjer
5.60  *
5.60 c                   if        b1valg = 7
5.60 c                   call      'FO506R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    w_aarr
5.60 c                   parm                    p_kund
5.60 c                   parm                    b1binr
5.60 c                   parm                    b1numm
5.60 c                   parm                    b1suff
5.60 c                   eval      b1valg = 0
5.60 c                   update    b1sfl
5.60 c                   iter
5.60 c                   endif
      *
      * Vis ordrehode
      *
5.60 c                   if        b1valg = 8
     c                   call      'SO515R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1binr
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
5.60  *
5.60  * Vis arkiv
5.60  *
5.60 c                   if        b1valg = 9
5.60  *
5.60  * Kontrollerer gyldig tilgang
5.60  *
5.60 c                   call      'AX700R'
5.60 c                   parm                    w_modu
5.60 c                   parm                    w_disp
5.60 c                   parm                    w_tilg
5.60  *
5.60 c                   if        w_tilg = '0'
5.60  *
6.21 c**                 eval      w_type = 'FAKTURA'
6.21 c                   eval      w_type = 'FAKTURA'
5.60 c                   move      w_firm        w_afir
5.60 c                   eval      w_pgrp = '20'
5.60 c                   eval      w_aara = %char(w_aarr)
6.nu c                   eval      w_bila = %char(b1binr)
6.21 c                   eval      w_refn = *blank
6.22 c**                 eval      w_refn = %trim(%char(b1binr))
6.22 c                   eval      w_refn = %char(w_aarr) + %trim(%char(b1binr))
5.60  *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.21 c                   parm                    w_refn
6.21 c**                 parm                    w_aara
6.21 c**                 parm                    w_bila
6.21 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
5.60  *
5.60 c                   endif
5.60 c                   eval      b1valg = 0
5.60 c                   update    b1sfl
5.60 c                   iter
5.60 c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
7.02  * Initierer SQL for søk
7.02  *
7.02 c                   if        b_open_sok = *on
7.02  *
7.02 c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ close STMT
7.02 c/end-exec
7.02 c
7.02 c                   eval      sqlwhere = ''
7.02 c                   if        b2fsok <> *blanks
7.02 c     lo:up         xlate     b2fsok        b2fsok
7.02 c                   call      'AS700R'
7.02 c                   parm                    b2fsok
7.02 c                   parm                    w_ste1
7.02 c                   parm                    w_ste2
7.02 c                   parm                    w_ste3
7.02 c                   parm                    w_ste4
7.02 c                   parm                    w_ste5
7.02 c
7.02 c                   eval      w_ste1 = %ScanRpl('%' : ' ' : w_ste1)
7.02 c     1             do        5             x
7.02 c                   select
7.02 c                   when      x=1
7.02 c                   if        w_ste1 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste1)+'%'''
7.02 c                   when      x=2
7.02 c                   if        %trim(w_ste2) = ''
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste2)+'%'''
7.02 c                   when      x=3
7.02 c                   if        w_ste3 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste3)+'%'''
7.02 c                   when      x=4
7.02 c                   if        w_ste4 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste4)+'%'''
7.02 c                   when      x=5
7.02 c                   if        w_ste5 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste5)+'%'''
7.02 c                   endsl
7.02 c
7.02 c     up:lo         xlate     sql_fri_s     sql_fri_sl
7.02 c                   eval      sql_fri_sl = %ScanRpl('%' : '' : sql_fri_sl)
7.02 c
7.02 c                   eval      sqlwhere  = %trim(sqlwhere) + ' +
7.02 c                             and ( +
7.02 c                                a.sokuna like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.sonavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rknavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rkkund like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.sonumm like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.soadr1 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.soadr2 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.sosted like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.sobinr like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.sodref) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.sorekv) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                              or lower(DAYNAME(a.sobdat)) +
7.02 c                                  = '+sql_fri_sl+ ' +
7.02 c                             or ((select count(*) from sodtpf +
7.02 c                             where sdfirm=a.sofirm and sdnumm= +
7.02 c                              a.sonumm and sdsuff = a.sosuff and +
7.02 c                             sdtek1 like '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                             or ((select count(*) from sodtpf +
7.02 c                             where sdfirm=a.sofirm and sdnumm= +
7.02 c                             a.sonumm and sdsuff = a.sosuff and +
7.02 c                             sdvare LIKE '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                               )'
7.02 c                   enddo
7.02 c                   endif
7.02 c
7.02 c                   eval      sqlquery  = 'select +
7.02 c                               a.sofirm +
7.02 c                              ,a.soaarr +
7.02 c                              ,a.sobinr +
7.02 c                              ,a.sonumm +
7.02 c                              ,a.sosuff +
7.02 c                              ,a.sootyp +
7.02 c                              ,a.sonavn +
7.02 c                              ,a.sofkda +
7.02 c                              ,a.sokund +
7.02 c                              ,a.sofsum +
7.02 c                               from   sohepf a +
7.02 c                               left outer join rkunpf b on b.rkfirm = +
7.02 c                                 a.sofirm and b.rkkund = a.sokund +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               where  a.sofirm = ? and a.soaarr = ? '
7.02 c                   eval      sqlquery  = %trim(sqlquery) +' '+
7.02 c                                            %trim(sqlwhere)
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               order by +
7.02 c                               a.sodate desc +
7.02 c                               , a.sotime desc +
7.02 c                               , a.sonumm +
7.02 c                               , a.sosuff +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               for read only +
7.02 c                               '
7.02 c
7.02 c/exec sql
7.02 c+ PREPARE SQL_STMT FROM :sqlquery
7.02 c/end-exec
7.02 c/exec sql
7.02 c+ DECLARE STMT CURSOR FOR SQL_STMT
7.02 c/end-exec
7.02 c
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   leavesr
7.02 c                   endif
7.02 c
7.02 c/exec sql
7.02 c+ OPEN STMT using :w_firm, :w_aarr
7.02 c/end-exec
7.02 c
8.01  *
8.01 c                   when      w_seqe = 'T'
8.01 c/exec sql
8.01 c+
8.01 c+                  declare sok_otyp cursor for
8.01 c+                  select sofirm,
8.01 c+                         soaarr,
8.01 c+                         sobinr,
8.01 c+                         sonumm,
8.01 c+                         sosuff,
8.01 c+                         sootyp,
8.01 c+                         sonavn,
8.01 c+                         sofkda,
8.01 c+                         sokund,
8.01 c+                         sofsum
8.01 c+                  from   sohepf
8.01 c+                  where  sofirm = :w_firm and
8.01 c+                         soaarr = :w_aarr and
8.01 c+                         sootyp = :b2otyp
8.01 c+                  order by sobinr desc, sonumm asc
8.01 c+                  for read only
8.01 c/end-exec
8.01 c/exec sql
8.01 c+                  close sok_otyp
8.01 c/end-exec
8.01 c/exec sql
8.01 c+                  open sok_otyp
8.01 c/end-exec
8.01 c
7.02 c                   endsl
7.02 c                   endif                                                  b_open_sok = *on
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ fetch STMT
7.02 c+ into  :sofirm,:soaarr,:sobinr,:sonumm,:sosuff,:sootyp,
7.02 c+       :sonavn,:sofkda,:sokund,:sofsum
7.02 c/end-exec
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   endif
     c                   when      w_seqe = 'O'
     c                   read      sohel3                                 15
8.01 c*                  when      w_seqe = 'T'
8.01 c*                  read      sohel4                                 15
8.01 c                   when      w_seqe = 'T'
8.01 c/exec sql
8.01 c+                  fetch next
8.01 c+                  from  sok_otyp
8.01 c+                  into  :sofirm,
8.01 c+                        :soaarr,
8.01 c+                        :sobinr,
8.01 c+                        :sonumm,
8.01 c+                        :sosuff,
8.01 c+                        :sootyp,
8.01 c+                        :sonavn,
8.01 c+                        :sofkda,
8.01 c+                        :sokund,
8.01 c+                        :sofsum
8.01 c/end-exec
8.01 c                   if        sqlcod = 100 or sqlstt = '02000'
8.01 c                   eval      *in15 = *on
8.01 c                   endif
      *
     c                   when      w_seqe = 'D'
     c                   read      sohel5                                 15
     c                   when      w_seqe = 'K'
     c                   read      sohela                                 15
     c                   when      w_seqe = 'A'
     c                   read      sohel6                                 15
     c                   other
     c                   read      sohel1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        sofirm <> w_firm or
     c                             soaarr <> w_aarr
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        p_otyp <> *blank and
     c                             sootyp <> p_otyp
     c                   iter
     c                   endif
      *
     c                   if        p_kund <> 0 and
     c                             sokund <> p_kund
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1binr = sobinr
     c                   eval      b1numm = sonumm
     c                   eval      b1suff = sosuff
     c                   eval      b1otyp = sootyp
     c                   eval      b1navn = sonavn
      *
3.11 c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        b1fkda
3.11 c                   endif
      *
     c                   eval      b1knav = *blank
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b1knav = rknavn
     c                   endif
      *
      * Henter fakturasum fra siste ordre på fakura
      *
     c                   eval      sohelr_binr = sobinr
     c     sohelr_key    setll     sohelr
     c     sohelr_key    reade     sohelr                                 90
     c                   dow       *in90 = *off
     c     sohelr_key    reade     sohelr                                 90
     c                   enddo
      *
     c                   eval      b1fsum = sofsum
      *
7.01 c                   exsr      newlo_vask
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
7.02 c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01  /FREE
7.01
7.01   begsr newlo_vask;
7.01     w_len = %len(%trim(b1navn));
7.01     if (w_len > 0);
7.01       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1navn));
7.01     if (w_len > 0);
7.01       b1navn = %ScanRpl(':':'':b1navn:w_len); // newlook tåler ikke : på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1knav));
7.01     if (w_len > 0);
7.01       b1knav = %ScanRpl('.':'':b1knav:w_len); // newlook tåler ikke . på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1knav));
7.01     if (w_len > 0);
7.01       b1knav = %ScanRpl(':':'':b1knav:w_len); // newlook tåler ikke : på slutten
7.01     endif;
7.01   endsr;
7.01  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_otyp
     c                   parm                    p_kund
      *
      * Key for les på fakturaregister
      *
     c     sohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohelr_binr
      *
      * Key for posisjonering på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
      *
      * Key for posisjonering på ordrenummer
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel3_numm
      *
      * Key for posisjonering på ordretype
      *
     c     sohel4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel4_otyp
      *
      * Key for posisjonering på fakturadato
      *
     c     sohel5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel5_fkda
      *
      * Key for posisjonering på kunde alfasøk
      *
     c     sohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohela_kuna
     c                   kfld                    sohela_navn
      *
      * Key for posisjonering på vareadresse
      *
     c     sohel6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel6_navn
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firmakode og år fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      b2firm = w_firm
     c                   eval      w_aarr = p_aarr
      *
      * Henter verdi til skjermbilde
      *
     c                   eval      b2aarr = w_aarr
     c                   eval      b2oty1 = p_otyp
     c                   eval      b2kund = p_kund
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
      *
     c     sohel1_key    setll     sohel1
      *
7.02 c                   eval      w_seqe = 'S'
7.02 c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
