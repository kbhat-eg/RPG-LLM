     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO780R
      * Beskrivelse . . : Kalkulering av vekt og volum for ordre
      *
      *                   Returnerer vekt i kg og volum i m3
      *                   Håndterer kun lager og skaffevarer
      *                   Dersom vekt og volum-informasjon ikke finnes i
      *                   EVR letes det etter informasjon i VA
      *                   Dersom en vare ikke har vekt og volum-informasjon
      *                   returneres avviksflagg
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251005 HKO  Nytt program
5.70  * PRGR  231008 sst  Prisgruppe
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.31  * 6.30  260114 bof  Utvidet nøkkel i JVPKPF
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.33  * 6.30  070319 bkv  Rettet vekt beregning fra VA
      *
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *K0000  160623 bsa  Utvidet volum for å ta med flere desimaler
8.03  *K0000  300625 bsa  Utvidet beregning av vekt og volum
8.03  *K000               Hvis vi ikke finner vekt, forsøk finne den via
8.03  *K000               omregning. Ofte ikke vekt på M2 enheter.
8.03  *K000               Sjekk først etter T-PAK hvis vekt ikke finnes på angitt
8.03  *K000               enhet. Deretter første mulige.
8.03  *K000               Bruker annen logikk for å finne antall. Må ta
8.03  *K000               høyde for at det bestilles F8 varer.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
8.03 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_vekt          s              9  3
     d p_volu          s              9  3
     d p_avvi          s              1
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d jvpkl2_vare     s                   like(jwvare)
6.31 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
8.03 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_vekt          s              1    inz(*off)
     d b_volu          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_vekt          s              9  3
8.02 d*w_volu          s              9  3
8.02 d w_volu          s             11  5
6.21 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
8.03 d w_omr1          s             12  6
8.03 d w_omr2          s             12  6
8.03 d w_jvekt         s              9  0
      *
     d w_firm          s                   like(fdfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_vekt = 0
     c                   eval      p_volu = 0
      *
     c                   eval      p_avvi = *off
      *
      * Leser alle varer i en ordre og summerer vekt og volum
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
      *
     c                   if        fdvare <> *blank
     c                   exsr      kalkuler
     c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Setter avviks-flagg dersom vekt- eller volum-informasjon ikke ble
      * funnet på alle varer
      *
     c                   if        b_vekt = *on or
     c                             b_vekt = *on
     c                   eval      p_avvi = *on
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer vekt og volum for varer i en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkuler      begsr
      *
5.70 c                   eval      p_vtyp = *blank
      *
     c                   eval      vevekt = 0
     c                   eval      vevolu = 0
     c                   eval      jwvekt = 0
     c                   eval      jwvolu = 0
      *
      * Behandler ikke tjenester og diverse-varer
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
8.03 c                   if        not %found
8.03 c                   eval      jvarl1_vare = fdvare
8.03 c     jvarl1_key    chain     jvarl1
8.03 c                   if        %found
8.03 c                   eval      vvldor = jvldor
8.03 c                   goto      va_vare
8.03 c                   endif
8.03 c                   endif
     c                   endif
5.70  *
5.70  * Kaller program for henting av prisgruppe
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    fdlage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
5.70 c                   if        p_vtyp = 'T'
     c                   leavesr
     c                   endif
      *
5.70 c                   if        p_vtyp = 'D'
     c                   eval      b_vekt = *on
     c                   eval      b_volu = *on
     c                   leavesr
     c                   endif
      *
8.03 c     va_vare       tag
      *
      * Finner vekt og volum
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found or
     c                             vevekt = 0 or
     c                             vevolu = 0
     c                   eval      jvpkl2_vare = fdvare
6-31 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = fdenh1
     c     jvpkl2_key    chain     jvpkl2
     c                   endif
      *
     c                   if        vevekt <> 0
     c                   eval      w_vekt = vevekt
     c                   else
6.33 c*                   eval      w_vekt = jwvekt * 1000
6.33 c                   eval      w_vekt = jwvekt / 1000
     c                   endif
      *
     c                   if        vevolu <> 0
     c                   eval      w_volu = vevolu
     c                   else
     c                   eval      w_volu = jwvolu
     c                   endif
8.03  *
8.03  * Hvis vekt er 0, prøv å finne vekt for annen enhet, og bruk
8.03  * omregningsfaktor mellom enhetene.
8.03  * Prøver først å finne T pakning.
8.03  *
8.03 c                   if        w_vekt = 0
8.03  *
8.03 c                   eval      w_omr1 = 0
8.03 c                   eval      w_omr2 = 0
8.03 c                   eval      w_jvekt = 0
8.03  *
8.03 c                   eval      jvpkl2_vare = fdvare
8.03 c                   eval      jvpkl2_ldor = vvldor
8.03 c                   eval      jvpkl2_enhe = *blanks
8.03 c     jvpkl2_key    setll     jvpkl2
8.03 c     jvpkl2_key2   reade     jvpkl2
8.03 c                   dow       not %eof
8.03 c                   if        jwenhe = fdenh1
8.03 c                   eval      w_omr1 = jwofak
8.03 c                   endif
8.03 c                   if        jwvekt <> 0 and
8.03 c                             jwofak <> 0 and
8.03 c                             jwpkla = 'T'
8.03 c                   eval      w_omr2 = jwofak
8.03 c                   eval      w_jvekt = jwvekt
8.03 c                   eval      w_volu = jwvolu
8.03 c                   endif
8.03  *
8.03 c                   if        w_omr1 <> 0 and
8.03 c                             w_omr2 <> 0 and
8.03 c                             w_jvekt <> 0
8.03 c                   eval      w_vekt = (w_omr1 / w_omr2) * w_jvekt / 1000
8.03 c                   leave
8.03 c                   endif
8.03 c     jvpkl2_key2   reade     jvpkl2
8.03 c                   enddo
8.03  *
8.03 c                   endif
8.03  *
8.03  * Så ny runde uten hensyn til pakningsklasse.
8.03  *
8.03 c                   if        w_vekt = 0
8.03  *
8.03 c                   eval      w_omr1 = 0
8.03 c                   eval      w_omr2 = 0
8.03 c                   eval      w_jvekt = 0
8.03  *
8.03 c                   eval      jvpkl2_vare = fdvare
8.03 c                   eval      jvpkl2_ldor = vvldor
8.03 c                   eval      jvpkl2_enhe = *blanks
8.03 c     jvpkl2_key    setll     jvpkl2
8.03 c     jvpkl2_key2   reade     jvpkl2
8.03 c                   dow       not %eof
8.03 c                   if        jwenhe = fdenh1
8.03 c                   eval      w_omr1 = jwofak
8.03 c                   endif
8.03 c                   if        jwvekt <> 0 and
8.03 c                             jwofak <> 0
8.03 c                   eval      w_omr2 = jwofak
8.03 c                   eval      w_jvekt = jwvekt
8.03 c                   eval      w_volu = jwvolu
8.03 c                   endif
8.03  *
8.03 c                   if        w_omr1 <> 0 and
8.03 c                             w_omr2 <> 0 and
8.03 c                             w_jvekt <> 0
8.03 c                   eval      w_vekt = (w_omr1 / w_omr2) * w_jvekt / 1000
8.03 c                   leave
8.03 c                   endif
8.03 c     jvpkl2_key2   reade     jvpkl2
8.03 c                   enddo
8.03  *
8.03 c                   endif
      *
     c                   if        w_vekt = 0
     c                   eval      b_vekt = *on
     c                   endif
      *
     c                   if        w_volu = 0
     c                   eval      b_volu = *on
     c                   endif
      *
      * Summerer vekt og volum
      *
     c                   eval      p_vekt = p_vekt + (w_vekt * fdanta)
     c                   eval      p_volu = p_volu + (w_volu * fdanta)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Feilhåndtering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_vekt = 0
     c                   eval      p_volu = 0
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_vekt
     c                   parm                    p_volu
     c                   parm                    p_avvi
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pakning (VA)
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.31 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
8.03  *
8.03  * Key for oppslag på vare-pakning (VA)
8.03  *
8.03 c     jvpkl2_key2   klist
8.03 c                   kfld                    jvpkl2_vare
8.03 c                   kfld                    jvpkl2_ldor
8.03  *
8.03  * Key for oppslag på varetabell VA
8.03  *
8.03 c     jvarl1_key    klist
8.03 c                   kfld                    jvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
