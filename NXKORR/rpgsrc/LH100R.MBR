     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LH100R
      * Beskrivelse . . : Bestillingsforslag, behandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130504 bø   Nytt program
      *       010904 bø   Justert parameter mot status best.forslag
5.30  *       140105 bø   Justert test og oppdat. av datoer pga. lock
5.30  *       260105 bø   Justert oppdatering av lfhepf pga. lock
5.30  *       060307 bø   Skifte mellom lev.navn-parm.tekst b.verdi-vekt
6.10  * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  050210 BSA  Viser og filtrerer på lager
6.12  * 6.10  210410 bof  Endret read i slå sammen best.forslag
6.13  *       300910 bof  Justert på sammenslåing
6.14  *       281010 ask  Lagt inn test på feilvalg i sammeslåing
6.15  *       021210 bof  Justert mer på sammenslåing
6.20  *       220310 bof  Utvidet med filter bestf.ordre og ordrenr
6.20  *       100410 bof  Utvidet med filter leveringstype, overf.av
6.20  *                   flere best, sjekke depositum mm.
6.21  *       210410 bof  Endret read i slå sammen best.forslag
6.22  *       260410 bof  Utvider med totalsum pr.utvalg  ++
6.23  *       161210 bof  Nytt valg, se på ordre
6.24  *       190111 bof  Vise leveringsdato istd. for best.dato
6.25  *       130312 nho  Dersom (9) endringer av leverandør til intern
6.25  *                   leverandør slaø ordretype fra distribusjons-
6.25  *                   liste legges ut
6.26  *       190312 bof  Korreksjon på sammenslåing.
6.27  *       170113 bof  Gi status 9 ved logiske feil på type = O
6.28  *       140114 nho  Feilmelding dersom lev.dato er blank
      *                   B1BDAT på bildet ble ikke blanket slik at
      *                   neste post fikk den samme som over dersom
      *                   hodet ble oppdatert med leveringsdato
6.29  *       051214 bof  Omskriving av "dårlige" reade, kan gi gen.forbedringer
6.2a  *       051214 bof  Rettelse  rundt sammenslå bestillingsforslag
6.2b  *       151214 bsa  Rettelse  rundt sammenslå bestillingsforslag (2)
6.2c  *       220115 bsa  Rettelse  rundt sammenslå bestillingsforslag (3)
6.2d  *       030815 bsa  Sjekk om det er krav til leveringsmåte. Da må en
6.2d  *                   avslutte overføring, og lage bestilling først.
6.nu  *       260514 bof  Utvidelser ihht. nummerserieutvidelse
6.31  *       040914 bof  Sjekker om legv. er passiv eller sperret
6.32a *       280217 nho  Kompilert pga endring i displ.file
6.33  *       180417 bof  Rettelse  rundt sammenslå bestillingsforslag (3)
6.34  *       210918 bsa  Legger ut vekt på vedlikeholdsbildet.
      *
7.xx  * 7.00  170316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  050419 bkv  Rettet feil i dato felt pga Newlook
Q.01  * 7.00  270319 bsa  Quick wins - skrevet om til å bruke 27x132 størrelse
Q.02  * 7.00  270319 bsa  Quick wins - muligheter for å slette flere forslag
Q.02  *                   om gangen.
7.02  *       090519 bof  Skal ikke sjekke på leveringsdato
7.03  *       251019 bkv  Utvidet til 132 bilde
7.04  *       080120 bkv  Rettelse  rundt sammenslå bestillingsforslag (4)
7.05  * 7.00  261020 bsa  Vasker vekk probl Newlook tegn
7.06  * 7.00  070521 bsa  Sprekker på total. Antagelig feil i et bestf, men
7.06  * 7.00              må takles.
7.14  *       250416 ask  Henter inn lager fra bruker og bruker som startverdi
7.14  *                   på filter. Henter fra alle lager på samme region.
7.15  * K00   110520 bsa  Hvis det ikke kjøres med regionsløsning, skal uansett
7.15  *                   angitt lager vises.
      *
7.fb  * K000  040117 bof  Hvis depositumskrav og ikke betalt, merk linjen rødt
      *
8.01  * 800   100223 ask  Sjekker switch om lager fra brukerid skal hentes
8.02  * 800   280825 bsa  Feil ved selektering av lager.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfhel1    if   e           k disk    rename(lfhepfr:lfhel1r)
     flfhel2    if   e           k disk    rename(lfhepfr:lfhel2r)
     flfhel3    if   e           k disk    rename(lfhepfr:lfhel3r)
     flfhel4    if   e           k disk    rename(lfhepfr:lfhel4r)
6.20 flfhel5    if   e           k disk    rename(lfhepfr:lfhel5r)
     flfhelr    if   e           k disk    rename(lfhepfr:lfhelrr)
     flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
6.15 flfdtl4    uf   e           k disk    rename(lfdtpfr:lfdtl4r)
6.15 f                                     prefix(xx:2)
6.2b flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
     flfdtpf    uf a e           k disk    rename(lfdtpfr:lfdtpfr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     flusrlu    uf a e           k disk    rename(lusrpfr:lusrlur)
     fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
6.20 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.20 fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
6.25 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
6.2d favall1    if   e           k disk    rename(avalpfr:avall1r)
7.14 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
7.14 fra91l2    if   e           k disk    rename(ra91pfr:ra91l2r)
7.fb ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
7.fb frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     flh100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d                 ds
6.nu d d_prec                        17
6.nu d  d_fbes                        8  0 overlay(d_prec)
6.nu d  d_tbes                        8  0 overlay(d_prec:9)
6.nu d  d_sort                        1  0 overlay(d_prec:17)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.25 d lldil1_ldor     s                   like(lpldor)
6.25 d lldil1_lage     s                   like(lplage)
     d lfhel1_numm     s                   like(lhnumm)
     d lfhel2_date     s                   like(lhdate)
     d lfhel3_lsor     s                   like(lhlsor)
     d lfhel4_selg     s                   like(lhselg)
6.20 d lfhel5_ldor     s                   like(lhldor)
     d lfhelr_numm     s                   like(lhnumm)
     d lfhelu_numm     s                   like(lhnumm)
     d lfdtl1_numm     s                   like(lfnumm)
6.15 d lfdtl1_line     s                   like(lfline)
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
     d lfdtlr_numm     s                   like(lfnumm)
     d lfdtlr_line     s                   like(lfline)
6.15 d lfdtl4_numm     s                   like(lfnumm)
     d rlevl1_levr     s                   like(rllevr)
     d votyl1_otyp     s                   like(vaotyp)
     d lusrlu_user     s                   like(lbuser)
     d lusrl1_user     s                   like(lbuser)
     d mppalr_ldor     s                   like(mpldor)
     d mppalr_lnum     s                   like(mplnum)
6.20 d fohel1_numm     s                   like(fonumm)
6.20 d fohel1_suff     s                   like(fosuff)
6.20 d sdepl1_numm     s                   like(sknumm)
6.20 d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_tell     s                   like(sktell)
7.fb d rkunl1_kund     s                   like(rkkund)
6.2d d avall1_apgm     s                   like(avapgm)
7.14 d ausrl1_user     s                   like(abuser)
7.14 d ra91l2_lage     s                   like(rrlage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.31 d b_feil2         s              1    inz(*off)
     d b_stat          s              1    inz(*off)
Q.02 d b_delt          s              1    inz(*off)
7.14 d b_regi          s              1    inz(*off)
7.fb d b_depo          s              1    inz(*off)
      *
6.2d d u_lmat          s              1    inz(*off)
      *
     d w_firm          s                   like(lhfirm)
     d w_numm          s                   like(lhnumm)
6.nu d w_num8          s              8  0
     d w_bnum          s                   like(lhbnum)
     d w_valg          s              2  0
     d w_ntnr          s                   like(lhnumm)
     d w_stat          s                   like(lhstat)
     d w_tbel          s                   like(lhtbel)
7.06 d w_bbel          s             15  2
     d w_tvkt          s             11  3
     d w_retk          s              1
     d w_levr          s                   like(lhldor)
     d w_navn          s             30
     d w_latx          s             20
     d w_avtx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_suff          s              2  0
     d w_line          s                   like(lfline)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
6.11 d w_lnav          s             20
6.20 d w_anta          s              6  0
6.20 d w_lety          s                   like(lflety)
6.31 d w_mld1          s             50
6.34 d w_vekt          s             11  3
7.05 d w_len           s              3  0
7.05 d w_teller        s              3  0
7.14 d w_regi          s                   like(rrregi)
7.fb d w_sumi          s             11  2
7.fb d w_sumu          s             11  2
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.00 d u_702           s              1    inz(*off)
8.01 d u_801           s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.03 d c_sfil          s              2  0 inz(17)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      xc3bld
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkk
     c                   if        *in51 = *off
     c                   eval      *in51 = *on
     c                   else
     c                   eval      *in51 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkm
     c                   exsr      xh2win
     c                   goto      b2tagb
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0 or
     c                             b2date <> 0 or
     c                             b2lsor <> *blank or
     c                             b2selg <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
      * oppdaterer brukers bla-valg
      *
     c                   clear                   lusrlur
     c                   eval      lusrlu_user = l_user
     c     lusrlu_key    chain     lusrlu
     c                   eval      lbbsts = h1stat
     c                   eval      lbbslg = h1selg
     c                   eval      lbblev = h1ldor
     c                   eval      lbboty = h1otyp
     c                   if        %found(lusrlu)
6.10 c                   time                    lbedat
6.10 c                   time                    lbetim
6.10 c                   eval      lbeusr = l_user
     c                   update    lusrlur
     c                   else
     c                   eval      lbfirm = l_firm
     c                   eval      lbuser = l_user
6.10 c                   time                    lbodat
6.10 c                   time                    lbotim
6.10 c                   eval      lbousr = l_user
6.10 c                   time                    lbedat
6.10 c                   time                    lbetim
6.10 c                   eval      lbeusr = l_user
     c                   write     lusrlur
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      lfhel4_selg = b1selg
     c     lfhel4_key    setll     lfhel4
     c                   when      w_seqe = 'C'
     c                   eval      lfhel3_lsor = b1lsor
     c     lfhel3_key    setll     lfhel3
     c                   when      w_seqe = 'B'
     c     *dmy          move      b1date        lfhel2_date
     c     lfhel2_key    setll     lfhel2
     c                   other
     c                   eval      lfhel1_numm = b1numm
     c     lfhel1_key    setll     lfhel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      *
      * Posisjoner startverdi på forslagsnummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = ' '
     c                   eval      lfhel1_numm = b2numm
     c     lfhel1_key    setll     lfhel1
     c                   goto      end_pos
     c                   endif
      *
     c                   if        b2date <> 0
     c                   eval      w_seqe = 'B'
     c     *dmy          move      b2date        lfhel2_date
     c     lfhel2_key    setll     lfhel2
     c                   goto      end_pos
     c                   endif
      *
     c                   if        b2lsor <> *blank
     c                   eval      w_seqe = 'C'
     c                   eval      lfhel3_lsor = b2lsor
     c     lfhel3_key    setll     lfhel3
     c                   goto      end_pos
     c                   endif
      *
     c                   if        b2selg <> 0
     c                   eval      w_seqe = 'D'
     c                   eval      lfhel4_selg = b2selg
     c     lfhel4_key    setll     lfhel4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2date = 0
     c                   eval      b2lsor = *blank
     c                   eval      b2selg = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
Q.02 c                   eval      b_delt = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
7.fb  *
7.fb  * sjekker om depositum er betalt
7.fb  *
7.fb c                   eval      b_depo = *off
7.fb c                   if        (b1valg =  8 or
7.fb c                             b1valg =  9 or
7.fb c                             b1valg = 10 or
7.fb c                             b1valg = 11 or
7.fb c                             b1valg = 18) and
7.fb c                             b1ornr <> 0
7.fb c                   exsr      sjekk_depo
7.fb c                   endif
7.fb c                   if        b_depo = *on
7.fb c                   exsr      depo_meld
7.fb c                   leavesr
7.fb c                   endif
      *
      * Endre forslagshode
      *
     c                   if        b1valg = 2
     c                   eval      b7numm = b1numm
     c                   exsr      xb7win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      b4numm = b1numm
     c                   exsr      xb4win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Godkjenn status
      *
     c                   if        b1valg = 5
     c                   eval      b5numm = b1numm
     c                   eval      b5stat = b1stat
     c                   exsr      xb5win
     c                   eval      b1valg = 0
     c                   exsr      forny
     c                   iter
     c                   endif
      *
      * Skrive ut post
      *
     c                   if        b1valg = 6
     c                   eval      w_numm = b1numm
     c                   exsr      xc4bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre forslagslinjer
      *
     c                   if        b1valg = 7
     c                   eval      w_numm = b1numm
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   exsr      forny
     c*                  update    b1sfl
     c                   iter
     c                   endif
      *
      * Overføre post til bestilling
      *
     c                   if        b1valg = 8
     c                   eval      b8numm = b1numm
     c                   exsr      xb8win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bytte leverandør på bestillingsforslag.
      *
     c                   if        b1valg = 9
     c                   eval      b9numm = b1numm
     c                   eval      b9flev = b1ldor
     c                   exsr      xb9win
     c                   eval      b1valg = 0
     c                   exsr      forny
     c                   iter
     c                   endif
      *
      * Bytte leverandør på bestillingsforslag.
      *
     c                   if        b1valg = 10
     c                   eval      b10numm = b1numm
     c                   eval      b10ldor = b1ldor
6.20 c                   eval      b10lety = b1lety
6.20 c                   eval      b10oppr = b1oppr
     c                   exsr      xb10win
     c                   eval      b1valg = 0
     c                   exsr      forny
     c                   iter
     c                   endif
      *
      * Bytte leverandør på bestillingsforslag.
      *
     c                   if        b1valg = 11
     c                   eval      b11numm = b1numm
     c                   exsr      xb11win
     c                   eval      b1valg = 0
     c                   exsr      forny
     c                   iter
     c                   endif
6.23  *
6.23  * Se på spørring ordre
6.23  *
6.23 c                   if        b1valg = 15
6.23 c                   call      'FO505R'
6 23 c                   parm                    w_firm
6.23 c                   parm                    b1ornr
6.23 c                   parm                    b1orsu
6.23 c                   eval      b1valg = 0
6.23 c                   exsr      forny
6.23 c                   iter
6.23 c                   endif
      *
      * Overføre post til bestilling og leverandør
      *
     c                   if        b1valg = 18
     c                   eval      b8numm = b1numm
     c                   exsr      xb18wi
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for generering av nytt forslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   call      'MP100R'
      *
     c                   eval      b_forn = *on
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for endring av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   call      'LH102R'
     c                   parm                    w_numm
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for innlesing fra håndterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   call      'LH705R'
      *
     c                   eval      b_forn = *on
      *
     c     end_c3bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for utskrift - ett forslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc4bld        begsr
      *
     c                   eval      d_fbes = w_numm
     c                   eval      d_tbes = w_numm
     c                   eval      d_sort = 0
     c                   call      'LH600C'
     c                   parm                    d_prec
      *
     c     end_c4bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
Q.02 c                   if        b_delt = *off
Q.02 c                   eval      q4valg = '1'
Q.02  * Skal ikke vise nummeret
Q.02 c                   if        1=2
Q.02 c                   exfmt     b4win
Q.02 c                   endif
Q.02 c                   exfmt     q4win
      *
Q.02 c                   if        *inkc = *on or
Q.02 c                             *inkl = *on
     c                   goto      end_b4win
     c                   endif
      *
Q.02 c                   if        q4valg = '1'
Q.02 c                   eval      b_delt = *on
Q.02 c                   endif
      *
Q.02 c                   endif
      *
      *  Kaller opp subprogram for å slette
      *
     c                   eval      b_forn = *on
     c                   call      'LH400R'
     c                   parm                    w_firm
     c                   parm                    b4numm
      *
     c     end_b4win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å endre status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb5win        begsr
      *
      *
     c                   exfmt     b5win
      *
     c                   if        *inkc = *on
     c                   goto      end_b5win
     c                   endif
      *
      * sjekk detaljene
      *
     c                   eval      b_stat = *off
     c                   eval      lfdtl1_numm = b5numm
     c     lfdtl1_key    setll     lfdtl1
6.29 c     lfdtl1_key    reade     lfdtl1
6.29 c                   dow       not %eof(lfdtl1)
     c                   if        lfstat <> '0'
     c                   eval      b_stat = *on
     c                   endif
6.29 c     lfdtl1_key    reade     lfdtl1
     c                   enddo
      *
     c                   if        b_stat = *off
     c                   eval      lfhelu_numm = b5numm
     c     lfhelu_key    chain     lfhelu                             90
     c                   if        %found(lfhelu)
     c                   eval      lhstat = '0'
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
     c                   endif
      *
     c     end_b5win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle forslagshode vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb7win        begsr
      *
      *  Henter inn opplysninger fra FORSLAGSHODE og LINJER
      *
     c                   eval      b7lage = 0
     c                   eval      b7user = *blank
     c                   eval      b7otyp = *blank
     c                   eval      b7bdat = 0
     c                   eval      b7ldat = 0
      *
     c                   eval      lfhelr_numm = b7numm
     c     lfhelr_key    chain     lfhelr                             90
     c                   if        *in90 = *on
     c                   goto      end_b7win
     c                   endif
      *
6.34 c                   eval      w_vekt = 0
     c                   eval      lfdtl1_numm = b7numm
     c     lfdtl1_key    setll     lfdtl1
     c     lfdtl1_key    reade     lfdtl1                                 90
     c                   if        *in90 = *on
     c                   goto      end_b7win
     c                   endif
6.34 c                   dow       not %eof
6.34 c                   eval      w_vekt = w_vekt + (lfvekt * lfanta)
6.34 c     lfdtl1_key    reade     lfdtl1
6.34 c                   enddo
      *
      * slår opp mot parameter og henter vekt og beløpsgrense
     c                   eval      mppalr_ldor = lhldor
     c                   eval      mppalr_lnum = lhlnum
     c     mppalr_key    chain     mppalr
     c                   if        %found(mppalr)
     c                   eval      b7gvek = mpgvek
     c                   eval      b7gbel = mpgbel
     c                   eval      b7ptxt = mpbesk
     c                   else
     c                   eval      b7gbel = 0
     c                   eval      b7gvek = 0
     c                   eval      b7ptxt = *blank
     c                   endif
      *
     c                   eval      b7sats = lhsats
     c                   eval      b7user = lhuser
6.20 c                   eval      b7ornr = lhornr
     c                   eval      b7otyp = lhotyp
     c                   eval      b7lage = lhlage
     c                   eval      b7avde = lhavde
6.34 c                   eval      b7vekt = w_vekt
     c                   exsr      sjekk_lager
     c                   exsr      sjekk_avd
     c                   if        lhbdat <> *loval
     c     *dmy          move      lhbdat        b7bdat
     c                   endif
     c                   if        lhldat <> *loval
     c     *dmy          move      lhldat        b7ldat
     c                   endif
      *
     c     b7win_ut      tag
     c                   exfmt     b7win
      *
      * Tester funksjonstaster og valg
      *
     c                   if        *inkc = *on
     c                   goto      end_b7win
     c                   endif
      *
     c                   if        *inkd = *on
     c                   if        wactfe = 'B7OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b7otyp
     c                   goto      b7win_ut
     c                   endif
      *
     c                   if        wactfe = 'B7LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b7lage
     c                   parm                    b7latx
     c                   goto      b7win_ut
     c                   endif
      *
     c                   if        wactfe = 'B7AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    b7avde
     c                   parm                    b7avtx
     c                   goto      b7win_ut
     c                   endif
     c                   endif
      *
      *  Sjekk   Finnes ordretype
      *
     c                   eval      votyl1_otyp = b7otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   goto      b7win_ut
     c                   endif
     c                   if        vaosys <> 1
     c                   eval      *in33 = *on
     c                   goto      b7win_ut
     c                   endif
      *
      *  Sjekk   Er det angitt gyldig lager
      *
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      b7win_ut
     c                   endif
      *
      *  Sjekk   Er det angitt gyldig avdeling
      *
     c                   if        b7avde <> 0
     c                   eval      w_avde = b7avde
     c                   exsr      sjekk_avd
     c                   if        b_feil = *on
     c                   eval      *in36 = *on
     c                   goto      b7win_ut
     c                   endif
     c                   endif
5.30  *
5.30  *  Sjekk  Datoer
5.30  *
5.30 c                   if        b7bdat <> 0
5.30 c     *dmy          test(d)                 b7bdat                 34
5.30 c                   if        *in34 = *on
5.30 c                   goto      b7win_ut
5.30 c                   endif
5.30 c                   endif
5.30 c                   if        b7ldat <> 0
5.30 c     *dmy          test(d)                 b7ldat                 35
5.30 c                   if        *in35 = *on
5.30 c                   goto      b7win_ut
5.30 c                   endif
5.30 c                   endif
      *
      *  Oppdater FORSLAGSHODE
      *
     c                   eval      lfhelu_numm = b7numm
     c     lfhelu_key    chain     lfhelur                            90
     c                   if        *in90 = *on
     c                   goto      end_b7win
     c                   endif
5.30 c                   if        b7bdat <> 0
5.30 c     *dmy          move      b7bdat        lhbdat
5.30 c                   else
5.30 c                   eval      lhbdat = *loval
5.30 c                   endif
5.30 c                   if        b7ldat <> 0
5.30 c     *dmy          move      b7ldat        lhldat
5.30 c                   else
5.30 c                   eval      lhldat = *loval
5.30 c                   endif
     c                   eval      lhuser = b7user
     c                   eval      lhotyp = b7otyp
     c                   eval      lhlage = b7lage
     c                   eval      lhavde = b7avde
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   eval      b_forn = *on
      *
      *  Endre lager på detaljlinjer
      *
     c                   eval      lfdtlu_numm = lfhelu_numm
     c     lfdtlu_key    setll     lfdtlur
     c     lfdtlu_key    reade     lfdtlur                                91
     c                   dow       *in91 = *off
     c                   eval      lflage = b7lage
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c     lfdtlu_key    reade     lfdtlur                                91
     c                   enddo
      *
     c     end_b7win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle overføring til bestilling - ett forslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb8win        begsr
      *
6.31 c                   exsr      sjekk_feil
6.31 c                   if        b_feil2 = *on
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   exfmt     b8win
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      end_b8win
     c                   endif
      *
     c                   eval      d_fbes = b8numm
     c                   eval      d_tbes = b8numm
      *
      * Utskrift av bestillingsforslag
      *
     c                   eval      d_sort= 0
     c                   call      'LH600C'
     c                   parm                    d_prec
      *
     c                   exsr      ovf_bestill
      *
      * hente opp bestilling for vedlikehold
     c                   eval      w_valg = 2
     c                   eval      w_suff = 0
     c                   call      'LO110R'
     c                   parm                    w_valg
     c                   parm                    w_bnum
     c                   parm                    w_suff
      *
     c     end_b8win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bytte leverandør på bestillingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        begsr
      *
     c                   eval      *in30 = *on
     c                   eval      b9tlev = 0
     C     b9taga        tag
     c                   exfmt     b9win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_b9win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b9taga
     c                   endsl
      *
      * Validering
      *
     c                   eval      w_retk = *blank
     c                   eval      w_levr = b9tlev
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
      *
     c                   if        w_retk <> *blank
     c                   eval      *in31  = *on
     c                   goto      b9taga
     c                   endif
      *
      * Endrer leverandørnr. på hode
      *
     c                   eval      lfhelu_numm = b9numm
     c     lfhelu_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   eval      lhldor = b9tlev
      *
6.25  * Finner ordretype dersom ny leverandør er 'INTERN'
6.25 c                   eval      lldil1_ldor = b9tlev
6.25 c                   move      lhlage        lldil1_lage
6.25 c     lldil1_key    chain     lldil1                             90
6.25 c                   if        *in90 = '9' and
6.25 c                             lpform = 'INTERNT   ' and
6.25 c                             lptype = 'INTERNT   '
6.25 c                   move      lpotyp        lhotyp
6.25 c                   endif
     c*
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
      *
      * Endrer på linjer
      *
readec                   eval      lfdtlu_numm = lfhelu_numm
readec     lfdtlu_key    setll     lfdtlu
readec     lfdtlu_key    reade     lfdtlu
6.29 c                   dow       not %eof(lfdtlu)
     c                   eval      lfldor = b9tlev
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
reade *
readec     lfdtlu_key    reade     lfdtlu
     c                   enddo
     c                   endif
      *
     c     end_b9win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å slå sammen to forslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb10win       begsr
      *
     c                   eval      *in30   = *on
     c                   eval      b10smnr = 0
     C     b10taga       tag
     c                   exfmt     b10win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_b10win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b10taga
     c                   endsl
      *
      * Validering
      *
6.20  * Skal kun kunne slå sammen best.forslag til lager
6.20 c                   if        b10lety <> 0
6.20 c                   eval      *in33 = *on
6.20 c                   goto      b10taga
6.20 c                   endif
6.20  * Kan ikke slå sammen når oppringelsers er O
6.20 c                   if        b10oppr = 'O'
6.20 c                   eval      *in35 = *on
6.20 c                   goto      b10taga
6.20 c                   endif
6.14  *
6.14  * Sjekk sammenslå mot samme forslagsnummer
6.14  *
6.14 c                   if        b10smnr = b10numm
6.14 c                   eval      *in32 = *on
6.14 c                   goto      b10taga
6.14 c                   endif
      *
     c                   eval      lfhelr_numm = b10smnr
     c     lfhelr_key    chain     lfhelrr
     c                   if        not %found(lfhelr)
     c                   eval      *in32 = *on
     c                   goto      b10taga
     c                   else
     c                   if        lhldor <> b10ldor
     c                   eval      *in31 = *on
     c                   goto      b10taga
     c                   endif
     c                   endif
6.20 c                   if        lhlety <> b10lety
6.20 c                   eval      *in34 = *on
6.20 c                   goto      b10taga
6.20 c                   endif
      *
      * finner sist brukt linjenr. på detaljene som skal ha linjene
      *
6.15 c                   eval      lfdtl1_numm = b10numm
6.2a c                   eval      lfdtl1_line = 9999
6.33 c     lfdtl1_key2   setll     lfdtl1
6.33 c                   readp     lfdtl1
6.2a  *
6.2a c                   eval      w_line  = 2
6.33 c                   dow       not %eof(lfdtl1)
6.33 c                   if        w_firm  = lffirm and
6.12 c                             b10numm = lfnumm
6.12 c                   eval      w_line     =  lfline
6.33 c                   else
6.33 c                   eval      w_line     =  2
6.33 c                   endif
6.33 c                   leave
6.33 c                   enddo
      *
      * sletter heading på den som blir slått sammen
      *
     c                   eval      lfhelu_numm = b10smnr
     c     lfhelu_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   delete    lfhelur
      * gir detaljene nytt headingnummer og sletter det gamle.
6.26 c                   eval      lfdtl4_numm = b10smnr
6.15 c     lfdtl4_key    setll     lfdtl4
6.15 c     lfdtl4_key    reade     lfdtl4
6.15 c                   dow       not %eof(lfdtl4)
6.2b c     ny_linje      tag
6.15 c                   eval      w_line = w_line + 2
6.2b c                   eval      lfdtlu_line = w_line
7.04 c                   eval      lfdtlu_numm = b10numm
6.2b c     lfdtlu_key2   chain     lfdtlu
6.2b c                   if        not %found
6.2b c                   clear                   lfdtlur
6.15 c                   eval      lffirm = xxfirm
6.15 c                   eval      lfnumm = b10numm
6.15 c                   eval      lfline = w_line
6.15 c                   eval      lfltyp = xxltyp
6.15 c                   eval      lflage = xxlage
6.15 c                   eval      lfogrp = xxogrp
6.15 c                   eval      lfhgrp = xxhgrp
6.15 c                   eval      lfugrp = xxugrp
6.15 c                   eval      lflvar = xxlvar
6.15 c                   eval      lfvare = xxvare
6.15 c                   eval      lfenh1 = xxenh1
6.15 c                   eval      lftek1 = xxtek1
6.15 c                   eval      lftek2 = xxtek2
6.15 c                   eval      lfanta = xxanta
6.15 c                   eval      lfstat = xxstat
6.15 c                   eval      lfldor = xxldor
6.15 c                   eval      lfvekt = xxvekt
6.15 c                   eval      lflbel = xxlbel
6.15 c                   eval      lfvlev = xxvlev
6.26 c                   eval      lfornr = xxornr
6.26 c                   eval      lforsu = xxorsu
6.26 c                   eval      lforli = xxorli
6.26 c                   eval      lflety = xxlety
6.26 c                   eval      lfldat = xxldat
6.15 c                   eval      lfodat = xxodat
6.15 c                   eval      lfotim = xxotim
6.15 c                   eval      lfousr = xxousr
6.15 c                   eval      lfedat = xxedat
6.15 c                   eval      lfetim = xxetim
6.15 c                   eval      lfeusr = xxeusr
7.04 c*                   eval      lfdtlu_numm = b10numm
6.2c c                   write     lfdtlur
6.2b c                   else
7.04 c                   unlock    lfdtlu
6.2b c                   goto      ny_linje
6.2b c                   endif
6.15 c                   delete    lfdtl4r
6.15 c     lfdtl4_key    reade     lfdtl4
6.15 c                   enddo
      *
     c                   endif
      *
     c                   eval      w_numm = b10numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    w_numm
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lfhelu_numm = b10numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
     c     end_b10win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å splitte ut cross - docking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb11win       begsr
      *
     c                   eval      *in30   = *on
     c                   eval      b11valg = *blank
     C     b11taga       tag
     c                   exfmt     b11win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_b11win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b11taga
     c                   endsl
      *
     c                   if        b11valg <> 'C' and
     c                             b11valg <> 'V'
     c                   eval      *in31 = *on
     c                   goto      b11taga
     c                   endif
      *
     c                   if        b11valg = 'V'
      * Splitt på vare-leverandør
     c                   eval      w_numm  = b11numm
     c                   call      'LH715R  '
     c                   parm                    w_numm
     c                   goto      end_b11win
     c                   endif
      *
      * Splitt på cross-docket
      *
      * finner sist brukt heading nummer
      *
     c                   exsr      hntnum
      *
      * Leser heading skriver en ny.
      *
     c                   clear                   lfhelur
     c                   eval      lfhel1_numm = b11numm
     c     lfhel1_key    chain     lfhel1r
     c                   if        %found(lfhel1)
readec                   eval      lhnumm = w_ntnr
6.10 c                   time                    lhdate
6.10 c                   time                    lhtime
6.10 c                   eval      lhousr = l_user
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   write     lfhelur
     c                   endif
      *
      * cross-docket
      *
      * gir detaljene nytt headingnummer og sletter det gamle.
readec                   eval      lfdtlu_numm = b11numm
readec     lfdtlu_key    setll     lfdtlu
6.29 c     lfdtlu_key    reade     lfdtlu
6.29 c                   dow       not %eof(lfdtlu)
     c                   if        lfstat = '7'
readec                   eval      lfnumm = w_ntnr
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   write     lfdtpfr
     c                   delete    lfdtlur
readec                   endif
6.29 c     lfdtlu_key    reade     lfdtlu
     c                   enddo
      *
      * finner sum for gammelt forslag
     c                   eval      w_numm = b11numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    b11numm
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lfhelu_numm = b11numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      * finner sum for nytt forslag
     c                   eval      w_numm = w_ntnr
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    w_ntnr
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lfhelu_numm = w_ntnr
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
     c     end_b11win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle overføring til bestilling - ett forslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb18wi        begsr
      *
6.31 c                   exsr      sjekk_feil
6.31 c                   if        b_feil2 = *on
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   eval      w_suff = 0
     c                   exfmt     b8win
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      end_b18wi
     c                   endif
6.2d  *
6.2d  *  Sjekk validering av leveringsmåte
6.2d  *
6.2d c                   if        u_lmat = *on
6.2d c                   eval      w_mld1 = 'Advarsel:Krav til le+
6.2d c                                      veringsmåte. Må via b+
6.2d c                                      est. !!'
6.2d c                   call      'AA005R'
6.2d c                   parm                    w_mld1
6.2d c                   goto      end_b18wi
6.2d c                   endif
      *
     c                   eval      d_fbes = b8numm
     c                   eval      d_tbes = b8numm
      *
     c                   exsr      ovf_bestill
      *
     c                   call      'LO740R'
     c                   parm                    w_bnum
     c                   parm                    w_suff
6.20 c                   parm                    w_retk
      *
     c     end_b18wi     endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å behandle overføring til bestilling - ett forslag
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     xb15win       begsr
6.20  *
6.20 c                   eval      b15ldo = h1ldor
6.20 c                   eval      w_retk = *blank
6.20 c                   eval      b15nav = *blank
6.20 c                   eval      rlevl1_levr = h1ldor
6.20 c     rlevl1_key    chain     rlevl1
6.20 c                   if        %found(rlevl1)
6.20 c                   movel     rlnavn        b15nav
6.20 c                   endif
6.20  *
6.20 c                   eval      w_anta = 0
6.20 c                   eval      lfhel5_ldor = h1ldor
6.20 c     lfhel5_key    setll     lfhel5
6.20 c     lfhel5_key    reade     lfhel5
6.20 c                   dow       not %eof
6.20  * tester alle utvalgskrit. på bla.alt
6.20 c                   if        lhstat <> '0'
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   if        h1selg <> 0 and
6.20 c                             h1selg <> lhselg
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   if        h1ldor <> 0 and
6.20 c                             h1ldor <> lhldor
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   if        h1lage <> 0 and
6.20 c                             h1lage <> lhlage
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   if        h1otyp <> *blank and
6.20 c                             h1otyp <> lhotyp
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   if        h1oppr <> *blank and
6.20 c                             h1oppr <> lhoppr
6.20 c                   goto      b15les
6.20 c                   endif
6.20 c                   eval      w_anta = w_anta + 1
6.20  *
6.20 c     b15les        tag
6.20 c     lfhel5_key    reade     lfhel5
6.20 c                   enddo
6.20 c
6.20 c                   eval      b15ant = w_anta
6.20 c                   if        w_anta = 0
6.20 c                   eval      *in32 = *on
6.20 c                   endif
6.20 c
6.20  *
6.20 c     b15taga       tag
6.20 c                   exfmt     b15win
6.20  *
6.20 c                   if        *inkc = *on
6.20 c                   goto      end_b15win
6.20 c                   endif
6.20  *
6.20 c                   if        w_retk <> *blank
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20 c                   if        h1ldor  = 0
6.20 c                   eval      *in31 = *on
6.20 c                   goto      b15taga
6.20 c                   endif
6.20  *
6.20 c                   if        b15ant  = 0
6.20 c                   eval      *in32 = *on
6.20 c                   goto      b15taga
6.20 c                   endif
6.20  *
6.20 c                   eval      lfhel5_ldor = h1ldor
6.20 c     lfhel5_key    setll     lfhel5
6.20 c     lfhel5_key    reade     lfhel5
6.20 c                   dow       not %eof
6.20  * tester alle utvalgskrit. på bla.alt
6.20 c                   if        lhstat <> '0'
6.20 c                   goto      b15les2
6.20 c                   endif
6.20 c                   if        h1selg <> 0 and
6.20 c                             h1selg <> lhselg
6.20 c                   goto      b15les2
6.20 c                   endif
6.20 c                   if        h1ldor <> 0 and
6.20 c                             h1ldor <> lhldor
6.20 c                   goto      b15les2
6.20 c                   endif
6.20 c                   if        h1lage <> 0 and
5.60 c                             h1lage <> lhlage
6.20 c                   goto      b15les2
6.20 c                   endif
6.20 c                   if        h1otyp <> *blank and
6.20 c                             h1otyp <> lhotyp
6.20 c                   goto      b15les2
6.20 c                   endif
6.20 c                   if        h1oppr <> *blank and
6.20 c                             h1oppr <> lhoppr
6.20 c                   goto      b15les2
6.20 c                   endif
6.20  ** overfører
6.20 c                   eval      d_fbes = lfnumm
6.20 c                   eval      d_tbes = lfnumm
6.20  *
6.20 c                   exsr      ovf_bestill
6.20  *
6.20 c                   call      'LO740R'
6.20 c                   parm                    w_bnum
6.20 c                   parm                    w_suff
6.20 c                   parm                    w_retk
6.20 c                   if        w_retk <> *blank
6.20 c                   goto      end_b15win
6.20 c                   endif
6.20  **
6.20 c     b15les2       tag
6.20 c     lfhel5_key    reade     lfhel5
6.20 c                   enddo
6.20  *
6.20  * overfør bestillinger
6.20  *
6.20 c     end_b15win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b7lage
     c                   parm                    b7latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/hente avdeling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_avd     begsr
      *
     c                   eval      b_feil = *off
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    b7avde
     c                   parm                    b7avtx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overføring til bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ovf_bestill   begsr
      *
     c                   call      'LH712R'
     c                   parm                    d_prec
      *
      *  Sletter forslag med linjer etter overføring
      *
     c                   eval      lfhelu_numm = d_fbes
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
      *
     c                   eval      w_bnum = lhbnum
     c                   eval      lfdtlu_numm = lfhelu_numm
     c     lfdtlu_key    setll     lfdtlur
6.29 c     lfdtlu_key    reade     lfdtlur
6.29 c                   dow       not %eof(lfdtlu)
     c                   delete    lfdtlur
6.29 c     lfdtlu_key    reade     lfdtlur
     c                   enddo
      *
     c                   delete    lfhelur
     c                   endif
      *
     c                   eval      b_forn = *on
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      h1taga
     c                   endsl
      *
     c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh2win  Bilde for forklaring på statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh2win        begsr
      *
     c     h2taga        tag
      *
     c                   write     h2win
     c                   exfmt     h2win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h2win
     c                   endsl
      *
     c     end_h2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'H1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    h1ldor
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
6.10  *
6.10  * Lager
6.10  *
6.10 c                   if        wactfe = 'H1LAGE'
6.10 c                   call      'RA510R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    h1lage
6.10 c                   parm                    w_lnav
6.10 c                   goto      end_spØrr
6.10 c                   endif
      *
      * Leverandør
      *
     c                   if        wactfe = 'B9TLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b9tlev
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
6.22 c                   eval      b2tbel = 0
7.06 c                   eval      w_bbel = 0
7.03 c                   eval      b2tvek = 0
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   read      lfhel4
     c                   when      w_seqe = 'C'
     c                   read      lfhel3
     c                   when      w_seqe = 'B'
     c                   read      lfhel2
     c                   other
     c                   read      lfhel1
     c                   endsl
      *
     c                   if        %eof or
     c                             lhfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Uaktuell status ?
      *
     c                   if        h1stat <> *blank and
     c                             h1stat <> lhstat
     c                   iter
     c                   endif
      *
      * Uaktuell selger ?
      *
     c                   if        h1selg <> 0 and
     c                             h1selg <> lhselg
     c                   iter
     c                   endif
      *
      * Uaktuell leverandør ?
      *
     c                   if        h1ldor <> 0 and
     c                             h1ldor <> lhldor
     c                   iter
     c                   endif
6.10  *
6.10  * Uaktuelt lager ?
6.10  *
7.14  *  Endret funksjon til å ta med alle lager i samme region
7.14  *
7.14 c                   if        h1lage <> 0
7.14 c                   exsr      finn_region
7.14 c                   exsr      sjekk_region
7.14 c                   if        b_regi = *off
7.14 c                   iter
7.14 c                   endif
7.14 c                   endif
7.14 c*                  if        h1lage <> 0 and
7.14 c*                            h1lage <> lhlage
7.14 c*                  iter
7.14 c*                  endif
      *
      * Uaktuell ordretype  ?
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> lhotyp
     c                   iter
     c                   endif
6.20  *
6.20  * Uaktuell oprrinnelse ?
6.20  *
6.20 c                   if        h1oppr <> *blank and
6.20 c                             h1oppr <> lhoppr
6.20 c                   iter
6.20 c                   endif
6.20  *
6.20  * Uaktuell leveringstype
6.20  *
6.20 c                   if        h1lety <> *blank
6.20 c                   move      h1lety        w_lety
6.20 c                   if        w_lety <> lhlety
6.20 c                   iter
6.20 c                   endif
6.20 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      lfhel4_selg = lhselg
     c                   when      w_seqe = 'C'
     c                   eval      lfhel3_lsor = lhlsor
     c                   when      w_seqe = 'B'
     c                   eval      lfhel2_date = lhdate
     c                   other
     c                   eval      lfhel1_numm = lhnumm
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = lhnumm
     c                   eval      b1selg = lhselg
6.10 c                   eval      b1lage = lhlage
6.20 c                   eval      b1ornr = lhornr
6.20 c                   eval      b1orsu = lhorsu
6.27 c                   if        lhoppr = 'O' and
6.27 c                             lhstat =  '0'
6.27 c                   exsr      sjekk_det
6.27 c                   eval      b1stat = w_stat
6.27 c                   else
     c                   eval      b1stat = lhstat
6.27 c                   endif
     c                   if        *in51  = *off
6.22 c                   eval      b1tbel = 0
6.22 c                   eval      b1lnav = *blank
     c                   eval      w_levr = lhldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   if        w_retk = *blank
     c                   movel     w_navn        b1lnav
     c                   else
     c                   eval      b1lnav = *blank
     c                   endif
     c                   eval      b1tbel = lhtbel
7.06 c                   eval      w_bbel = b2tbel + lhtbel
7.06 c                   if        w_bbel < 999999999
6.22 c                   eval      b2tbel = b2tbel + lhtbel
7.06 c                   else
7.06 c                   eval      b2tbel = 999999999,99
7.06 c                   endif
     c                   else
6.22 c                   eval      b1tbel = 0
6.22 c                   eval      b1lnav = *blank
     c                   eval      mppalr_ldor = lhldor
     c                   eval      mppalr_lnum = lhlnum
     c     mppalr_key    chain     mppalr
     c                   if        %found(mppalr)
     c                   movel     mpbesk        b1lnav
     c                   eval      b1tbel = lhvekt
     c                   endif
     c                   endif
7.03 c                   eval      b1para = *blank
     c                   eval      b1gvek = 0
     c                   eval      b1gbel = 0
7.03 c                   eval      b1dbel = 0
7.03 c                   eval      b1dvek = 0
7.03 c                   eval      mppalr_ldor = lhldor
7.03 c                   eval      mppalr_lnum = lhlnum
7.03 c     mppalr_key    chain     mppalr
7.03 c                   if        %found(mppalr)
7.03 c                   eval      b1para = mpbesk
7.03 c                   if        mpgbel > 999999
7.03 c                   eval(h)   b1gbel = 999999
7.03 c                   else
7.03 c                   eval(h)   b1gbel = mpgbel
7.03 c                   endif
7.03 c                   if        b1gbel > 0
7.03 c                   if        (lhtbel - b1gbel) > 999999
7.03 c                   eval      b1dbel = 999999
7.03 c                   elseif    (lhtbel - b1gbel) < -999999
7.03 c                   eval      b1dbel = -999999
7.03 c                   else
7.03 c                   eval      b1dbel = lhtbel - b1gbel
7.03 c                   endif
7.03 c                   endif
7.03 c                   if        mpgvek > 999999
7.03 c                   eval(h)   b1gvek = 999999
7.03 c                   else
7.03 c                   eval      b1gvek = mpgvek
7.03 c                   endif
7.03 c                   if        b1gvek > 0
7.03 c                   if        (lhvekt - b1gvek) > 999999
7.03 c                   eval      b1dvek = 999999
7.03 c                   elseif    (lhvekt - b1gvek) < -999999
7.03 c                   eval      b1dvek = -999999
7.03 c                   else
7.03 c                   eval      b1dvek = lhvekt - b1gvek
7.03 c                   endif
7.03 c                   endif
7.03 c                   endif
7.03 c                   if        lhvekt > 999999
7.03 c                   eval      b1vekt = 999999
7.03 c                   else
7.03 c                   eval      b1vekt = lhvekt
7.03 c                   endif
7.03 c                   eval      b2tvek = b2tvek + lhvekt
7.03 c                   eval      b1lsor = lhlsor
     c                   eval      b1ldor = lhldor
     c                   eval      b1oppr = lhoppr
      *
     c                   if        lhdate <> *loval
     c     *dmy          move      lhdate        b1date
     c                   endif
      *
      *
6.28 c                   eval      b1bdat = *zero
6.24 c                   if        lhldat <> *loval
6.24 c     *dmy          move      lhldat        b1bdat
     c                   endif
7.05  * Vasker vekk tegn som gjør at visningen blir "rotete".
7.05 c                   exsr      newlo_vask
      *
7.fb c                   exsr      sjekk_depo
7.fb c                   if        b_depo = *on
7.fb c                   eval      *in45  = *on
7.fb c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'D'
     c     lfhel4_key    setgt     lfhel4
     c                   readp     lfhel4
     c                   when      w_seqe = 'C'
     c     lfhel3_key    setgt     lfhel3
     c                   readp     lfhel3
     c                   when      w_seqe = 'B'
     c     lfhel2_key    setgt     lfhel2
     c                   readp     lfhel2
     c                   other
     c     lfhel1_key    setgt     lfhel1
     c                   readp     lfhel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   readp     lfhel4
     c                   when      w_seqe = 'C'
     c                   readp     lfhel3
     c                   when      w_seqe = 'B'
     c                   readp     lfhel2
     c                   other
     c                   readp     lfhel1
     c                   endsl
      *
     c                   if        %eof or
     c                             lhfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      lfhel4_selg = lhselg
     c                   when      w_seqe = 'C'
     c                   eval      lfhel3_lsor = lhlsor
     c                   when      w_seqe = 'B'
     c                   eval      lfhel2_date = lhdate
     c                   other
     c                   eval      lfhel1_numm = lhnumm
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'D'
     c     lfhel4_key    setll     lfhel4
     c                   when      w_seqe = 'C'
     c     lfhel3_key    setll     lfhel3
     c                   when      w_seqe = 'B'
     c     lfhel2_key    setll     lfhel2
     c                   other
     c     lfhel1_key    setll     lfhel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_num8
      *
6.nu c                   eval      w_numm      = w_num8
     c                   eval      w_ntnr      = w_numm
      *
     c                   endsr
6.27  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.27  *  Sjekker om det er noe ulogisk med varelinjer for best.fors. fra ordre
6.27  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.27  *
6.27 c     sjekk_det     begsr
6.27  *
6.27 c                   eval      w_stat = '0'
6.27 c                   eval      lfdtl1_numm = b1numm
6.27 c     lfdtl1_key    setll     lfdtl1
6.27 c     lfdtl1_key    reade     lfdtl1
6.27 c                   dow       not %eof(lfdtl1)
6.27 c                   if        lfvare <> *blank and
6.27 c                             lfltyp = 'TX'
6.27 c                   eval      w_stat = '9'
6.27 c                   leavesr
6.27 c                   endif
6.27 c                   if        lfornr = 0
6.27 c                   eval      w_stat = '9'
6.27 c                   leavesr
6.27 c                   endif
6.27 c     lfdtl1_key    reade     lfdtl1
6.27 c                   enddo
6.27  *
6.27 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke evt. feil-situasjon før bestilling sendes
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_feil    begsr
6.31 c                   eval      b_feil2 = *off
6.31  *
6.31  * tester om status er forskjellig fra 0
6.31  *
6.31 c                   if        b1stat <> '0'
6.31 c                   eval      b_feil2 = *on
6.31 c                   eval      w_mld1 = 'Status må være 0 for å bestille'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   if        b1oppr = 'O'
6.31 c                   exsr      sjekk_det
6.31 c                   eval      b1stat = w_stat
6.31 c                   if        b1stat <> '0'
6.31 c                   eval      b_feil2 = *on
6.31 c                   eval      w_mld1 = 'Status må være 0 for å bestille'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endif
6.31  *
6.31  * tester om leveringsdato er fyllt ut
7.02 c                   if        u_702 = *off
6.31 c                   if        b1bdat = *zero
6.31 c                   eval      b_feil2 = *on
6.31 c                   eval      w_mld1 = 'Leveringsdato må fylles ut     '
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   leavesr
6.31 c                   endif
7.02 c                   endif
6.31  *
6.31  * Sjekker om depositum er betalt
6.31  *
6.31 c                   if        b1ornr > 0
6.31 c                   eval      fohel1_numm = b1ornr
6.31 c                   eval      fohel1_suff = b1orsu
6.31 c     fohel1_key    chain     fohel1
6.31 c                   if        %found(fohel1)
6.31 c                   eval      sdepl1_numm = fonumm
6.31 c                   eval      sdepl1_kund = fokund
6.31 c     sdepl1_key    chain     sdepl1
6.31 c                   if        %found(sdepl1)
6.31 c                   if        skdepo > 0 and
6.31 c                             skdepo > skdepb
6.31 c                   eval      b_feil2 = *on
6.31 c                   eval      w_mld1 = 'Depositum er ikke betalt på ordren'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endif
6.31 c                   endif
6.31 c                   endif
6.31  *
6.31  * tester om leverandør er sperret eller passiv
6.31 c                   eval      rlevl1_levr = b1ldor
6.31 c     rlevl1_key    chain     rlevl1
6.31 c                   if        %found(rlevl1) and
6.31 c                             (rlsprk = 'J' or
6.31 c                              rlpass = 'J')
6.31 c                   eval      b_feil2 = *on
6.31 c                   eval      w_mld1 = 'Leverandør er sperret eller passiv'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   leavesr
6.31 c                   endif
6.31 c
6.31  *
6.31 c                   endsr
7.14  *
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  * Subrutine for å finne region på lager i filter
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  *
7.14 c     finn_region   begsr
7.14  *
7.14 c                   eval      w_regi = 0
7.14 c                   eval      ra91l2_lage = h1lage
7.14 c     ra91l2_key    chain     ra91l2
7.14 c                   if        %found
7.14 c                   eval      w_regi = rrregi
7.14 c                   endif
7.14  *
7.14 c                   endsr
7.14  *
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  * Subrutine for å sjekke om region på post er lik region i filter
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  *
7.14 c     sjekk_region  begsr
7.14  *
7.14 c                   eval      b_regi = *off
7.14 c                   eval      ra91l2_lage = lhlage
7.14 c     ra91l2_key    chain     ra91l2
7.14 c                   if        %found and
7.14 c                             rrregi = w_regi
7.14 c                   eval      b_regi = *on
7.14 c                   endif
7.15  *
7.15  * Angitt lager skal uansett vises.
7.15  *
7.15 c                   if        b_regi = *off
7.15 c                   if        h1lage = lhlage
7.14 c                   eval      b_regi = *on
7.15 c                   endif
7.15 c                   endif
7.15  *
7.14 c                   endsr
7.14  *
      *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Newlook vask av data
7.05  *  Fjerner tegn som gjør at Newlook feiltolker
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05   begsr newlo_vask;
7.05  *
7.05 c     x'1C':' '     xlate     b1lnav        b1lnav
7.05 c     x'25':' '     xlate     b1lnav        b1lnav
7.05  *
7.05     for w_teller = 1 to 5 ;
7.05       w_len = %len(%trim(b1lnav));
7.05       if (w_len > 0);
7.12       b1lnav = %ScanRpl('.':'':b1lnav:w_len); // newlook tåler ikke . på slutten
7.12       endif;
7.12       w_len = %len(%trim(b1lnav));
7.12       if (w_len > 0);
7.12       b1lnav = %ScanRpl(':':'':b1lnav:w_len); // newlook tåler ikke : på slutten
7.12       endif;
7.12     endfor ;
7.12   endsr;
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
7.fb c                   eval      b_depo = *off
7.fb c                   eval      *in45  = *off
7.fb c                   eval      *in46  = *off
7.fb  * Sjekker om depositum er betalt
7.fb  * 2 muligheter, hvis man kjører depositumsløsning og kode kontant-kode satt, sjekk eller at
7.fb  * man ikke kjører depositumsløsning
7.fb c                   if        faak11 = 0 or
7.fb c                             (faak11 = 1 and
7.fb c                              faak12 = 1)
7.fb  * hvis krav til beløp, sjekk først om ordre er større enn dette
7.fb c                   if        faak11 = 1 and
7.fb c                             faakbs <> 0
7.fb  * Beregn ordretotal
7.fb c                   eval      w_sumi = 0
7.fb c                   eval      w_sumu = 0
7.fb c                   call      'FO704R '
7.fb c                   parm                    w_firm
7.fb c                   parm                    b1ornr
7.fb c                   parm                    b1orsu
7.fb c                   parm                    w_sumi
7.fb c                   parm                    w_sumu
7.fb c                   if        w_sumi < faakbs
7.fb c                   leavesr
7.fb c                   endif
7.fb c                   endif
7.fb  *
7.fb c                   eval      fohel1_numm = b1ornr
7.fb c                   eval      fohel1_suff = b1orsu
7.fb c     fohel1_key    chain     fohel1
7.fb c                   if        %found(fohel1)
7.fb  * sjekker på kunde om er kreditt-kunde, da kan best.sendes
7.fb c                   eval      rkunl1_kund = fokund
7.fb c     rkunl1_key    chain     rkunl1
7.fb  *
7.fb c                   eval      sdepl1_numm = fonumm
7.fb c                   eval      sdepl1_kund = fokund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
7.fb c                   if        %found(sdepl1)
7.fb c                   if        skdepo > skdepb
7.fb c                   eval      b_depo = *on
7.fb c                   endif
7.fb c                   if        skdepo <> 0 and
7.fb c                             skdepb >= skdepo
7.fb c                   eval      *in46 = *on
7.fb c                   endif
7.fb  *  eller har BM-kort eller Santander med kode 5 eller 6.
7.fb c                   if        fopsuf = 5 or fopsuf = 6
7.fb c                   eval      b_depo = *on
7.fb c                   endif
7.fb  * hvis kredittkunde skal den alikevel få sende
7.fb c                   if        rkkres = 0
7.fb c                   eval      b_depo = *off
7.fb c                   endif
7.fb c                   endif
7.fb c                   endif
7.fb c                   endif
7.fb c                   endsr
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Gir melding om at depositum ikke er betalt
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb c     depo_meld     begsr
7.fb c
7.fb c                   eval      w_mld1 = 'Depositum /Forskudd +
7.fb c                                      er ikke betalt på ordren'
7.fb c                   call      'AA005R'
7.fb c                   parm                    w_mld1
7.fb  *
7.fb c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
6.25  *  Key for posisjonering leverandøre distribusjon
6.25  *
6.25 c     lldil1_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    lldil1_ldor
6.25 c                   kfld                    lldil1_lage
      *
      *  Key for posisjonering av FORSLAG-HODE
      *
     c     lfhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhel1_numm
      *
      *  Key for posisjonering av FORSLAG-HODE - dato
      *
     c     lfhel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhel2_date
      *
      *  Key for posisjonering av FORSLAG-HODE - lev.navn
      *
     c     lfhel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhel3_lsor
      *
      *  Key for posisjonering av FORSLAG-HODE  - selgarlig
      *
     c     lfhel4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhel4_selg
6.20  *
6.20  *  Key for les  FORSLAG-HODE  - leverandør
6.20  *
6.20 c     lfhel5_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lfhel5_ldor
      *
      *  Key for les av FORSLAG-HODE
      *
     c     lfhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelr_numm
      *
      *  Key for oppdatering av FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for les av FORSLAG-LINJE
      *
     c     lfdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtl1_numm
6.15  *
6.15  *  Key for oppdat av FORSLAG-LINJE
6.15  *
6.15 c     lfdtl4_key    klist
6.15 c                   kfld                    w_firm
6.15 c                   kfld                    lfdtl4_numm
      *
      *  Key for les av FORSLAG-LINJE
      *
6.15 c     lfdtl1_key2   klist
     c                   kfld                    w_firm
6.15 c                   kfld                    lfdtl1_numm
6.15 c                   kfld                    lfdtl1_line
      *
      *  Key for oppdatering av FORSLAG-LINJE
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
6.2b  *
6.2b  *  Key for oppdatering av FORSLAG-LINJE
6.2b  *
6.2b c     lfdtlu_key2   klist
6.2b c                   kfld                    w_firm
6.2b c                   kfld                    lfdtlu_numm
6.2b c                   kfld                    lfdtlu_line
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av ORDRE-TYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av bruker-register
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      *  Key for oppdater av bruker-register
      *
     c     lusrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrlu_user
      *
      *  Key for les av bestillingsparameter
      *
     c     mppalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalr_ldor
     c                   kfld                    mppalr_lnum
6.20  *
6.20  *  Key for les  av ordrehode
6.20  *
6.20 c     fohel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    fohel1_numm
6.20 c                   kfld                    fohel1_suff
6.20  *
6.20  *  Key for les  av depositum
6.20  *
6.20 c     sdepl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    sdepl1_numm
6.20 c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
6.2d  *
6.2d  * Key for file : Firmastyrt validering
6.2d  *
6.2d c     avall1_key    klist
6.2d c                   kfld                    w_firm
6.2d c                   kfld                    avall1_apgm
7.14  *
7.14  * Key for brukerregister
7.14  *
7.14 c     ausrl1_key    klist
7.14 c                   kfld                    ausrl1_user
7.14  *
7.14  * Key for oppslag på regionskobling
7.14  *
7.14 c     ra91l2_key    klist
7.14 c                   kfld                    w_firm
7.14 c                   kfld                    ra91l2_lage
7.fb  *
7.fb  * Key for oppslag på ofak-koderegister sideregister
7.fb  *
7.fb c     fst2l1_key    klist
7.fb c                   kfld                    w_firm
7.fb  *
7.fb  * Key for oppslag på kunde
7.fb  *
7.fb c     rkunl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    rkunl1_kund
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'LH100R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'BESTF_LAG_FILTER':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
      *
      * fyller inn brukers bla-valg
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1
     c                   if        %found(lusrl1)
     c                   eval      h1stat = lbbsts
     c                   eval      h1selg = lbbslg
     c                   eval      h1ldor = lbblev
     c                   eval      h1otyp = lbboty
     c                   else
     c                   eval      h1stat = *blank
     c                   eval      h1selg = 0
     c                   eval      h1ldor = 0
     c                   eval      h1otyp = *blank
     c                   endif
7.14  *
7.14  * Slå opp bruker-register
7.14  *
8.01 c                   if        u_801 = *off
7.14 c                   eval      ausrl1_user = l_user
7.14 c     ausrl1_key    chain     ausrl1
7.14 c                   eval      h1lage = ablagr
8.01 c                   endif
      *
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
7.fb c     fst2l1_key    chain     fst2l1
6.2d  *
6.2d  * Firmastyrt validering av utvalgte felter
6.2d  *
6.2d c                   eval      avall1_apgm = 'LO102R'
6.2d c     avall1_key    setll     avall1
6.2d c     avall1_key    reade     avall1
6.2d c                   dow       not %eof(avall1)
6.2d c                   if        avaflt = 'LOLMAT'  and
6.2d c                             avaval = 1
6.2d c                   eval      u_lmat = *on
6.2d c                   endif
6.2d c     avall1_key    reade     avall1
6.2d c                   enddo
6.2d  *
6.2d c                   eval      avall1_apgm = 'LO101R'
6.2d c     avall1_key    setll     avall1
6.2d c     avall1_key    reade     avall1
6.2d c                   dow       not %eof(avall1)
6.2d c                   if        avaflt = 'LOLMAT'  and
6.2d c                             avaval = 1
6.2d c                   eval      u_lmat = *on
6.2d c                   endif
6.2d c     avall1_key    reade     avall1
6.2d c                   enddo
6.2d  *
6.2d c                   eval      avall1_apgm = 'LO110R'
6.2d c     avall1_key    setll     avall1
6.2d c     avall1_key    reade     avall1
6.2d c                   dow       not %eof(avall1)
6.2d c                   if        avaflt = 'LOLMAT'  and
6.2d c                             avaval = 1
6.2d c                   eval      u_lmat = *on
6.2d c                   endif
6.2d c     avall1_key    reade     avall1
6.2d c                   enddo
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lfhel1_numm = 0
     c     lfhel1_key    setll     lfhel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
