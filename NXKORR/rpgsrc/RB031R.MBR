     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB031R
      * Beskrivelse . . : Leser linjer fra FOVF til kommaseparert til
      *                   (Uni Micro)
      * Spesialversjon. : Skal kjøre denne versjonen av summering (6.20
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *K00    031120 sst  Uni Micro
7.00  *K00    120121 sst  Justert beregning av periode
7.01  *K00    260121 sst  Justert oppslag mot kunde
7.02  *K00    260521 sst  Justert oppslag mot momskoder
7.03  *K00    061023 mst  Fjerne ',' fra RKNVN hvis det finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffovupf    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
     frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
      *
      *
     frv01pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(fffdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d tell            s              1  0
     d xxbilk          s              5  0
     d xxmvad          s              3
     d xxmvac          s              3
     d f1momd          s                   like(ffdmom)
     d f1momc          s                   like(ffcmom)
     d f1avde          s                   like(ffcavd)
     d zfbeld          s             12  4
     d zfbelc          s             12  4
     d w_peri          s              2
     d w_forf          s              6  0
     d w_bdat          s              6  0
     d w_fdat          s              6  0
     d w_fdaxd         s              6
     d w_fdaxc         s              6
     d w_fdax          s              6
     d w_bdax          s              6
     d w_ffd           s              6  0
     d w_ffc           s              6  0
     d w_ff            s              6  0
     d w_fxa           s             10
     d w_fx            s             10
     d w_bxa           s             10
     d w_bx            s             10
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_type          s             10
     d w_aarr          s                   like(soaarr)
     d w_kund          s                   like(ffkund)
     d w_selg          s                   like(ffdkto)
     d w_oref          s                   like(sodref)
     d w_ponr          s              4
     d w_biln          s              8  0
     d w_80biln        s              8  0
     d w_totbel        s             12  4
     d w_totmva        s             12  4
     d w_totdek        s             12  4
     d w_knavn         s                   like(rknavn)
7.00 d w_aaxx          s              4
      *
      * Variabler for nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d aforl1_ruti     s                   like(afruti)
     d rb10l1_gkod     s                   like(raakod)
     d rb00l1_gkod     s                   like(r00kod)
     d rkunl1_kund     s                   like(rkkund)
      *
     d*w_kidr          s              6  0
     d w_kidr          s              8  0
     d w_kidd          s                   like(sokidd)
     d w_kid2          s                   like(sokidd)
     d t1kkid          s             23
      *
     d b_kkid          s              1    inz(*off)
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
      * Key for posisjonering på bilagskoder
      *
     c     rb10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rb10l1_gkod
      *
      * Key for posisjonering på koder (moms)
      *
     c     rb00l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
     c                   kfld                    rb00l1_gkod
      *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
      * Key for les av ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les av rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      *
     c                   if        l_firm <> fffirm
     c                   goto      slutt
     c                   endif
      *
     c                   eval      rvdata = *blank
      *
      *  Ev. legge ut recordart 80   Må legges ut i etterhånd
      *
     c                   if        ffbiln <> w_biln
     c                             and w_biln <> 0
     c                   exsr      rec80
     c                   eval      w_biln = ffbiln
     c                   endif
      *
      ***********************************************************
     c
     c* Blankef felt
      *
     c                   eval      w_biln  = ffbiln
      *
      * Forfallsdato
     c                   if        fffdat <> *loval
     c     *dmy          move      fffdat        w_fdat
     c                   move      fffdat        w_fxa
     c                   eval      w_fx = %subst(w_fxa:9:2) + '.' +
     c                                    %subst(w_fxa:6:2) + '.' +
     c                                    %subst(w_fxa:1:4)
     c                   else
     c                   eval      w_fdat = *zero
     c                   eval      w_fx   = *blank
     c                   endif
     c                   move      w_fdat        w_fdax
     c                   if        w_fdat = *zero
     c                   eval      w_fdax = '0'
     c                   endif
     c                   if        ffdkto > 9999
     c                   eval      w_ffd = w_fdat
     c                   eval      w_ffc = *zero
     c                   move      w_ffd         w_fdaxd
     c                   endif
     c                   if        ffckto > 9999
     c                   eval      w_ffc = w_forf
     c                   move      w_ffc         w_fdaxc
     c                   eval      w_ffd = *zero
     c                   endif
     c                   eval      w_ff  = w_forf
     c                   move      w_ffc         w_fdaxc
     c                   if        w_fdaxc = '000000'
     c                   eval      w_fdaxc = *blank
     c                   endif
     c                   if        w_fdaxd = '000000'
     c                   eval      w_fdaxd = *blank
     c                   endif
      *
      * Periode
     c                   movel     ffperi        w_peri
      *
      * Bilagsdato
     c     *dmy          move      ffbdat        w_bdat
     c                   move      w_bdat        w_bdax
     c                   move      ffbdat        w_bxa
     c                   eval      w_bx = %subst(w_bxa:9:2) + '.' +
     c                                    %subst(w_bxa:6:2) + '.' +
     c                                    %subst(w_bxa:1:4)
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   eval      zfbeld = ffbelp
     c                   eval      zfbelc = ffbelp * -1
      * Legger ut detaljer fra FOVF
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
      *
     c*  Flytt til rec80 Besr      control_init
      *
      * Sjekker om bilagstype skal konverteres
     c                   eval      xxbilk = ffbilk
     c                   eval      rb10l1_gkod = ffbilk
     c     rb10l1_key    chain     rb10l1
     c                   if        %found
     c                   eval      xxbilk = rankod
     c                   endif
      *
      * Sjekker om momskode  skal konverteres  debet
     c                   eval      xxmvad = ffdmom
     c                   eval      w_type      = 'MVAKODE'
7.02 c**                 eval      rb00l1_gkod = ffdmom
7.02 c                   move      ffdmom        rb00l1_gkod
     c     rb00l1_key    chain     rb00l1
     c                   if        %found
7.02 c                   move      r00nko        xxmvad
     c                   endif
      *
      * Sjekker om momskode  skal konverteres  kredit
     c                   eval      xxmvac = ffcmom
7.02 c**                 eval      rb00l1_gkod = ffcmom
7.02 c                   move      ffcmom        rb00l1_gkod
     c     rb00l1_key    chain     rb00l1
     c                   if        %found
7.02 c                   move      r00nko        xxmvac
     c                   endif
      *
      *
     c                   if        ffdkto <> *zeros and
     c                             ffdkto >=  1000 and
     c                             ffdkto <=  9999
     c                   eval      f1momd = ffdmom
     c                   endif
      *
     c                   if        ffckto <> *zeros and
     c                             ffckto >=  1000 and
     c                             ffckto <=  9999
     c                   eval      f1momc = ffcmom
     c                   eval      f1momd = '0'
     c                   endif
      *
      * Legger ut avdeling
     c                   eval      f1avde = *zeros
      *
     c                   if        ffcavd <> *zeros
     c                   eval      f1avde = ffcavd
     c                   endif
      *
     c                   if        ffdavd <> *zeros
     c                   eval      f1avde = ffdavd
     c                   endif
      *
     c**********
     c                   if        ffdkto <> *zero
      * Start    Debet postering
     c                   eval      rvdata = *blank
      * Recordart
     c                   eval      rvdata = '60'             + ',' +
      * Bilagsnr.
     c                                     %char(ffbiln)     + ',' +
      * Bil.dato
     c                                           w_bdax      + ',' +
      * Deb.konto
     c                                     %char(ffdkto)     + ',' +
      * Avdeling
     c                                     %char(ffdavd)     + ',' +
      * Prosjekt
     c                                     %trim(ffproj)     + ',' +
      * Deb.mva kode
     c                                     %trim(xxmvad)     + ',' +
      * Bil.tekst
     c                                     %trim(fftext)     + ',' +
      * Beløp på linje
     c                                     %char(zfbeld)     + ',' +
      * Operatør
     c                                     'Uni'             + ',' +
      * Dim.5
     c                                     '0'               + ',' +
      * Dim.6
     c                                     '0'               + ',' +
      * Dim.7
     c                                     '0'               + ',' +
      * Dim.8
     c                                     '0'               + ',' +
      * Periode
     c                                     w_peri            + ',' +
      * Fakturanr (Bilagsnummer)
     c                                     %char(ffbiln)     + ',' +
      * Forfallsdato
     c                                     %trim(w_fdaxd)    + ',' +
      * Valutakode
     c                                     %trim(ffvalk)     + ',' +
      * Valutabeløp
     c                                     '0'               + ',' +
      * Funksjon/Ansvar (dim3)
     c                                     '0'               + ',' +
      * Område          (dim4)
     c                                     '0'               + ','
     c                   write     rv01pfr
      * ENd Debet Post
     c                   endif
      * ___________________________________________________________________
     c**********
     c                   if        ffckto <> *zero
      * Start    Kredit postering
     c                   eval      rvdata = *blank
      * Recordart
     c                   eval      rvdata = '60'             + ',' +
      * Bilagsnr.
     c                                     %char(ffbiln)     + ',' +
      * Bil.dato
     c                                           w_bdax      + ',' +
      * Deb.konto
     c                                     %char(ffckto)     + ',' +
      * Avdeling
     c                                     %char(ffcavd)     + ',' +
      * Prosjekt
     c                                     %trim(ffproj)     + ',' +
      * Deb.mva kode
     c                                     %trim(xxmvac)     + ',' +
      * Bil.tekst
     c                                     %trim(fftext)     + ',' +
      * Beløp på linje
     c                                     %char(zfbelc)     + ',' +
      * Operatør
     c                                     'Uni'             + ',' +
      * Dim.5
     c                                     '0'               + ',' +
      * Dim.6
     c                                     '0'               + ',' +
      * Dim.7
     c                                     '0'               + ',' +
      * Dim.8
     c                                     '0'               + ',' +
      * Periode
     c                                     w_peri            + ',' +
      * Fakturanr (Bilagsnummer)
     c                                     %char(ffbiln)     + ',' +
      * Forfallsdato
     c                                     %trim(w_fdaxc)    + ',' +
      * Valutakode
     c                                     %trim(ffvalk)     + ',' +
      * Valutabeløp
     c                                     '0'               + ',' +
      * Funksjon/Ansvar (dim3)
     c                                     '0'               + ',' +
      * Område          (dim4)
     c                                     '0'               + ','
     c                   write     rv01pfr
      * ENd Debet Post
     c                   endif
      * ___________________________________________________________________
      *
     c                   eval      t1kkid = *blanks
      *   Legge ut record 80 på siste faktura
     cLR                 if        w_80biln <> w_biln
     cLR                 exsr      rec80
     cLR                 endif
      *
     c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = fffirm
     c                   movel     ffperi        w_aaxx
7.00 c                   eval      w_aarr = 2000 + %dec(%subst(w_aaxx:3:2):2:0)
     c**                 eval      w_binr = ffbiln
     c                   eval      w_binr = w_biln
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
      *
     c                   eval      w_oref  = sodref
     c                   eval      w_selg  = soselg
     c                   eval      w_kund  = sokund
     c                   eval      w_ponr  = %subst(sosted:1:4)
     c                   eval      w_totbel = sofsum
     c                   eval      w_totmva = sosalp + sosalh - somvgp - somvgh
     c                   eval      w_totdek = somvgf + somvgp + somvgh
     c                   eval      w_totdek = w_totdek - sokstf -sokstp - soksth
      *
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
      *
      * Henter rutine fra ordretype
      *
     c                   eval      b_kkid = *off
     c                   eval      votyl1_otyp = sootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found(votyl1)   and
     c                             vaorut <> *blank
     c                   eval      aforl1_ruti = vaorut
     c     aforl1_key    chain     aforl1
     c                   if        %found(aforl1) and
     c                             afkidk = 1
     c                   movel     sokidd        t1kkid
     c                   else
     c                   if        afkidk = 2
     c                   movel     sokidr        t1kkid
     c                   else
     c                   if        afkidk = 3
     c                   movel     sokidr        w_kidr
     c                   movel     sokidr        w_kidr
     c                   call      'AK710R'
     c                   parm                    w_firm
     c                   parm                    w_kidr
     c                   parm                    w_kidd
     c                   parm                    w_kid2
     c                   movel     w_kid2        t1kkid
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * _____________________________________________________________________________________
     c     REC80         begsr
      * _____________________________________________________________________________________
      *
     c                   exsr      control_init
      *
     c                   if        w_totbel <> 0
      *
7.01 c**                 eval      rkunl1_kund = ffkund
7.01 c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      w_knavn = rknavn
     c                   else
     c                   eval      w_knavn = sonavn
     c                   endif
7.03  *
7.03 c                   eval      w_knavn = %ScanRpl(',' : ' ' : w_knavn)
7.03  *
      *
      * Start    Debet postering
     c                   eval      rvdata = *blank
      * Recordart
     c                   eval      rvdata = '80'             + ',' +
      * Bilagsnr.
     c                                     %char(w_biln)     + ',' +
      * Kundenummer
     c                                     %char(w_kund)     + ',' +
      * Status
     c                                     '1'               + ',' +
      * Totalsum
     c                                     %char(w_totbel)   + ',' +
      * Innbetalt
     c                                     '0'               + ',' +
      * Kredit / tillegsfaktura
     c                                     '0'               + ',' +
      * Sum moms
     c                                     %char(w_totmva)   + ',' +
      * Ant. purringer
     c                                     '0'               + ',' +
      * Selgernummer
     c                                     %char(w_selg)     + ',' +
      * Dekningsbidrag
     c                                     %char(w_totdek)   + ',' +
      * Navn
     c                                     %trim(w_knavn)    + ',' +
      * Adresse
     c                                     %trim(soadr1)     + ',' +
      * Adresse2
     c                                     %trim(soadr2)     + ',' +
      * Postnummer
     c                                     %char(w_ponr)     + ',' +
      * Poststed
     c                                     %trim(sosted)     + ',' +
      * Referanse
     c                                     %trim(w_oref)     + ',' +
      * Innbet. dato
     c                                     ''                + ',' +
      * Bil.dato
     c**                                   %char(w_bdat)     + ',' +
     c                                     w_bx              + ',' +
      * Forfallsdato
     c*                                    %char(w_ff)       + ',' +
     c                                     w_fx              + ',' +
      * Blank 21
     c                                     ''                + ',' +
      * Blank 22
     c                                     ''                + ',' +
      * Rekvisisjon
     c                                     ''                + ',' +
      * Factoring
     c                                     '0'               + ',' +
      * Avtalegirostatus
     c                                     '0'               + ',' +
      * Prosjekt (Dim1)
     c                                     '0'               + ',' +
      * Avdeling
     c                                     '0'               + ',' +
      * Funksjon / ansvar
     c                                     '0'               + ',' +
      * Område
     c                                     '0'               + ',' +
      * APstatus
     c                                     '0'               + ',' +
      * Funksjon/Ansvar (dim3)
     c                                     '0'               + ',' +
      * Bilagsnummer
     c                                     %char(w_biln)     + ','
     c                   write     rv01pfr
      *
     c                   eval      w_kund   = *zero
     c                   eval      w_totbel = *zero
     c                   eval      w_totmva = *zero
     c                   eval      w_totdek = *zero
     c                   eval      w_selg   = *zero
     c                   eval      w_oref   = *blank
     c                   eval      w_80biln = w_biln
      *
      * ENd Rec-80
     c                   endif
     c                   endsr
      *
     c     *inzsr        begsr
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
