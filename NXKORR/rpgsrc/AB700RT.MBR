     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AB700R
      * Beskrivelse . . : Skriver oppstartstransaksjon til jobbloggfil og
      *                   endrer status på jobbfil.
      *                   Input parameter er jobbnavn, returnerer unik key
      *                   for jobben basert på jobbnavn og timestamp.
      *
      * Spesialversjon. : Hardkodet med firma 000
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130809 BSA  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fajlslu    uf a e           k disk    rename(ajlspfr:ajlslur)
     fajltlu    uf a e           k disk    rename(ajltpfr:ajltlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_jkey                        41
     d  d_jnav                       15    overlay(d_jkey:1)
     d  d_tstp                       26    overlay(d_jkey:16)
      *
      * Definering av variabler i nøkler
      *
     d ajlslu_nav      s                   like(ajsnav)
      *
     d ajltlu_jbid     s                   like(ajtjid)
     d ajltlu_lin      s                   like(ajtlin)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ajsfir)
     d w_tstp          s               z
     d w_melding       s                   like(ajttxt)
      *
      * Definering av parametre
     d p_jnav          s                   like(ajsnav)
     d p_jkey          s                   like(ajskey)
     d p_jtxt          s                   like(ajsbes)
      *
     d w_rec           s              1    dim(41)
     d w_jkey          s              1    dim(41)
     d x               s              2  0
     d y               s              2  0
      *
      * Definering av tabell
     d a_meld          s            100    dim(2) ctdata perrcd(1)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis jobbid er blank, avslutter vi uten mer logging
     c                   if        p_jnav = *blanks
     c                   eval      p_jkey = *blanks
     c                   eval      *inlr = *on
     c                   return
     c                   endif
      *
      * Skriv til jobblogging - statusfil
     c                   eval      w_melding = *blanks
     c                   eval      d_jnav = p_jnav
     c                   move      w_tstp        d_jkey
      * Fjerner alle blanke tegn.
     c                   eval      x = 1
     c                   eval      y = 1
     c                   eval      w_rec = *blanks
     c                   eval      w_jkey = *blanks
     c                   movea     d_jkey        w_rec
     c     x             doueq     41
     c                   if        w_rec(x) <> *blank
     c                   move      w_rec(x)      w_jkey(y)
     c                   add       1             y
     c                   endif
     c                   add       1             x
     c                   enddo
      * Legg over "trimmet" nøkkel
     c                   movea     w_jkey        d_jkey
      *
     c                   eval      ajlslu_nav = p_jnav
     c     ajlslu_key    chain     ajlslur
      *
     c                   if        %found
     c                   eval      w_melding = a_meld(1)
     c                   eval      ajssta = 10
     c                   eval      ajsskj = w_tstp
     c                   eval      ajskey = d_jkey
     c                   eval      ajsrun = 1
     c                   time                    ajseda
     c                   time                    ajseti
     c                   eval      ajseus = l_user
     c                   update    ajlslur
     c                   else
      * Jobben finnes ikke fra før. Vi oppretter den med noen defaultverdier
     c                   clear                   ajlslur
     c                   eval      ajsfir = w_firm
     c*                  eval      w_melding = a_meld(2)
     c                   eval      w_melding = a_meld(1)
     c                   eval      ajsnav = p_jnav
     c                   eval      ajssta = 10
     c                   eval      ajsrun = 1
     c                   eval      ajsskj = w_tstp
      * Bruker tekst hvis dette er angitt i parameterlista
     c                   if        p_jtxt <> *blank
     c                   eval      ajsbes = p_jtxt
     c                   else
     c                   eval      ajsbes = 'Autogenerert'
     c                   endif
     c                   eval      ajskey = d_jkey
     c                   time                    ajseda
     c                   time                    ajseti
     c                   eval      ajseus = l_user
     c                   time                    ajsoda
     c                   time                    ajsoti
     c                   eval      ajsous = l_user
     c                   write     ajlslur
     c                   endif
      *
      * Skriv jobblogging - startlinje
      *
     c                   eval      ajltlu_jbid = ajskey
     c                   eval      ajltlu_lin  = 1
     c*
     c     ajltlu_key    chain     ajltlur
      *
     c                   if        %found
      * Her er det noe galt. Vi avslutter uten å skrive noen statusrecord.
     c                   eval      *inlr = *on
     c                   return
      *
     c                   else
     c                   clear                   ajltlur
     c                   eval      ajtfir = w_firm
     c                   eval      ajtjid = ajltlu_jbid
     c                   eval      ajtlin = ajltlu_lin
     c                   eval      ajttxt = w_melding
     c                   eval      ajtsta = ajssta
     c                   time                    ajtoda
     c                   time                    ajtoti
     c                   eval      ajtous = l_user
     c                   write     ajltlur
     c                   endif
      *
      * Avslutt
      *
     c                   eval      p_jkey = d_jkey
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_jnav
     c                   parm                    p_jkey
     c                   parm                    p_jtxt
      *
      * Key for oppdatering av jobblogging - status
      *
     c     ajlslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ajlslu_nav
      *
      * Key for oppdatering av jobblogging - transaksjoner
      *
     c     ajltlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ajltlu_jbid
     c                   kfld                    ajltlu_lin
      *
      *
      * Henter firma fra LDA
      *
     c*                  eval      w_firm = l_firm
     c                   eval      w_firm = 0
      * Setter timestampvalue for jobben
     c                   time                    w_tstp
      *
     c                   endsr
**
Jobben ble startet på normal måte
Jobben er autogenerert i jobbfila. Endre beskrivelse og ajourfør informasjon
