     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA022R
      * Beskrivelse . . : Brukere, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250804 AMD  Nytt program
5.31  * V5.31 010704 AMD  Lagt inn variabel for miljø
5.32  * V5.32 200804 AMD  Lagt inn sletting av AS400 brukerprofil
5.33  * V5.33 130904 AMD  Lagt inn overgang til Butikk Bruker
      *                   NB! Sletting av AS400 brukerprofil skapte såpass
      *                   mye problemer ved overgang til V5.30 at dette er
      *                   deaktivert inntil videre.
5.40  * V5.40 020605 AMD  Lagt inn gruppe-referanse på bruker
5.51  * V5.51 240107 AMD  Lagt inn tilgangskode på bruker (Ref SST)
5.61  * V5.61 300109 AMD  Lagt inn generering av IQ-program 1 og 2
6.21  * V6.20 130514 AMD  Ny funksjon for oppdatering av filegruppe
6.21  *                   og firma ved innlegging av ny bruker
6.22  * V6.20 280915 bsa  Mulighet for kopiering av brukerrettigheter
6.23  * V6.20 290915 bsa  Ved sletting, fjernes også info fra FUSR, LUSR og BUSR
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
7.01  * 7.00      110919  Utvidet AA034C til å sende med miljø som parameter
      *
8.01  * 8.xx  281122 bsa  Skal sperre for å legge startmeny på ASP brukeren.
8.02  * 8.xx  041225 bsa  Skal varsle om du kopierer bruker med tilgang 99.
8.03  * 8.xx  041225 bsa  Justert lengde på passord.
      *
9.01  *K0000  180725 bsa  Laget som en spesial for MGR.
9.02  *K0000  020226 bsa  Hvis du kopierer brukere med tilgang 99
9.02  *K0000              skal de oprpettes med 50, hvis du svarer 'N'.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fausrl2    if   e           k disk    rename(ausrpfr:ausrl2r)
     fausrl3    if   e           k disk    rename(ausrpfr:ausrl3r)
5.40 fausrl4    if   e           k disk    rename(ausrpfr:ausrl4r)
     fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
     fausrlu    uf a e           k disk    rename(ausrpfr:ausrlur)
     falibl1    if   e           k disk    rename(alibpfr:alibl1r)
     faliblu    uf a e           k disk    rename(alibpfr:aliblur)
6.22 ffusrlu    uf a e           k disk    rename(fusrpfr:fusrlur)
6.22 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
6.22 f                                     prefix(fx:2)
6.22 flusrlu    uf a e           k disk    rename(lusrpfr:lusrlur)
6.22 flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
6.22 f                                     prefix(lx:2)
6.22 fbusriu    uf a e           k disk    rename(busrst:busriur)
6.22 fbusri1    if   e           k disk    rename(busrst:busr1ur)
6.22 f                                     prefix(bx:2)
      *
     fax022d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
5.31 d p_milj          s                   like(abmilj)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av datastruktur
      *
     d                 ds
     d d_bibl                        10
     d  d_del1                        3    overlay(d_bibl)
     d  d_del2                        3    overlay(d_bibl:4)
     d  d_del3                        4    overlay(d_bibl:7)
8.01 d                 ds
8.01 d d_user                        10
8.01 d  d_user1                       4    overlay(d_user)
      *
      * Definering av variabler i nøkler
      *
     d ausrl1_user     s                   like(abuser)
6.22 d fusrlu_user     s                   like(abuser)
6.22 d lusrlu_user     s                   like(abuser)
6.22 d busriu_user     s                   like(abuser)
     d ausrl2_navn     s                   like(abnavn)
     d ausrl3_rlib     s                   like(abrlib)
5.40 d ausrl4_grup     s                   like(abgrup)
     d ausrlr_user     s                   like(abuser)
     d ausrlu_user     s                   like(abuser)
     d alibl1_user     s                   like(aluser)
     d alibl1_plas     s                   like(alplas)
     d aliblu_user     s                   like(aluser)
     d aliblu_plas     s                   like(alplas)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
9.02 d b_tg99          s              1    inz(*off)
      *
     d w_user          s                   like(abuser)
     d w_meny          s                   like(abmeny)
     d w_navn          s                   like(abnavn)
6.21 d w_fgrp          s                   like(absfil)
6.21 d w_bibl          s                   like(absbib)
6.21 d w_sfir          s                   like(absfir)
6.21 d w_fnav          s                   like(abfnav)
     d w_parm          s              4
5.31 d w_svar          s              1
5.32 d w_pf03          s              1
6.22 d w_firm          s                   like(fbfirm)
     d w_stat          s             10
     d w_meld          s             50
8.02 d w_mld2          s             50
8.02 d w_in12          s              1
      *
8.01 d x               s              2  0
      *
     d s_user          s                   like(abuser)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      **D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
5.40  *
5.40  * F22=Skifte av bilde
5.40  *
5.40 c                   if        *inkw
5.40 c                   if        *in40   = *on
5.40 c                   eval      *in40   = *off
5.40 c                   else
5.40 c                   eval      *in40   = *on
5.40 c                   endif
5.40 c                   exsr      forny
5.40 c                   goto      b2taga
5.40 c                   endif
      *
      * Posisjonering
      *
     c                   if        b2user <> *blank or
     c                             b2navn <> *blank or
     c                             b2rlib <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c                   eval      ausrl4_grup = b1rlib
5.40 c     ausrl4_key    setll     ausrl4
     c                   when      w_seqe = 'R'
     c                   eval      ausrl3_rlib = b1rlib
     c     ausrl3_key    setll     ausrl3
     c                   when      w_seqe = 'B'
     c                   eval      ausrl2_navn = b1navn
     c     ausrl2_key    setll     ausrl2
     c                   other
     c                   eval      ausrl1_user = b1user
     c     ausrl1_key    setll     ausrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Bruker
      *
     c                   if        b2user <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      ausrl1_user = b2user
     c     ausrl1_key    setll     ausrl1
     c                   goto      end_pos
     c                   endif
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ausrl2_navn = b2navn
     c     ausrl2_key    setll     ausrl2
     c                   goto      end_pos
     c                   endif
      *
      * Referansebibliotek
      *
5.40 c                   if        *in40  = *off
     c                   if        b2rlib <> *blank
     c                   eval      w_seqe = 'R'
     c                   eval      ausrl3_rlib = b2rlib
     c     ausrl3_key    setll     ausrl3
     c                   goto      end_pos
     c                   endif
5.40 c                   endif
5.40  *
5.40  * Gruppe
5.40  *
5.40 c                   if        *in40  = *on
5.40 c                   if        b2rlib <> *blank
5.40 c                   eval      w_seqe = 'G'
5.40 c                   eval      ausrl4_grup = b2rlib
5.40 c     ausrl4_key    setll     ausrl4
5.40 c                   goto      end_pos
5.40 c                   endif
5.40 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2user = *blank
     c                   eval      b2navn = *blank
     c                   eval      b2rlib = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
     c                   eval      b_feil = *off
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      sjk_acces
     c                   if        b_feil = *off
     c                   eval      c1user = b1user
     c                   exsr      xc2bld
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
8.02 c                   exsr      sjk_acces3
     c                   if        b_feil = *off
     c                   eval      k1user = b1user
     c                   exsr      xk1win
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Aktiver/deaktiver
      *
     c                   if        b1valg = 4
     c                   exsr      sjk_acces
     c                   if        b_feil = *off
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   endif
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1user = b1user
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Sett nytt passord
      *
     c                   if        b1valg = 7
     c                   exsr      sjk_acces
     c                   if        b_feil = *off
     c                   exsr      xp1win
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Call program for
      * generering av
      * Ip-program
      *
     c                   if        b1valg = 8
     c                   exsr      sjk_acces
     c                   if        b_feil = *off
8.03 c                   exsr      xp1win8
8.03  * Hvis ikke passord, kan vi ikke oprette bruker
8.03 c                   if        %trim(p1newp) <> *blanks
     c                   eval      w_user = b1user
     c                   eval      w_navn = b1navn
     c                   eval      w_meny = b1meny
     c                   call      'AX030C'
     c                   parm                    w_user
     c                   call      'AX034C'
     c                   parm                    w_user
     c                   parm                    w_navn
     c                   parm                    w_meny
7.01 c                   parm                    p_milj
8.03 c                   parm                    p1newp
5.61  *
5.61  * Generer IQ-program 1
5.61  *
5.61 c                   call      'AX036C'
5.61 c                   parm                    w_user
5.61  *
5.61  * Generer IQ-program 2
5.61  *
5.61 c                   call      'AX038C'
5.61 c                   parm                    w_user
5.61  *
8.03 c                   endif
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Resette bruker
      *
     c                   if        b1valg = 9
     c                   exsr      sjk_acces
     c                   if        b_feil = *off
     c                   eval      w_stat = *blanks
     c                   call      'AX026C'
     c                   parm                    b1user
     c                   parm                    w_stat
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1user = b2user
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c***                if        *inkd
     c***                goto      c1taga
     c***                endif
      *
     c***                if        c1user = *blank
     c***                goto      end_c1bld
     c***                endif
      *
      * Validering
      *
     c                   if        c1user = *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
5.31  *
5.31  * Sjekke om brukerprofil allerede
5.31  * finnes som en AS400 profil
5.31  *
5.31 c                   eval      w_svar = ' '
5.31  *
5.31 c                   call      'AA710C'
5.31 c                   parm                    c1user
5.31 c                   parm                    w_svar
5.31  *
5.31 c                   if        w_svar = '1'
5.31 c                   goto      c1taga
5.31 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ausrlr_user = c1user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1user = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2user = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ausrlr_user = c1user
     c     ausrlr_key    chain     ausrlr
      *
     c                   if        %found
     c                   eval      c2navn = abnavn
5.40 c                   eval      c2grup = abgrup
5.51 c                   eval      c2tilg = abtilg
     c                   eval      c2tlfn = abtlfn
9.02  *
9.02  * Hvis kopiert og du ikke bekrefter sikkerhetsnivå 99, settes det til 50.
9.02  *
     c                   eval      c2levl = ablevl
9.02  *
9.02 c                   if        b1valg = 3 and
9.02 c                             b_tg99 = *off
9.02 c                   eval      c2levl = 50
9.02 c                   endif
9.02  *
     c                   eval      c2bfil = abbfil
     c                   eval      c2bfir = abbfir
     c                   eval      c2meny = abmeny
     c                   eval      c2sfil = absfil
     c                   eval      c2sbib = absbib
     c                   eval      c2sfir = absfir
     c                   eval      c2fnav = abfnav
     c                   eval      c2savd = absavd
     c                   eval      c2lagr = ablagr
     c                   eval      c2bavd = abbavd
     c                   eval      c2a4s3 = aba4s3
     c                   eval      c2rlib = abrlib
     c                   eval      c2eusr = abeusr
     c     *dmy          move      abedat        c2edat
     c                   else
     c                   eval      c2navn = *blank
5.40 c                   eval      c2grup = *blank
5.51 c                   eval      c2tilg = *blank
     c                   eval      c2tlfn = *blank
     c                   eval      c2levl = 0
     c                   eval      c2bfil = *blank
     c                   eval      c2bfir = *blank
     c                   eval      c2meny = *blank
     c                   eval      c2sfil = *blank
     c                   eval      c2sbib = *blank
     c                   eval      c2fnav = *blank
     c                   eval      c2sfir = 0
     c                   eval      c2savd = 0
     c                   eval      c2lagr = 0
     c                   eval      c2rlib = *blank
     c                   eval      c2bavd = *blank
     c                   eval      c2a4s3 = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1user = k1user
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
6.21 c                   if        *inkd
6.21 c                   exsr      spØrring
6.21 c                   goto      c2taga
6.21 c                   endif
      *
      * Validering av input
      *
     c                   if        c2navn = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Siste filegruppe kan ikke
      * være blank, når F6 benyttes
      *
     c                   if        *inkf  = *on
     c                   if        c2sfil = *blank
     c                   eval      *in32  = *on
     c                   goto      c2taga
     c                   endif
     c                   exsr      xi1win
     c                   goto      c2taga
     c                   endif
6.21  *
6.21  * Siste filegruppe kan ikke
6.21  * være blank, i det hele tatt :-)
6.21  *
6.21 c                   if        c2sfil = *blank
6.21 c                   eval      *in36  = *on
6.21 c                   goto      c2taga
6.21 c                   endif
5.31  *
5.31  * Kontroll på at oppgitt miljø
5.31  * er definert på systemet
5.31  *
5.31 c                   eval      w_svar = ' '
5.31 c                   if        c2milj <> *blank
5.31  *
5.31 c                   call      'AR710R'
5.31 c                   parm                    w_svar
5.31 c                   parm                    c2milj
5.31  *
5.31 c                   if        w_svar = '1'
5.31 c                   eval      *in34  = *on
5.31 c                   goto      c2taga
5.31 c                   endif
5.31  *
5.31 c                   endif
      *
      * Kontroll på at dersom det
      * er begrenset tilgang til
      * firma, må det også være
      * begrenset tilgang til
      * file-gruppe
      *
     c                   if        c2bfir =  '1'
     c                   if        c2bfil <> '1'
     c                   eval      *in35  = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
8.01  *
8.01  * Skal ikke ha meny på ASP brukeren.
8.01  *
8.01 c                   if        %trim(c2meny) <> *blank
8.01 c                   eval      d_user = c1user
8.01 c                   eval      x = %len(%trim(d_user))
8.01 c                   if        x = 7 and
8.01 c                             d_user1 = 'ASP_'
8.01 c                   eval      *in37  = *on
8.01 c                   goto      c2taga
8.01 c                   endif
6.21 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ausrlur
      *
     c                   eval      ausrlu_user = c1user
     c     ausrlu_key    chain     ausrlur
      *
     c                   eval      abnavn = c2navn
5.40 c                   eval      abgrup = c2grup
5.51 c                   eval      abtilg = c2tilg
     c                   eval      abtlfn = c2tlfn
     c                   eval      ablevl = c2levl
5.31 c                   eval      abmilj = c2milj
     c                   eval      abbfil = c2bfil
     c                   eval      abbfir = c2bfir
6.21 c                   eval      abfnav = c2fnav
     c                   eval      abmeny = c2meny
     c                   eval      abrlib = c2rlib
     c                   eval      abbavd = c2bavd
     c                   eval      aba4s3 = c2a4s3
      *
     c                   move      'ADB'         d_del1
     c                   move      c2sfil        d_del2
     c                   move      *blank        d_del3
     c                   move      d_bibl        absbib
     c                   move      c2sfil        absfil
     c                   move      c2sfir        absfir
     c                   move      c2savd        absavd
     c                   move      c2lagr        ablagr
      *
     c                   time                    abedat
     c                   time                    abetim
     c                   eval      abeusr = l_user
      *
     c                   if        %found
     c                   update    ausrlur
     c                   else
     c                   eval      abuser = c1user
     c*>                 time                    abodat               ¤¤¤¤¤¤
     c                   write     ausrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1user = abuser
     c                   eval      b1navn = abnavn
     c                   eval      b1levl = ablevl
     c                   eval      b1bfil = abbfil
     c                   eval      b1bfir = abbfir
     c                   eval      b1meny = abmeny
     c                   eval      b1rlib = abrlib
     c                   eval      b1bavd = abbavd
     c                   endif
      *
5.40 c                   if        b1valg = 2
5.40 c                   if        *in40  = *on
5.40 c                   eval      b1rlib = abgrup
5.40 c                   endif
5.40 c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   if        b1stat = '*DISABLED'
     c                   eval      b1stat = '*ENABLED'
     c                   else
     c                   eval      b1stat = '*DISABLED'
     c                   endif
      * Oppdatert brukeren
     c                   call      'AX024C'
     c                   parm                    b1user
     c                   parm                    b1stat
      *
     c                   eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_user = k1user
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ausrlr_user = k1user
     c     ausrlr_key    chain     ausrlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1user = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ausrlr_user = k1user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1user = s_user
     c                   exsr      xc2bld
      *
      * Oppdater bibliotekslister
      * dersom dette er valgt
      *
     c                   if        k1jane = 1
     c                   eval      alibl1_user = s_user
     c     alibl1_key    setll     alibl1r
     c     alibl1_key    reade     alibl1r
      *
     c                   dow       not %eof
     c                   eval      aliblu_user = k1user
     c                   eval      aliblu_plas = alplas
     c     aliblu_key    chain     aliblur
     c                   eval      aluser = k1user
     c                   if        not %found
     c                   write     aliblur
     c                   endif
     c     alibl1_key    reade     alibl1r
     c                   enddo
      *
     c                   endif
6.22  *
6.22  * Oppdater bibliotekslister
6.22  * dersom dette er valgt
6.22  *
6.22 c                   if        k1brjn = 1
6.22  * Legg til rettigheter for ordre
6.22 c                   exsr      add_ordre
6.22  * Legg til rettigheter for lager
6.22 c                   exsr      add_lager
6.22  * Legg til rettigheter for butikk
6.22 c                   exsr      add_shop
6.22  *
6.22 c                   endif
6.22  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sett nytt passord          (P1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xp1win        begsr
      *
      * Legg ut skjermbilde il rad det skal kopieres fra
      *
     c                   eval      p1user = b1user
      *
      * Fyller kopierings-bildet
      *
     c                   eval      p1navn = *blanks
     c                   eval      ausrlr_user = p1user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found
     c                   eval      p1navn = abnavn
     c                   endif
      *
     c     p1taga        tag
      *
     c                   exfmt     p1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
8.03 c                   if        %len(%trim(p1newp)) <14
8.03 c*                            %len(%trim(p1newp)) > 10
     c                   eval      *in41 = *on
     c                   goto      p1taga
     c                   endif
      *
      * Oppdater med nytt passord
      *
     c                   eval      w_stat = *blanks
     c                   call      'AX025C'
     c                   parm                    p1user
     c                   parm                    p1newp
     c                   parm                    w_stat
      *
     c                   endsr
      *
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  * Subrutine for å sette passord ved opprettelse av bruker
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *
8.03 c     xp1win8       begsr
8.03  *
8.03  * Legg ut skjermbilde il rad det skal kopieres fra
8.03  *
8.03 c                   eval      p1user = b1user
8.03  *
8.03  * Fyller kopierings-bildet
8.03  *
8.03 c                   eval      p1navn = *blanks
8.03 c                   eval      ausrlr_user = p1user
8.03 c     ausrlr_key    chain     ausrlr
8.03 c                   if        %found
8.03 c                   eval      p1navn = abnavn
8.03 c                   endif
8.03  *
8.03 c     p1tag8        tag
8.03  *
8.03 c                   exfmt     p1win
8.03  *
8.03 c                   if        *inkc or *inkl
8.03 c                   eval      p1newp = *blanks
8.03 c                   leavesr
8.03 c                   endif
8.03  *
8.03  * Sjekk om utfylt
8.03  *
8.03 c                   if        %len(%trim(p1newp)) <14
8.03 c                   eval      *in41 = *on
8.03 c                   goto      p1tag8
8.03 c                   endif
8.03  *
8.03 c                   endsr
8.03  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av bruker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_acces     begsr
      *
     c                   select
     c                   when      %scan('ASP_':b1user) > 0
     c                   eval      b_feil = *on
     c                   when      %scan('GRP_':b1user) > 0
     c                   eval      b_feil = *on
     c                   when      %scan('POA_':b1user) > 0
     c                   eval      b_feil = *on
     c                   when      %scan('EG -':b1navn) > 0
     c                   eval      b_feil = *on
     c                   endsl
      *
     c                   if        b_feil = *on
     c                   eval      w_meld = 'Du har ikke tilgang til denn+
     c                                       e brukerprofilen'
     c                   call      'AA005R'
     c                   parm                    w_meld
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_sjk       endsr
      *
8.02  *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for sjekk av bruker ved kopiering
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     sjk_acces3    begsr
8.02  *
9.02 c                   eval      b_tg99 = *off
8.02  *
8.02 c                   select
8.02 c                   when      %scan('ASP_':b1user) > 0
8.02 c                   eval      b_feil = *on
8.02 c                   when      %scan('GRP_':b1user) > 0
8.02 c                   eval      b_feil = *on
8.02 c                   when      %scan('POA_':b1user) > 0
8.02 c                   eval      b_feil = *on
8.02 c                   when      %scan('EG -':b1navn) > 0
8.02 c                   eval      b_feil = *on
8.02 c                   endsl
8.02  *
8.02 c                   if        b_feil = *on
8.02 c                   eval      w_meld = 'Du har ikke tilgang til denn+
8.02 c                                       e brukerprofilen'
8.02 c                   call      'AA005R'
8.02 c                   parm                    w_meld
8.02 c                   goto      end_sjk3
8.02 c                   endif
8.02  *
8.02 c                   if        b1levl = 99
8.02  *
8.02 c                   eval      b_feil = *on
8.02 c                   eval      w_meld = 'Denne brukeren har tilgang til -
8.02 c                             MGRSUPPORT menyen.'
8.02 c                   eval      w_mld2 = 'Bekreft at dette er ok !'
8.02 c                   eval      w_svar = 'N'
8.02  *
8.02 c                   call      'AA007R'
8.02 c                   parm                    w_meld
8.02 c                   parm                    w_mld2
8.02 c                   parm                    w_svar
8.02 c                   parm                    w_in12
8.02  *
8.02 c                   if        w_in12 = '1'
8.02 c                   goto      end_sjk3
8.02 c                   endif
8.02  *
8.02 c                   if        w_svar = 'J'
9.02 c                   eval      b_tg99 = *off
8.02 c                   eval      b_feil = *off
9.02 c                   else
9.02 c                   eval      b_feil = *off
8.02 c                   endif
8.02  *
8.02 c                   endif
8.02  *
8.02 c                   eval      b_forn = *on
8.02  *
8.02 c     end_sjk3      endsr
8.02  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Legger ut rettigheter tilknyttet ordre
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     add_ordre     begsr
6.22  * Hent info fra fra-person
6.22 c                   eval      fusrlu_user = s_user
6.22 c     fusrlu_key    chain     fusrl1
6.22 c                   if        %found
6.22 c                   eval      fusrlu_user = k1user
6.22 c     fusrlu_key    chain     fusrlu
6.22 c                   if        not %found
6.22 c                   clear                   fusrlur
6.22  *
6.22 c                   eval      fbfirm = fxfirm
6.22 c                   eval      fbuser = k1user
6.22 c                   eval      fbvref = fxvref
6.22 c                   eval      fbselg = fxselg
6.22 c                   eval      fbbind = fxbind
6.22 c                   eval      fbotyp = fxotyp
6.22 c                   eval      fbko98 = fxko98
6.22 c                   eval      fbcord = fxcord
6.22 c                   eval      fbvise = fxvise
6.22 c                   eval      fbko01 = fxko01
6.22 c                   eval      fbko02 = fxko02
6.22 c                   eval      fbko03 = fxko03
6.22 c                   eval      fbko04 = fxko04
6.22 c                   eval      fbko05 = fxko05
6.22 c                   eval      fbko06 = fxko06
6.22 c                   eval      fbko07 = fxko07
6.22 c                   eval      fbko08 = fxko08
6.22 c                   eval      fbko09 = fxko09
6.22 c                   eval      fbko10 = fxko10
6.22 c                   eval      fbko11 = fxko11
6.22 c                   eval      fbko12 = fxko12
6.22 c                   eval      fbko13 = fxko13
6.22 c                   eval      fbko14 = fxko14
6.22 c                   eval      fbko15 = fxko15
6.22 c                   eval      fbko16 = fxko16
6.22 c                   eval      fbko17 = fxko17
6.22 c                   eval      fbko18 = fxko18
6.22 c                   eval      fbko19 = fxko19
6.22 c                   eval      fbko20 = fxko20
6.22 c                   eval      fbutla = fxutla
6.22 c                   eval      fboty1 = fxoty1
6.22 c                   eval      fboty2 = fxoty2
6.22 c                   eval      fboty3 = fxoty3
6.22 c                   eval      fbityp = fxityp
6.22 c                   eval      fbuse2 = fxuse2
6.22 c                   eval      fbkund = fxkund
6.22 c                   eval      fbbenr = fxbenr
6.22 c                   eval      fbsel2 = fxsel2
6.22 c                   eval      fbavde = fxavde
6.22 c                   eval      fblage = fxlage
6.22 c                   eval      fbrekv = fxrekv
6.22 c                   eval      fbbelp = fxbelp
6.22 c                   eval      fbvalg = fxvalg
6.22 c                   eval      fblmat = fxlmat
6.22 c                   eval      fboanm = fxoanm
6.22  *
6.22 c                   time                    fbodat
6.22 c                   time                    fbotim
6.22 c                   eval      fbousr = l_user
6.22 c                   time                    fbedat
6.22 c                   time                    fbetim
6.22 c                   eval      fbeusr = l_user
6.22  *
6.22 c                   write     fusrlur
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endsr
6.22  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Legger ut rettigheter tilknyttet lager
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     add_lager     begsr
6.22  * Hent info fra fra-person
6.22 c                   eval      lusrlu_user = s_user
6.22 c     lusrlu_key    chain     lusrl1
6.22 c                   if        %found
6.22 c                   eval      lusrlu_user = k1user
6.22 c     lusrlu_key    chain     lusrlu
6.22 c                   if        not %found
6.22 c                   clear                   lusrlur
6.22  *
6.22 c                   eval      lbfirm = lxfirm
6.22 c                   eval      lbuser = k1user
6.22 c                   eval      lbbind = lxbind
6.22 c                   eval      lbotyp = lxotyp
6.22 c                   eval      lbcbes = lxcbes
6.22 c                   eval      lbvise = lxvise
6.22 c                   eval      lbko01 = lxko01
6.22 c                   eval      lbko02 = lxko02
6.22 c                   eval      lbko03 = lxko03
6.22 c                   eval      lbko04 = lxko04
6.22 c                   eval      lbko05 = lxko05
6.22 c                   eval      lbko06 = lxko06
6.22 c                   eval      lbko07 = lxko07
6.22 c                   eval      lbko08 = lxko08
6.22 c                   eval      lbko09 = lxko09
6.22 c                   eval      lbko10 = lxko10
6.22 c                   eval      lbko11 = lxko11
6.22 c                   eval      lbko12 = lxko12
6.22 c                   eval      lbko13 = lxko13
6.22 c                   eval      lbko14 = lxko14
6.22 c                   eval      lbko15 = lxko15
6.22 c                   eval      lbtans = lxtans
6.22 c                   eval      lbboty = lxboty
6.22 c                   eval      lbbsts = lxbsts
6.22 c                   eval      lbbslg = lxbslg
6.22 c                   eval      lbblev = lxblev
6.22 c                   eval      lbflow = lxflow
6.22 c                   eval      lbctrl = lxctrl
6.22  *
6.22 c                   time                    lbodat
6.22 c                   time                    lbotim
6.22 c                   eval      lbousr = l_user
6.22 c                   time                    lbedat
6.22 c                   time                    lbetim
6.22 c                   eval      lbeusr = l_user
6.22  *
6.22 c                   write     lusrlur
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endsr
6.22  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Legger ut rettigheter tilknyttet butikk
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     add_shop      begsr
6.22  * Hent info fra fra-person
6.22 c                   eval      busriu_user = s_user
6.22 c     busriu_key    chain     busri1
6.22 c                   if        %found
6.22 c                   eval      busriu_user = k1user
6.22 c     busriu_key    chain     busriu
6.22 c                   if        not %found
6.22 c                   clear                   busriur
6.22  *
6.22 c                   eval      bbfirm = bxfirm
6.22 c                   eval      bbuser = k1user
6.22 c                   eval      bbvref = bxvref
6.22 c                   eval      bbselg = bxselg
6.22 c                   eval      bbkret = bxkret
6.22 c                   eval      bbkopg = bxkopg
6.22 c                   eval      bbkkpr = bxkkpr
6.22 c                   eval      bbkspr = bxkspr
6.22 c                   eval      bbkrab = bxkrab
6.22 c                   eval      bbkvtx = bxkvtx
6.22 c                   eval      bbklag = bxklag
6.22 c                   eval      bbkavd = bxkavd
6.22 c                   eval      bbkkgr = bxkkgr
6.22 c                   eval      bbkkup = bxkkup
6.22 c                   eval      bbkkop = bxkkop
6.22 c                   eval      bbkavd = bxkavd
6.22 c                   eval      bbktas = bxktas
6.22 c                   eval      bbkcan = bxkcan
6.22 c                   eval      bbkbse = bxkbse
6.22 c                   eval      bbeksh = bxeksh
6.22 c                   eval      bbhhor = bxhhor
6.22  *
6.22 c                   time                    bbodat
6.22 c                   time                    bbotim
6.22 c                   eval      bbousr = l_user
6.22 c                   time                    bbedat
6.22 c                   time                    bbetim
6.22 c                   eval      bbeusr = l_user
6.22  *
6.22 c                   write     busriur
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * I1WIN Velge andre systemer med brukerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xi1win        begsr
      *
     c                   eval      i1sv02 = 0
5.33 c                   eval      i1sv03 = 0
5.33 c                   eval      i1sv04 = 0
      *
     c     i1tagb        tag
      *
     c                   exfmt     i1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      *  Kaller opp SUB-program for Ordre/fakturering
      *
     c                   if        i1sv02 = 1
     c                   move      'OFAK'        w_parm
     c                   call      'AA060C'
     c                   parm                    w_parm
     c                   parm                    c2sbib
     c                   parm                    c2sfir
     c                   parm                    c1user
     c                   goto      i1tag1
     c                   endif
      *
      *  Kaller opp SUB-program for Lager/innkjøp
      *
     c                   if        i1sv03 = 1
     c                   move      'LAGR'        w_parm
     c                   call      'AA060C'
     c                   parm                    w_parm
     c                   parm                    c2sbib
     c                   parm                    c2sfir
     c                   parm                    c1user
     c                   goto      i1tag1
     c                   endif
      *
      *  Kaller opp SUB-program for Butikk
      *
     c                   if        i1sv04 = 1
     c                   move      'BUTI'        w_parm
     c                   call      'AA060C'
     c                   parm                    w_parm
     c                   parm                    c2sbib
     c                   parm                    c2sfir
     c                   parm                    c1user
     c                   goto      i1tag1
     c                   endif
      *
     c     i1tag1        tag
      *
      * Varsle om
      * biblioteksliste finnes ikke
      *
     c                   if        w_parm = 'FEIL'                                       .....
     c                   move      *on           *in31                                       .
     c                   goto      i1tagb                                                    .
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c                   read      ausrl4
     c                   when      w_seqe = 'R'
     c                   read      ausrl3
     c                   when      w_seqe = 'B'
     c                   read      ausrl2
     c                   other
     c                   read      ausrl1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c                   eval      ausrl4_grup = abgrup
     c                   when      w_seqe = 'R'
     c                   eval      ausrl3_rlib = abrlib
     c                   when      w_seqe = 'B'
     c                   eval      ausrl2_navn = abnavn
     c                   other
     c                   eval      ausrl1_user = abuser
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1user = abuser
     c                   eval      b1navn = abnavn
     c                   eval      b1levl = ablevl
     c                   eval      b1bfil = abbfil
     c                   eval      b1bfir = abbfir
     c                   eval      b1meny = abmeny
     c                   eval      b1rlib = abrlib
     c                   eval      b1bavd = abbavd
      * Hent status på brukeren
     c                   eval      b1stat = *blanks
     c                   call      'AX023C'
     c                   parm                    b1user
     c                   parm                    b1stat
5.40  *
5.40  * F22 velger mellom å vise :
5.40  *   - referansebibliotek
5.40  *   - gruppe (for bl.annet printerstyring)
5.40  *
5.40 c                   if        *in40  = *on
5.40 c                   eval      b1rlib = abgrup
5.40 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c     ausrl4_key    setgt     ausrl4
5.40 c                   readp     ausrl4
     c                   when      w_seqe = 'R'
     c     ausrl3_key    setgt     ausrl3
     c                   readp     ausrl3
     c                   when      w_seqe = 'B'
     c     ausrl2_key    setgt     ausrl2
     c                   readp     ausrl2
     c                   other
     c     ausrl1_key    setgt     ausrl1
     c                   readp     ausrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c                   readp     ausrl4
     c                   when      w_seqe = 'R'
     c                   readp     ausrl3
     c                   when      w_seqe = 'B'
     c                   readp     ausrl2
     c                   other
     c                   readp     ausrl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c                   eval      ausrl4_grup = abgrup
     c                   when      w_seqe = 'R'
     c                   eval      ausrl3_rlib = abrlib
     c                   when      w_seqe = 'B'
     c                   eval      ausrl2_navn = abnavn
     c                   other
     c                   eval      ausrl1_user = abuser
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
5.40 c                   when      w_seqe = 'G'
5.40 c     ausrl4_key    setll     ausrl4
     c                   when      w_seqe = 'R'
     c     ausrl3_key    setll     ausrl3
     c                   when      w_seqe = 'B'
     c     ausrl2_key    setll     ausrl2
     c                   other
     c     ausrl1_key    setll     ausrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for behandling av spørring
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     spØrring      begsr
6.21  *
6.21  * Valg av filgruppe / firma
6.21  *
6.21 c                   if        w_afld = 'C2SFIL'
6.21 c                   eval      c2sfil = *blank
6.21 c                   eval      c2sbib = *blank
6.21 c                   call      'AA501R'
6.21 c                   parm                    w_fgrp
6.21 c                   parm                    w_bibl
6.21 c                   call      'AA502C'
6.21 c                   parm                    w_bibl
6.21 c                   parm                    w_sfir
6.21 c                   parm                    w_fnav
6.21  *
6.21  * Oppdater med valg
6.21  *
6.21 c                   eval      c2sfil = w_fgrp
6.21 c                   eval      c2sbib = w_bibl
6.21 c                   eval      c2sfir = w_sfir
6.21 c                   eval      c2fnav = w_fnav
6.21 c                   eval      absfil = w_fgrp
6.21 c                   eval      absbib = w_bibl
6.21 c                   eval      absfir = w_sfir
6.21 c                   eval      abfnav = w_fnav
6.21  *
6.21 c                   goto      end_spØrr
6.21 c                   endif
6.21  *
6.21 c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
5.31  *
5.31  * Parametre
5.31  *
5.31 c     *entry        plist
5.31 c                   parm                    p_milj
5.31  *
5.31  * Legg inn miljø fra parameter
5.31  *
5.31 c                   eval      c2milj = p_milj
      *
      * Key for posisjonering på bruker
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for posisjonering på navn
      *
     c     ausrl2_key    klist
     c                   kfld                    ausrl2_navn
      *
      * Key for posisjonering på referansebibliotek
      *
     c     ausrl3_key    klist
     c                   kfld                    ausrl3_rlib
5.40  *
5.40  * Key for posisjonering på referansebibliotek
5.40  *
5.40 c     ausrl4_key    klist
5.40 c                   kfld                    ausrl4_grup
      *
      * Key for les på bruker
      *
     c     ausrlr_key    klist
     c                   kfld                    ausrlr_user
      *
      * Key for oppdatering av bruker
      *
     c     ausrlu_key    klist
     c                   kfld                    ausrlu_user
      *
      * Key for bibliotekslister les
      *
     c     alibl1_key    klist
     c                   kfld                    alibl1_user
      *
      * Key for bibliotekslister update
      *
     c     aliblu_key    klist
     c                   kfld                    aliblu_user
     c                   kfld                    aliblu_plas
6.22  *
6.22  * Key for posisjonering på bruker
6.22  *
6.22 c     fusrlu_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fusrlu_user
6.22  *
6.22  * Key for posisjonering på bruker
6.22  *
6.22 c     lusrlu_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    lusrlu_user
6.22  *
6.22  * Key for posisjonering på bruker
6.22  *
6.22 c     busriu_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    busriu_user
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      *in22 = *on
6.22 c                   eval      w_firm = l_firm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ausrl1_user = *blank
     c     ausrl1_key    setll     ausrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
