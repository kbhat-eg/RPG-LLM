     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB002R
      * Beskrivelse . . : Leser linjer fra bovf til kommaseparert til
      *                   (Business, kundeinfo)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.20  *       040414 nho  Firma ble ikke flyttet til nøkkel
6.21  * V6.20 141014 nho  Test på firma
6.22  *       110319 nho  Flere linjer er fjernet, hentet fra I12
6.23  *       050219 ask  Lagt inn mailadresse på kunden
7.00  *       250619 nho  Henter inn firm_end
8.00  *       041021 sst  Fjerne postnummer fra poststed
8.01  *       181021 sst  Fjerne postnummer fra poststed Hvis ikke switch
8.01  *                   ØKONOMI_EKSTERNT_POSTNR_POSTSTED er satt
8.02  *       220422 mst  InvoCust og FactNO skal sendes blank, hvis de er 0 i NextStep
8.03  *       150125 mst  Mangler en " før mailadresse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fbovfl1    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
      *
      *
     frv02pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
8.01 d l_filg                931    933
     d l_firm                944    946  0
      *
     d w_kund          s                   like(rkkund)
     d w_biln          s                   like(bfbiln)
     d w_firm          s                   like(bffirm)
     d rkunl1_kund     s                   like(rkkund)
     d tell            s              1  0
     d w_rkfaku        s              8
     d w_rkfacn        s              8
      *
8.01  *
8.01  * Eksternt program
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_post          s              1    inz(*off)
      * ______________________________________________________________
      *
6.20 c                   eval      w_firm = l_firm
      *
      * Leser post
     c     nyles         tag
      *
     c                   read      bovfl1                                 60
      *
610  c                   if        *in60 = '1'
610  c                   goto      slutt
610  c                   endif
      *
6.21  *
6.21 c                   if        l_firm <> bffirm
6.21 c                   goto      nyles
6.21 c                   endif
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
      *
     c                   eval      rvdata = *blank
     c                   write     rv02pfr
      *
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(1)')
     c                   write     rv02pfr
      *
     c                   eval      rvdata = %trim('@ActoR(=CustNO,') +
     c                                           ('Nm,Shrt,Ad1,Ad2,') +
     c                                           ('PNo,PArea,Phone,') +
     c                                           ('PrivPh,Fax,') +
     c                                           ('InvoCust,FactNo)') +
8.03 c*                                          ('InvoCust,FactNo)') +
8.03 c                                           ('InvoCust,FactNo') +
6.23 c                                           ('Mailad)')
     c                   write     rv02pfr
      *
      * Ta vare på første bilagsnr
     c                   eval      w_biln = bfbiln
      *
     c                   endif
      *
     c                   exsr      finnadr
     c                   goto      nyles
      *
      *
     c     slutt         tag
      *
      * Avslutnings tekster
      *
     c                   eval      rvdata = *blank
     c                   write     rv02pfr
      *
      *
     c                   eval      rvdata = *blank
7.00 c                   read      rb10l1                                 90
7.00 c**                 eval      rvdata = %trim('@FIRM_END(22642)')
7.00 c                   eval      rvdata = %trim('@FIRM_END(') +
7.00 c                                      %trim(rafirb) +
7.00 c                                      %trim(')')
     c                   write     rv02pfr
      *
     c                   eval      *inlr = *on
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner navn, adresse osv. / skriv post
      *
     c     finnadr       begsr
      *
     c                   if        bfckto <> 0
     c                   eval      w_kund = bfckto
     c                   else
     c                   eval      w_kund = bfdkto
     c                   endif
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
8.00 c                   if        %found(rkunl1)
8.01 c                             and u_post = *off
8.00 c                             and %char(rkponr) = %subst(rksted:1:4)
8.00 c                   eval      rksted = %subst(rksted:6:25)
8.00 c                   endif
8.02 c*
8.02 c                   if        rkfaku = 0
8.02 c                   movel     *blanks       w_rkfaku
8.02 c                   else
8.02 c                   movel     rkfaku        w_rkfaku
8.02 c                   endif
8.02 c                   if        rkfacn = 0
8.02 c                   movel     *blanks       w_rkfacn
8.02 c                   else
8.02 c                   movel     rkfacn        w_rkfacn
8.02 c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('"') +
      * Kundenr
     c                                     %char(w_kund)     + '" "' +
      * Navn
     c                                     %trim(rknavn)     + '" "' +
      * Søkenavn
     c                                     %trim(rkalfa)    + '" "' +
      * Adresse 1
     c                                     %trim(rkgate)     + '" "' +
      * Adresse 2
     c                                     %trim(rkadr2)     + '" "' +
      * Postnummer
     c                                     %char(rkponr)     + '" "' +
      * Poststed
     c                                     %trim(rksted)     + '" "' +
      * Telefon
     c                                     %trim(rkmobn)     + '" "' +
      * Priv. telefon
     c                                     %trim(rktlfn)     + '" "' +
      * Fax
     c                                     %trim(rktlfx)     + '" "' +
      * Fakturaknr.
8.02 c*                                    %char(rkfaku)     + '" "' +
8.02 c                                     %trim(w_rkfaku)     + '" "' +
      * Factoringnr.
8.02 c*                                    %char(rkfacn)     + '"'
6.23 c                                     %trim(w_rkfacn)     + '" "' +
6.23  * Mailadresse.
6.23 c                                     %trim(rkemal)     + '"'
      *
     c                   write     rv02pfr
     c                   endif
      *
     c                   endsr
8.01  *
8.01 c     *inzsr        begsr
8.01  *
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
8.01  * Skal postnummer fjernes fra Poststed
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'ØKONOMI_EKSTERNT_POSTNR_POSTSTED':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_post = *on;
8.01   endif;
8.01  *
8.01  *
8.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
