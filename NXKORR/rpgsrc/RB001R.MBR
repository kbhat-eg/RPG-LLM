     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB001R
      * Beskrivelse . . : Leser linjer fra FOVF til kommaseparert til
      *                   (Business)
9.01  * Spesialversjon. : Skal kjøre denne versjonen av summering (6.20
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
      *
6.20  * V6.20 130213 nho  Sjekk av kid
6.21  * V6.20 051113 nho  Avstand mellom felter
6.22  * V6.20 141014 nho  Test på firma
6.23  * V6.20 141014 nho  Blanker kid etter hver write
6.24  * V6.20 120515 nho  Val.dato skal være lik bilagsdato
6.nu  * V6.30 230613 bof  Gjennomgått mtp. nummerutvidelse
6.nu  * R6.20     280515  Utvidelse av kort kid                          NHO  *
6.30  * V6.30 131218 nho  Legger ut avdeling
7.00  * V7.00 260419 nho  Henter inn konvertering av bilagskode
7.01  * V7.00 240619 nho  Henter inn firm_begin
6.30  * V7.00 190820 sst  Hentet inn igjen utlegg av avdeling fra 630 versjonen
7.03  *       120521 mst  Henter avvikende momskode, hvis den finnes
7.04  *       210423 mst  Fjerne ledende blanke på momskode
7.05  *       050923 mst  Blanker momskode mellom hver post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffovupf    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
6.20 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.20 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
7.03 frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
      *
      *
     frv01pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(fffdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d tell            s              1  0
7.00 d xxbilk          s              5  0
     d f1momd          s                   like(ffdmom)
     d fxmomd          s              2
     d f1momc          s                   like(ffcmom)
     d fxmomc          s              2
6.30 d f1avde          s                   like(ffcavd)
     d zfbelp          s             12  4
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
      *
6.20  * Variabler for nøkler
6.20  *
6.20 d votyl1_otyp     s                   like(vaotyp)
6.20 d aforl1_ruti     s                   like(afruti)
7.00 d rb10l1_gkod     s                   like(raakod)
      *
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
6.20 d w_kidd          s                   like(sokidd)
6.20 d w_kid2          s                   like(sokidd)
6.20 d t1kkid          s             23
      *
6.21 d b_kkid          s              1    inz(*off)
7.03 d rb00l1_gkod     s                   like(r00kod)
7.03 d w_type          s             10
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
7.00  * Key for posisjonering på bilagskoder
7.00  *
7.00 c     rb10l1_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rb10l1_gkod
7.00  *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
6.20  * Key for les av ordretype
6.20  *
6.20 c     votyl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for les av rutine-register
6.20  *
6.20 c     aforl1_key    klist
6.20 c                   kfld                    aforl1_ruti
6.20  *
7.03  * Key for posisjonering på koder (moms)
7.03  *
7.03 c     rb00l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    w_type
7.03 c                   kfld                    rb00l1_gkod
6.22  *
6.22 c                   if        l_firm <> fffirm
6.22 c                   goto      slutt
6.22 c                   endif
      *
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
7.01 c                   read      rb10l1                                 90
     c                   eval      rvdata = *blank
7.01 c****               eval      rvdata = %trim('@FIRM_BEGIN(40908)')
7.01 c                   eval      rvdata = %trim('@FIRM_BEGIN(') +
7.01 c                                      %trim(rafirb) +
7.01 c                                      %trim(')')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(3)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaBnd(Descr,ValDT)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
      *
      *
      * Dagense dato
     c                   time                    valdat
     c                   movel     valdat        d_fdati
     c                   movel     d_fa          nval08
     c                   movel     d_fn          nval04
     c                   move      d_fd          nval04
     c                   move      nval04        nval08
6.21 c                   eval      rvdata = %trim('"KREDITSYSTEM" "') +
     c                                      %char(nval08) +
     c                                      %trim('"')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaVo(VoNo,VoDt,') +
     c                                           ('ValDt,Votp,Txt,') +
     c                                           ('DbAcNo,DbTxcd,CrAcNo,') +
     c                                           ('CrTxCd,InvoNo,Am,R1,') +
     c                                           ('DueDt,Cid,R2)')
     c                   write     rv01pfr
      *
     c                   endif
      *
      ***********************************************************
     c
     c* Blanker felt
     c                   eval      d_dati  = *blanks
      *
     c                   move      ffbdat        d_dati
      *
      * Forfallsdato
     c                   movel     fffdat        f_fdati
     c                   movel     f_fa          nfor08
     c                   movel     f_fn          nfor04
     c                   move      f_fd          nfor04
     c                   move      nfor04        nfor08
      *
      * Bilagsdato
     c                   movel     ffbdat        b_fdati
     c                   movel     b_fa          nbil08
     c                   movel     b_fn          nbil04
     c                   move      b_fd          nbil04
     c                   move      nbil04        nbil08
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   z-add     ffbelp        zfbelp
      * Legger ut detaljer fra FOVF
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
7.05 c                   eval      fxmomd = '  '
7.05 c                   eval      fxmomc = '  '
      *
     c                   exsr      control_init
      *
7.00  * Sjekker om bilagstype skal konverteres
7.00 c                   eval      xxbilk = ffbilk
7.00 c                   eval      rb10l1_gkod = ffbilk
7.00 c     rb10l1_key    chain     rb10l1
7.00 c                   if        %found
7.00 c                   eval      xxbilk = rankod
7.00 c                   endif
7.00  *
      *
     c                   if        ffdkto <> *zeros and
     c                             ffdkto >=  1000 and
     c                             ffdkto <=  9999
     c                   eval      f1momd = ffdmom
7.03 c                   move      ffdmom        fxmomd
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momd        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c*                  movel     r00nko        f1momd
7.03 c                   move      r00nko        fxmomd
7.03 c                   endif
     c                   endif
      *
     c                   if        ffckto <> *zeros and
     c                             ffckto >=  1000 and
     c                             ffckto <=  9999
     c                   eval      f1momc = ffcmom
7.03 c                   move      ffcmom        fxmomc
     c                   eval      f1momd = '0'
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momc        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c*                  movel     r00nko        f1momc
7.03 c                   move      r00nko        f1momc
7.03 c                   move      r00nko        fxmomc
7.03 c                   endif
     c                   endif
      *
6.30  * Legger ut avdeling
6.30 c                   eval      f1avde = *zeros
6.30  *
6.30 c                   if        ffcavd <> *zeros
6.30 c                   eval      f1avde = ffcavd
6.30 c                   endif
6.30  *
6.30 c                   if        ffdavd <> *zeros
6.30 c                   eval      f1avde = ffdavd
6.30 c                   endif
6.30  *
     c**********
     c                   eval      rvdata = *blank
      * Start
     c                   eval      rvdata = %trim('"') +
      * Bilagsnr.
     c                                     %char(ffbiln)     + '" "' +
      * Bil.dato
     c                                     %char(nbil08)     + '" "' +
      * Val.dat fra heading
6.24 c*                                    %char(nval08)    + '" "' +
6.24 c                                     %char(nbil08)     + '" "' +
      * Bil.type
7.00 c**                                   %char(ffbilk)     + '" "' +
7.00 c                                     %char(xxbilk)     + '" "' +
      * Bil.tekst
     c                                     %trim(fftext)     + '" "' +
      * Deb.konto
     c                                     %char(ffdkto)     + '" "' +
      * Deb.mva kode
     c*                                    %trim(f1momd)     + '" "' +
     c*                                    %trim(fxmomd)     + '"  "' +
7.04 c                                     %trim(fxmomd)     + '" "' +
      *
      * Kredkonto
     c                                     %char(ffckto)     + '" "' +
      * Kredit mva kode
     c*                                    %trim(f1momc)     + '" "' +
     c*                                    %trim(fxmomc)     + '"  "' +
7.04 c                                     %trim(fxmomc)     + '" "' +
      * Fakturanr (Bilagsnummer)
     c                                     %char(ffbiln)     + '" "' +
      *
      * Beløp på linje
     c                                     %char(zfbelp)     + '" "' +
      * Avdeling?? Kan prosjekt legges her?
6.30 c**                                   %trim('" "') +
6.30 c                                     %char(f1avde)     + '" "' +
      * Forfallsdato
     c                                     %char(nfor08)    + '" "' +
      * Kid nr ???????
     c                                     %trim(t1kkid)    + '" "' +
      * Prosjekt
     c                                     %trim(ffproj)    + '"'
     c                   write     rv01pfr
6.23  *
6.23 c                   eval      t1kkid = *blanks
      *
6.22 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = fffirm
     c                   eval      w_binr = ffbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
6.20  *
6.20  * Henter rutine fra ordretype
6.20  *
6.20 c                   eval      b_kkid = *off
6.20 c                   eval      votyl1_otyp = sootyp
6.20 c     votyl1_key    chain     votyl1
6.20 c                   if        %found(votyl1)   and
6.20 c                             vaorut <> *blank
6.20 c                   eval      aforl1_ruti = vaorut
6.20 c     aforl1_key    chain     aforl1
6.20 c                   if        %found(aforl1) and
6.20 c                             afkidk = 1
6.20 c                   movel     sokidd        t1kkid
6.20 c                   else
6.20 c                   if        afkidk = 2
6.20 c                   movel     sokidr        t1kkid
6.20 c                   else
6.20 c                   if        afkidk = 3
6.20 c                   movel     sokidr        w_kidr
6.20 c                   movel     sokidr        w_kidr
6.20 c                   call      'AK710R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    w_kidr
6.20 c                   parm                    w_kidd
6.20 c                   parm                    w_kid2
6.20 c                   movel     w_kid2        t1kkid
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
