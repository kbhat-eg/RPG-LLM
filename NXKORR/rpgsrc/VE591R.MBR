     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE591R
      * Beskrivelse . . : Vise varer med utgått nobbdato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050925 bsa  Nytt program
8.01  *800    031125 bsa  Legger ut opprinnelig utgår dato
8.02  *800    241125 bsa  Logger hvem som har fjernet utgårdato
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
      *
     fve591d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variabler
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_seqe          s              1
      *
     d b_enter         s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
     d w_udat          s               d   datfmt(*iso)
     d w_ndat          s               d   datfmt(*iso)
8.02 d w_nuda          s                   like(vvnuda)
      *
     d sqlquery        s           4096
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(17)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
8.02 C+             commit= *none
     C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2pvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      b_enter = *on
      *
      * Validering av input-felter
      *
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      b_open_sok = *on
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2pvar <> *blank
     c*                  eval      w_seqe = 'B'
     c*                  eval      vvarlr_pvar = b2pvar
     c*    vvarl1_key    setll     vvepl1
     c*                  goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pvar = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Legg inn ny statistikkenhet
      *
     c                   if        b1valg = 6
     c                   eval      c2vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vvarl1_vare = c2vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c2tek1 = vvtek1
     c                   endif
      *
8.01 c     *dmy          move      vvnuda        c2nuda
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkd
     c                   endif
      *
      * Sjekk gyldig dato
      *
     c                   if        c2nuda <> 0
     c     *dmy          test(d)                 c2nuda                 31
     c                   if        *in31  = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Oppdater med ny nobb utgårdato
      *
     c                   eval      vvarlu_vare = c2vare
     c     vvarlu_key    chain     vvarlu
     c                   if        %found
8.02 c                   eval      w_nuda = vvnuda
     c                   if        c2nuda <> 0
     c     *dmy          move      c2nuda        vvnuda
     c                   else
     c                   eval      vvnuda = *loval
     c                   endif
     c                   time                    vvedat
     c                   time                    vvetim
     c                   eval      vveusr = l_user
     c                   update    vvarlur
      * Skriver til logg om hvem som har endret utgårdato
8.02        exec sql
8.02        insert into vutdst
8.02          (vudfir,vudvar,vudtek,vududa,vudnud,
8.02           vudoda,vudoti,vudous)
8.02        values(:w_firm,:c2vare,:vvtek1,:w_nuda,:vvnuda,
8.02               :vvedat,:vvetim,:l_user);
      *
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   if        b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvfirm,
     c+                         vvvare,
     c+                         vvtek1
     c+                  from   vvarpf
     c+                  where  vvfirm = :w_firm and
     c+                         vvnuda <> :w_ndat and
     c+                         vvnuda < :w_udat
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvfirm,
     c+                        :vvvare,
     c+                        :vvtek1
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        b2pvar <> *blanks and
     c                             vvvare <  b2pvar
     c                   iter
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      * Sørg for å ha alle verdier tigjengelig
     c                   eval      vvarl1_vare = vvvare
     c     vvarl1_key    chain     vvarl1
      *
      *
     c                   eval      b1vare = vvvare
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1tek2 = vvtek2
     c     *dmy          move      vvnuda        b1nuda
     c*                  if        vvnuda <> *loval and
     c*                            vvnuda <= w_udat
     c*                  eval      b1tek1 = %subst(vvtek1:1:22) +
     c*                                     ' **NOBB.UTG**'
     c*                  endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   exsr      forny
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2PVAR'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2pvar
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma og pvar fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   time                    w_udat
     c                   eval      w_ndat = *loval
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      b_open_sok = *on
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
