      /TITLE  Spesial program - lageroppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LX717R
      *  Beskrivelse . . : Oppdatering av informasjon på lagerregister
      *                    fra ekstern fil. S -> L
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. : Excel-fil med varenr + lager
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           070922  Nytt program                             bof
8.01  *           221122  Utvidet med endringsfelter               bof
8.02  *K0000      210824  Skriver kvitteringsliste til IFS         bsa
      *
      *
      *
     flwexpf    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
8.02 flxl6st    uf a e           k disk    rename(lxl6st:lxl6str)
      *
      *  Datastruktur - excel-fil
      *
     d                 ds
     d d_ex                         512
     d  d_fill                        2    overlay(d_ex:1)
     d  d_lage                        2    overlay(d_ex:6)
     d    d_lagen                     2  0 overlay(d_lage:1)
     d  d_vare                        8    overlay(d_ex:28)
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      *  Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_posi          s              2  0
     d len             s              3  0
     d st              s              3  0
     d w_2x            s              2
7.03 d w_8x            s              8
     d w_stat          s              1
8.02 d w_numm          s              8  0
8.02 d w_kode          s              1
8.02 d w_fell          s              6
8.02 d w_type          s              2
8.02 d w_fast          s             10
8.02 d w_besk          s             50
8.02 d w_tiltyp        s              4
8.02 d w_sql_stm       s          10000
8.02 d w_tildok        s             50
8.02 d w_tilmap        s             20
8.02 d w_fide          s             20
8.02 d w_fnut          s             46
      *
8.02 d p_excel_url     s            100
      *
8.02 d u_excl          s              1    inz(*off)
      *
     d b_ok            s              1    inz(*off)
      *
     d c_null          s             15    inz('000000000000000')
     d digits          c                   ' 0123456789'
8.02  *
8.02  * Eksternt program
8.02  *
8.02   dcl-s co402_verdi1  char(1);
8.02   dcl-s co402_verdi2  char(2048);
8.02   DCL-PR CO402R EXTPGM('CO402R');
8.02     p_filgrp            char(3)     const;
8.02     p_firm              packed(3:0) const;
8.02     p_lager             packed(2:0) const;
8.02     p_nokkel            char(50)    const;
8.02     p_verdi1            char(1);
8.02     p_verdi2            char(2048);
8.02   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
     c                   exsr      utled_ds
      *
      *  Sjekk at ikke heading-rad er lest inn
      *
     c                   if        b_ok = *on
     c                   exsr      behand
     c                   endif
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.02 c                   if        u_excl = *on
8.02 c                   eval      w_fide = 'VT_'
8.02 c                   eval      w_fnut = *blanks
8.02 c                   call      'LX718R'
8.02 c                   parm                    w_fide
8.02 c                   parm                    w_fnut
      *
8.02 c                   eval      w_tildok = %trim(w_fnut)
8.02 c                   eval      w_tilmap = 'lister'
8.02 c                   eval      w_tiltyp = 'csv'
8.02 c                   eval      w_sql_stm = 'SELECT +
8.02 c                             lx6var AS "Varenummer", +
8.02 c                             lx6lag AS "Lager", +
8.02 c                             lx6bes AS "Beskrivelse"  FROM lxl6st +
8.02 c                             WHERE lx6ide ='
8.02 c                   eval      w_sql_stm = %trim(w_sql_stm) + %char(w_numm)
8.02 c                   call      'ASPTOCSV'
8.02 c                   parm                    w_sql_stm
8.02 c                   parm                    w_tilmap
8.02 c                   parm                    w_tildok
8.02 c                   parm                    w_tiltyp
8.02  *
8.02 c                   endif
8.02  *
8.02 c/exec sql
8.02 c+ DELETE FROM LXL6ST WHERE LX6IDE = :w_numm
8.02 c/end-exec
8.02   exec sql commit;
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
      *
8.02 c                   eval      w_besk = *blanks
     c     ' ':'0'       xlate     d_vare        d_vare
      *
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1r                            92
     c                   if        %found(vvarl1)
      *
     c                   clear                   vlaglur
     c                   eval      vlaglu_vare = vvvare
     c                   eval      vlaglu_lage = d_lagen
     c     vlaglu_key    chain     vlaglur
      *
     c                   if        %found(vlaglu) and
     c                             vlvtyp = 'S'

     c                   eval      vlvtyp = 'L'
      *
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
8.01 c                   time                    vlfeda
8.01 c                   time                    vlfeti
8.01 c                   eval      vlfeus = l_user
     c                   update    vlaglur
     c                   else
     c                   if        %found(vlaglu)
     c                   unlock    vlaglu
8.02 c                   else
8.02 c                   eval      w_besk = 'Fant ikke varen på angitt lager'
     c                   endif
     c                   endif
      *
8.02 c                   else
8.02 c                   eval      w_besk = 'Fant ikke varen'
     c                   endif
8.02  *
8.02  * Skriv kvitteringsliste
8.02  *
8.02 c                   eval      lx6ide = w_numm
8.02 c                   eval      lx6var = d_vare
8.02 c                   eval      lx6lag = d_lagen
8.02 c                   eval      lx6bes = w_besk
8.02 c                   write     lxl6str
      *
     c     behand_ut     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c                   eval      b_ok = *off
     c     '"':' '       xlate     exclin        exclin
      *
      * vare    A
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_vare) <>0
     c                   leavesr
     c                   endif
7.03 c                   eval      len    = %len(%trim(d_vare))
7.03 c                   eval      st = 9 - len
7.03 c                   eval      w_8x  = *blank
7.03 c                   eval      %subst(w_8x:st:len) = %subst(d_vare:1:len)
7.03 c                   eval      d_vare = w_8x
7.03 c                   eval      d_vare = %xlate(' ':'0':d_vare)

      *
      * lage         B
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
7.02 c                   if        w_posi > 0
     c                   eval      d_lage = %trim(%subst(exclin:1:w_posi-1))
7.02 c                   else
7.02 c                   eval      d_lage = %trim(%subst(exclin:1:2))
7.02 c                   endif
     c                   if        d_lage = *blank
     c                   leavesr
     c                   endif
     c                   eval      len    = %len(%trim(d_lage))
     c                   eval      st = 3 - len
     c                   eval      w_2x  = *blank
     c                   eval      %subst(w_2x:st:len) = %subst(d_lage:1:len)
     c                   eval      d_lage = w_2x
     c                   if        %check(digits : d_lage) <>0
     c                   leavesr
     c                   endif
     c                   eval      d_lage = %xlate(' ':'0':d_lage)

     c                   eval      b_ok = *on
      *
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
8.02  *
8.02  * Henter inn løpenummer teller
8.02  *
8.02 c                   eval      w_firm = 0
8.02 c                   eval      w_numm = 0
8.02 c                   eval      w_kode = '0'
8.02 c                   eval      w_fell = 'NXTKLI'
8.02 c                   eval      w_type = *blank
8.02 c                   eval      w_fast = *blank
8.02 c                   eval      w_numm = *zero
8.02  *
8.02 c                   call      'AS100R'
8.02 c                   parm                    w_kode
8.02 c                   parm                    w_firm
8.02 c                   parm                    w_fell
8.02 c                   parm                    w_type
8.02 c                   parm                    w_fast
8.02 c                   parm                    w_numm
8.02  *
8.02  * OBS!!  Felles for alle filgrupper og firma
8.02  *
8.02   CallP CO402R('   ':0:0:'NX_EXCEL_URL':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_excl = *on;
8.02   endif;
8.02  *
     c                   endsr

