     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RX740R
      * Beskrivelse . . : Leser RKUNPF - flyttes til RKU2PF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231024 bsa  Nytt program
8.01  *K0000  151124 bsa  Lagt inn parameter som sier at de som har
8.01  *K0000              fakturatype 24/28 skal ha kode 1 i RK2FAS.
8.02  *K0000  071025 bsa  Tar ikke hensyn til firmakode
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frku2lu    uf a e           k disk    rename(rku2pfr:rku2lur)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av variabler i nøkler
      *
     d rku2lu_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
      *
8.01 d p_fast          s              1
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      *-------------------------------------------------------------------*
      * Main program                                                      *
      *-------------------------------------------------------------------*
      *
     c                   read      rkunlr
      *
     c                   dow       not %eof
8.02 c                   eval      w_firm = rkfirm
     c                   eval      rku2lu_kund = rkkund
     c     rku2lu_key    chain     rku2lu
     c                   if        not %found
     c                   clear                   rku2lur
     c                   exsr      crt_rku2
8.01 c                   else
8.01 c                   exsr      upd_rku2
     c                   endif
      *
     c                   read      rkunlr
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette sideregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_rku2      begsr
      *
     c                   eval      rk2fir = rkfirm
     c                   eval      rk2kun = rkkund
      *
8.01 c                   if        p_fast = '1'
8.01 c                   if        rkfaty = 24 or
8.01 c                             rkfaty = 28
8.01 c                   eval      rk2fas = 1
8.01 c                   endif
8.01 c                   endif
      *
     c                   eval      rk2eus = l_user
     c                   time                    rk2eti
     c                   time                    rk2eda
     c                   eval      rk2sig = l_user
     c                   time                    rk2tim
     c                   time                    rk2dat
     c                   write     rku2lur
      *
     c                   endsr
      *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Subrutine for å oppdatere sideregister
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     upd_rku2      begsr
8.01  *
8.01 c                   if        p_fast = '1'
8.01 c                   if        rkfaty = 24 or
8.01 c                             rkfaty = 28
8.01 c                   eval      rk2fas = 1
8.01 c                   eval      rk2eus = l_user
8.01 c                   time                    rk2eti
8.01 c                   time                    rk2eda
8.01 c                   endif
8.01 c                   endif
8.01  *
8.01 c                   update    rku2lur
8.01  *
8.01 c                   endsr
8.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
8.01 c     *entry        plist
8.01 c                   parm                    p_fast
      *
      * Key for oppdat. av kontaktperson-register
      *
     c     rku2lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rku2lu_kund
      *
8.02 c                   eval      w_firm = l_firm
      *
     c                   endsr
