     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIOKO
      * Program . . . . : IG120R
      * Beskrivelse . . : Spørring på feillogg på overføringer fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130415 ASK  Nytt program
8.01  * 800   010725 ask  Lagt inn endring for å håndtere retur på masterdata
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fierrir    if   e           k disk    rename(ierrst:ierrirr)
     fierri1    if   e           k disk    rename(ierrst:ierri1r)
      *
     fig120d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ierri1_odat     s                   like(ieodat)
     d ierri1_otim     s                   like(ieotim)
     d ierrir_uid      s                   like(uid)
      *
      * Definering av variable
      *
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_spge          s              4  0
     d w_srrn          s              4  0
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(iefirm)
     d w_odat          s                   like(ieodat)
     d w_otim          s                   like(ieotim)
     d w_uid           s                   like(uid)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2dato <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   time                    w_odat
     c                   time                    w_otim
     c                   eval      ierri1_odat = w_odat
     c                   eval      ierri1_otim = w_otim
     c     ierri1_key    setll     ierri1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på dato
      *
     c     *dmy          move      b2dato        w_odat
     c     *hms          move      235959        w_otim
     c                   eval      ierri1_odat = w_odat
     c                   eval      ierri1_otim = w_otim
     c     ierri1_key    setll     ierri1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2dato = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Se detaljer på logg post
      *
     c                   if        b1valg = 5
     c                   eval      w_uid  = b1uid
     c                   exsr      xc1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se lang parameterstreng
      *
     c                   if        b1valg = 8
     c                   eval      w_uid  = b1uid
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise loggpost detaljer (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     b1uid         chain     ierrir
      *
     c                   if        %found
     c     *dmy          move      ieodat        c1dato
     c     *hms          move      ieotim        c1time
     c                   eval      c1prog = ieprog
     c                   eval      c1comd = iecomd
     c                   movel     ieparm        c1parm
      *
     c                   exfmt     c1bld
     c                   endif
      *
     c     end_c1bld     endsr
6.10  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise parameterstreng (D1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   movel     ieparm        d1parm
      *
     c                   exfmt     d1bld
      *
6.21  *
     c     end_d1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ierri1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        iefirm <> l_firm
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1uid  = uid
     c     *dmy          move      ieodat        b1dato
     c     *hms          move      ieotim        b1time
     c                   eval      b1fkod = iefkod
     c                   eval      b1prog = %subst(ieprog:14:26)
     c                   eval      b1comd = %subst(iecomd:1:1)
8.01 c                   if        %subst(ieprog:1:10) = 'NEXSTEPHUB'
8.01 c                   eval      b1prog = %subst(iecomd:1:20)
8.01 c                   eval      b1comd = 'M'
8.01 c                   endif
     c                   movel     ieparm        b1parm
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in12 = *off
      *
     c                   endsr
6.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering feillogg
      *
     c     ierri1_key    klist
     C                   kfld                    w_firm
     c                   kfld                    ierri1_odat
     c                   kfld                    ierri1_otim
      *
      * Key for oppslag på feillogg
      *
     c     ierrir_key    klist
     c                   kfld                    ierrir_uid
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_odat
     c                   time                    w_otim
     c                   eval      ierri1_odat = w_odat
     c                   eval      ierri1_otim = w_otim
     c     ierri1_key    setll     ierri1
     c                   exsr      crt_subfile
      *
     c                   endsr
