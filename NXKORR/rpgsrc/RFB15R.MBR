      /TITLE  Tilrettelegger trans fra Ordre/Fakturering
     h decedit(',') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RFA15R
      * Beskrivelse . . : Tilrettelegger trans fra Ordre/Fakturering
      *                   Avvikende periode i følge koderegister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   NB! Dette program er en kopi av RFA15R
      *                   FOVF.. er endret til BOVF..
      *                   Feltnavn i FOVF.. er endret til feltnavn i BOVF..
      *       060104 KOB  Lagt inn overføring av mva-grunnlag
      * V5.30 200404 KOB  Endret beregning av buntsummer
6.10  * V6.10 291009 NHO  Kun innestående firma skal være på overføring
6.20  * V6.20 241110 NHO  Proj er nå alfa
6.21  * 6.20  160410 NHO  Henting av siste buntnr er feil må teste
      *                   på om READP-post = member i key
7.00  *K00  sst 191120  NXS-6236 Utligne reskontropost hvis forskudd.
8.01  *800  ask 280825 Sørger for at ikke negative beløp blir lagt ut i RTRAPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.10 fbovfl1    uf   e           k disk    rename(bovfpfr:bovfl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3pf    if   e           k disk
7.00 frktrld    if   e           k disk    rename(rktrpfr:rktrldr)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o    e           k disk
      *
      * Definering av array's
610  d                uds
610  d l_user                911    920
610  d l_firm                944    946  0
      *
     d per             s              2  0 dim(13)                              Perioder
      *
      * Definering av variable i nøkler
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
      *
     d w_firm          s                   like(bffirm)
     d bovfl1_peri     s                   like(bfperi)
     d rbunlu_bunt     s                   like(rsbunt)
      *
7.00 d rktrld_firm     s                   like(rnfirm)
7.00 d rktrld_kund     s                   like(rnkund)
7.00 d rktrld_bdat     s                   like(rnbdat)
      *
      * Definering av variable
      *
     d w_bunt          s                   like(rtbunt)
     d w_xper          s              8  0
     d w_linj          s              8  0
     d w_arec          s              9  0
     d w_Årm1          s              2  0
     d wpe             s              2  0
     d w_rper          s              2  0
     d w_raar          s              4  0
     d w_paar          s              2  0
      *
     d w_bsum          s                   like(rsbsum)
     d w_psum          s                   like(rspsum)
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
      *
      * Definering av save-variable
      *
     d s_firm          s                   like(bffirm)
     d s_peri          s                   like(bfperi)
      *
      *  Statusopplysninger - avvikende perioder
      *
     iraa3pfr
     i              ra3p01                      per(01)
     i              ra3p02                      per(02)
     i              ra3p03                      per(03)
     i              ra3p04                      per(04)
     i              ra3p05                      per(05)
     i              ra3p06                      per(06)
     i              ra3p07                      per(07)
     i              ra3p08                      per(08)
     i              ra3p09                      per(09)
     i              ra3p10                      per(10)
     i              ra3p11                      per(11)
     i              ra3p12                      per(12)
     i              ra3p13                      per(13)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les transaksjonsfile til e.o.f.
      *
6.10 c                   eval      w_firm = l_firm
     c                   eval      bovfl1_peri = 0
     c     bovfl1_key    setll     bovfl1
     c                   read      bovfl1                                 90
6.10 c                   if        *in90 = *on
6.10 c                   goto      slutt1
6.10 c                   end
6.10  *
6.10 c                   if        bffirm > l_firm
6.10 c                   goto      slutt1
6.10 c                   end
6.10  *
     c                   eval      w_firm = bffirm
     c                   eval      s_firm = bffirm
     c                   eval      s_peri = bfperi
     c                   exsr      nytt_firma
     c                   exsr      ny_periode
      *
6.10 c                   if         *in90 = *off
6.10 c                   delete    bovfl1r
6.10 c                   endif
     c                   dow       *in90 = *off
      *
      * Nytt firma ?
      *
6.10 c**                 if        bffirm <> s_firm
6.10 c**                 exsr      nytt_firma
6.10 c**                 endif
6.10  *
      * Ny periode ?
      *
     c                   if        bffirm <> s_firm or
     c                             bfperi <> s_peri
     c                   exsr      ny_periode
     c                   endif
      *
      * Ta vare på gamle verdier
      *
     c                   eval      w_firm = bffirm
     c                   eval      s_firm = bffirm
     c                   eval      s_peri = bfperi
      *
      * Redigerer regnskapsperiode og år
      *
     c                   movel     bfperi        w_rper
     c                   move      bfperi        w_paar
     c                   movel     20            w_raar
     c                   move      w_paar        w_raar
      *
      * Oppdater RTRAPF
      *
     c                   exsr      opdat_rtra
     c                   eval      w_arec = w_arec + 1
      *
     c                   read      bovfl1                                 90
6.10 c                   if        *in90 = *on
6.10 c                   goto      slutt
6.10 c                   end
6.10  *
6.10  *
6.10 c                   if        bffirm > l_firm
6.10 c                   goto      slutt
6.10 c                   end
6.10  *
      *
      * Dersom brudd,
      * oppdater RBUNPF
      *
     c                   if        bffirm <> s_firm or
     c                             bfperi <> s_peri
     c                   exsr      opdat_rbun
     c                   endif
      *
6.10 c                   if        *in90 = *off
6.10 c                   delete    bovfl1r
6.10 c                   end
     c                   enddo
      *
6.10 c     slutt         tag
      * Siste buntsum,
      * oppdater RBUNPF
      *
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   endif
      *
6.10 c     slutt1        tag
      * Avslutt program
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd ved nytt firmanr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nytt_firma    begsr
      *
      * Hent inn firmaregister
      *
     c     afirl1_key    chain     afirl1                             91
      *
      * Hent inn statuskoder (avvikende perioder)
      *
     c     raa3pf_key    chain     raa3pf                             91         Avvikende perioder
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd periode / år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_periode    begsr
      *
      * Brudd periode/år
      * Finn neste ledige buntnr
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999                          Type
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 91
      *
     c                   if        *in91 = *on
     c                   eval      w_bunt = 1
     c                   else
6.21 c                   if        w_memb <> rsmemb
6.21 c                   eval      w_bunt = 1
6.21 c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
6.21 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RTRAPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rtra    begsr
      *
8.01 c                   if        bfbelp < 0
8.01 c                   eval      bfbelp = bfbelp * -1
8.01 c                   endif
8.01  *
     c                   eval      w_bsum = w_bsum + bfbelp
      *
      * Beregn buntsummer
      *
     c                   if        bfdkto <> 0
     c                   eval      w_psum = w_psum + bfbelp
     c                   eval      w_bsum = w_bsum + bfbelp
     c                   eval      w_pdeb = w_pdeb + bfbelp
     c                   endif
      *
     c                   if        bfckto <> 0
     c                   eval      w_psum = w_psum + bfbelp
     c                   eval      w_pkre = w_pkre + bfbelp
     c                   endif
      *
     c                   eval      w_linj = w_linj + 1                          Linjenr.
      *
     c                   eval      rtreid = 'T'                                 Rec.-id
     c                   eval      rtfirm = bffirm                              Firma
     c                   eval      rtbunt = w_bunt                              Bunt-nr.
     c                   eval      rtlinj = w_linj                              Linjenr.
     c                   eval      rtstat = *blank                              Buntstatus
     c                   eval      rtraar = w_raar                              Regn.år
     c                   eval      rtrper = w_rper                              Regn.år
     c                   eval      rtbiln = bfbiln                              Bilag-nr.
     c                   eval      rtbill = bfbill                              Bilag-nr.
     c                   eval      rtdavd = bfdavd                              Debet-avd.
     c                   eval      rtdkto = bfdkto                              Debet-kto.
     c                   eval      rtdsps = bfdsps                              Debet-spes.
     c                   eval      rtdmva = bfdmom                              Debet-mva.
     c                   eval      rtbelØ = bfbelp                              Beløp
     c                   eval      rtgmva = bfmvag                              Beløp
     c                   eval      rtcavd = bfcavd                              Kredit-avd.
     c                   eval      rtckto = bfckto                              Kredit-kto.
     c                   eval      rtcsps = bfcsps                              Kredit-spes.
     c                   eval      rtcmva = bfcmom                              Kredit-mva.
     c                   eval      rtpros = bfproj                              Prosjekt
     c                   eval      rtrefn = bfkref                              Referanse
7.00  *                                                                         ____________________
7.00 c                   if        rtrefn = 0                                   Ev. krysse
7.00 c                   eval      rktrld_kund = 0                              reskontropostering
7.00  *                                                                         mot forskuddsbetalin
7.01 c                   if        rtckto > 9999                                F
7.01 c                   eval      rktrld_kund = rtckto                         F
7.01 c                   endif                                                  F
7.00 c                   if        rtdkto > 9999                                F
7.00 c                   eval      rktrld_kund = rtdkto                         F
7.00 c                   endif                                                  F
7.00  *                                                                         F
7.00 c                   if        rktrld_kund > 0                              Først prøver vi
7.00 c                   eval      rktrld_firm = w_firm                         å finne bilagskode 6
7.00 c                   eval      rktrld_bdat = *loval                         med likt beløp
7.00 c     rktrld_key    setll     rktrld                                       F
7.00 c     rktrld_key    reade     rktrld                                       F
7.00 c                   dow       not %eof(rktrld)                             F
7.00 c                   if        rnbilk = 62                                  F
7.00 c                             and rtbelØ = rnbelØ * -1
7.00 c                   eval      rtrefn = rnbiln                              F
7.00 c                   eval      rtautx = 'K'                                 F
7.00 c                   leave                                                  F
7.00 c                   endif                                                  F
7.00  *                                                                         F
7.00 c     rktrld_key    reade     rktrld                                       F
7.00 c                   enddo                                                  F
7.00  *                                                                         F
7.00 c                   if        rtrefn = 0                                   Hvis ikke det ble fu
7.00 c                   eval      rktrld_firm = w_firm                         forsøker vi å finne
7.00 c                   eval      rktrld_bdat = *loval                         en åpen bilagskode 6
7.00 c     rktrld_key    setll     rktrld                                       uavhengig av beløp
7.00 c     rktrld_key    reade     rktrld                                       F
7.00 c                   dow       not %eof(rktrld)                             F
7.00 c                   if        rnbilk = 62                                  F
7.01 c**                           and rtdkto > 9999
7.01 c                             and rnbelØ < 0
7.00 c                   eval      rtrefn = rnbiln                              F
7.00 c                   eval      rtautx = 'K'                                 F
7.00 c                   leave                                                  F
7.00 c                   endif                                                  F
7.00  *                                                                         F
7.00 c     rktrld_key    reade     rktrld                                       F
7.00 c                   enddo                                                  F
7.00 c                   endif                                                  F
7.00  *                                                                         F
7.00 c                   endif                                                  F
7.00 c                   endif                                                  F
7.00  *                                                                         ____________________
     c                   eval      rtkrab = 0                                   Rabatt
     c                   eval      rtvalk = bfvalk                              Valuta-kode
     c                   eval      rtvalb = bfvalb                              Valuta-beløp
     c                   eval      rtakti = bfakti                              Aktivitet
     c                   eval      rtmngd = bfmngd                              Mengde
     c                   eval      rtmngk = bfmngk                              Mengde-kode
     c                   eval      rtresn = bfkund                              Reskontronr.
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtautx = *blank                              Avkryssing     V2.03
     c                   eval      rtatte = *blank                              Attestert
      *
     c                   eval      rtbdat = bfbdat                              Forfall
     c                   eval      rtfdat = bffdat                              Forfall
      *
     c                   if        bfautx = 'K'                                                V2.03
     c                   eval      rtautx = bfautx                              Avkryssing     V2.03
     c                   else                                                     automatisk   V2.03
     c                   eval      rtkrys = bfautx                              Avkryssing
     c                   endif                                                                 V2.03
      *
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbilk = bfbilk                              Bilagskode
     c                   eval      rttext = bftext                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet.
     c                   eval      rtbref = *blank                              Bank-referanse
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr                                      Skriv post
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RBUNPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rbun    begsr
      *
     c                   xfoot     per           w_xper                         Tabell finne
     c                   movel     rtrper        wpe                            Per på trans
      *
     c                   if        ra3stp > 01 and                              Start r.år
     c                             w_xper > *zero                                tab.utfyllt
     c                   movel     per(wpe)      rtrper                           bruk p.fra tab.
     c                   if        wpe    < ra3stp                              og fjorår
     c                   move      w_Årm1        rtrper                          bruk fjorår
     c                   endif
     c                   endif
      *
     c                   eval      rsfirm = w_firm                              Bunt-nr.
     c                   eval      rsbunt = w_bunt                              Bunt-nr.
     c                   eval      rsstat = *blank                              Buntstatus
     c                   eval      rsraar = rtraar                              Periode/År
     c                   eval      rsrper = rtrper                              Periode/År
     c                   eval      rsfØrs = 1                                   Første linje
     c                   eval      rssist = w_linj                              Siste linje
     c                   eval      rsbsum = w_bsum                              Beløp
     c                   eval      rspsum = w_psum                              Beløp
     c                   eval      rspdeb = w_pdeb                              Beløp
     c                   eval      rspkre = w_pkre                              Beløp
     c                   time                    rspdat                         Postert dato
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur                                      Skriv post
      *
      * Nullstill buntsum
      *
     c                   eval      w_bsum = 0
     c                   eval      w_psum = 0
     c                   eval      w_pdeb = 0
     c                   eval      w_pkre = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb                         user
      *
     c                   eval      wpe = 0
     c                   eval      w_bsum = 0
     c                   eval      w_Årm1 = uyear - 1
      *
      * Key for les av transaksjonsfile
      *
     c     bovfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bovfl1_peri
      *
      * Key for les av firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av statuskoder 3 (avvikende perioder)
      *
     c     raa3pf_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av buntfile
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
7.00  *
7.00  * Key for oppslag mot kundetranser
7.00  *
7.00 c     rktrld_key    klist
7.00 c                   kfld                    rktrld_firm
7.00 c                   kfld                    rktrld_kund
7.00 c***                kfld                    rktrld_bdat
      *
     c                   endsr
